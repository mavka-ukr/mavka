взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_метода_структури_ПеребірСловника__далі(
    М: адреса<Машина>,
    здійснювач: Значення,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ЗначенняАбоСтанПадіння {
    якщо здійснювач.вид != ЗПредмет {
      увімкнути_стан_падіння(
        М,
        ПРЕДМЕТ(створити_текст(М, "Неможливо здійснити дію перебору словника \"далі\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути НЕДІЙСНЕ();
    }

    змінна предмет_перебору_словника = здійснювач.дані як адреса<Предмет> як адреса<ПредметПереборуСловника>;

    якщо предмет_перебору_словника.тип != М.предмет_структури_ПеребірСловника {
      увімкнути_стан_падіння(
        М,
        ПРЕДМЕТ(створити_текст(М, "Неможливо здійснити дію перебору словника \"далі\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути НЕДІЙСНЕ();
    }

    якщо предмет_перебору_словника.почато {
      предмет_перебору_словника.позиція += 1;
    } інакше {
      предмет_перебору_словника.позиція = 0;
      предмет_перебору_словника.почато = так;
    }

    якщо предмет_перебору_словника.позиція < предмет_перебору_словника.предмет_словника.розмір {
      предмет_перебору_словника.поточне_значення = предмет_перебору_словника.предмет_словника.елементи[предмет_перебору_словника.позиція].ключ;

      вернути ДІЙСНЕ();
    }

    вернути НЕДІЙСНЕ();
  }

  місцева дія записати_метод_структури_ПеребірСловника__далі(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._далі,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_метода_структури_ПеребірСловника__далі,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_ПеребірСловника,
      М.константи_назв._далі,
      предмет_дії
    );
  }
}