взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_Словник__містить(
    М: адреса<Машина>,
    здійснювач: Значення,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ЗначенняАбоСтанПадіння {
    якщо здійснювач.вид != ЗПредмет {
      вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію словника \"містить\"."));
    }

    змінна предмет_словника = здійснювач.дані як адреса<Предмет> як адреса<ПредметСловника>;

    якщо предмет_словника.тип != М.предмет_структури_Словник {
      вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію словника \"містить\"."));
    }

    змінна задіяне_ключ = НЕДІЙСНЕ();

    знайти_задіяне(М, задіяні, 0, М.константи_назв._ключ, задіяне_ключ::адреса);

    змінна п: природне = 0;

    поки п < предмет_словника.розмір {
      якщо перевірити_чи_значення_рівні(М, предмет_словника.елементи[п].ключ, задіяне_ключ) {
        вернути ДІЙСНЕ();
      }

      п += 1;
    }

    вернути НЕДІЙСНЕ();
  }

  місцева дія записати_метод_структури_Словник__містить(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._містить,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_Словник__містить,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_Словник,
      М.константи_назв._містить,
      предмет_дії
    );

    якщо М.стан_падіння {
      ЗАПАНІКУВАТИ(М, "Помилка при записі дії \"містить\" для структури \"Словник\".");
    }
  }
}