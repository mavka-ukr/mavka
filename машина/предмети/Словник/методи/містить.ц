взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_Словник__містить(
    М: адреса<Машина>,
    здійснювач: адреса<Предмет>,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ПредметАбоСтанПадіння {
    якщо здійснювач == пусто {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію словника \"містить\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна предмет_словника = здійснювач як адреса<ПредметСловника>;

    якщо предмет_словника.тип != М.предмет_структури_Словник {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію словника \"містить\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна задіяне_ключ: адреса<Предмет> = пусто;

    знайти_задіяне(М, задіяні, 0, М.константи_назв._ключ, задіяне_ключ::адреса);

    змінна п: природне = 0;

    поки п < предмет_словника.розмір {
      якщо перевірити_чи_значення_рівні(М, предмет_словника.елементи[п].ключ, задіяне_ключ) {
        вернути М.значення_дійсне;
      }

      п += 1;
    }

    вернути пусто;
  }

  місцева дія записати_метод_структури_Словник__містить(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._містить,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_Словник__містить,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_Словник,
      М.константи_назв._містить,
      предмет_дії
    );

    якщо М.стан_падіння {
      запанікувати(М, "Помилка при записі дії \"містить\" для структури \"Словник\".");
    }
  }
}