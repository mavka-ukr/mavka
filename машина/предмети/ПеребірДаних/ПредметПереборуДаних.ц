взяти означення мавка/машина;

простір мавка {
  місцева дія записати_метод_структури_ПеребірДаних__далі(М: адреса<Машина>);

  місцева дія заповнити_структуру_ПеребірДаних(
    М: адреса<Машина>
  ) {
    М.предмет_структури_ПеребірДаних.заборонити_наслідування = так;

    записати_метод_структури_ПеребірДаних__далі(М);
  }

  місцева дія створити_перебір_даних(
    М: адреса<Машина>,
    предмет_даних: адреса<ПредметДаних>
  ) -> адреса<ПредметПереборуДаних> {
    змінна предмет = виділити_предмет<ПредметПереборуДаних>(М, М.предмет_структури_ПеребірДаних);

    предмет.предмет_даних = предмет_даних;
    предмет.почато = ні;
    предмет.позиція = 0;
    предмет.поточне_значення = 0;

    вернути предмет;
  }

  місцева дія предмет_перебору_даних_отримати_властивість(
    М: адреса<Машина>,
    предмет_перебору_даних: адреса<ПредметПереборуДаних>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо назва == М.константи_назв._значення або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._значення) {
      вернути створити_ціле_з_ц64(М, ц64(предмет_перебору_даних.поточне_значення)) як адреса<Предмет>;
    }

    якщо назва == М.константи_назв._дані або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._дані) {
      вернути предмет_перебору_даних.предмет_даних як адреса<Предмет>;
    }

    змінна предмет_методу = знайти_метод_в_структурі(
      М,
      М.предмет_структури_ПеребірДаних,
      назва
    );

    якщо предмет_методу == пусто {
      вернути пусто;
    }

    вернути предмет_методу як адреса<Предмет>;
  }
}