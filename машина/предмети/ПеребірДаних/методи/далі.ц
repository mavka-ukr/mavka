взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_метода_структури_ПеребірДаних__далі(
    М: адреса<Машина>,
    здійснювач: адреса<Предмет>,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ПредметАбоСтанПадіння {
    якщо здійснювач == пусто {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію перебору даних \"далі\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна предмет_перебору_даних = здійснювач як адреса<ПредметПереборуДаних>;

    якщо предмет_перебору_даних.тип != М.предмет_структури_ПеребірДаних {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію перебору даних \"далі\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    якщо предмет_перебору_даних.почато {
      предмет_перебору_даних.позиція += 1;
    } інакше {
      предмет_перебору_даних.позиція = 0;
      предмет_перебору_даних.почато = так;
    }

    якщо предмет_перебору_даних.позиція < предмет_перебору_даних.предмет_даних.розмір {
      предмет_перебору_даних.поточне_значення = предмет_перебору_даних.предмет_даних.дані[предмет_перебору_даних.позиція];

      вернути М.значення_дійсне;
    }

    вернути пусто;
  }

  місцева дія записати_метод_структури_ПеребірДаних__далі(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._далі,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_метода_структури_ПеребірДаних__далі,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_ПеребірДаних,
      М.константи_назв._далі,
      предмет_дії
    );
  }
}