взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_Дані__чародія_перебір(
    М: адреса<Машина>,
    здійснювач: Значення,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ЗначенняАбоСтанПадіння {
    якщо здійснювач.вид != ЗПредмет {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію даних \"чародія_перебір\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути значення_недійсне();
    }

    змінна предмет_даних = здійснювач.дані як адреса<Предмет> як адреса<ПредметДаних>;

    якщо предмет_даних.тип != М.предмет_структури_Дані {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію даних \"чародія_перебір\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути значення_недійсне();
    }

    вернути значення_предмет(створити_перебір_даних(
      М,
      предмет_даних
    ));
  }

  дія записати_метод_структури_Дані__чародія_перебір(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._чародія_перебір,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_Дані__чародія_перебір,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_Дані,
      М.константи_назв._чародія_перебір,
      предмет_дії
    );

    якщо М.стан_падіння {
      запанікувати(М, "Помилка при записі дії \"чародія_перебір\" для структури \"Дані\".");
    }
  }
}

простір мавка {
  місцева дія заповнити_структуру_Дані(
    М: адреса<Машина>
  ) {
    М.предмет_структури_Дані.заборонити_наслідування = так;

    записати_метод_структури_Дані__чародія_перебір(М);
  }

  місцева дія створити_дані(
    М: адреса<Машина>,
    розмір: природне,
    дані: памʼять<п8>
  ) -> адреса<ПредметДаних> {
    змінна предмет = виділити_предмет<ПредметДаних>(М, М.предмет_структури_Дані);
    змінна копія_даних = виділити_памʼять<п8>(М, розмір);

    змінна п: природне = 0;
    поки п < розмір {
      копія_даних[п] = дані[п];
      п += 1;
    }

    предмет.розмір = розмір;
    предмет.дані = копія_даних;

    вернути предмет;
  }

  місцева дія створити_дані_без_копіювання(
    М: адреса<Машина>,
    розмір: природне,
    дані: памʼять<п8>
  ) -> адреса<ПредметДаних> {
    змінна предмет = виділити_предмет<ПредметДаних>(М, М.предмет_структури_Дані);

    предмет.розмір = розмір;
    предмет.дані = дані;

    вернути предмет;
  }

  місцева дія предмет_даних_отримати_властивість(
    М: адреса<Машина>,
    предмет_даних: адреса<ПредметДаних>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    змінна предмет_методу = знайти_метод_в_структурі(
      М,
      М.предмет_структури_Дані,
      назва
    );

    якщо предмет_методу == пусто {
      вернути значення_недійсне();
    }

    вернути значення_предмет(предмет_методу);
  }

  місцева дія предмет_даних_отримати_елемент(
    М: адреса<Машина>,
    предмет_даних: адреса<ПредметДаних>,
    ключ: Значення,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    вернути значення_недійсне();
  }
}