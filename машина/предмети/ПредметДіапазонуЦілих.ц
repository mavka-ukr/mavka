взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_ДіапазонЦілих__чародія_перебір(
    М: адреса<Машина>,
    здійснювач: Значення,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ЗначенняАбоСтанПадіння {
    якщо здійснювач.вид != ЗПредмет {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію діапазону цілих \"чародія_перебір\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути значення_недійсне();
    }

    змінна предмет_діапазону_цілих = здійснювач.дані як адреса<Предмет> як адреса<ПредметДіапазонуЦілих>;

    якщо предмет_діапазону_цілих.тип != М.предмет_структури_ДіапазонЦілих {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію діапазону цілих \"чародія_перебір\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути значення_недійсне();
    }

    вернути значення_предмет(створити_перебір_діапазону_цілих(
      М,
      предмет_діапазону_цілих
    ));
  }

  дія записати_метод_структури_ДіапазонЦілих__чародія_перебір(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._чародія_перебір,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_ДіапазонЦілих__чародія_перебір,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_ДіапазонЦілих,
      М.константи_назв._чародія_перебір,
      предмет_дії
    );

    якщо М.стан_падіння {
      запанікувати(М, "Помилка при записі дії \"чародія_перебір\" для структури \"ДіапазонЦілих\".");
    }
  }
}

простір мавка {
  місцева дія заповнити_структуру_ДіапазонЦілих(
    М: адреса<Машина>
  ) {
    М.предмет_структури_ДіапазонЦілих.заборонити_наслідування = так;

    записати_метод_структури_ДіапазонЦілих__чародія_перебір(М);
  }

  місцева дія створити_діапазон_цілих(
    М: адреса<Машина>,
    від: ц64,
    до: ц64,
    включно: логічне
  ) -> адреса<ПредметДіапазонуЦілих> {
    змінна предмет = виділити_предмет<ПредметДіапазонуЦілих>(М, М.предмет_структури_ДіапазонЦілих);

    предмет.від = від;
    предмет.до = до;
    предмет.включно = включно;

    вернути предмет;
  }

  місцева дія предмет_діапазону_цілих_отримати_властивість(
    М: адреса<Машина>,
    предмет_діапазону_цілих: адреса<ПредметДіапазонуЦілих>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    змінна предмет_методу = знайти_метод_в_структурі(
      М,
      М.предмет_структури_ДіапазонЦілих,
      назва
    );

    якщо предмет_методу == пусто {
      вернути значення_недійсне();
    }

    вернути значення_предмет(предмет_методу);
  }
}