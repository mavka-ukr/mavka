взяти означення мавка/машина;

простір мавка {
  місцева дія записати_метод_структури_ПеребірТексту__далі(М: адреса<Машина>);

  місцева дія заповнити_структуру_ПеребірТексту(
    М: адреса<Машина>
  ) {
    М.предмет_структури_ПеребірТексту.заборонити_наслідування = так;

    записати_метод_структури_ПеребірТексту__далі(М);
  }

  місцева дія створити_перебір_тексту(
    М: адреса<Машина>,
    предмет_тексту: адреса<ПредметТексту>
  ) -> адреса<ПредметПереборуТексту> {
    змінна предмет = виділити_предмет<ПредметПереборуТексту>(М, М.предмет_структури_ПеребірТексту);

    предмет.предмет_тексту = предмет_тексту;
    предмет.почато = ні;
    предмет.позиція = 0;
    предмет.поточне_значення = пусто;

    вернути предмет;
  }

  місцева дія предмет_перебору_тексту_отримати_властивість(
    М: адреса<Машина>,
    предмет_перебору_тексту: адреса<ПредметПереборуТексту>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо назва == М.константи_назв._значення або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._значення) {
      вернути предмет_перебору_тексту.поточне_значення;
    }

    якщо назва == М.константи_назв._текст або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._текст) {
      вернути предмет_перебору_тексту.предмет_тексту як адреса<Предмет>;
    }

    змінна предмет_методу = знайти_метод_в_структурі(
      М,
      М.предмет_структури_ПеребірТексту,
      назва
    );

    якщо предмет_методу == пусто {
      вернути пусто;
    }

    вернути предмет_методу як адреса<Предмет>;
  }
}