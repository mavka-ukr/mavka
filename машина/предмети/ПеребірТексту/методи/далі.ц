взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_метода_структури_ПеребірТексту__далі(
    М: адреса<Машина>,
    здійснювач: адреса<Предмет>,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ПредметАбоСтанПадіння {
    якщо здійснювач == пусто {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію перебору тексту \"далі\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна предмет_перебору_тексту = здійснювач як адреса<ПредметПереборуТексту>;

    якщо предмет_перебору_тексту.тип != М.предмет_структури_ПеребірТексту {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію перебору тексту \"далі\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    якщо предмет_перебору_тексту.почато {
      предмет_перебору_тексту.позиція += 1;
    } інакше {
      предмет_перебору_тексту.позиція = 0;
      предмет_перебору_тексту.почато = так;
    }

    якщо предмет_перебору_тексту.позиція < предмет_перебору_тексту.предмет_тексту.значення.розмір {
      предмет_перебору_тексту.поточне_значення = створити_ціле_з_ц64(М,
        ц64(предмет_перебору_тексту.предмет_тексту.значення.дані[предмет_перебору_тексту.позиція])
      ) як адреса<Предмет>;

      вернути М.значення_дійсне;
    }

    вернути пусто;
  }

  місцева дія записати_метод_структури_ПеребірТексту__далі(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._далі,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_метода_структури_ПеребірТексту__далі,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_ПеребірТексту,
      М.константи_назв._далі,
      предмет_дії
    );
  }
}