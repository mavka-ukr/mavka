взяти означення мавка/машина;

простір мавка {
  місцева дія заповнити_структуру_Код(
    М: адреса<Машина>
  ) {
    М.предмет_структури_Код.заборонити_наслідування = так;
  }

  місцева дія створити_код(
    М: адреса<Машина>,
    константи: адреса<ПредметПослідовності>,
    вказівки: адреса<ПредметДаних>,
    шлях: адреса<ПредметДаних>,
    місцезнаходження_вказівок: МісцезнаходженняВказівок
  ) -> адреса<ПредметКоду> {
    змінна предмет = виділити_предмет<ПредметКоду>(М, М.предмет_структури_Код);

    предмет.константи = константи;
    предмет.вказівки = вказівки;
    предмет.шлях = шлях;
    предмет.місцезнаходження_вказівок = місцезнаходження_вказівок;

    вернути предмет;
  }

  місцева дія предмет_коду_отримати_властивість(
    М: адреса<Машина>,
    предмет_коду: адреса<ПредметКоду>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо назва == М.константи_назв._константи або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._константи) {
      якщо предмет_коду.константи != пусто {
        вернути предмет_коду.константи як адреса<Предмет>;
      }

      вернути пусто;
    }

    якщо назва == М.константи_назв._вказівки або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._вказівки) {
      якщо предмет_коду.вказівки != пусто {
        вернути предмет_коду.вказівки як адреса<Предмет>;
      }

      вернути пусто;
    }

    якщо назва == М.константи_назв._шлях або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._шлях) {
      якщо предмет_коду.шлях != пусто {
        вернути предмет_коду.шлях як адреса<Предмет>;
      }

      вернути пусто;
    }

    змінна предмет_методу = знайти_метод_в_структурі(
      М,
      М.предмет_структури_Код,
      назва
    );

    якщо предмет_методу == пусто {
      вернути пусто;
    }

    вернути предмет_методу як адреса<Предмет>;
  }

  місцева дія знайти_рядок_вказівки(
    місцезнаходження_вказівок: МісцезнаходженняВказівок,
    позиція_вказівки: п32
  ) -> п32 {
    змінна п: природне = 0;

    поки п < місцезнаходження_вказівок.розмір {
      змінна м = місцезнаходження_вказівок.дані[п];

      якщо м.початок <= позиція_вказівки {
        якщо м.кінець >= позиція_вказівки {
          вернути м.рядок;
        }
      }

      п += 1;
    }

    вернути 0;
  }
}