взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_метода_структури_ПеребірДіапазонуДробових__далі(
    М: адреса<Машина>,
    здійснювач: адреса<Предмет>,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ПредметАбоСтанПадіння {
    якщо здійснювач == пусто {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію перебору діапазону дробових \"далі\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна предмет_перебору_діапазону_дробових = здійснювач як адреса<ПредметПереборуДіапазонуДробових>;

    якщо предмет_перебору_діапазону_дробових.тип != М.предмет_структури_ПеребірДіапазонуДробових {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію перебору діапазону дробових \"далі\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    якщо предмет_перебору_діапазону_дробових.почато {
      предмет_перебору_діапазону_дробових.поточне_значення = предмет_перебору_діапазону_дробових.поточне_значення + 1.0;
    } інакше {
      предмет_перебору_діапазону_дробових.поточне_значення = предмет_перебору_діапазону_дробових.предмет_діапазону.від;
      предмет_перебору_діапазону_дробових.почато = так;
    }

    якщо предмет_перебору_діапазону_дробових.предмет_діапазону.включно {
      якщо предмет_перебору_діапазону_дробових.поточне_значення <= предмет_перебору_діапазону_дробових.предмет_діапазону.до {
        вернути М.значення_дійсне;
      }
    } інакше {
      якщо предмет_перебору_діапазону_дробових.поточне_значення < предмет_перебору_діапазону_дробових.предмет_діапазону.до {
        вернути М.значення_дійсне;
      }
    }

    вернути пусто;
  }

  місцева дія записати_метод_структури_ПеребірДіапазонуДробових__далі(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._далі,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_метода_структури_ПеребірДіапазонуДробових__далі,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_ПеребірДіапазонуДробових,
      М.константи_назв._далі,
      предмет_дії
    );
  }
}