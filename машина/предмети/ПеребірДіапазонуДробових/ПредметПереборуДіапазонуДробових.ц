взяти означення мавка/машина;

простір мавка {
  місцева дія записати_метод_структури_ПеребірДіапазонуДробових__далі(М: адреса<Машина>);

  місцева дія заповнити_структуру_ПеребірДіапазонуДробових(
    М: адреса<Машина>
  ) {
    М.предмет_структури_ПеребірДіапазонуДробових.заборонити_наслідування = так;

    записати_метод_структури_ПеребірДіапазонуДробових__далі(М);
  }

  місцева дія створити_перебір_діапазону_дробових(
    М: адреса<Машина>,
    предмет_діапазону: адреса<ПредметДіапазонуДробових>
  ) -> адреса<ПредметПереборуДіапазонуДробових> {
    змінна предмет = виділити_предмет<ПредметПереборуДіапазонуДробових>(М, М.предмет_структури_ПеребірДіапазонуДробових);

    предмет.предмет_діапазону = предмет_діапазону;
    предмет.почато = ні;
    предмет.поточне_значення = 0;

    вернути предмет;
  }

  місцева дія предмет_перебору_діапазону_дробових_отримати_властивість(
    М: адреса<Машина>,
    предмет_перебору_діапазону_дробових: адреса<ПредметПереборуДіапазонуДробових>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    якщо назва == М.константи_назв._значення або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._значення) {
      вернути значення_дробове(предмет_перебору_діапазону_дробових.поточне_значення);
    }

    якщо назва == М.константи_назв._діапазон або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._діапазон) {
      вернути значення_предмет(предмет_перебору_діапазону_дробових.предмет_діапазону);
    }

    змінна предмет_методу = знайти_метод_в_структурі(
      М,
      М.предмет_структури_ПеребірДіапазонуДробових,
      назва
    );

    якщо предмет_методу == пусто {
      вернути значення_недійсне();
    }

    вернути значення_предмет(предмет_методу);
  }
}