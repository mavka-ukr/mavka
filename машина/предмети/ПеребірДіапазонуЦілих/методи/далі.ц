взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_метода_структури_ПеребірДіапазонуЦілих__далі(
    М: адреса<Машина>,
    здійснювач: адреса<Предмет>,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ПредметАбоСтанПадіння {
    якщо здійснювач == пусто {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію перебору діапазону цілих \"далі\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна предмет_перебору_діапазону_цілих = здійснювач як адреса<ПредметПереборуДіапазонуЦілих>;

    якщо предмет_перебору_діапазону_цілих.тип != М.предмет_структури_ПеребірДіапазонуЦілих {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію перебору діапазону цілих \"далі\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    якщо предмет_перебору_діапазону_цілих.поточне_значення != пусто {
      змінна нове_значення: ДЧ::Ціле;
      змінна один: ДЧ::Ціле;

      якщо не ДЧ::ініціалізувати_ціле_один(М.виділяч_ДЧ, один::адреса) {
        увімкнути_стан_падіння(
          М,
          створити_текст(М, "Неможливо здійснити дію перебору діапазону цілих \"далі\".") як адреса<Предмет>,
          Місцезнаходження { пусто, 0 }
        );
        вернути пусто;
      }

      якщо не ДЧ::додати_цілі(
        М.виділяч_ДЧ,
        предмет_перебору_діапазону_цілих.поточне_значення.значення,
        один,
        нове_значення::адреса
      ) {
        увімкнути_стан_падіння(
          М,
          створити_текст(М, "Неможливо здійснити дію перебору діапазону цілих \"далі\".") як адреса<Предмет>,
          Місцезнаходження { пусто, 0 }
        );
        вернути пусто;
      }

      ДЧ::звільнити_ціле(М.виділяч_ДЧ, один);

      предмет_перебору_діапазону_цілих.поточне_значення = створити_ціле_без_копіювання(М, нове_значення);
    } інакше {
      предмет_перебору_діапазону_цілих.поточне_значення = предмет_перебору_діапазону_цілих.предмет_діапазону.від;
    }

    якщо предмет_перебору_діапазону_цілих.предмет_діапазону.включно {
      якщо ДЧ::порівняти_цілі(предмет_перебору_діапазону_цілих.поточне_значення.значення, предмет_перебору_діапазону_цілих.предмет_діапазону.до.значення) <= 0 {
        вернути М.значення_дійсне;
      }
    } інакше {
      якщо ДЧ::порівняти_цілі(предмет_перебору_діапазону_цілих.поточне_значення.значення, предмет_перебору_діапазону_цілих.предмет_діапазону.до.значення) < 0 {
        вернути М.значення_дійсне;
      }
    }

    вернути пусто;
  }

  місцева дія записати_метод_структури_ПеребірДіапазонуЦілих__далі(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._далі,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_метода_структури_ПеребірДіапазонуЦілих__далі,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_ПеребірДіапазонуЦілих,
      М.константи_назв._далі,
      предмет_дії
    );
  }
}