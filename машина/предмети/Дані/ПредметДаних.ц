взяти означення мавка/машина;

простір мавка {
  місцева дія записати_метод_структури_Дані__чародія_перебір(М: адреса<Машина>);

  місцева дія заповнити_структуру_Дані(
    М: адреса<Машина>
  ) {
    М.предмет_структури_Дані.заборонити_наслідування = так;

    записати_метод_структури_Дані__чародія_перебір(М);
  }

  місцева дія створити_дані(
    М: адреса<Машина>,
    розмір: природне,
    дані: памʼять<п8>
  ) -> адреса<ПредметДаних> {
    змінна предмет = виділити_предмет<ПредметДаних>(М, М.предмет_структури_Дані);
    змінна копія_даних = виділити_памʼять<п8>(М, розмір);

    змінна п: природне = 0;
    поки п < розмір {
      копія_даних[п] = дані[п];
      п += 1;
    }

    предмет.розмір = розмір;
    предмет.дані = копія_даних;

    вернути предмет;
  }

  місцева дія створити_дані_без_копіювання(
    М: адреса<Машина>,
    розмір: природне,
    дані: памʼять<п8>
  ) -> адреса<ПредметДаних> {
    змінна предмет = виділити_предмет<ПредметДаних>(М, М.предмет_структури_Дані);

    предмет.розмір = розмір;
    предмет.дані = дані;

    вернути предмет;
  }

  місцева дія предмет_даних_отримати_властивість(
    М: адреса<Машина>,
    предмет_даних: адреса<ПредметДаних>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    змінна предмет_методу = знайти_метод_в_структурі(
      М,
      М.предмет_структури_Дані,
      назва
    );

    якщо предмет_методу == пусто {
      вернути пусто;
    }

    вернути значення_предмет(предмет_методу);
  }

  місцева дія предмет_даних_отримати_елемент(
    М: адреса<Машина>,
    предмет_даних: адреса<ПредметДаних>,
    ключ: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    вернути пусто;
  }
}