взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_метода_структури_ПеребірДаних__далі(
    М: адреса<Машина>,
    здійснювач: Значення,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ЗначенняАбоСтанПадіння {
    якщо здійснювач.вид != ЗПредмет {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію перебору даних \"далі\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути значення_недійсне();
    }

    змінна предмет_перебору_даних = здійснювач.дані як адреса<Предмет> як адреса<ПредметПереборуДаних>;

    якщо предмет_перебору_даних.тип != М.предмет_структури_ПеребірДаних {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію перебору даних \"далі\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути значення_недійсне();
    }

    якщо предмет_перебору_даних.почато {
      предмет_перебору_даних.позиція += 1;
    } інакше {
      предмет_перебору_даних.позиція = 0;
      предмет_перебору_даних.почато = так;
    }

    якщо предмет_перебору_даних.позиція < предмет_перебору_даних.предмет_даних.розмір {
      предмет_перебору_даних.поточне_значення = предмет_перебору_даних.предмет_даних.елементи[предмет_перебору_даних.позиція];

      вернути значення_дійсне();
    }

    вернути значення_недійсне();
  }

  дія записати_метод_структури_ПеребірДаних__далі(М: адреса<Машина>) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._далі,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_метода_структури_ПеребірДаних__далі,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_ПеребірДаних,
      М.константи_назв._далі,
      предмет_дії
    );
  }
}

простір мавка {
  місцева дія заповнити_структуру_ПеребірДаних(
    М: адреса<Машина>
  ) {
    М.предмет_структури_ПеребірДаних.заборонити_наслідування = так;

    записати_метод_структури_ПеребірДаних__далі(М);
  }

  місцева дія створити_перебір_даних(
    М: адреса<Машина>,
    предмет_даних: адреса<ПредметДаних>
  ) -> адреса<ПредметПереборуДаних> {
    змінна предмет = виділити_предмет<ПредметПереборуДаних>(М, М.предмет_структури_ПеребірДаних);

    предмет.предмет_даних = предмет_даних;
    предмет.почато = ні;
    предмет.позиція = 0;
    предмет.поточне_значення = значення_недійсне();

    вернути предмет;
  }

  місцева дія предмет_перебору_даних_отримати_властивість(
    М: адреса<Машина>,
    предмет_перебору_даних: адреса<ПредметПереборуДаних>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    якщо назва == М.константи_назв._значення або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._значення) {
      вернути предмет_перебору_даних.поточне_значення як Значення;
    }

    якщо назва == М.константи_назв._дані або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._дані) {
      вернути значення_предмет(предмет_перебору_даних.предмет_даних);
    }

    змінна предмет_методу = знайти_метод_в_структурі(
      М,
      М.предмет_структури_ПеребірДаних,
      назва
    );

    якщо предмет_методу == пусто {
      вернути значення_недійсне();
    }

    вернути значення_предмет(предмет_методу);
  }
}