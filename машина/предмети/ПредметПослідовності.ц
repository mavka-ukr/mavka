взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_Послідовність__чародія_перебір(
    М: адреса<Машина>,
    здійснювач: Значення,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ЗначенняАбоСтанПадіння {
    якщо здійснювач.вид != ЗПредмет {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію послідовності \"чародія_перебір\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути значення_недійсне();
    }

    змінна предмет_послідовності = здійснювач.дані як адреса<Предмет> як адреса<ПредметПослідовності>;

    якщо предмет_послідовності.тип != М.предмет_структури_Послідовність {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію послідовності \"чародія_перебір\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути значення_недійсне();
    }

    вернути значення_предмет(створити_перебір_послідовності(
      М,
      предмет_послідовності
    ));
  }

  дія записати_метод_структури_Послідовність__чародія_перебір(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._чародія_перебір,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_Послідовність__чародія_перебір,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_Послідовність,
      М.константи_назв._чародія_перебір,
      предмет_дії
    );

    якщо М.стан_падіння {
      запанікувати(М, "Помилка при записі дії \"чародія_перебір\" для структури \"Послідовність\".");
    }
  }
}

простір мавка {
  місцева дія заповнити_структуру_Послідовність(
    М: адреса<Машина>
  ) {
    М.предмет_структури_Послідовність.заборонити_наслідування = так;

    записати_метод_структури_Послідовність__чародія_перебір(М);
  }

  місцева дія створити_послідовність(
    М: адреса<Машина>,
    розмір: природне,
    елементи: памʼять<Значення>
  ) -> адреса<ПредметПослідовності> {
    змінна копія_елементів = виділити_памʼять<Значення>(М, розмір);

    змінна п: природне = 0;
    поки п < розмір {
      копія_елементів[п] = елементи[п];
      п += 1;
    }

    змінна послідовність = виділити_предмет<ПредметПослідовності>(М, М.предмет_структури_Послідовність);

    послідовність.розмір = розмір;
    послідовність.елементи = копія_елементів;

    вернути послідовність;
  }

  місцева дія створити_послідовність_без_копіювання(
    М: адреса<Машина>,
    розмір: природне,
    елементи: памʼять<Значення>
  ) -> адреса<ПредметПослідовності> {
    змінна послідовність = виділити_предмет<ПредметПослідовності>(М, М.предмет_структури_Послідовність);

    послідовність.розмір = розмір;
    послідовність.елементи = елементи;

    вернути послідовність;
  }

  місцева дія створити_сиру_послідовність(
    М: адреса<Машина>,
    розмір: природне
  ) -> адреса<ПредметПослідовності> {
    змінна послідовність = виділити_предмет<ПредметПослідовності>(М, М.предмет_структури_Послідовність);

    послідовність.розмір = розмір;
    послідовність.елементи = виділити_памʼять<Значення>(М, розмір);

    вернути послідовність;
  }

  місцева дія предмет_послідовності_отримати_властивість(
    М: адреса<Машина>,
    предмет_послідовності: адреса<ПредметПослідовності>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    якщо назва == М.константи_назв._розмір або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._розмір) {
      вернути значення_ціле(ц64(предмет_послідовності.розмір));
    }

    змінна предмет_методу = знайти_метод_в_структурі(
      М,
      М.предмет_структури_Послідовність,
      назва
    );

    якщо предмет_методу == пусто {
      вернути значення_недійсне();
    }

    вернути значення_предмет(предмет_методу);
  }

  місцева дія предмет_послідовності_отримати_елемент(
    М: адреса<Машина>,
    предмет_послідовності: адреса<ПредметПослідовності>,
    ключ: Значення,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    вернути значення_недійсне();
  }
}