взяти означення мавка/машина;

простір мавка {
  місцева дія заповнити_структуру_Дія(
    М: адреса<Машина>
  ) {
    М.предмет_структури_Дія.заборонити_наслідування = так;
  }

  місцева дія створити_дію(
    М: адреса<Машина>,
    дійсність_створення: адреса<Дійсність>,
    назва: адреса<ПредметТексту>,
    параметри: адреса<ПредметПослідовності>,
    тип_результату: адреса<Предмет>,
    код: адреса<ПредметКоду>,
    шлях_до_теки_модулів: адреса<ПредметДаних>
  ) -> адреса<ПредметДії> {
    змінна предмет = виділити_предмет<ПредметДії>(М, М.предмет_структури_Дія);

    предмет.дійсність_створення = дійсність_створення;
    дійсність_створення.кількість_запозичень += 1;

    предмет.рідна = ні;
    предмет.назва = назва;
    предмет.параметри = параметри;
    предмет.код = код;
    предмет.тип_результату = тип_результату;
    предмет.шлях_до_теки_модулів = шлях_до_теки_модулів;
    предмет.метод_структури = пусто;

    вернути предмет;
  }

  місцева дія створити_рідну_дію(
    М: адреса<Машина>,
    назва: адреса<ПредметТексту>,
    параметри: адреса<ПредметПослідовності>,
    тип_результату: адреса<Предмет>,
    рідна_дія: РіднаДія,
    шлях_до_теки_модулів: адреса<ПредметДаних>
  ) -> адреса<ПредметДії> {
    змінна предмет = виділити_предмет<ПредметДії>(М, М.предмет_структури_Дія);

    предмет.дійсність_створення = пусто;
    предмет.рідна = так;
    предмет.назва = назва;
    предмет.параметри = параметри;
    предмет.тип_результату = тип_результату;
    предмет.код = рідна_дія;
    предмет.шлях_до_теки_модулів = шлях_до_теки_модулів;
    предмет.метод_структури = пусто;

    вернути предмет;
  }

  місцева дія предмет_дії_здійснити(
    М: адреса<Машина>,
    здійснювач: адреса<Предмет>,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо предмет_дії.рідна {
      змінна рідна_дія = предмет_дії.код як РіднаДія;

      покласти_в_історію_здійснення(
        М,
        пусто,
        здійснювач,
        предмет_дії,
        задіяні,
        місцезнаходження
      );

      змінна результат = рідна_дія(
        М,
        здійснювач,
        предмет_дії,
        задіяні
      );

      якщо не М.стан_падіння {
        видалити_з_історії_здійснення(М, 1);
      }

      вернути результат;
    } інакше {
      змінна дійсність = створити_дійсність(
        М,
        предмет_дії.дійсність_створення
      );

      змінна елемент_історії = покласти_в_історію_здійснення(
        М,
        дійсність,
        здійснювач,
        предмет_дії,
        задіяні,
        місцезнаходження
      );

      якщо предмет_дії.параметри.розмір > 0 {
        змінна п: природне = 0;

        поки п < предмет_дії.параметри.розмір {
          змінна параметр = предмет_дії.параметри.елементи[п] як адреса<ПредметПараметра>;

          якщо задіяні.розмір > п {
            записати_в_дійсність(
              М,
              дійсність,
              параметр.назва,
              задіяні.дані[п]
            );
          } інакше {
            записати_в_дійсність(
              М,
              дійсність,
              параметр.назва,
              пусто
            );
          }

          п += 1;
        }

        якщо задіяні.іменовано_задіяні != пусто {
          п = 0;

          поки п < задіяні.іменовано_задіяні.розмір {
            змінна іменовано_задіяне = задіяні.іменовано_задіяні.елементи[п];

            записати_в_дійсність(
              М,
              дійсність,
              іменовано_задіяне.назва,
              іменовано_задіяне.значення
            );

            п += 1;
          }
        }
      }

      змінна структура_предка: адреса<ПредметСтруктури> = пусто;
      якщо предмет_дії.метод_структури != пусто {
        структура_предка = предмет_дії.метод_структури.предок;
      }

      змінна результат = виконати_код(
        М,
        М.накопичувач_значень::адреса,
        елемент_історії.спроби::адреса,
        дійсність,
        здійснювач,
        предмет_дії.код як адреса<ПредметКоду>,
        0,
        структура_предка,
        предмет_дії.шлях_до_теки_модулів
      );

      якщо М.стан_падіння {
        вернути пусто;
      }

      видалити_з_історії_здійснення(М, 1);

      вернути результат;
    }
  }

  місцева дія предмет_дії_отримати_властивість(
    М: адреса<Машина>,
    предмет_дії: адреса<ПредметДії>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо назва == М.константи_назв._назва або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._назва) {
      якщо предмет_дії.назва != пусто {
        вернути предмет_дії.назва як адреса<Предмет>;
      }

      вернути пусто;
    }

    якщо назва == М.константи_назв._параметри або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._параметри) {
      якщо предмет_дії.параметри != пусто {
        вернути предмет_дії.параметри як адреса<Предмет>;
      }

      вернути пусто;
    }

    якщо назва == М.константи_назв._тип_результату або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._тип_результату) {
      якщо предмет_дії.тип_результату != пусто {
        вернути предмет_дії.тип_результату як адреса<Предмет>;
      }

      вернути пусто;
    }

    якщо назва == М.константи_назв._код або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._код) {
      якщо не предмет_дії.рідна {
        вернути предмет_дії.код як адреса<ПредметКоду> як адреса<Предмет>;
      }

      вернути пусто;
    }

    змінна предмет_методу = знайти_метод_в_структурі(
      М,
      М.предмет_структури_Дія,
      назва
    );

    якщо предмет_методу == пусто {
      вернути пусто;
    }

    вернути предмет_методу як адреса<Предмет>;
  }
}