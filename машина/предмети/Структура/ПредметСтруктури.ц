взяти означення мавка/машина;

простір мавка {
  місцева дія заповнити_структуру_Структура(
    М: адреса<Машина>
  ) {
    М.предмет_структури_Структура.заборонити_наслідування = так;
  }

  місцева дія створити_структуру(
    М: адреса<Машина>,
    назва: адреса<ПредметТексту>,
    предок: адреса<ПредметСтруктури>,
    параметри: адреса<ПредметПослідовності>
  ) -> адреса<ПредметСтруктури> {
    змінна предмет = виділити_предмет<ПредметСтруктури>(М, М.предмет_структури_Структура);

    предмет.заборонити_наслідування = ні;
    предмет.назва = назва;
    предмет.предок = предок;
    предмет.параметри = параметри;
    предмет.методи = Методи { 0, пусто };
    предмет.властивості = Властивості { 0, пусто, 0 };

    вернути предмет;
  }

  // позначка: прохід по типах предметів
  місцева дія предмет_структури_здійснити(
    М: адреса<Машина>,
    предмет_структури: адреса<ПредметСтруктури>,
    задіяні: Задіяні,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    якщо предмет_структури == М.предмет_структури_Предмет {
      вернути МІСЦЕПАДІННЯ(М, створити_текст(М, "Неможливо здійснити структуру \"Предмет\"."), місцезнаходження);
    }

    якщо предмет_структури == М.предмет_структури_Модуль {
      вернути МІСЦЕПАДІННЯ(М, створити_текст(М, "Неможливо здійснити структуру \"Модуль\"."), місцезнаходження);
    }

    якщо предмет_структури == М.предмет_структури_Параметр {
      вернути МІСЦЕПАДІННЯ(М, створити_текст(М, "Неможливо здійснити структуру \"Параметр\"."), місцезнаходження);
    }

    якщо предмет_структури == М.предмет_структури_Число {
      вернути МІСЦЕПАДІННЯ(М, створити_текст(М, "Неможливо здійснити структуру \"Число\"."), місцезнаходження);
    }

    якщо предмет_структури == М.предмет_структури_Текст {
      вернути МІСЦЕПАДІННЯ(М, створити_текст(М, "Неможливо здійснити структуру \"Текст\"."), місцезнаходження);
    }

    якщо предмет_структури == М.предмет_структури_Код {
      вернути МІСЦЕПАДІННЯ(М, створити_текст(М, "Неможливо здійснити структуру \"Код\"."), місцезнаходження);
    }

    якщо предмет_структури == М.предмет_структури_Дія {
      вернути МІСЦЕПАДІННЯ(М, створити_текст(М, "Неможливо здійснити структуру \"Дія\"."), місцезнаходження);
    }

    якщо предмет_структури == М.предмет_структури_Список {
      вернути МІСЦЕПАДІННЯ(М, створити_текст(М, "Неможливо здійснити структуру \"Список\"."), місцезнаходження);
    }

    якщо предмет_структури == М.предмет_структури_Словник {
      вернути МІСЦЕПАДІННЯ(М, створити_текст(М, "Неможливо здійснити структуру \"Словник\"."), місцезнаходження);
    }

    якщо предмет_структури == М.предмет_структури_Дані {
      вернути МІСЦЕПАДІННЯ(М, створити_текст(М, "Неможливо здійснити структуру \"Дані\"."), місцезнаходження);
    }

    якщо предмет_структури == М.предмет_структури_ДіапазонЧисел {
      вернути МІСЦЕПАДІННЯ(М, створити_текст(М, "Неможливо здійснити структуру \"ДіапазонЧисел\"."), місцезнаходження);
    }

    якщо предмет_структури == М.предмет_структури_ПеребірДіапазонуЧисел {
      вернути МІСЦЕПАДІННЯ(М, створити_текст(М, "Неможливо здійснити структуру \"ПеребірДіапазонуЧисел\"."), місцезнаходження);
    }

    якщо предмет_структури == М.предмет_структури_ПеребірДаних {
      вернути МІСЦЕПАДІННЯ(М, створити_текст(М, "Неможливо здійснити структуру \"ПеребірДаних\"."), місцезнаходження);
    }

    якщо предмет_структури == М.предмет_структури_ПеребірПослідовності {
      вернути МІСЦЕПАДІННЯ(М, створити_текст(М, "Неможливо здійснити структуру \"ПеребірПослідовності\"."), місцезнаходження);
    }

    якщо предмет_структури == М.предмет_структури_ПеребірСловника {
      вернути МІСЦЕПАДІННЯ(М, створити_текст(М, "Неможливо здійснити структуру \"ПеребірСловника\"."), місцезнаходження);
    }

    якщо предмет_структури == М.предмет_структури_ПеребірСписку {
      вернути МІСЦЕПАДІННЯ(М, створити_текст(М, "Неможливо здійснити структуру \"ПеребірСписку\"."), місцезнаходження);
    }

    якщо предмет_структури == М.предмет_структури_ПеребірТексту {
      вернути МІСЦЕПАДІННЯ(М, створити_текст(М, "Неможливо здійснити структуру \"ПеребірТексту\"."), місцезнаходження);
    }

    якщо предмет_структури == М.предмет_структури_Послідовність {
      вернути МІСЦЕПАДІННЯ(М, створити_текст(М, "Неможливо здійснити структуру \"Послідовність\"."), місцезнаходження);
    }

    змінна предмет = створити_складений_предмет(М, предмет_структури);

    змінна предмет_метода = знайти_метод_в_структурі(
      М,
      предмет_структури,
      М.константи_назв._чародія
    );

    якщо предмет_метода != пусто {
      предмет_дії_здійснити(
        М,
        ПРЕДМЕТ(предмет),
        предмет_метода,
        задіяні,
        місцезнаходження
      );

      вернути ПРЕДМЕТ(предмет);
    }

    покласти_в_історію_здійснення(
      М,
      пусто,
      ПРЕДМЕТ(предмет),
      пусто,
      задіяні,
      місцезнаходження
    );

    якщо предмет_структури.параметри.розмір > 0 {
      змінна п: природне = 0;

      поки п < предмет_структури.параметри.розмір {
        змінна параметр = предмет_структури.параметри.елементи[п];
        змінна властивості_параметра = параметр.дані як адреса<Предмет> як адреса<ПредметПараметра>;

        якщо задіяні.розмір > п {
          записати_властивість(
            М,
            предмет.властивості::адреса,
            властивості_параметра.назва,
            задіяні.дані[п]
          );
        }

        п += 1;
      }

      якщо задіяні.іменовано_задіяні != пусто {
        п = 0;

        поки п < задіяні.іменовано_задіяні.розмір {
          змінна іменовано_задіяне = задіяні.іменовано_задіяні.елементи[п];

          записати_властивість(
            М,
            предмет.властивості::адреса,
            іменовано_задіяне.назва,
            іменовано_задіяне.значення
          );

          п += 1;
        }
      }
    }

    видалити_з_історії_здійснення(М, 1);

    вернути ПРЕДМЕТ(предмет);
  }

  місцева дія записати_метод_структури(
    М: адреса<Машина>,
    предмет_структури: адреса<ПредметСтруктури>,
    назва: адреса<ПредметТексту>,
    значення_дії: адреса<ПредметДії>
  ) {
    змінна п: природне = 0;

    поки п < предмет_структури.методи.розмір {
      змінна метод = предмет_структури.методи.дані[п]::адреса;

      якщо метод.назва == назва або перевірити_чи_предмети_тексту_рівні(М, метод.назва, назва) {
        метод.значення = значення_дії;

        вернути;
      }

      п += 1;
    }

    предмет_структури.методи.дані = перевиділити_памʼять<Метод>(М, предмет_структури.методи.дані, предмет_структури.методи.розмір + 1);
    предмет_структури.методи.дані[предмет_структури.методи.розмір] = Метод { назва, значення_дії };
    предмет_структури.методи.розмір += 1;
  }

  місцева дія знайти_метод_в_структурі(
    М: адреса<Машина>,
    предмет_структури: адреса<ПредметСтруктури>,
    назва: адреса<ПредметТексту>
  ) -> адреса<ПредметДії> {
    змінна п: природне = 0;

    поки п < предмет_структури.методи.розмір {
      змінна метод = предмет_структури.методи.дані[п];

      якщо метод.назва == назва або перевірити_чи_предмети_тексту_рівні(М, метод.назва, назва) {
        вернути метод.значення;
      }

      п += 1;
    }

    якщо предмет_структури.предок != пусто {
      вернути знайти_метод_в_структурі(
        М,
        предмет_структури.предок,
        назва
      );
    }

    вернути пусто;
  }

  місцева дія предмет_структури_змінити_властивість(
    М: адреса<Машина>,
    предмет_структури: адреса<ПредметСтруктури>,
    назва: адреса<ПредметТексту>,
    значення: Значення,
    місцезнаходження: Місцезнаходження
  ) {
    записати_властивість(М, предмет_структури.властивості::адреса, назва, значення);
  }

  місцева дія предмет_структури_отримати_властивість(
    М: адреса<Машина>,
    предмет_структури: адреса<ПредметСтруктури>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    якщо назва == М.константи_назв._назва або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._назва) {
      вернути ПРЕДМЕТ(предмет_структури.назва);
    }

    якщо назва == М.константи_назв._предок або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._предок) {
      якщо предмет_структури.предок == пусто {
        вернути НЕДІЙСНЕ();
      }

      вернути ПРЕДМЕТ(предмет_структури.предок);
    }

    якщо назва == М.константи_назв._параметри або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._параметри) {
      вернути ПРЕДМЕТ(предмет_структури.параметри);
    }

    змінна значення_властивості = НЕДІЙСНЕ();

    якщо знайти_властивість(
      М,
      предмет_структури.властивості,
      назва,
      значення_властивості::адреса
    ) {
      вернути значення_властивості;
    }

    змінна предмет_методу = знайти_метод_в_структурі(
      М,
      М.предмет_структури_Структура,
      назва
    );

    якщо предмет_методу == пусто {
      вернути НЕДІЙСНЕ();
    }

    вернути ПРЕДМЕТ(предмет_методу);
  }
}