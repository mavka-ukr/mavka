взяти означення мавка/машина;

простір мавка {
  місцева дія записати_метод_структури_ПеребірДіапазонуЧисел__далі(М: адреса<Машина>);

  місцева дія заповнити_структуру_ПеребірДіапазонуЧисел(
    М: адреса<Машина>
  ) {
    М.предмет_структури_ПеребірДіапазонуЧисел.заборонити_наслідування = так;

    записати_метод_структури_ПеребірДіапазонуЧисел__далі(М);
  }

  місцева дія створити_перебір_діапазону_чисел(
    М: адреса<Машина>,
    предмет_діапазону: адреса<ПредметДіапазонуЧисел>
  ) -> адреса<ПредметПереборуДіапазонуЧисел> {
    змінна предмет = виділити_предмет<ПредметПереборуДіапазонуЧисел>(М, М.предмет_структури_ПеребірДіапазонуЧисел);

    предмет.предмет_діапазону = предмет_діапазону;
    предмет.почато = ні;
    предмет.поточне_значення = 0.0;

    вернути предмет;
  }

  місцева дія предмет_перебору_діапазону_чисел_отримати_властивість(
    М: адреса<Машина>,
    предмет_перебору_діапазону_чисел: адреса<ПредметПереборуДіапазонуЧисел>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    якщо назва == М.константи_назв._значення або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._значення) {
      вернути ЧИСЛО(предмет_перебору_діапазону_чисел.поточне_значення);
    }

    якщо назва == М.константи_назв._діапазон або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._діапазон) {
      вернути ПРЕДМЕТ(М, предмет_перебору_діапазону_чисел.предмет_діапазону);
    }

    змінна предмет_методу = знайти_метод_в_структурі(
      М,
      М.предмет_структури_ПеребірДіапазонуЧисел,
      назва
    );

    якщо предмет_методу == пусто {
      вернути НЕДІЙСНЕ;
    }

    вернути ПРЕДМЕТ(М, предмет_методу);
  }
}