взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_метода_структури_ПеребірДіапазонуЧисел__далі(
    М: адреса<Машина>,
    діяльність: адреса<Діяльність>
  ) -> ЗначенняАбоСтанПадіння {
    якщо діяльність.здійснювач.вид != ЗПредмет {
      вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію перебору діапазону чисел \"далі\"."));
    }

    змінна предмет_перебору_діапазону_чисел = діяльність.здійснювач.дані як адреса<Предмет> як адреса<ПредметПереборуДіапазонуЧисел>;

    якщо предмет_перебору_діапазону_чисел.тип != М.предмет_структури_ПеребірДіапазонуЧисел {
      вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію перебору діапазону чисел \"далі\"."));
    }

    якщо предмет_перебору_діапазону_чисел.почато {
      предмет_перебору_діапазону_чисел.поточне_значення = предмет_перебору_діапазону_чисел.поточне_значення + 1.0;
    } інакше {
      предмет_перебору_діапазону_чисел.поточне_значення = предмет_перебору_діапазону_чисел.предмет_діапазону.від;
      предмет_перебору_діапазону_чисел.почато = так;
    }

    якщо предмет_перебору_діапазону_чисел.предмет_діапазону.включно {
      якщо предмет_перебору_діапазону_чисел.поточне_значення <= предмет_перебору_діапазону_чисел.предмет_діапазону.до {
        вернути ДІЙСНЕ;
      }
    } інакше {
      якщо предмет_перебору_діапазону_чисел.поточне_значення < предмет_перебору_діапазону_чисел.предмет_діапазону.до {
        вернути ДІЙСНЕ;
      }
    }

    вернути НЕДІЙСНЕ;
  }

  місцева дія записати_метод_структури_ПеребірДіапазонуЧисел__далі(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._далі,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_метода_структури_ПеребірДіапазонуЧисел__далі,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_ПеребірДіапазонуЧисел,
      М.константи_назв._далі,
      предмет_дії
    );
  }
}