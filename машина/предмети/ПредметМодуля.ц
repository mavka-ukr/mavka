взяти означення мавка/машина;

простір мавка {
  місцева дія заповнити_структуру_Модуль(
    М: адреса<Машина>
  ) {
    М.предмет_структури_Модуль.заборонити_наслідування = так;
  }

  місцева дія створити_модуль(
    М: адреса<Машина>,
    назва: адреса<ПредметТексту>
  ) -> адреса<ПредметМодуля> {
    змінна предмет = виділити_предмет<ПредметМодуля>(М, М.предмет_структури_Модуль);

    предмет.готовий = ні;
    предмет.назва = назва;
    предмет.властивості = Властивості { 0, пусто };

    вернути предмет;
  }

  місцева дія предмет_модуля_отримати_властивість(
    М: адреса<Машина>,
    предмет_модуля: адреса<ПредметМодуля>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    змінна п: природне = 0;

    поки п < предмет_модуля.властивості.розмір {
      змінна властивість = предмет_модуля.властивості.дані[п];

      якщо властивість.назва == назва або перевірити_чи_предмети_тексту_рівні(М, властивість.назва, назва) {
        вернути властивість.значення;
      }

      п += 1;
    }

    змінна предмет_методу = знайти_метод_в_структурі(
      М,
      М.предмет_структури_Модуль,
      назва
    );

    якщо предмет_методу == пусто {
      вернути значення_недійсне();
    }

    вернути значення_предмет(предмет_методу);
  }

  місцева дія предмет_модуля_змінити_властивість(
    М: адреса<Машина>,
    предмет_модуля: адреса<ПредметМодуля>,
    назва: адреса<ПредметТексту>,
    значення: Значення,
    місцезнаходження: Місцезнаходження
  ) {
    змінна властивості = предмет_модуля.властивості::адреса;

    змінна п: природне = 0;
    поки п < властивості.розмір {
      змінна властивість = властивості.дані[п]::адреса;

      якщо перевірити_чи_значення_рівні(М, значення_предмет(властивість.назва), значення_предмет(назва)) {
        властивість.значення = значення;

        вернути;
      }

      п += 1;
    }

    властивості.дані = перевиділити_памʼять<Властивість>(М, властивості.дані, властивості.розмір + 1);
    властивості.дані[властивості.розмір] = Властивість { назва, значення };
    властивості.розмір += 1;
  }
}