взяти означення мавка/машина;

простір мавка {
  місцева дія записати_метод_структури_ПеребірСписку__далі(М: адреса<Машина>);

  місцева дія заповнити_структуру_ПеребірСписку(
    М: адреса<Машина>
  ) {
    М.предмет_структури_ПеребірСписку.заборонити_наслідування = так;

    записати_метод_структури_ПеребірСписку__далі(М);
  }

  місцева дія створити_перебір_списку(
    М: адреса<Машина>,
    предмет_списку: адреса<ПредметСписку>
  ) -> адреса<ПредметПереборуСписку> {
    змінна предмет = виділити_предмет<ПредметПереборуСписку>(М, М.предмет_структури_ПеребірСписку);

    предмет.предмет_списку = предмет_списку;
    предмет.почато = ні;
    предмет.позиція = 0;
    предмет.поточне_значення = НЕДІЙСНЕ;

    вернути предмет;
  }

  місцева дія предмет_перебору_списку_отримати_властивість(
    М: адреса<Машина>,
    предмет_перебору_списку: адреса<ПредметПереборуСписку>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    якщо назва == М.константи_назв._значення або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._значення) {
      вернути предмет_перебору_списку.поточне_значення;
    }

    якщо назва == М.константи_назв._список або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._список) {
      вернути ПРЕДМЕТ(М, предмет_перебору_списку.предмет_списку);
    }

    змінна предмет_методу = знайти_метод_в_структурі(
      М,
      М.предмет_структури_ПеребірСписку,
      назва
    );

    якщо предмет_методу == пусто {
      вернути НЕДІЙСНЕ;
    }

    вернути ПРЕДМЕТ(М, предмет_методу);
  }
}