взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_метода_структури_ПеребірСписку__далі(
    М: адреса<Машина>,
    здійснювач: Значення,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ЗначенняАбоСтанПадіння {
    якщо здійснювач.вид != ЗПредмет {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію перебору списку \"далі\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути значення_недійсне();
    }

    змінна предмет_перебору_списку = здійснювач.дані як адреса<Предмет> як адреса<ПредметПереборуСписку>;

    якщо предмет_перебору_списку.тип != М.предмет_структури_ПеребірСписку {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію перебору списку \"далі\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути значення_недійсне();
    }

    якщо предмет_перебору_списку.почато {
      предмет_перебору_списку.позиція += 1;
    } інакше {
      предмет_перебору_списку.позиція = 0;
      предмет_перебору_списку.почато = так;
    }

    якщо предмет_перебору_списку.позиція < предмет_перебору_списку.предмет_списку.розмір {
      предмет_перебору_списку.поточне_значення = предмет_перебору_списку.предмет_списку.елементи[предмет_перебору_списку.позиція];

      вернути значення_дійсне();
    }

    вернути значення_недійсне();
  }

  місцева дія записати_метод_структури_ПеребірСписку__далі(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._далі,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_метода_структури_ПеребірСписку__далі,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_ПеребірСписку,
      М.константи_назв._далі,
      предмет_дії
    );
  }
}