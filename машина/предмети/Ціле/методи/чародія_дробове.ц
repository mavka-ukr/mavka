взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_Ціле__чародія_дробове(
    М: адреса<Машина>,
    здійснювач: адреса<Предмет>,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ПредметАбоСтанПадіння {
    якщо здійснювач == пусто {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію цілого \"чародія_дробове\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна предмет_цілого = здійснювач як адреса<ПредметЦілого>;

    якщо предмет_цілого.тип != М.предмет_структури_Ціле {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію цілого \"чародія_дробове\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна дч_дробове: ДЧ::Дробове;

    якщо не ДЧ::ініціалізувати_дробове_з_цілого(
      М.виділяч_ДЧ,
      предмет_цілого.значення,
      дч_дробове::адреса
    ) {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію цілого \"чародія_дробове\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    вернути створити_дробове_без_копіювання(М, дч_дробове) як адреса<Предмет>;
  }

  місцева дія записати_метод_структури_Ціле__чародія_дробове(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._чародія_дробове,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_Ціле__чародія_дробове,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_Ціле,
      М.константи_назв._чародія_дробове,
      предмет_дії
    );

    якщо М.стан_падіння {
      запанікувати(М, "Помилка при записі дії \"чародія_дробове\" для структури \"Ціле\".");
    }
  }
}