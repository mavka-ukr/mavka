взяти означення машина_мавки;

простір мавка {
  дія рідна_дія_метода__далі(
    М: адреса<Машина>,
    здійснювач: Значення,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ЗначенняАбоСтанПадіння {
    якщо здійснювач.вид != ЗПредмет {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію перебору діапазону цілих \"далі\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути значення_недійсне();
    }

    змінна предмет_перебору_діапазону_цілих = здійснювач.дані як адреса<ПредметПереборуДіапазонуЦілих>;

    якщо предмет_перебору_діапазону_цілих.тип != М.предмет_структури_ПеребірДіапазонуЦілих {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію перебору діапазону цілих \"далі\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути значення_недійсне();
    }

    якщо предмет_перебору_діапазону_цілих.почато {
      предмет_перебору_діапазону_цілих.поточне_значення = предмет_перебору_діапазону_цілих.поточне_значення + 1;
    } інакше {
      предмет_перебору_діапазону_цілих.поточне_значення = предмет_перебору_діапазону_цілих.предмет_діапазону.від;
      предмет_перебору_діапазону_цілих.почато = так;
    }

    якщо предмет_перебору_діапазону_цілих.предмет_діапазону.включно {
      якщо предмет_перебору_діапазону_цілих.поточне_значення <= предмет_перебору_діапазону_цілих.предмет_діапазону.до {
        вернути значення_дійсне();
      }
    } інакше {
      якщо предмет_перебору_діапазону_цілих.поточне_значення < предмет_перебору_діапазону_цілих.предмет_діапазону.до {
        вернути значення_дійсне();
      }
    }

    вернути значення_недійсне();
  }

  дія записати_метод__далі(М: адреса<Машина>) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._далі,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_метода__далі,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_ПеребірДіапазонуЦілих,
      М.константи_назв._далі,
      предмет_дії
    );
  }
}

простір мавка {
  місцева дія заповнити_структуру_ПеребірДіапазонуЦілих(
    М: адреса<Машина>
  ) {
    М.предмет_структури_ПеребірДіапазонуЦілих.заборонити_наслідування = так;

    записати_метод__далі(М);
  }

  місцева дія створити_перебір_діапазону_цілих(
    М: адреса<Машина>,
    предмет_діапазону: адреса<ПредметДіапазонуЦілих>
  ) -> адреса<ПредметПереборуДіапазонуЦілих> {
    змінна предмет = виділити_предмет<ПредметПереборуДіапазонуЦілих>(М, М.предмет_структури_ПеребірДіапазонуЦілих);

    предмет.предмет_діапазону = предмет_діапазону;
    предмет.почато = ні;
    предмет.поточне_значення = 0;

    вернути предмет;
  }

  місцева дія предмет_перебору_діапазону_цілих_отримати_властивість(
    М: адреса<Машина>,
    предмет_перебору_діапазону_цілих: адреса<ПредметПереборуДіапазонуЦілих>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    якщо назва == М.константи_назв._значення або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._значення) {
      вернути значення_ціле(предмет_перебору_діапазону_цілих.поточне_значення);
    }

    змінна предмет_дії = знайти_метод_в_структурі(
      М,
      предмет_перебору_діапазону_цілих.тип,
      назва
    );

    якщо предмет_дії == пусто {
      вернути значення_недійсне();
    }

    вернути значення_предмет(предмет_дії);
  }
}