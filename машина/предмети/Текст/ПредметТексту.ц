взяти означення мавка/машина;

простір мавка {
  місцева дія записати_метод_структури_Текст__чародія_ціле(М: адреса<Машина>);
  місцева дія записати_метод_структури_Текст__чародія_дробове(М: адреса<Машина>);
  місцева дія записати_метод_структури_Текст__чародія_текст(М: адреса<Машина>);
  місцева дія записати_метод_структури_Текст__чародія_перебір(М: адреса<Машина>);

  місцева дія записати_дію_структури_Текст__з(М: адреса<Машина>);

  місцева дія заповнити_структуру_Текст(
    М: адреса<Машина>
  ) {
    М.предмет_структури_Текст.заборонити_наслідування = так;

    записати_метод_структури_Текст__чародія_ціле(М);
    записати_метод_структури_Текст__чародія_дробове(М);
    записати_метод_структури_Текст__чародія_текст(М);
    записати_метод_структури_Текст__чародія_перебір(М);

    записати_дію_структури_Текст__з(М);
  }

  місцева дія створити_текст(
    М: адреса<Машина>,
    значення: кд
  ) -> адреса<ПредметТексту> {
    змінна предмет = виділити_предмет<ПредметТексту>(М, М.предмет_структури_Текст);

    змінна копія_тексту = виділити_памʼять<п8>(М, значення.розмір);

    змінна п: природне = 0;
    поки п < значення.розмір {
      копія_тексту[п] = значення.дані[п];

      п += 1;
    }

    предмет.значення = кд { значення.розмір, копія_тексту };

    вернути предмет;
  }

  місцева дія створити_текст_без_копіювання(
    М: адреса<Машина>,
    розмір: природне,
    дані: памʼять<п8>
  ) -> адреса<ПредметТексту> {
    змінна предмет = виділити_предмет<ПредметТексту>(М, М.предмет_структури_Текст);

    предмет.значення = кд { розмір, дані };

    вернути предмет;
  }

  місцева дія перевірити_чи_предмети_тексту_рівні(
    М: адреса<Машина>,
    а: адреса<ПредметТексту>,
    б: адреса<ПредметТексту>
  ) -> логічне {
    якщо а.значення.розмір != б.значення.розмір {
      вернути ні;
    }

    змінна п: природне = 0;
    поки п < а.значення.розмір {
      якщо а.значення.дані[п] != б.значення.дані[п] {
        вернути ні;
      }

      п += 1;
    }

    вернути так;
  }

  місцева дія предмет_тексту_отримати_властивість(
    М: адреса<Машина>,
    предмет_тексту: адреса<ПредметТексту>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    якщо назва == М.константи_назв._розмір або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._розмір) {
      вернути значення_ціле(ц64(предмет_тексту.значення.розмір));
    }

    змінна предмет_методу = знайти_метод_в_структурі(
      М,
      М.предмет_структури_Текст,
      назва
    );

    якщо предмет_методу == пусто {
      вернути значення_недійсне();
    }

    вернути значення_предмет(предмет_методу);
  }

  місцева дія предмет_тексту_отримати_елемент(
    М: адреса<Машина>,
    предмет_тексту: адреса<ПредметТексту>,
    ключ: Значення,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    вернути значення_недійсне();
  }
}