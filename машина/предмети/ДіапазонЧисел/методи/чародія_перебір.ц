взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_ДіапазонЧисел__чародія_перебір(
    М: адреса<Машина>,
    діяльність: адреса<Діяльність>
  ) -> ЗначенняАбоСтанПадіння {
    якщо діяльність.здійснювач.вид != ЗМашиннийПредмет і діяльність.здійснювач.вид != ЗСкладенийПредмет {
      вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію діапазону чисел \"чародія_перебір\"."));
    }

    змінна предмет_діапазону_чисел = діяльність.здійснювач.дані як адреса<Предмет> як адреса<ПредметДіапазонуЧисел>;

    якщо предмет_діапазону_чисел.тип != М.предмет_структури_ДіапазонЧисел {
      вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію діапазону чисел \"чародія_перебір\"."));
    }

    вернути МАШИННИЙ_ПРЕДМЕТ(створити_перебір_діапазону_чисел(
      М,
      предмет_діапазону_чисел
    ));
  }

  місцева дія записати_метод_структури_ДіапазонЧисел__чародія_перебір(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._чародія_перебір,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_ДіапазонЧисел__чародія_перебір,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_ДіапазонЧисел,
      М.константи_назв._чародія_перебір,
      предмет_дії
    );

    якщо М.стан_падіння {
      ЗАПАНІКУВАТИ(М, "Помилка при записі дії \"чародія_перебір\" для структури \"ДіапазонЧисел\".");
    }
  }
}