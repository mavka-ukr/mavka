взяти означення мавка/машина;

простір мавка {
  місцева дія записати_метод_структури_Послідовність__чародія_перебір(М: адреса<Машина>);

  місцева дія заповнити_структуру_Послідовність(
    М: адреса<Машина>
  ) {
    М.предмет_структури_Послідовність.заборонити_наслідування = так;

    записати_метод_структури_Послідовність__чародія_перебір(М);
  }

  місцева дія створити_послідовність(
    М: адреса<Машина>,
    розмір: природне,
    елементи: памʼять<адреса<Предмет>>
  ) -> адреса<ПредметПослідовності> {
    змінна копія_елементів = виділити_памʼять<адреса<Предмет>>(М, розмір);

    змінна п: природне = 0;
    поки п < розмір {
      копія_елементів[п] = елементи[п];
      п += 1;
    }

    змінна послідовність = виділити_предмет<ПредметПослідовності>(М, М.предмет_структури_Послідовність);

    послідовність.розмір = розмір;
    послідовність.елементи = копія_елементів;

    вернути послідовність;
  }

  місцева дія створити_послідовність_без_копіювання(
    М: адреса<Машина>,
    розмір: природне,
    елементи: памʼять<адреса<Предмет>>
  ) -> адреса<ПредметПослідовності> {
    змінна послідовність = виділити_предмет<ПредметПослідовності>(М, М.предмет_структури_Послідовність);

    послідовність.розмір = розмір;
    послідовність.елементи = елементи;

    вернути послідовність;
  }

  місцева дія створити_сиру_послідовність(
    М: адреса<Машина>,
    розмір: природне
  ) -> адреса<ПредметПослідовності> {
    змінна послідовність = виділити_предмет<ПредметПослідовності>(М, М.предмет_структури_Послідовність);

    послідовність.розмір = розмір;
    послідовність.елементи = виділити_памʼять<адреса<Предмет>>(М, розмір);

    вернути послідовність;
  }

  місцева дія предмет_послідовності_отримати_властивість(
    М: адреса<Машина>,
    предмет_послідовності: адреса<ПредметПослідовності>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо назва == М.константи_назв._розмір або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._розмір) {
      вернути створити_ціле_з_ц64(М, ц64(предмет_послідовності.розмір)) як адреса<Предмет>;
    }

    змінна предмет_методу = знайти_метод_в_структурі(
      М,
      М.предмет_структури_Послідовність,
      назва
    );

    якщо предмет_методу == пусто {
      вернути пусто;
    }

    вернути предмет_методу як адреса<Предмет>;
  }

  місцева дія предмет_послідовності_отримати_елемент(
    М: адреса<Машина>,
    предмет_послідовності: адреса<ПредметПослідовності>,
    ключ: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    вернути пусто;
  }
}