взяти означення мавка/машина;

простір мавка {
  місцева дія записати_метод_структури_Список__чародія_текст(М: адреса<Машина>);
  місцева дія записати_метод_структури_Список__чародія_перебір(М: адреса<Машина>);
  місцева дія записати_метод_структури_Список__додати(М: адреса<Машина>);
  місцева дія записати_метод_структури_Список__забрати(М: адреса<Машина>);
  місцева дія записати_метод_структури_Список__знайти(М: адреса<Машина>);
  місцева дія записати_метод_структури_Список__знайти_позицію(М: адреса<Машина>);
  місцева дія записати_метод_структури_Список__фільтрований(М: адреса<Машина>);
  місцева дія записати_метод_структури_Список__перетворений(М: адреса<Машина>);
  місцева дія записати_метод_структури_Список__обернений(М: адреса<Машина>);
  місцева дія записати_метод_структури_Список__містить(М: адреса<Машина>);

  місцева дія заповнити_структуру_Список(
    М: адреса<Машина>
  ) {
    М.предмет_структури_Список.заборонити_наслідування = так;

    записати_метод_структури_Список__чародія_текст(М);
    записати_метод_структури_Список__чародія_перебір(М);
    записати_метод_структури_Список__додати(М);
    записати_метод_структури_Список__забрати(М);
    записати_метод_структури_Список__знайти(М);
    записати_метод_структури_Список__знайти_позицію(М);
    записати_метод_структури_Список__фільтрований(М);
    записати_метод_структури_Список__перетворений(М);
    записати_метод_структури_Список__обернений(М);
    записати_метод_структури_Список__містить(М);
  }

  місцева дія створити_список(
    М: адреса<Машина>,
    розмір: природне
  ) -> адреса<ПредметСписку> {
    змінна предмет = виділити_предмет<ПредметСписку>(М, М.предмет_структури_Список);

    змінна дані: памʼять<Значення> = пусто;

    якщо розмір > 0 {
      дані = виділити_памʼять<Значення>(М, розмір);
      змінна п: природне = 0;

      поки п < розмір {
        дані[п] = значення_недійсне();

        п += 1;
      }
    }

    предмет.розмір = розмір;
    предмет.елементи = дані;

    вернути предмет;
  }

  місцева дія записати_в_список(
    М: адреса<Машина>,
    предмет_списку: адреса<ПредметСписку>,
    позиція: природне,
    значення: Значення
  ) {
    якщо позиція >= предмет_списку.розмір {
      предмет_списку.елементи = перевиділити_памʼять<Значення>(М, предмет_списку.елементи, позиція + 1);

      змінна п = предмет_списку.розмір;

      поки п < позиція + 1 {
        предмет_списку.елементи[п] = значення_недійсне();

        п += 1;
      }

      предмет_списку.розмір = позиція + 1;
    }

    предмет_списку.елементи[позиція] = значення;
  }

  місцева дія предмет_списку_отримати_властивість(
    М: адреса<Машина>,
    предмет_списку: адреса<ПредметСписку>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    якщо назва == М.константи_назв._розмір або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._розмір) {
      вернути значення_ціле(ц64(предмет_списку.розмір));
    }

    змінна предмет_методу = знайти_метод_в_структурі(
      М,
      М.предмет_структури_Список,
      назва
    );

    якщо предмет_методу == пусто {
      вернути значення_недійсне();
    }

    вернути значення_предмет(предмет_методу);
  }

  місцева дія предмет_списку_отримати_елемент(
    М: адреса<Машина>,
    предмет_списку: адреса<ПредметСписку>,
    ключ: Значення,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    якщо ключ.вид == ЗЦіле {
      змінна позиція = природне(ключ.дані як ц64);

      якщо позиція >= 0 і позиція < предмет_списку.розмір {
        вернути предмет_списку.елементи[позиція];
      }
    }

    вернути значення_недійсне();
  }

  місцева дія предмет_списку_змінити_елемент(
    М: адреса<Машина>,
    предмет_списку: адреса<ПредметСписку>,
    ключ: Значення,
    значення_елемента: Значення,
    місцезнаходження: Місцезнаходження
  ) -> СтанПадіння {
    якщо ключ.вид == ЗЦіле {
      змінна позиція = ключ.дані як ц64;

      якщо позиція >= 0 {
        записати_в_список(М, предмет_списку, природне(позиція), значення_елемента);
      }
    }
  }
}