взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_Список__фільтрований(
    М: адреса<Машина>,
    здійснювач: Значення,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ЗначенняАбоСтанПадіння {
    якщо здійснювач.вид != ЗПредмет {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію списку \"фільтрований\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути значення_недійсне();
    }

    змінна предмет_списку = здійснювач.дані як адреса<Предмет> як адреса<ПредметСписку>;

    якщо предмет_списку.тип != М.предмет_структури_Список {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію списку \"фільтрований\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути значення_недійсне();
    }

    змінна задіяне_дія_перевірки = значення_недійсне();

    знайти_задіяне(М, задіяні, 0, М.константи_назв._дія_перевірки, задіяне_дія_перевірки::адреса);

    змінна новий_список = створити_список(М, 0);
    змінна притриманий_список = притримати_глобально(М, значення_предмет(новий_список));

    змінна п: природне = 0;

    поки п < предмет_списку.розмір {
      змінна поточне_значення = предмет_списку.елементи[п];

      змінна задіяні = Задіяні { 3, виділити_памʼять<Значення>(М, 3), пусто };
      задіяні.дані[0] = поточне_значення;
      задіяні.дані[1] = значення_ціле(ц64(п));
      задіяні.дані[2] = значення_предмет(предмет_списку);

      змінна результат_здійснення = здійснити(
        М,
        значення_недійсне(),
        задіяне_дія_перевірки,
        задіяні,
        Місцезнаходження { пусто, 0 }
      );

      якщо М.стан_падіння {
        відпустити_притримане_глобально(М, притриманий_список);

        вернути значення_недійсне();
      }

      якщо результат_здійснення.вид != ЗНедійсне {
        записати_в_список(
          М,
          новий_список,
          новий_список.розмір,
          поточне_значення
        );
      }

      п += 1;
    }

    відпустити_притримане_глобально(М, притриманий_список);

    вернути значення_предмет(новий_список);
  }

  місцева дія записати_метод_структури_Список__фільтрований(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._фільтрований,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_Список__фільтрований,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_Список,
      М.константи_назв._фільтрований,
      предмет_дії
    );

    якщо М.стан_падіння {
      запанікувати(М, "Помилка при записі дії \"фільтрований\" для структури \"Список\".");
    }
  }
}