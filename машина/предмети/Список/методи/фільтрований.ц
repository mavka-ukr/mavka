взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_Список__фільтрований(
    М: адреса<Машина>,
    діяльність: адреса<Діяльність>
  ) -> ЗначенняАбоСтанПадіння {
    якщо діяльність.здійснювач.вид != ЗМашиннийПредмет і діяльність.здійснювач.вид != ЗСкладенийПредмет {
      вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію списку \"фільтрований\"."));
    }

    змінна предмет_списку = діяльність.здійснювач.дані як адреса<Предмет> як адреса<ПредметСписку>;

    якщо предмет_списку.тип != М.предмет_структури_Список {
      вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію списку \"фільтрований\"."));
    }

    змінна задіяне_дія_перевірки = НЕДІЙСНЕ;

    знайти_задіяне(М, діяльність.задіяні, 0, М.константи_назв._дія_перевірки, задіяне_дія_перевірки::адреса);

    змінна новий_список = створити_список(М, 0);
    змінна притриманий_список = притримати_глобально(М, новий_список як адреса<Предмет>);

    змінна п: природне = 0;

    поки п < предмет_списку.розмір {
      змінна поточне_значення = предмет_списку.елементи[п];

      змінна задіяні = Задіяні { 3, виділити_памʼять<Значення>(М, 3), 0, пусто };
      задіяні.дані[0] = поточне_значення;
      задіяні.дані[1] = ЧИСЛО(д64(п));
      задіяні.дані[2] = МАШИННИЙ_ПРЕДМЕТ(предмет_списку);

      змінна результат_здійснення = здійснити(
        М,
        НЕДІЙСНЕ,
        задіяне_дія_перевірки,
        задіяні,
        Місцезнаходження { пусто, 0 }
      );

      якщо М.стан_падіння {
        відпустити_притриманий_глобально(М, притриманий_список);

        вернути НЕДІЙСНЕ;
      }

      якщо результат_здійснення.вид != ЗНедійсне {
        записати_в_список(
          М,
          новий_список,
          новий_список.розмір,
          поточне_значення
        );
      }

      п += 1;
    }

    відпустити_притриманий_глобально(М, притриманий_список);

    вернути МАШИННИЙ_ПРЕДМЕТ(новий_список);
  }

  місцева дія записати_метод_структури_Список__фільтрований(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._фільтрований,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_Список__фільтрований,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_Список,
      М.константи_назв._фільтрований,
      предмет_дії
    );

    якщо М.стан_падіння {
      ЗАПАНІКУВАТИ(М, "Помилка при записі дії \"фільтрований\" для структури \"Список\".");
    }
  }
}