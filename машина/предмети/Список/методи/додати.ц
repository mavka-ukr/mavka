взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_Список__додати(
    М: адреса<Машина>,
    здійснювач: адреса<Предмет>,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ПредметАбоСтанПадіння {
    якщо здійснювач == пусто {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію списку \"додати\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна предмет_списку = здійснювач як адреса<ПредметСписку>;

    якщо предмет_списку.тип != М.предмет_структури_Список {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію списку \"додати\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна задіяне_значення: адреса<Предмет> = пусто;

    знайти_задіяне(М, задіяні, 0, М.константи_назв._значення, задіяне_значення::адреса);

    записати_в_список(
      М,
      предмет_списку,
      предмет_списку.розмір,
      задіяне_значення
    );

    вернути створити_ціле_з_ц64(М, ц64(предмет_списку.розмір)) як адреса<Предмет>;
  }

  місцева дія записати_метод_структури_Список__додати(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._додати,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_Список__додати,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_Список,
      М.константи_назв._додати,
      предмет_дії
    );

    якщо М.стан_падіння {
      запанікувати(М, "Помилка при записі дії \"додати\" для структури \"Список\".");
    }
  }
}