взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_Список__додати(
    М: адреса<Машина>,
    діяльність: адреса<Діяльність>
  ) -> ЗначенняАбоСтанПадіння {
    якщо діяльність.здійснювач.вид != ЗПредмет {
      вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію списку \"додати\"."));
    }

    змінна предмет_списку = діяльність.здійснювач.дані як адреса<Предмет> як адреса<ПредметСписку>;

    якщо предмет_списку.тип != М.предмет_структури_Список {
      вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію списку \"додати\"."));
    }

    змінна задіяне_значення = НЕДІЙСНЕ();

    знайти_задіяне(М, діяльність.задіяні, 0, М.константи_назв._значення, задіяне_значення::адреса);

    записати_в_список(
      М,
      предмет_списку,
      предмет_списку.розмір,
      задіяне_значення
    );

    вернути ЧИСЛО(д64(предмет_списку.розмір));
  }

  місцева дія записати_метод_структури_Список__додати(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._додати,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_Список__додати,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_Список,
      М.константи_назв._додати,
      предмет_дії
    );

    якщо М.стан_падіння {
      ЗАПАНІКУВАТИ(М, "Помилка при записі дії \"додати\" для структури \"Список\".");
    }
  }
}