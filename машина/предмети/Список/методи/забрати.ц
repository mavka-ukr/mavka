взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_Список__забрати(
    М: адреса<Машина>,
    здійснювач: адреса<Предмет>,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ПредметАбоСтанПадіння {
    якщо здійснювач == пусто {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію списку \"забрати\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна предмет_списку = здійснювач як адреса<ПредметСписку>;

    якщо предмет_списку.тип != М.предмет_структури_Список {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію списку \"забрати\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    якщо предмет_списку.розмір == 0 {
      вернути пусто;
    }

    предмет_списку.розмір -= 1;

    вернути предмет_списку.елементи[предмет_списку.розмір];
  }

  місцева дія записати_метод_структури_Список__забрати(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._забрати,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_Список__забрати,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_Список,
      М.константи_назв._забрати,
      предмет_дії
    );

    якщо М.стан_падіння {
      запанікувати(М, "Помилка при записі дії \"забрати\" для структури \"Список\".");
    }
  }
}