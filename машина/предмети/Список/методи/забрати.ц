взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_Список__забрати(
    М: адреса<Машина>,
    діяльність: адреса<Діяльність>
  ) -> ЗначенняАбоСтанПадіння {
    якщо діяльність.здійснювач.вид != ЗМашиннийПредмет і діяльність.здійснювач.вид != ЗСкладенийПредмет {
      вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію списку \"забрати\"."));
    }

    змінна предмет_списку = діяльність.здійснювач.дані як адреса<Предмет> як адреса<ПредметСписку>;

    якщо предмет_списку.тип != М.предмет_структури_Список {
      вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію списку \"забрати\"."));
    }

    якщо предмет_списку.розмір == 0 {
      вернути НЕДІЙСНЕ;
    }

    предмет_списку.розмір -= 1;

    вернути предмет_списку.елементи[предмет_списку.розмір];
  }

  місцева дія записати_метод_структури_Список__забрати(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._забрати,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_Список__забрати,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_Список,
      М.константи_назв._забрати,
      предмет_дії
    );

    якщо М.стан_падіння {
      ЗАПАНІКУВАТИ(М, "Помилка при записі дії \"забрати\" для структури \"Список\".");
    }
  }
}