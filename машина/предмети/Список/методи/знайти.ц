взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_Список__знайти(
    М: адреса<Машина>,
    здійснювач: адреса<Предмет>,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ПредметАбоСтанПадіння {
    якщо здійснювач == пусто {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію списку \"знайти\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна предмет_списку = здійснювач як адреса<ПредметСписку>;

    якщо предмет_списку.тип != М.предмет_структури_Список {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію списку \"знайти\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна задіяне_дія_перевірки: адреса<Предмет> = пусто;

    знайти_задіяне(М, задіяні, 0, М.константи_назв._дія_перевірки, задіяне_дія_перевірки::адреса);

    змінна п: природне = 0;

    поки п < предмет_списку.розмір {
      змінна поточне_значення = предмет_списку.елементи[п];

      змінна задіяні = Задіяні { 3, виділити_памʼять<адреса<Предмет>>(М, 3), пусто };
      задіяні.дані[0] = поточне_значення;
      задіяні.дані[1] = створити_ціле_з_ц64(М, ц64(п)) як адреса<Предмет>;
      задіяні.дані[2] = предмет_списку як адреса<Предмет>;

      змінна результат_здійснення = здійснити(
        М,
        пусто,
        задіяне_дія_перевірки,
        задіяні,
        Місцезнаходження { пусто, 0 }
      );

      якщо М.стан_падіння {
        вернути пусто;
      }

      якщо результат_здійснення != пусто {
        вернути поточне_значення;
      }

      п += 1;
    }

    вернути пусто;
  }

  місцева дія записати_метод_структури_Список__знайти(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._знайти,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_Список__знайти,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_Список,
      М.константи_назв._знайти,
      предмет_дії
    );

    якщо М.стан_падіння {
      запанікувати(М, "Помилка при записі дії \"знайти\" для структури \"Список\".");
    }
  }
}