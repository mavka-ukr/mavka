взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_Список__знайти(
    М: адреса<Машина>,
    діяльність: адреса<Діяльність>
  ) -> ЗначенняАбоСтанПадіння {
    якщо діяльність.здійснювач.вид != ЗПредмет {
      вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію списку \"знайти\"."));
    }

    змінна предмет_списку = діяльність.здійснювач.дані як адреса<Предмет> як адреса<ПредметСписку>;

    якщо предмет_списку.тип != М.предмет_структури_Список {
      вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію списку \"знайти\"."));
    }

    змінна задіяне_дія_перевірки = НЕДІЙСНЕ();

    знайти_задіяне(М, діяльність.задіяні, 0, М.константи_назв._дія_перевірки, задіяне_дія_перевірки::адреса);

    змінна п: природне = 0;

    поки п < предмет_списку.розмір {
      змінна поточне_значення = предмет_списку.елементи[п];

      змінна задіяні = Задіяні { 3, виділити_памʼять<Значення>(М, 3), 0, пусто };
      задіяні.дані[0] = поточне_значення;
      задіяні.дані[1] = ЧИСЛО(д64(п));
      задіяні.дані[2] = ПРЕДМЕТ(предмет_списку);

      змінна результат_здійснення = здійснити(
        М,
        НЕДІЙСНЕ(),
        задіяне_дія_перевірки,
        задіяні,
        Місцезнаходження { пусто, 0 }
      );

      якщо М.стан_падіння {
        вернути НЕДІЙСНЕ();
      }

      якщо результат_здійснення.вид != ЗНедійсне {
        вернути поточне_значення;
      }

      п += 1;
    }

    вернути НЕДІЙСНЕ();
  }

  місцева дія записати_метод_структури_Список__знайти(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._знайти,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_Список__знайти,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_Список,
      М.константи_назв._знайти,
      предмет_дії
    );

    якщо М.стан_падіння {
      ЗАПАНІКУВАТИ(М, "Помилка при записі дії \"знайти\" для структури \"Список\".");
    }
  }
}