взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_Список__знайти_позицію(
    М: адреса<Машина>,
    здійснювач: Значення,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ЗначенняАбоСтанПадіння {
    якщо здійснювач.вид != ЗПредмет {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію списку \"знайти_позицію\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути значення_недійсне();
    }

    змінна предмет_списку = здійснювач.дані як адреса<Предмет> як адреса<ПредметСписку>;

    якщо предмет_списку.тип != М.предмет_структури_Список {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію списку \"знайти_позицію\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути значення_недійсне();
    }

    змінна задіяне_дія_перевірки = значення_недійсне();

    знайти_задіяне(М, задіяні, 0, М.константи_назв._дія_перевірки, задіяне_дія_перевірки::адреса);

    змінна п: природне = 0;

    поки п < предмет_списку.розмір {
      змінна поточне_значення = предмет_списку.елементи[п];

      змінна задіяні = Задіяні { 3, виділити_памʼять<Значення>(М, 3), пусто };
      задіяні.дані[0] = поточне_значення;
      задіяні.дані[1] = значення_ціле(ц64(п));
      задіяні.дані[2] = значення_предмет(предмет_списку);

      змінна результат_здійснення = здійснити(
        М,
        значення_недійсне(),
        задіяне_дія_перевірки,
        задіяні,
        Місцезнаходження { пусто, 0 }
      );

      якщо М.стан_падіння {
        вернути значення_недійсне();
      }

      якщо результат_здійснення.вид != ЗНедійсне {
        вернути значення_ціле(ц64(п));
      }

      п += 1;
    }

    вернути значення_недійсне();
  }

  місцева дія записати_метод_структури_Список__знайти_позицію(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._знайти_позицію,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_Список__знайти_позицію,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_Список,
      М.константи_назв._знайти_позицію,
      предмет_дії
    );

    якщо М.стан_падіння {
      запанікувати(М, "Помилка при записі дії \"знайти_позицію\" для структури \"Список\".");
    }
  }
}