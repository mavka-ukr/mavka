взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_Список__обернений(
    М: адреса<Машина>,
    здійснювач: адреса<Предмет>,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ПредметАбоСтанПадіння {
    якщо здійснювач == пусто {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію списку \"обернений\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна предмет_списку = здійснювач як адреса<ПредметСписку>;

    якщо предмет_списку.тип != М.предмет_структури_Список {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію списку \"обернений\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна новий_список = створити_список(М, предмет_списку.розмір);

    змінна п: природне = 0;

    поки п < предмет_списку.розмір {
      записати_в_список(
        М,
        новий_список,
        предмет_списку.розмір - п - 1,
        предмет_списку.елементи[п]
      );

      п += 1;
    }

    вернути новий_список як адреса<Предмет>;
  }

  місцева дія записати_метод_структури_Список__обернений(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._обернений,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_Список__обернений,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_Список,
      М.константи_назв._обернений,
      предмет_дії
    );

    якщо М.стан_падіння {
      запанікувати(М, "Помилка при записі дії \"обернений\" для структури \"Список\".");
    }
  }
}