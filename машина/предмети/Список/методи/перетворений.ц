взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_Список__перетворений(
    М: адреса<Машина>,
    діяльність: адреса<Діяльність>
  ) -> ЗначенняАбоСтанПадіння {
    якщо діяльність.здійснювач.вид != ЗПредмет {
      вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію списку \"перетворений\"."));
    }

    змінна предмет_списку = діяльність.здійснювач.дані як адреса<Предмет> як адреса<ПредметСписку>;

    якщо предмет_списку.тип != М.предмет_структури_Список {
      вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію списку \"перетворений\"."));
    }

    змінна задіяне_дія_перетворення = НЕДІЙСНЕ();

    знайти_задіяне(М, діяльність.задіяні, 0, М.константи_назв._дія_перетворення, задіяне_дія_перетворення::адреса);

    змінна новий_список = створити_список(М, предмет_списку.розмір);
    змінна притриманий_список = притримати_глобально(М, новий_список як адреса<Предмет>);

    змінна п: природне = 0;

    поки п < предмет_списку.розмір {
      змінна поточне_значення = предмет_списку.елементи[п];

      змінна задіяні = Задіяні { 3, виділити_памʼять<Значення>(М, 3), 0, пусто };
      задіяні.дані[0] = поточне_значення;
      задіяні.дані[1] = ЧИСЛО(д64(п));
      задіяні.дані[2] = ПРЕДМЕТ(предмет_списку);

      змінна результат_здійснення = здійснити(
        М,
        НЕДІЙСНЕ(),
        задіяне_дія_перетворення,
        задіяні,
        Місцезнаходження { пусто, 0 }
      );

      якщо М.стан_падіння {
        відпустити_притримане_глобально(М, притриманий_список);

        вернути НЕДІЙСНЕ();
      }

      записати_в_список(
        М,
        новий_список,
        п,
        результат_здійснення
      );

      п += 1;
    }

    відпустити_притримане_глобально(М, притриманий_список);

    вернути ПРЕДМЕТ(новий_список);
  }

  місцева дія записати_метод_структури_Список__перетворений(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._перетворений,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_Список__перетворений,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_Список,
      М.константи_назв._перетворений,
      предмет_дії
    );

    якщо М.стан_падіння {
      ЗАПАНІКУВАТИ(М, "Помилка при записі дії \"перетворений\" для структури \"Список\".");
    }
  }
}