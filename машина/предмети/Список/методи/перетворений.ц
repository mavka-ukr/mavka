взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_Список__перетворений(
    М: адреса<Машина>,
    здійснювач: адреса<Предмет>,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ПредметАбоСтанПадіння {
    якщо здійснювач == пусто {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію списку \"перетворений\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна предмет_списку = здійснювач як адреса<ПредметСписку>;

    якщо предмет_списку.тип != М.предмет_структури_Список {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити дію списку \"перетворений\".") як адреса<Предмет>,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна задіяне_дія_перетворення: адреса<Предмет> = пусто;

    знайти_задіяне(М, задіяні, 0, М.константи_назв._дія_перетворення, задіяне_дія_перетворення::адреса);

    змінна новий_список = створити_список(М, предмет_списку.розмір);
    змінна притриманий_список = притримати_глобально(М, новий_список як адреса<Предмет>);

    змінна п: природне = 0;

    поки п < предмет_списку.розмір {
      змінна поточне_значення = предмет_списку.елементи[п];

      змінна задіяні = Задіяні { 3, виділити_памʼять<адреса<Предмет>>(М, 3), пусто };
      задіяні.дані[0] = поточне_значення;
      задіяні.дані[1] = створити_ціле_з_ц64(М, ц64(п)) як адреса<Предмет>;
      задіяні.дані[2] = предмет_списку як адреса<Предмет>;

      змінна результат_здійснення = здійснити(
        М,
        пусто,
        задіяне_дія_перетворення,
        задіяні,
        Місцезнаходження { пусто, 0 }
      );

      якщо М.стан_падіння {
        відпустити_притримане_глобально(М, притриманий_список);

        вернути пусто;
      }

      записати_в_список(
        М,
        новий_список,
        п,
        результат_здійснення
      );

      п += 1;
    }

    відпустити_притримане_глобально(М, притриманий_список);

    вернути новий_список як адреса<Предмет>;
  }

  місцева дія записати_метод_структури_Список__перетворений(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._перетворений,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_Список__перетворений,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_Список,
      М.константи_назв._перетворений,
      предмет_дії
    );

    якщо М.стан_падіння {
      запанікувати(М, "Помилка при записі дії \"перетворений\" для структури \"Список\".");
    }
  }
}