взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_методу_структури_Список__містить(
    М: адреса<Машина>,
    діяльність: адреса<Діяльність>
  ) -> ЗначенняАбоСтанПадіння {
    якщо діяльність.здійснювач.вид != ЗМашиннийПредмет і діяльність.здійснювач.вид != ЗСкладенийПредмет {
      вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію списку \"містить\"."));
    }

    змінна предмет_списку = діяльність.здійснювач.дані як адреса<Предмет> як адреса<ПредметСписку>;

    якщо предмет_списку.тип != М.предмет_структури_Список {
      вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію списку \"містить\"."));
    }

    змінна задіяне_значення = НЕДІЙСНЕ;

    знайти_задіяне(М, діяльність.задіяні, 0, М.константи_назв._значення, задіяне_значення::адреса);

    змінна п: природне = 0;

    поки п < предмет_списку.розмір {
      якщо перевірити_чи_значення_рівні(М, предмет_списку.елементи[п], задіяне_значення) {
        вернути ДІЙСНЕ;
      }

      п += 1;
    }

    вернути НЕДІЙСНЕ;
  }

  місцева дія записати_метод_структури_Список__містить(
    М: адреса<Машина>
  ) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._містить,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_методу_структури_Список__містить,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_Список,
      М.константи_назв._містить,
      предмет_дії
    );

    якщо М.стан_падіння {
      ЗАПАНІКУВАТИ(М, "Помилка при записі дії \"містить\" для структури \"Список\".");
    }
  }
}