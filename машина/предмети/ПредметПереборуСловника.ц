взяти означення мавка/машина;

простір мавка {
  дія рідна_дія_метода_структури_ПеребірСловника__далі(
    М: адреса<Машина>,
    здійснювач: Значення,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ЗначенняАбоСтанПадіння {
    якщо здійснювач.вид != ЗПредмет {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію перебору словника \"далі\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути значення_недійсне();
    }

    змінна предмет_перебору_словника = здійснювач.дані як адреса<Предмет> як адреса<ПредметПереборуСловника>;

    якщо предмет_перебору_словника.тип != М.предмет_структури_ПеребірСловника {
      увімкнути_стан_падіння(
        М,
        значення_предмет(створити_текст(М, "Неможливо здійснити дію перебору словника \"далі\".")),
        Місцезнаходження { пусто, 0 }
      );
      вернути значення_недійсне();
    }

    якщо предмет_перебору_словника.почато {
      предмет_перебору_словника.позиція += 1;
    } інакше {
      предмет_перебору_словника.позиція = 0;
      предмет_перебору_словника.почато = так;
    }

    якщо предмет_перебору_словника.позиція < предмет_перебору_словника.предмет_словника.розмір {
      предмет_перебору_словника.поточне_значення = предмет_перебору_словника.предмет_словника.елементи[предмет_перебору_словника.позиція];

      вернути значення_дійсне();
    }

    вернути значення_недійсне();
  }

  дія записати_метод_структури_ПеребірСловника__далі(М: адреса<Машина>) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._далі,
      М.предмет_пустої_послідовності,
      пусто,
      рідна_дія_метода_структури_ПеребірСловника__далі,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_ПеребірСловника,
      М.константи_назв._далі,
      предмет_дії
    );
  }
}

простір мавка {
  місцева дія заповнити_структуру_ПеребірСловника(
    М: адреса<Машина>
  ) {
    М.предмет_структури_ПеребірСловника.заборонити_наслідування = так;

    записати_метод_структури_ПеребірСловника__далі(М);
  }

  місцева дія створити_перебір_словника(
    М: адреса<Машина>,
    предмет_словника: адреса<ПредметСловника>
  ) -> адреса<ПредметПереборуСловника> {
    змінна предмет = виділити_предмет<ПредметПереборуСловника>(М, М.предмет_структури_ПеребірСловника);

    предмет.предмет_словника = предмет_словника;
    предмет.почато = ні;
    предмет.позиція = 0;
    предмет.поточне_значення = значення_недійсне();

    вернути предмет;
  }

  місцева дія предмет_перебору_словника_отримати_властивість(
    М: адреса<Машина>,
    предмет_перебору_словника: адреса<ПредметПереборуСловника>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    якщо назва == М.константи_назв._значення або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._значення) {
      вернути предмет_перебору_словника.поточне_значення як Значення;
    }

    якщо назва == М.константи_назв._словник або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._словник) {
      вернути значення_предмет(предмет_перебору_словника.предмет_словника);
    }

    змінна предмет_методу = знайти_метод_в_структурі(
      М,
      М.предмет_структури_ПеребірСловника,
      назва
    );

    якщо предмет_методу == пусто {
      вернути значення_недійсне();
    }

    вернути значення_предмет(предмет_методу);
  }
}