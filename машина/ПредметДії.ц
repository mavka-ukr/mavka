взяти означення мавка;

простір мавка {
  місцева дія створити_дію(
    М: адреса<Машина>,
    дійсність_створення: адреса<Дійсність>,
    назва: адреса<ПредметТексту>,
    параметри: адреса<Послідовність>,
    тип_результату: Значення,
    код: адреса<ПредметКоду>,
    шлях_до_файлу_модуля: адреса<ПредметДаних>
  ) -> адреса<ПредметДії> {
    змінна предмет = виділити_предмет<ПредметДії>(М, М.предмет_структури_Дія);
    
    предмет.дійсність_створення = дійсність_створення;
    предмет.рідна = ні;
    предмет.назва = назва;
    предмет.параметри = параметри;
    предмет.код = код;
    предмет.тип_результату = тип_результату;
    предмет.шлях_до_файлу_модуля = шлях_до_файлу_модуля;

    вернути предмет;
  }

  місцева дія створити_рідну_дію(
    М: адреса<Машина>,
    назва: адреса<ПредметТексту>,
    параметри: адреса<Послідовність>,
    тип_результату: Значення,
    рідна_дія: РіднаДія,
    шлях_до_файлу_модуля: адреса<ПредметДаних>
  ) -> адреса<ПредметДії> {
    змінна предмет = виділити_предмет<ПредметДії>(М, М.предмет_структури_Дія);
   
    предмет.дійсність_створення = пусто;
    предмет.рідна = так;
    предмет.назва = назва;
    предмет.параметри = параметри;
    предмет.тип_результату = тип_результату;
    предмет.код = рідна_дія;
    предмет.шлях_до_файлу_модуля = шлях_до_файлу_модуля;

    вернути предмет;
  }

  дія обробити_стан_падіння(
    М: адреса<Машина>,
    дійсність: адреса<Дійсність>,
    код: адреса<ПредметКоду>,
    позиція_вказівки_падіння: природне,
    позиція: адреса<природне>
  ) -> логічне {
    якщо дійсність.спроби.розмір > 0 {
      дійсність.спроби.розмір -= 1;
      змінна спроба = дійсність.спроби.дані[дійсність.спроби.розмір];

      якщо спроба.назва_зловити != пусто {
        записати_в_дійсність(М, дійсність, спроба.назва_зловити, М.значення_падіння);
      }

      позиція::вміст = спроба.позиція_вказівки_зловити;
      М.історія_здійснення.розмір = спроба.розмір_історії_здійснення;
      М.накопичувач_значень.розмір = спроба.розмір_накопичувача_значень;
      
      М.стан_падіння = ні;
      М.значення_падіння = пусто;
      М.місцезнаходження_падіння = Місцезнаходження { пусто, 0 };

      вернути так;
    }
    
    якщо (М.місцезнаходження_падіння.шлях_або_код як адреса<ПредметКоду>) == пусто {
      М.місцезнаходження_падіння = Місцезнаходження { код, позиція_вказівки_падіння };
    }

    вернути ні;                     
  }

  дія прочитати_аргумент_вказівки(вказівки: адреса<ПредметДаних>, позиція: адреса<природне>) -> п32 {
    позиція::вміст += 1;
    змінна адреса_аргумента = вказівки.дані[позиція::вміст]::адреса як адреса<п32>;
    позиція::вміст += 3;
    вернути адреса_аргумента::вміст;
  }

  дія виконати_код(
    М: адреса<Машина>,
    дійсність: адреса<Дійсність>,
    шлях_до_файлу_поточного_модуля: адреса<ПредметДаних>,
    код: адреса<ПредметКоду>
  ) -> ЗначенняАбоСтанПадіння {
    змінна предмет_коду = код як адреса<ПредметКоду>;
    змінна константи = предмет_коду.константи;
    змінна вказівки = предмет_коду.вказівки;

    змінна результат_вернути: Значення = пусто;
    змінна результат: Значення = пусто;

    змінна позиція: природне = 0;
    змінна позиція_поточної_вказівки: природне = 0;
    змінна вказівка: п8 = 0;

    // надрукувати_ю8(М, ю8"Початок виконання коду");
    // надрукувати_д64(М, д64(вказівки.розмір));

    поки позиція < вказівки.розмір {
  крок_виконання_вказівки:
      позиція_поточної_вказівки = позиція;
      вказівка = вказівки.дані[позиція_поточної_вказівки];
      
      // надрукувати_ю8(М, ю8"Виконується вказівка: ");
      // надрукувати_д64(М, д64(вказівка));

      якщо вказівка == ВПокластиНедійсне {
        покласти_в_накопичувач_значень(М, пусто);
        
        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВПокластиДійсне {
        покласти_в_накопичувач_значень(М, М.значення_дійсне);
        
        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВПокластиПредмет {
        покласти_в_накопичувач_значень(М, М.значення_предмет);
        
        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВПокластиПослідовність {
        покласти_в_накопичувач_значень(М, М.значення_послідовність);
        
        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВПокластиЯ {
        покласти_в_накопичувач_значень(М, дійсність.здійснювач);
        
        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВПокластиКонстанту {
        змінна позиція_константи = прочитати_аргумент_вказівки(вказівки, позиція::адреса);

        покласти_в_накопичувач_значень(М, константи.дані[позиція_константи]);
        
        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВСтворитиДію {
        змінна кількість_параметрів = прочитати_аргумент_вказівки(вказівки, позиція::адреса);
        
        змінна код = забрати_з_накопичувача_значень(М) як адреса<ПредметКоду>;

        змінна параметри = створити_сиру_послідовність(М, природне(кількість_параметрів));
        
        змінна п: п32 = 0;
        поки п < кількість_параметрів {
          параметри.дані[кількість_параметрів - п - 1] = забрати_з_накопичувача_значень(М);

          п += 1;
        }
        
        змінна тип_результату = забрати_з_накопичувача_значень(М);
        змінна назва = забрати_з_накопичувача_значень(М) як адреса<ПредметТексту>;

        змінна предмет_дії = створити_дію(
          М,
          дійсність,
          назва,
          параметри,
          тип_результату,
          код,
          шлях_до_файлу_поточного_модуля
        );

        покласти_в_накопичувач_значень(М, предмет_дії як Значення);
        
        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВСтворитиСтруктуру {
        змінна кількість_параметрів = прочитати_аргумент_вказівки(вказівки, позиція::адреса);
                
        змінна параметри: адреса<Послідовність> = пусто;
      
        якщо кількість_параметрів > 0 {
          параметри = створити_сиру_послідовність(М, природне(кількість_параметрів));
          
          змінна п: п32 = 0;
          поки п < кількість_параметрів {
            параметри.дані[кількість_параметрів - п - 1] = забрати_з_накопичувача_значень(М);

            п += 1;
          }
        }

        змінна предок = забрати_з_накопичувача_значень(М) як адреса<ПредметСтруктури>;        
        змінна назва = забрати_з_накопичувача_значень(М) як адреса<ПредметТексту>;

        змінна предмет_структури = створити_структуру(М, назва, предок, параметри);

        покласти_в_накопичувач_значень(М, предмет_структури як Значення);

        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВСтворитиМодуль {
        змінна код = забрати_з_накопичувача_значень(М) як адреса<ПредметКоду>;
        змінна назва = забрати_з_накопичувача_значень(М) як адреса<ПредметТексту>;

        змінна предмет_модуля = створити_модуль(М, назва);

        змінна предмет_дії = створити_дію(
          М,
          дійсність,
          назва,
          пусто,
          пусто,
          код,
          шлях_до_файлу_поточного_модуля
        );

        змінна результат = здійснити(
          М,
          предмет_модуля як Значення,
          предмет_дії як Значення,
          0,
          пусто,
          пусто,
          Місцезнаходження { код, позиція_поточної_вказівки }
        );

        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, предмет_модуля як Значення);

        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВСтворитиСписок {
        змінна кількість_елементів = прочитати_аргумент_вказівки(вказівки, позиція::адреса);

        змінна значення_списку = створити_список(М, природне(кількість_елементів));
                
        змінна п: п32 = 0;
        поки п < кількість_елементів {
          записати_в_список(
            М,
            значення_списку,
            природне(кількість_елементів - п - 1),
            забрати_з_накопичувача_значень(М)
          );

          п += 1;
        }

        покласти_в_накопичувач_значень(М, значення_списку як Значення);

        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВСтворитиСловник {
        змінна кількість_елементів = прочитати_аргумент_вказівки(вказівки, позиція::адреса);
                
        змінна предмет_словника = створити_словник(М);

        змінна п: п32 = 0;
        поки п < кількість_елементів {
          змінна значення = забрати_з_накопичувача_значень(М);
          змінна ключ = забрати_з_накопичувача_значень(М);
          
          записати_в_словник(М, предмет_словника, ключ, значення);

          п += 1;
        }

        покласти_в_накопичувач_значень(М, предмет_словника як Значення);

        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВСтворитиПослідовність {
        змінна кількість_елементів = прочитати_аргумент_вказівки(вказівки, позиція::адреса);

        змінна послідовність = створити_сиру_послідовність(М, природне(кількість_елементів));
                
        змінна п: п32 = 0;
        поки п < кількість_елементів {
          послідовність.дані[кількість_елементів - п - 1] = забрати_з_накопичувача_значень(М);
        }

        покласти_в_накопичувач_значень(М, послідовність як Значення);

        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВСтворитиДіапазон {
        змінна до = забрати_з_накопичувача_значень(М);
        змінна включно = забрати_з_накопичувача_значень(М);
        змінна від = забрати_з_накопичувача_значень(М);

        змінна включно_логічне: логічне = ні;
        якщо включно != пусто {
          включно_логічне = так;
        }

        змінна предмет_діапазону = отримати_діапазон(
          М,
          від,
          до,
          включно_логічне,
          Місцезнаходження { код, позиція_поточної_вказівки }
        );
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, предмет_діапазону);

        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВСтворитиПараметр {
        змінна дія_значення = забрати_з_накопичувача_значень(М);
        змінна тип = забрати_з_накопичувача_значень(М);
        змінна назва = забрати_з_накопичувача_значень(М) як адреса<ПредметТексту>;

        змінна предмет_параметра = створити_параметр(М, назва, тип, дія_значення);
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, предмет_параметра як Значення);

        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВЗаписатиМетод {
        змінна значення_структури = забрати_з_накопичувача_значень(М) як адреса<ПредметСтруктури>;
        змінна значення_дії = забрати_з_накопичувача_значень(М) як адреса<ПредметДії>;
        змінна назва = забрати_з_накопичувача_значень(М) як адреса<ПредметТексту>;

        записати_метод_структури(М, значення_структури, назва, значення_дії);

        покласти_в_накопичувач_значень(М, значення_дії як Значення);

        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВВизначити {
        змінна значення = забрати_з_накопичувача_значень(М);
        змінна назва = забрати_з_накопичувача_значень(М) як адреса<ПредметТексту>;

        записати_в_дійсність(М, дійсність, назва, значення);

        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВВизначитиНавпаки {
        змінна назва = забрати_з_накопичувача_значень(М) як адреса<ПредметТексту>;
        змінна значення = забрати_з_накопичувача_значень(М);

        записати_в_дійсність(М, дійсність, назва, значення);

        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВЗвернутись {
        змінна назва = забрати_з_накопичувача_значень(М) як адреса<ПредметТексту>;

        змінна значення: Значення;
        якщо не знайти_в_дійсності(М, дійсність, назва, значення::адреса) {
          увімкнути_стан_падіння(
            М, 
            створити_текст(М, "Діяча з такою назвою не знайдено.") як Значення, 
            Місцезнаходження { код, позиція_поточної_вказівки }
          );
          
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }
  
          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, значення);

        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВЗдійснити {
        змінна кількість_задіяних = прочитати_аргумент_вказівки(вказівки, позиція::адреса);
        змінна кількість_іменованих_аргументів = прочитати_аргумент_вказівки(вказівки, позиція::адреса);

        змінна іменовано_задіяні: адреса<ІменованіЗадіяні> = пусто;
        змінна п: п32 = 0;

        якщо кількість_іменованих_аргументів > 0 {
          іменовано_задіяні = створити_іменовано_задіяні(М);
          
          поки п < кількість_іменованих_аргументів {
            змінна значення = забрати_з_накопичувача_значень(М);
            змінна назва = забрати_з_накопичувача_значень(М) як адреса<ПредметТексту>;

            покласти_іменовано_задіяне(М, іменовано_задіяні, назва, значення);

            п += 1;
          }
        }

        змінна дані_аргументів = виділити_памʼять<Значення>(М, природне(кількість_задіяних));

        п = 0;
        поки п < кількість_задіяних {
          дані_аргументів[кількість_задіяних - п - 1] = забрати_з_накопичувача_значень(М);

          п += 1;
        }
        
        змінна предмет_дії = забрати_з_накопичувача_значень(М);

        результат = здійснити(
          М,
          пусто, 
          предмет_дії,
          природне(кількість_задіяних), 
          дані_аргументів,
          іменовано_задіяні,
          Місцезнаходження { код, позиція_поточної_вказівки }
        );
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);

        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВОтриматиВластивість {
        змінна назва = забрати_з_накопичувача_значень(М) як адреса<ПредметТексту>;
        змінна предмет = забрати_з_накопичувача_значень(М);

        результат = отримати_властивість(М, предмет, назва, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);

        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВЗмінитиВластивість {
        змінна значення = забрати_з_накопичувача_значень(М);
        змінна назва = забрати_з_накопичувача_значень(М) як адреса<ПредметТексту>;
        змінна предмет = забрати_з_накопичувача_значень(М);

        змінити_властивість(
          М,
          предмет,
          назва,
          значення,
          Місцезнаходження { код, позиція_поточної_вказівки }
        );
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВЗдійснитиВластивість {
        змінна кількість_задіяних = прочитати_аргумент_вказівки(вказівки, позиція::адреса);
        змінна кількість_іменованих_аргументів = прочитати_аргумент_вказівки(вказівки, позиція::адреса);

        змінна іменовано_задіяні: адреса<ІменованіЗадіяні> = пусто;
        змінна п: п32 = 0;

        якщо кількість_іменованих_аргументів > 0 {
          іменовано_задіяні = створити_іменовано_задіяні(М);
          
          поки п < кількість_іменованих_аргументів {
            змінна значення = забрати_з_накопичувача_значень(М);
            змінна назва = забрати_з_накопичувача_значень(М) як адреса<ПредметТексту>;

            покласти_іменовано_задіяне(М, іменовано_задіяні, назва, значення);

            п += 1;
          }
        }

        змінна дані_аргументів = виділити_памʼять<Значення>(М, природне(кількість_задіяних));

        п = 0;
        поки п < кількість_задіяних {
          дані_аргументів[кількість_задіяних - п - 1] = забрати_з_накопичувача_значень(М);

          п += 1;
        }

        змінна назва = забрати_з_накопичувача_значень(М) як адреса<ПредметТексту>;
        змінна предмет = забрати_з_накопичувача_значень(М);

        результат = здійснити_властивість(
          М, 
          предмет, 
          назва, 
          природне(кількість_задіяних), 
          дані_аргументів, 
          іменовано_задіяні,
          Місцезнаходження { код, позиція_поточної_вказівки }
        );
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);

        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВОтриматиЕлемент {
        змінна ключ = забрати_з_накопичувача_значень(М);
        змінна предмет = забрати_з_накопичувача_значень(М);

        змінна значення = отримати_елемент(М, предмет, ключ, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, значення);

        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВЗмінитиЕлемент {
        змінна значення = забрати_з_накопичувача_значень(М);
        змінна ключ = забрати_з_накопичувача_значень(М);
        змінна предмет = забрати_з_накопичувача_значень(М);
        
        змінити_елемент(М, предмет, ключ, значення, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВОтриматиПеребір {
        змінна значення = забрати_з_накопичувача_значень(М);

        результат = отримати_перебір(М, значення, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);

        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВДодати {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_додати(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВВідняти {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_відняти(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВПомножити {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_помножити(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВПоділити {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_поділити(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВОтриматиОстачуДілення {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_остача(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВОтриматиНеповнуЧасткуДілення {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_частка(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВПіднестиДоСтепеня {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_степінь(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВЗсунутиВліво {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_вліво(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВЗсунутиВправо {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_вправо(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВДвійковеІ {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_двійкове_і(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВДвійковеАБО {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_двійкове_або(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВДвійковеВиключнеАБО {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_двійкове_вабо(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВМенше {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_менше(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВБільше {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_більше(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВНеБільше {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_більше(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        якщо результат == пусто {
          покласти_в_накопичувач_значень(М, М.значення_дійсне);
        } інакше {
          покласти_в_накопичувач_значень(М, пусто);
        }

        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВНеМенше {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_менше(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        якщо результат == пусто {
          покласти_в_накопичувач_значень(М, М.значення_дійсне);
        } інакше {
          покласти_в_накопичувач_значень(М, пусто);
        }
        
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВРівно {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_рівно(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВНеРівно {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_рівно(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        якщо результат == пусто {
          покласти_в_накопичувач_значень(М, М.значення_дійсне);
        } інакше {
          покласти_в_накопичувач_значень(М, пусто);
        }
        
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВМістить {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_містить(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВНеМістить {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_містить(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        якщо результат == пусто {
          покласти_в_накопичувач_значень(М, М.значення_дійсне);
        } інакше {
          покласти_в_накопичувач_значень(М, пусто);
        }
        
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВЄ {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_є(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВНеЄ {
        змінна б = забрати_з_накопичувача_значень(М);
        змінна а = забрати_з_накопичувача_значень(М);

        результат = виконати_є(М, а, б, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        якщо результат == пусто {
          покласти_в_накопичувач_значень(М, М.значення_дійсне);
        } інакше {
          покласти_в_накопичувач_значень(М, пусто);
        }
        
        стрибнути крок_виконати_далі;      
      }
      
      якщо вказівка == ВДвійковеНЕ {
        змінна значення = забрати_з_накопичувача_значень(М);

        результат = виконати_двійкове_не(М, значення, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВВідʼємне {
        змінна значення = забрати_з_накопичувача_значень(М);

        результат = виконати_змінити_знак(М, значення, Місцезнаходження { код, позиція_поточної_вказівки });
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
        
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВЗаперечити {
        змінна значення = забрати_з_накопичувача_значень(М);
        
        якщо значення == пусто {
          покласти_в_накопичувач_значень(М, М.значення_дійсне);
        } інакше {
          покласти_в_накопичувач_значень(М, пусто);
        }
        
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВПочатиСпробу {
        змінна назва_зловити = забрати_з_накопичувача_значень(М) як адреса<ПредметТексту>;

        змінна позиція_вказівки_зловити = природне(прочитати_аргумент_вказівки(вказівки, позиція::адреса));
        
        покласти_спробу(М, дійсність.спроби::адреса, Спроба {
          назва_зловити = назва_зловити,
          позиція_вказівки_зловити = позиція_вказівки_зловити,
          розмір_історії_здійснення = М.історія_здійснення.розмір,
          розмір_накопичувача_значень = М.накопичувач_значень.розмір
        });
  
        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВВпасти {
        змінна значення_помилки = забрати_з_накопичувача_значень(М);

        увімкнути_стан_падіння(
          М, 
          значення_помилки, 
          Місцезнаходження { код, позиція_поточної_вказівки }
        );        

        якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
          стрибнути крок_виконання_вказівки;
        }
  
        стрибнути крок_вихід;
      }
  
      якщо вказівка == ВЗакінчитиСпробу {
        змінна позиція_вказівки_виходу = природне(прочитати_аргумент_вказівки(вказівки, позиція::адреса));

        видалити_останню_спробу(М, дійсність.спроби::адреса);
        
        позиція = позиція_вказівки_виходу;
  
        стрибнути крок_виконання_вказівки;
      }

      якщо вказівка == ВВзяти {
        змінна кількість_назв = природне(прочитати_аргумент_вказівки(вказівки, позиція::адреса));

        змінна дані_назв: памʼять<адреса<ПредметТексту>> = пусто;

        якщо кількість_назв > 0 {
          дані_назв = виділити_памʼять<адреса<ПредметТексту>>(М, кількість_назв);
          
          змінна п: природне = 0;
          
          поки п < кількість_назв {
            змінна назва = забрати_з_накопичувача_значень(М) як адреса<ПредметТексту>;
          
            дані_назв[кількість_назв - п - 1] = назва;
          
            п += 1;
          }
        }

        змінна звідки = забрати_з_накопичувача_значень(М) як адреса<ПредметТексту>;

        якщо звідки == М.константи_назв._модуль або перевірити_чи_предмети_тексту_рівні(М, звідки, М.константи_назв._модуль) {
          результат = взяти_модуль(
            М,
            шлях_до_файлу_поточного_модуля,
            ні,
            кількість_назв, 
            дані_назв,
            Місцезнаходження { код, позиція_поточної_вказівки }
          );
        } інакше якщо звідки == М.константи_назв._пак або перевірити_чи_предмети_тексту_рівні(М, звідки, М.константи_назв._пак) {
          результат = взяти_пак(
            М,
            кількість_назв, 
            дані_назв,
            Місцезнаходження { код, позиція_поточної_вказівки }
          );
        } інакше {
          увімкнути_стан_падіння(
            М, 
            створити_текст(М, "Взяти можна лише модуль або пак.") як Значення, 
            Місцезнаходження { код, позиція_поточної_вказівки }
          );
        }

        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }

        покласти_в_накопичувач_значень(М, результат);
  
        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВДати {
        змінна назва = забрати_з_накопичувача_значень(М) як адреса<ПредметТексту>;
        змінна значення = забрати_з_накопичувача_значень(М);

        якщо дійсність.здійснювач == пусто {
          увімкнути_стан_падіння(
            М, 
            створити_текст(М, "Неможливо дати.") як Значення, 
            Місцезнаходження { код, позиція_поточної_вказівки }
          );

          якщо М.стан_падіння {
            якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
              стрибнути крок_виконання_вказівки;
            }
  
            стрибнути крок_вихід;
          }
        }
        
        змінити_властивість(
          М,
          дійсність.здійснювач,
          назва,
          значення,
          Місцезнаходження { код, позиція_поточної_вказівки }
        );
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }
  
        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВПеребратиДалі {
        змінна значення_перебору = забрати_з_накопичувача_значень(М);

        покласти_в_накопичувач_значень(М, значення_перебору);
        
        результат = здійснити_властивість(
          М,
          значення_перебору,
          М.константи_назв._далі,
          0,
          пусто,
          пусто,
          Місцезнаходження { код, позиція_поточної_вказівки }
        );
        
        якщо М.стан_падіння {
          якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
            стрибнути крок_виконання_вказівки;
          }

          стрибнути крок_вихід;
        }
        
        покласти_в_накопичувач_значень(М, результат);
  
        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВЗбитиТекст {
        змінна кількість_предметів = природне(прочитати_аргумент_вказівки(вказівки, позиція::адреса));
        
        змінна накопичувач_тексту = зробити_накопичувач_тексту(М, кількість_предметів);
        
        змінна п: природне = 0;
        
        поки п < кількість_предметів {
          змінна значення = отримати_з_накопичувача_значень(М, кількість_предметів - п - 1);

          змінна предмет_як_текст = отримати_текст(
            М, 
            значення,
            Місцезнаходження { код, позиція_поточної_вказівки }
          );
        
          якщо М.стан_падіння {
            звільнити_памʼять(М, накопичувач_тексту.дані);

            якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
              стрибнути крок_виконання_вказівки;
            }
  
            стрибнути крок_вихід;
          }
        
          якщо предмет_як_текст == пусто або предмет_як_текст.тип != М.предмет_структури_Текст {
            увімкнути_стан_падіння(
              М, 
              створити_текст(М, "Не вдалось перетворити на текст.") як Значення, 
              Місцезнаходження { код, позиція_поточної_вказівки }
            );

            звільнити_памʼять(М, накопичувач_тексту.дані);
           
            якщо обробити_стан_падіння(М, дійсність, код, позиція_поточної_вказівки, позиція::адреса) {
              стрибнути крок_виконання_вказівки;
            }
  
            стрибнути крок_вихід;
          }

          змінна властивості_тексту = предмет_як_текст як адреса<ПредметТексту>;

          покласти_в_накопичувач_тексту(М, накопичувач_тексту::адреса, властивості_тексту.значення);
        
          п += 1;
        }

        видалити_з_накопичувача_значень(М, кількість_предметів);
                
        покласти_в_накопичувач_значень(
          М, 
          створити_текст_без_копіювання(М, накопичувач_тексту.розмір, накопичувач_тексту.дані) як Значення
        );
          
        стрибнути крок_виконати_далі;
      }
      
      якщо вказівка == ВСтрибнути {
        змінна позиція_вказівки_стрибка = природне(прочитати_аргумент_вказівки(вказівки, позиція::адреса));

        позиція = позиція_вказівки_стрибка;
  
        стрибнути крок_виконання_вказівки;
      }
  
      якщо вказівка == ВСтрибнутиЯкщоТак {
        змінна значення = забрати_з_накопичувача_значень(М);

        змінна позиція_вказівки_стрибка = природне(прочитати_аргумент_вказівки(вказівки, позиція::адреса));

        якщо значення != пусто {
          позиція = позиція_вказівки_стрибка;

          стрибнути крок_виконання_вказівки;
        }
  
        стрибнути крок_виконати_далі;
      }
  
      якщо вказівка == ВСтрибнутиЯкщоНі {
        змінна значення = забрати_з_накопичувача_значень(М);

        змінна позиція_вказівки_стрибка = природне(прочитати_аргумент_вказівки(вказівки, позиція::адреса));

        якщо значення == пусто {
          позиція = позиція_вказівки_стрибка;

          стрибнути крок_виконання_вказівки;
        }
  
        стрибнути крок_виконати_далі;
      }
  
      якщо вказівка == ВЗабрати {
        видалити_з_накопичувача_значень(М, 1);
  
        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВДублювати {
        змінна значення = забрати_з_накопичувача_значень(М);

        покласти_в_накопичувач_значень(М, значення);
        покласти_в_накопичувач_значень(М, значення);
  
        стрибнути крок_виконати_далі;
      }

      якщо вказівка == ВВернути {
        результат_вернути = забрати_з_накопичувача_значень(М);
        
        стрибнути крок_вихід;
      }

      запанікувати(М, "Невідома вказівка під час виконання коду.");
      
  крок_виконати_далі:
      позиція += 1;
    }
  
  крок_вихід:
    вернути результат_вернути;
  }

  місцева дія предмет_дії_здійснити(
    М: адреса<Машина>,
    здійснювач: Значення,
    предмет_дії: адреса<ПредметДії>,
    кількість_задіяних: природне,
    задіяні: памʼять<Значення>,
    іменовано_задіяні: адреса<ІменованіЗадіяні>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    якщо предмет_дії.рідна {
      змінна рідна_дія = предмет_дії.код як РіднаДія;

      покласти_в_історію_здійснення(
        М,
        здійснювач,
        пусто,
        предмет_дії,
        місцезнаходження
      );

      змінна результат = рідна_дія(
        М,
        здійснювач,
        предмет_дії,
        кількість_задіяних,
        задіяні,
        іменовано_задіяні
      );

      якщо не М.стан_падіння {
        видалити_з_історії_здійснення(М, 1);
      }

      звільнити_памʼять(М, задіяні);
      якщо іменовано_задіяні != пусто {
        знищити_іменовано_задіяні(М, іменовано_задіяні);
      }

      вернути результат;
    } інакше {        
      змінна дійсність = створити_дійсність(
        М, 
        предмет_дії.дійсність_створення,
        здійснювач
      );

      покласти_в_історію_здійснення(
        М,
        здійснювач,
        дійсність,
        предмет_дії,
        місцезнаходження
      );

      якщо предмет_дії.параметри != пусто {
        змінна п: природне = 0;
        
        поки п < предмет_дії.параметри.розмір {
          змінна параметр = предмет_дії.параметри.дані[п];
          змінна властивості_параметра = параметр як адреса<ПредметПараметра>;
    
          якщо кількість_задіяних > п {
            записати_в_дійсність(
              М,
              дійсність,
              властивості_параметра.назва,
              задіяні[п]
            );
          }
    
          п += 1;
        }
    
        якщо іменовано_задіяні != пусто {
          п = 0;
    
          поки п < іменовано_задіяні.розмір {
            змінна іменовано_задіяне = іменовано_задіяні.елементи[п];
    
            записати_в_дійсність(
              М,
              дійсність,
              іменовано_задіяне.назва,
              іменовано_задіяне.значення
            );
    
            п += 1;
          }
        }
      }

      змінна результат = виконати_код(
        М,
        дійсність,
        предмет_дії.шлях_до_файлу_модуля,
        предмет_дії.код як адреса<ПредметКоду>
      );

      якщо не М.стан_падіння {
        видалити_з_історії_здійснення(М, 1);
      }

      звільнити_памʼять(М, задіяні);
      якщо іменовано_задіяні != пусто {
        знищити_іменовано_задіяні(М, іменовано_задіяні);
      }

      вернути результат;
    }
  }

  місцева дія предмет_дії_отримати_властивість(
    М: адреса<Машина>,
    предмет_дії: адреса<ПредметДії>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    вернути пусто;
  }
}