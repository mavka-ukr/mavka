взяти означення мавка/машина;

простір мавка {
  дія змішати_кд(
    значення: кд
  ) -> природне {
    змінна змішане: природне = 5381;
    змінна п: природне = 0;

    поки п < значення.розмір {
      змішане = (змішане * 33) + природне(значення.дані[п]);

      п += 1;
    }

    вернути змішане;
  }

  дія розширити_діячів(
    М: адреса<Машина>,
    діячі: адреса<Діячі>,
    нова_вмісткість: природне
  ) {
    якщо нова_вмісткість == 0 {
      нова_вмісткість = діячі.вмісткість * 2;
    } інакше якщо нова_вмісткість <= діячі.вмісткість {
      вернути;
    }

    змінна нові_дані = виділити_памʼять<адреса<Діяч>>(М, нова_вмісткість);

    змінна п: природне = 0;

    поки п < нова_вмісткість {
      нові_дані[п] = пусто як адреса<Діяч>;

      п += 1;
    }

    п = 0;

    поки п < діячі.вмісткість {
      змінна поточний_діяч = діячі.дані[п];

      поки поточний_діяч != пусто {
        змінна нова_позиція = змішати_кд(поточний_діяч.назва.значення) % нова_вмісткість;

        змінна наступний = поточний_діяч.наступний;

        поточний_діяч.наступний = нові_дані[нова_позиція];

        нові_дані[нова_позиція] = поточний_діяч;

        поточний_діяч = наступний;
      }

      п += 1;
    }

    звільнити_памʼять(М, діячі.дані);

    діячі.дані = нові_дані;
    діячі.вмісткість = нова_вмісткість;
  }

  місцева дія записати_діяча(
    М: адреса<Машина>,
    діячі: адреса<Діячі>,
    назва: адреса<ПредметТексту>,
    значення: Значення
  ) {
    якщо діячі.вмісткість == 0 {
      діячі.дані = виділити_памʼять<адреса<Діяч>>(М, 1);
      діячі.вмісткість = 1;
      діячі.дані[0] = пусто як адреса<Діяч>;
    } інакше якщо ((д64(діячі.розмір) / д64(діячі.вмісткість)) > 0.7) {
       розширити_діячів(М, діячі, 0);
    }

    змінна позиція = змішати_кд(назва.значення) % діячі.вмісткість;

    змінна діяч = діячі.дані[позиція];

    поки діяч != пусто {
      якщо діяч.назва == назва або перевірити_чи_предмети_тексту_рівні(М, діяч.назва, назва) {
        діяч.назва = назва;
        діяч.значення = значення;

        вернути;
      }

      діяч = діяч.наступний;
    }

    змінна нова_діяч = виділити<Діяч>(М);
    нова_діяч.назва = назва;
    нова_діяч.значення = значення;
    нова_діяч.наступний = діячі.дані[позиція];
    діячі.дані[позиція] = нова_діяч;

    діячі.розмір += 1;
  }

  місцева дія знайти_діяча(
    М: адреса<Машина>,
    діячі: Діячі,
    назва: адреса<ПредметТексту>,
    вихід: адреса<Значення>
  ) -> логічне {
    якщо діячі.вмісткість == 0 {
      вернути ні;
    }

    змінна позиція = змішати_кд(назва.значення) % діячі.вмісткість;

    змінна діяч = діячі.дані[позиція];

    поки діяч != пусто {
      якщо діяч.назва == назва або перевірити_чи_предмети_тексту_рівні(М, діяч.назва, назва) {
        вихід::вміст = діяч.значення;

        вернути так;
      }

      діяч = діяч.наступний;
    }

    вернути ні;
  }

  місцева дія перебрати_діячів(
    М: адреса<Машина>,
    діячі: Діячі,
    дія_перебору: (М: адреса<Машина>, діячі: Діячі, назва: адреса<ПредметТексту>, значення: Значення, аргумент: невідома_адреса) -> логічне,
    аргумент: невідома_адреса
  ) -> логічне {
    змінна п: природне = 0;

    поки п < діячі.вмісткість {
      змінна діяч = діячі.дані[п];

      поки діяч != пусто {
        якщо не дія_перебору(М, діячі, діяч.назва, діяч.значення, аргумент) {
          вернути ні;
        }

        діяч = діяч.наступний;
      }

      п += 1;
    }

    вернути так;
  }
}