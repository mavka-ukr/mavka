взяти означення мавка/машина;

простір мавка {
  дія змішати_кд(
    значення: кд
  ) -> природне {
    змінна змішане: природне = 5381;
    змінна п: природне = 0;

    поки п < значення.розмір {
      змішане = (змішане * 33) + природне(значення.дані[п]);

      п += 1;
    }

    вернути змішане;
  }

  дія розширити_властивості(
    М: адреса<Машина>,
    властивості: адреса<Властивості>,
    нова_вмісткість: природне
  ) {
    якщо нова_вмісткість == 0 {
      нова_вмісткість = властивості.вмісткість * 2;
    } інакше якщо нова_вмісткість <= властивості.вмісткість {
      вернути;
    }

    змінна нові_дані = виділити_памʼять<адреса<Властивість>>(М, нова_вмісткість);

    змінна п: природне = 0;

    поки п < нова_вмісткість {
      нові_дані[п] = пусто як адреса<Властивість>;

      п += 1;
    }

    п = 0;

    поки п < властивості.вмісткість {
      змінна поточна_властивість = властивості.дані[п];

      поки поточна_властивість != пусто {
        змінна нова_позиція = змішати_кд(поточна_властивість.назва.значення) % нова_вмісткість;

        змінна наступна = поточна_властивість.наступна;

        поточна_властивість.наступна = нові_дані[нова_позиція];

        нові_дані[нова_позиція] = поточна_властивість;

        поточна_властивість = наступна;
      }

      п += 1;
    }

    звільнити_памʼять(М, властивості.дані);

    властивості.дані = нові_дані;
    властивості.вмісткість = нова_вмісткість;
  }

  місцева дія записати_властивість(
    М: адреса<Машина>,
    властивості: адреса<Властивості>,
    назва: адреса<ПредметТексту>,
    значення: Значення
  ) {
    якщо властивості.вмісткість == 0 {
      властивості.дані = виділити_памʼять<адреса<Властивість>>(М, 1);
      властивості.вмісткість = 1;
      властивості.дані[0] = пусто як адреса<Властивість>;
    } інакше якщо ((д64(властивості.розмір) / д64(властивості.вмісткість)) > 0.7) {
       розширити_властивості(М, властивості, 0);
    }

    змінна позиція = змішати_кд(назва.значення) % властивості.вмісткість;

    змінна властивість = властивості.дані[позиція];

    поки властивість != пусто {
      якщо властивість.назва == назва або перевірити_чи_предмети_тексту_рівні(М, властивість.назва, назва) {
        властивість.назва = назва;
        властивість.значення = значення;

        вернути;
      }

      властивість = властивість.наступна;
    }

    змінна нова_властивість = виділити<Властивість>(М);
    нова_властивість.назва = назва;
    нова_властивість.значення = значення;
    нова_властивість.наступна = властивості.дані[позиція];
    властивості.дані[позиція] = нова_властивість;

    властивості.розмір += 1;
  }

  місцева дія знайти_властивість(
    М: адреса<Машина>,
    властивості: Властивості,
    назва: адреса<ПредметТексту>,
    вихід: адреса<Значення>
  ) -> логічне {
    якщо властивості.вмісткість == 0 {
      вернути ні;
    }

    змінна позиція = змішати_кд(назва.значення) % властивості.вмісткість;

    змінна властивість = властивості.дані[позиція];

    поки властивість != пусто {
      якщо властивість.назва == назва або перевірити_чи_предмети_тексту_рівні(М, властивість.назва, назва) {
        вихід::вміст = властивість.значення;
        вернути так;
      }

      властивість = властивість.наступна;
    }

    вернути ні;
  }
}