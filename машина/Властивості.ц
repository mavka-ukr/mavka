взяти означення мавка;

простір мавка {
  місцева дія записати_властивість(
    М: адреса<Машина>,
    властивості: адреса<Властивості>,
    назва: адреса<ПредметТексту>,
    значення: Значення
  ) {
    змінна п: природне = 0;

    поки п < властивості.розмір {
      змінна властивість = властивості.дані[п]::адреса;

      якщо властивість.назва == назва або перевірити_чи_предмети_тексту_рівні(М, властивість.назва, назва) {
        властивість.значення = значення;

        вернути;
      }

      п += 1;
    }

    властивості.дані = перевиділити_памʼять<Властивість>(М, властивості.дані, властивості.розмір + 1);
    властивості.дані[властивості.розмір] = Властивість { назва, значення };
    властивості.розмір += 1;
  }

  місцева дія знайти_властивість(
    М: адреса<Машина>,
    властивості: Властивості,
    назва: адреса<ПредметТексту>,
    вихід: адреса<Значення>
  ) -> логічне {
    змінна п: природне = 0;

    поки п < властивості.розмір {
      змінна властивість = властивості.дані[п];

      якщо властивість.назва == назва або перевірити_чи_предмети_тексту_рівні(М, властивість.назва, назва) {
        вихід::вміст = властивість.значення;

        вернути так;
      }

      п += 1;
    }

    вернути ні;
  }
}