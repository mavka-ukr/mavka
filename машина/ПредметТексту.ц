взяти означення мавка;

простір мавка {
  дія рідна_дія__з(
    М: адреса<Машина>,
    здійснювач: Значення,
    предмет_дії: адреса<ПредметДії>,
    кількість_задіяних: природне, 
    задіяні: памʼять<Значення>,
    іменовано_задіяні: адреса<ІменованіЗадіяні>
  ) -> ЗначенняАбоСтанПадіння {
    змінна задіяне_значення: Значення = пусто;
    
    якщо кількість_задіяних > 0 {
      задіяне_значення = задіяні[0];
    }

    якщо іменовано_задіяні != пусто {
      знайти_іменоване_задіяне(М, іменовано_задіяні, М.константи_назв._значення, задіяне_значення::адреса);
    }

    вернути отримати_текст(
      М,
      задіяне_значення,
      Місцезнаходження { пусто, 0 }
    );
  }

  дія записати_дію__з(М: адреса<Машина>) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._з,
      пусто,
      пусто,
      рідна_дія__з,
      пусто
    );

    змінити_властивість(
      М,
      М.предмет_структури_Текст як Значення,
      М.константи_назв._з,
      предмет_дії як Значення,
      Місцезнаходження { пусто, 0 }
    );

    якщо М.стан_падіння {
      запанікувати(М, "Помилка при записі дії \"з\" для структури \"Текст\".");
    }
  }
}

простір мавка {
  місцева дія заповнити_структуру_Текст(М: адреса<Машина>) {
    записати_дію__з(М);
  }

  місцева дія створити_текст(М: адреса<Машина>, значення: кд) -> адреса<ПредметТексту> {
    змінна предмет = виділити_предмет<ПредметТексту>(М, М.предмет_структури_Текст);

    змінна копія_тексту = виділити_памʼять<п8>(М, значення.розмір);
    
    змінна п: природне = 0;
    поки п < значення.розмір {
      копія_тексту[п] = значення.дані[п];

      п += 1;
    }
    
    предмет.значення = кд { значення.розмір, копія_тексту };
    
    вернути предмет;
  }

  місцева дія створити_текст_без_копіювання(М: адреса<Машина>, розмір: природне, дані: памʼять<п8>) -> адреса<ПредметТексту> {
    змінна предмет = виділити_предмет<ПредметТексту>(М, М.предмет_структури_Текст);

    предмет.значення = кд { розмір, дані };
    
    вернути предмет;
  }

  місцева дія перевірити_чи_предмети_тексту_рівні(М: адреса<Машина>, а: адреса<ПредметТексту>, б: адреса<ПредметТексту>) -> логічне {
    якщо а.значення.розмір != б.значення.розмір {
      вернути ні;
    }

    змінна п: природне = 0;
    поки п < а.значення.розмір {
      якщо а.значення.дані[п] != б.значення.дані[п] {
        вернути ні;
      }

      п += 1;
    }

    вернути так;
  }
  
  місцева дія предмет_тексту_отримати_властивість(
    М: адреса<Машина>,
    предмет_тексту: адреса<ПредметТексту>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    якщо назва == М.константи_назв._довжина або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._довжина) {
      вернути створити_ціле(М, ц64(предмет_тексту.значення.розмір)) як Значення;
    }

    вернути пусто;
  }
}