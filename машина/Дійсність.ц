взяти означення мавка;

простір мавка {
  місцева дія створити_дійсність(
    М: адреса<Машина>,
    зовні: адреса<Дійсність>,
    здійснювач: Значення
  ) -> адреса<Дійсність> {
    змінна дійсність = виділити_дійсність(М);

    дійсність.зовні = зовні;
    дійсність.здійснювач = здійснювач;

    вернути дійсність;
  }

  місцева дія записати_в_дійсність(
    М: адреса<Машина>,
    дійсність: адреса<Дійсність>,
    назва: адреса<ПредметТексту>,
    значення: Значення
  ) {
    змінна п: природне = 0;

    поки п < дійсність.кількість_діячів {
      змінна діяч = дійсність.діячі[п]::адреса;

      якщо перевірити_чи_значення_рівні(М, діяч.назва як Значення, назва як Значення) {
        діяч.значення = значення;

        вернути;
      }

      п += 1;
    }

    дійсність.діячі = перевиділити_памʼять<Діяч>(М, дійсність.діячі, дійсність.кількість_діячів + 1);
    дійсність.діячі[дійсність.кількість_діячів] = Діяч { назва, значення };
    дійсність.кількість_діячів += 1;
  }

  місцева дія знайти_в_дійсності(
    М: адреса<Машина>,
    дійсність: адреса<Дійсність>,
    назва: адреса<ПредметТексту>,
    вихід_значення: адреса<Значення>
  ) -> логічне {
    змінна п: природне = 0;

    поки п < дійсність.кількість_діячів {
      змінна діяч = дійсність.діячі[п];

      якщо перевірити_чи_значення_рівні(М, діяч.назва як Значення, назва як Значення) {
        вихід_значення::вміст = діяч.значення;

        вернути так;
      }

      п += 1;
    }

    якщо дійсність.зовні != пусто {
      вернути знайти_в_дійсності(М, дійсність.зовні, назва, вихід_значення);
    }

    вернути ні;
  }
}