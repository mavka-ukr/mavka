взяти означення мавка;

простір мавка {
  місцева дія створити_словник(М: адреса<Машина>) -> адреса<ПредметСловника> {
    змінна предмет = виділити_предмет<ПредметСловника>(М, М.предмет_структури_Словник);

    предмет.розмір = 0;
    предмет.елементи = пусто;

    вернути предмет;
  }

  місцева дія записати_в_словник(М: адреса<Машина>, словник: адреса<ПредметСловника>, ключ: Значення, значення: Значення) {
    змінна властивості_словника = словник;

    змінна п: природне = 0;
    поки п < властивості_словника.розмір {
      змінна елемент = властивості_словника.елементи[п]::адреса;

      якщо перевірити_чи_значення_рівні(М, елемент.ключ, ключ) {
        елемент.значення = значення;

        вернути;
      }

      п += 1;
    }

    властивості_словника.елементи = перевиділити_памʼять<ЕлементСловника>(М, властивості_словника.елементи, властивості_словника.розмір + 1);
    властивості_словника.елементи[властивості_словника.розмір] = ЕлементСловника { ключ, значення };
    властивості_словника.розмір += 1;
  }

  місцева дія предмет_словника_отримати_властивість(
    М: адреса<Машина>,
    предмет_словника: адреса<ПредметСловника>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    якщо назва == М.константи_назв._довжина або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._довжина) {
      вернути створити_ціле(М, ц64(предмет_словника.розмір)) як Значення;
    }

    вернути пусто;
  }
}