взяти означення мавка/пристрій;

простір мавка {
  простір пристрій {
    дія рідна_дія_друк(
      М: адреса<Машина>,
      здійснювач: Значення,
      предмет_дії: адреса<ПредметДії>,
      задіяні: Задіяні
    ) -> ЗначенняАбоСтанПадіння {
      змінна розмір_відступів: природне = 0;

      якщо задіяні.розмір > 0 {
        якщо задіяні.розмір == 1 {
          розмір_відступів = 2;
        }

        змінна п: природне = 0;

        поки п < задіяні.розмір {
          вивести_значення(М, задіяні.дані[п], розмір_відступів, 0);

          якщо задіяні.розмір - п > 1 {
            вивести_кд(М, " ");
          }

          п += 1;
        }
      }

      вивести_кд(М, "\р");

      вернути значення_недійсне();
    }

    місцева дія визначити_глобальний_мавка(М: адреса<Машина>) {
      змінна модуль_мавка = створити_модуль(
        М,
        створити_текст(М, "мавка")
      );

      змінна версія_мавки_кд: кд;
      пристрій_мавки_отримати_версію(версія_мавки_кд.розмір::адреса, версія_мавки_кд.дані::адреса);
      змінна версія_мавка = створити_текст_без_копіювання(М, версія_мавки_кд.розмір, версія_мавки_кд.дані);

      предмет_модуля_змінити_властивість(
        М,
        модуль_мавка,
        створити_текст(М, "версія"),
        значення_предмет(версія_мавка),
        Місцезнаходження { пусто, 0 }
      );

      записати_в_дійсність(
        М,
        М.глобальна_дійсність,
        створити_текст(М, "мавка"),
        значення_предмет(модуль_мавка)
      );
    }

    місцева дія визначити_глобальні_виводу(М: адреса<Машина>) {
      змінна дія_друк = створити_рідну_дію(
        М,
        створити_текст(М, "друк"),
        М.предмет_пустої_послідовності,
        пусто,
        рідна_дія_друк,
        пусто
      );

      записати_в_дійсність(
        М,
        М.глобальна_дійсність,
        створити_текст(М, "друк"),
        значення_предмет(дія_друк)
      );
    }
  }
}