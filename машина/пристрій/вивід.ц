взяти означення мавка/пристрій;

простір мавка {
  простір пристрій {
    місцева дія надрукувати_інформацію_падіння(
      М: адреса<Машина>,
      інформація_падіння: ІнформаціяПадіння
    ) {
      пристрій_мавки_вивести_формат(ФКолірТекстуЧервоний);
      вивести_кд(М, "Помилка");
      пристрій_мавки_вивести_формат(ФОчистити);

      якщо інформація_падіння.місцезнаходження.шлях != пусто {
        вивести_кд(М, " в ");

        пристрій_мавки_вивести_шлях(інформація_падіння.місцезнаходження.шлях.розмір, інформація_падіння.місцезнаходження.шлях.дані);

        якщо інформація_падіння.місцезнаходження.рядок > 0 {
          вивести_кд(М, ":");
          вивести_п64(М, п64(інформація_падіння.місцезнаходження.рядок));
        }

        вивести_кд(М, "\р");
      }

      надрукувати_значення(М, інформація_падіння.значення, 2, 0);

      змінна п: природне = 0;

      якщо інформація_падіння.розмір_історії > 0 {
        надрукувати_кд(М, "\рІсторія здійснення:");
      }

      поки п < інформація_падіння.розмір_історії {
        змінна елемент_історії = інформація_падіння.історія[п];

        якщо елемент_історії.назва != пусто {
          вивести_кд(М, "  ");
          пристрій_мавки_вивести_формат(ФКолірТекстуСиній);
          вивести_кд(М, елемент_історії.назва.значення);
          пристрій_мавки_вивести_формат(ФОчистити);

          якщо елемент_історії.місцезнаходження.шлях == пусто {
            вивести_кд(М, "\р");
          } інакше {
            вивести_кд(М, " в ");
            пристрій_мавки_вивести_шлях(елемент_історії.місцезнаходження.шлях.розмір, елемент_історії.місцезнаходження.шлях.дані);

            якщо елемент_історії.місцезнаходження.рядок > 0 {
              вивести_кд(М, ":");
              вивести_п64(М, п64(елемент_історії.місцезнаходження.рядок));
            }

            вивести_кд(М, "\р");
          }
        }

        п += 1;
      }
    }

    дія вивести_пропуски(М: адреса<Машина>, кількість: природне) {
      змінна п: природне = 0;

      поки п < кількість {
        вивести_кд(М, " ");

        п += 1;
      }
    }

    // позначка: прохід по типах предметів
    місцева дія вивести_значення(М: адреса<Машина>, значення: Значення, розмір_відступу: природне, глибина: природне) {
      якщо значення.вид == ЗНедійсне {
        пристрій_мавки_вивести_формат(ФКолірТекстуЖовтий);

        вивести_кд(М, "недійсне");

        пристрій_мавки_вивести_формат(ФОчистити);

        вернути;
      }

      якщо значення.вид == ЗДійсне {
        пристрій_мавки_вивести_формат(ФКолірТекстуЖовтий);

        вивести_кд(М, "дійсне");

        пристрій_мавки_вивести_формат(ФОчистити);

        вернути;
      }

      якщо значення.вид == ЗЧисло {
        пристрій_мавки_вивести_формат(ФКолірТекстуЖовтий);

        змінна значення_кд: кд;

        якщо перетворити_д64_в_кд(М, значення.дані як д64, значення_кд::адреса) {
          вивести_кд(М, значення_кд);
        }

        звільнити_памʼять(М, значення_кд.дані);

        пристрій_мавки_вивести_формат(ФОчистити);

        вернути;
      }

      змінна предмет = значення.дані як адреса<Предмет>;

      якщо предмет.тип == М.предмет_структури_Структура {
        змінна властивості = предмет як адреса<ПредметСтруктури>;

        пристрій_мавки_вивести_формат(ФКолірТекстуСиній);

        якщо властивості.назва == пусто {
          вивести_кд(М, "<структура>");
        } інакше {
          вивести_кд(М, "<структура ");

          вивести_значення(М, ПРЕДМЕТ(М, властивості.назва), 0, 0);

          вивести_кд(М, ">");
        }

        пристрій_мавки_вивести_формат(ФОчистити);

        вернути;
      }

      якщо предмет.тип == М.предмет_структури_Модуль {
        змінна властивості_модуля = предмет як адреса<ПредметМодуля>;

        пристрій_мавки_вивести_формат(ФКолірТекстуСиній);

        якщо властивості_модуля.назва == пусто {
          вивести_кд(М, "<модуль");
        } інакше {
          вивести_кд(М, "<модуль ");

          вивести_значення(М, ПРЕДМЕТ(М, властивості_модуля.назва), 0, 0);
        }

        якщо властивості_модуля.властивості.вмісткість > 0 {
          вивести_кд(М, "[");
          змінна п: природне = 0;
          змінна виведено_першу = ні;

          поки п < властивості_модуля.властивості.вмісткість {
            змінна діяч = властивості_модуля.властивості.дані[п];

            поки діяч != пусто {
              якщо виведено_першу {
                вивести_кд(М, ", ");
              }

              виведено_першу = так;

              вивести_значення(М, ПРЕДМЕТ(М, діяч.назва), 0, 0);

              діяч = діяч.наступний;
            }

            п += 1;
          }
          вивести_кд(М, "]");
        }

        вивести_кд(М, ">");

        пристрій_мавки_вивести_формат(ФОчистити);

        вернути;
      }

      якщо предмет.тип == М.предмет_структури_Параметр {
        вивести_кд(М, "Параметр()");
        вернути;
      }

      якщо предмет.тип == М.предмет_структури_Число {
        ЗАПАНІКУВАТИ(М, "Неможливо вивести значення типу Число.");
      }

      якщо предмет.тип == М.предмет_структури_Текст {
        змінна властивості = предмет як адреса<ПредметТексту>;

        якщо глибина != 0 {
          пристрій_мавки_вивести_формат(ФКолірТекстуЗелений);
          вивести_кд(М, "\"");
        }

        якщо глибина == 0 {
          вивести_кд(М, властивості.значення);
        } інакше {
          змінна п: природне = 0;
          поки п < властивості.значення.розмір {
            змінна символ = властивості.значення.дані[п];

            якщо символ == КД::символи::Міжряд {
              вивести_кд(М, "\\р");
            } інакше якщо символ == КД::символи::Дволапка {
              вивести_кд(М, "\\\"");
            } інакше якщо символ == КД::символи::Обернена_похила {
              вивести_кд(М, "\\\\");
            } інакше якщо символ == КД::символи::Дія {
              вивести_кд(М, "\\д");
            } інакше {
              вивести_кд(М, кд { 1, символ::адреса як памʼять<п8> });
            }

            п += 1;
          }
        }

        якщо глибина != 0 {
          вивести_кд(М, "\"");
          пристрій_мавки_вивести_формат(ФОчистити);
        }

        вернути;
      }

      якщо предмет.тип == М.предмет_структури_Код {
        вивести_кд(М, "Код()");
        вернути;
      }

      якщо предмет.тип == М.предмет_структури_Дія {
        змінна властивості = предмет як адреса<ПредметДії>;

        пристрій_мавки_вивести_формат(ФКолірТекстуСиній);

        якщо властивості.назва == пусто {
          вивести_кд(М, "<дія>");
        } інакше {
          вивести_кд(М, "<дія ");

          вивести_значення(М, ПРЕДМЕТ(М, властивості.назва), 0, 0);

          вивести_кд(М, ">");
        }

        пристрій_мавки_вивести_формат(ФОчистити);

        вернути;
      }

      якщо предмет.тип == М.предмет_структури_Список {
        змінна властивості_списку = предмет як адреса<ПредметСписку>;

        вивести_кд(М, "[");

        якщо властивості_списку.розмір > 0 {
          якщо розмір_відступу > 0 {
            вивести_кд(М, "\р");
          }

          змінна п: природне = 0;
          поки п < властивості_списку.розмір {
            змінна елемент = властивості_списку.елементи[п];
            вивести_пропуски(М, розмір_відступу * (глибина + 1));
            вивести_значення(М, елемент, розмір_відступу, глибина + 1);
            якщо п + 1 < властивості_списку.розмір {
              вивести_кд(М, ",");
              якщо розмір_відступу > 0 {
                вивести_кд(М, "\р");
              } інакше {
                вивести_кд(М, " ");
              }
            }

            п += 1;
          }

          якщо розмір_відступу > 0 {
            вивести_кд(М, "\р");
          }
          вивести_пропуски(М, розмір_відступу * глибина);
        }

        вивести_кд(М, "]");

        вернути;
      }

      якщо предмет.тип == М.предмет_структури_Словник {
        змінна властивості_словника = предмет як адреса<ПредметСловника>;

        вивести_кд(М, "{");
        якщо властивості_словника.розмір > 0 {
          якщо розмір_відступу > 0 {
            вивести_кд(М, "\р");
          }

          змінна п: природне = 0;
          поки п < властивості_словника.розмір {
            змінна пара = властивості_словника.елементи[п];
            вивести_пропуски(М, розмір_відступу * (глибина + 1));
            вивести_значення(М, пара.ключ, розмір_відступу, глибина + 1);
            вивести_кд(М, " => ");
            вивести_значення(М, пара.значення, розмір_відступу, глибина + 1);
            якщо п + 1 < властивості_словника.розмір {
              вивести_кд(М, ",");
              якщо розмір_відступу > 0 {
                вивести_кд(М, "\р");
              } інакше {
                вивести_кд(М, " ");
              }
            }

            п += 1;
          }

          якщо розмір_відступу > 0 {
            вивести_кд(М, "\р");
          }
          вивести_пропуски(М, розмір_відступу * глибина);
        }

        вивести_кд(М, "}");

        вернути;
      }

      якщо предмет.тип == М.предмет_структури_Дані {
        вивести_кд(М, "Дані()");
        вернути;
      }

      якщо предмет.тип == М.предмет_структури_ДіапазонЧисел {
        вивести_кд(М, "ДіапазонЧисел()");
        вернути;
      }

      якщо предмет.тип == М.предмет_структури_ПеребірДіапазонуЧисел {
        вивести_кд(М, "ПеребірДіапазонуЧисел()");
        вернути;
      }

      якщо предмет.тип == М.предмет_структури_ПеребірДаних {
        вивести_кд(М, "ПеребірДаних()");
        вернути;
      }

      якщо предмет.тип == М.предмет_структури_ПеребірПослідовності {
        вивести_кд(М, "ПеребірПослідовності()");
        вернути;
      }

      якщо предмет.тип == М.предмет_структури_ПеребірСловника {
        вивести_кд(М, "ПеребірСловника()");
        вернути;
      }

      якщо предмет.тип == М.предмет_структури_ПеребірСписку {
        вивести_кд(М, "ПеребірСписку()");
        вернути;
      }

      якщо предмет.тип == М.предмет_структури_ПеребірТексту {
        вивести_кд(М, "ПеребірТексту()");
        вернути;
      }

      якщо предмет.тип == М.предмет_структури_Послідовність {
        вивести_кд(М, "Послідовність()");
        вернути;
      }

      змінна властивості_структури = предмет.тип як адреса<ПредметСтруктури>;

      пристрій_мавки_вивести_формат(ФКолірТекстуСиній);

      якщо властивості_структури.назва == пусто {
        вивести_кд(М, "<без_назви>");
      } інакше {
        вивести_значення(М, ПРЕДМЕТ(М, властивості_структури.назва), 0, 0);
      }

      пристрій_мавки_вивести_формат(ФОчистити);

      вивести_кд(М, "(");

      якщо властивості_структури.параметри.розмір > 0 {
        якщо розмір_відступу > 0 {
          вивести_кд(М, "\р");
        }

        змінна п: природне = 0;
        поки п < властивості_структури.параметри.розмір {
          змінна параметр = властивості_структури.параметри.елементи[п];
          змінна властивості_параметра = параметр.дані як адреса<Предмет> як адреса<ПредметПараметра>;

          змінна значення_властивості = отримати_властивість(М, значення, властивості_параметра.назва, Місцезнаходження { пусто, 0});
          вивести_пропуски(М, розмір_відступу * (глибина + 1));
          вивести_значення(М, ПРЕДМЕТ(М, властивості_параметра.назва), 0, 0);
          вивести_кд(М, "=");
          вивести_значення(М, значення_властивості, розмір_відступу, глибина + 1);
          якщо п + 1 < властивості_структури.параметри.розмір {
            вивести_кд(М, ",");
            якщо розмір_відступу > 0 {
              вивести_кд(М, "\р");
            } інакше {
              вивести_кд(М, " ");
            }
          }

          п += 1;
        }

        якщо розмір_відступу > 0 {
          вивести_кд(М, "\р");
        }
        вивести_пропуски(М, розмір_відступу * глибина);
      }

      вивести_кд(М, ")");
    }

    місцева дія надрукувати_значення(М: адреса<Машина>, значення: Значення, розмір_відступу: природне, глибина: природне) {
      вивести_значення(М, значення, розмір_відступу, глибина);
      вивести_кд(М, "\р");
    }
  }
}