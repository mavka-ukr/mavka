взяти означення мавка;

простір мавка {
  дія рідна_дія_метода__далі(
    М: адреса<Машина>,
    здійснювач: Значення,
    предмет_дії: адреса<ПредметДії>,
    задіяні: Задіяні
  ) -> ЗначенняАбоСтанПадіння {
    якщо здійснювач.тип != М.предмет_структури_ПеребірСписку {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Перевірте свій код, щось неправильно.") як Значення,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна предмет_перебору_списку = здійснювач як адреса<ПредметПереборуСписку>;

    якщо предмет_перебору_списку.почато {
      предмет_перебору_списку.позиція += 1;
    } інакше {
      предмет_перебору_списку.позиція = 0;
      предмет_перебору_списку.почато = так;
    }

    якщо предмет_перебору_списку.позиція < предмет_перебору_списку.предмет_списку.розмір {
      предмет_перебору_списку.поточне_значення = предмет_перебору_списку.предмет_списку.елементи[предмет_перебору_списку.позиція];

      вернути М.значення_дійсне;
    }

    вернути пусто;
  }

  дія записати_метод__далі(М: адреса<Машина>) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._далі,
      пусто,
      пусто,
      рідна_дія_метода__далі,
      пусто
    );

    записати_метод_структури(
      М,
      М.предмет_структури_ПеребірСписку,
      М.константи_назв._далі,
      предмет_дії
    );
  }
}

простір мавка {
  місцева дія заповнити_структуру_ПеребірСписку(
    М: адреса<Машина>
  ) {
    записати_метод__далі(М);
  }

  місцева дія створити_перебір_списку(
    М: адреса<Машина>,
    предмет_списку: адреса<ПредметСписку>
  ) -> адреса<ПредметПереборуСписку> {
    змінна предмет = виділити_предмет<ПредметПереборуСписку>(М, М.предмет_структури_ПеребірСписку);

    предмет.предмет_списку = предмет_списку;
    предмет.почато = ні;
    предмет.позиція = 0;
    предмет.поточне_значення = пусто;

    вернути предмет;
  }

  місцева дія предмет_перебору_списку_отримати_властивість(
    М: адреса<Машина>,
    предмет_перебору_списку: адреса<ПредметПереборуСписку>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    якщо назва == М.константи_назв._значення або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._значення) {
      вернути предмет_перебору_списку.поточне_значення як Значення;
    }

    вернути знайти_метод_в_структурі(М, предмет_перебору_списку.тип, назва) як Значення;
  }
}