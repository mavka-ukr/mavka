взяти означення мавка/машина;
взяти означення КД;

простір мавка {
  місцева дія здійснити(
    М: адреса<Машина>,
    здійснювач: адреса<Предмет>,
    значення: адреса<Предмет>,
    задіяні: Задіяні,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо значення == пусто {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити значення \"недійсне\".") як адреса<Предмет>,
        місцезнаходження
      );
      вернути пусто;
    }

    якщо значення == М.значення_дійсне {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо здійснити значення \"дійсне\".") як адреса<Предмет>,
        місцезнаходження
      );
      вернути пусто;
    }

    змінна предмет = значення;

    якщо предмет.тип == М.предмет_структури_Дія {
      вернути предмет_дії_здійснити(
        М,
        здійснювач,
        предмет як адреса<ПредметДії>,
        задіяні,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_Структура {
      вернути предмет_структури_здійснити(
        М,
        предмет як адреса<ПредметСтруктури>,
        задіяні,
        місцезнаходження
      );
    }

    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Неможливо здійснити предмет.") як адреса<Предмет>,
      місцезнаходження
    );
    вернути пусто;
  }

  // позначка: прохід по типах предметів
  місцева дія отримати_властивість(
    М: адреса<Машина>,
    значення: адреса<Предмет>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо значення == пусто {
      вернути пусто;
    }

    якщо значення == М.значення_дійсне {
      вернути пусто;
    }

    змінна предмет = значення;

    якщо предмет.тип == М.предмет_структури_Предмет {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "ПОМИЛКА МАШИНИ: отримання властивості предмета структури \"Предмет\".") як адреса<Предмет>,
        місцезнаходження
      );
      вернути пусто;
    }

    якщо предмет.тип == М.предмет_структури_Структура {
      вернути предмет_структури_отримати_властивість(
        М,
        предмет як адреса<ПредметСтруктури>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_Дані {
      вернути предмет_даних_отримати_властивість(
        М,
        предмет як адреса<ПредметДаних>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_ДіапазонДробових {
      вернути предмет_діапазону_дробових_отримати_властивість(
        М,
        предмет як адреса<ПредметДіапазонуДробових>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_ДіапазонЦілих {
      вернути предмет_діапазону_цілих_отримати_властивість(
        М,
        предмет як адреса<ПредметДіапазонуЦілих>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_Дія {
      вернути предмет_дії_отримати_властивість(
        М,
        предмет як адреса<ПредметДії>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_Дробове {
      вернути предмет_дробового_отримати_властивість(
        М,
        значення як адреса<ПредметДробового>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_Код {
      вернути предмет_коду_отримати_властивість(
        М,
        предмет як адреса<ПредметКоду>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_Модуль {
      вернути предмет_модуля_отримати_властивість(
        М,
        предмет як адреса<ПредметМодуля>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_Параметр {
      вернути предмет_параметра_отримати_властивість(
        М,
        предмет як адреса<ПредметПараметра>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_ПеребірДаних {
      вернути предмет_перебору_даних_отримати_властивість(
        М,
        предмет як адреса<ПредметПереборуДаних>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_ПеребірДіапазонуДробових {
      вернути предмет_перебору_діапазону_дробових_отримати_властивість(
        М,
        предмет як адреса<ПредметПереборуДіапазонуДробових>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_ПеребірДіапазонуЦілих {
      вернути предмет_перебору_діапазону_цілих_отримати_властивість(
        М,
        предмет як адреса<ПредметПереборуДіапазонуЦілих>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_ПеребірПослідовності {
      вернути предмет_перебору_послідовності_отримати_властивість(
        М,
        предмет як адреса<ПредметПереборуПослідовності>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_ПеребірСловника {
      вернути предмет_перебору_словника_отримати_властивість(
        М,
        предмет як адреса<ПредметПереборуСловника>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_ПеребірСписку {
      вернути предмет_перебору_списку_отримати_властивість(
        М,
        предмет як адреса<ПредметПереборуСписку>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_ПеребірТексту {
      вернути предмет_перебору_тексту_отримати_властивість(
        М,
        предмет як адреса<ПредметПереборуТексту>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_Послідовність {
      вернути предмет_послідовності_отримати_властивість(
        М,
        предмет як адреса<ПредметПослідовності>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_Словник {
      вернути предмет_словника_отримати_властивість(
        М,
        предмет як адреса<ПредметСловника>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_Список {
      вернути предмет_списку_отримати_властивість(
        М,
        предмет як адреса<ПредметСписку>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_Текст {
      вернути предмет_тексту_отримати_властивість(
        М,
        предмет як адреса<ПредметТексту>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_Ціле {
      вернути предмет_цілого_отримати_властивість(
        М,
        значення як адреса<ПредметЦілого>,
        назва,
        місцезнаходження
      );
    }

    якщо предмет.тип == М.предмет_структури_Число {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "ПОМИЛКА МАШИНИ: отримання властивості предмета структури \"Число\".") як адреса<Предмет>,
        місцезнаходження
      );
      вернути пусто;
    }

    вернути складений_предмет_отримати_властивість(
      М,
      предмет як адреса<СкладенийПредмет>,
      назва,
      місцезнаходження
    );
  }

  // позначка: прохід по типах предметів
  місцева дія змінити_властивість(
    М: адреса<Машина>,
    значення: адреса<Предмет>,
    назва: адреса<ПредметТексту>,
    значення_властивості: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> СтанПадіння {
    якщо значення == пусто {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо змінити властивість значення \"недійсне\".") як адреса<Предмет>,
        місцезнаходження
      );
      вернути;
    }

    якщо значення == М.значення_дійсне {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Неможливо змінити властивість значення \"дійсне\".") як адреса<Предмет>,
        місцезнаходження
      );
      вернути;
    }

    змінна предмет = значення;

    якщо предмет.тип == М.предмет_структури_Предмет {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "ПОМИЛКА МАШИНИ: зміна властивості предмета структури \"Предмет\".") як адреса<Предмет>,
        місцезнаходження
      );
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_Структура {
      предмет_структури_змінити_властивість(
        М,
        предмет як адреса<ПредметСтруктури>,
        назва,
        значення_властивості,
        місцезнаходження
      );
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_Дані {
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_ДіапазонДробових {
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_ДіапазонЦілих {
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_Дія {
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_Дробове {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "ПОМИЛКА МАШИНИ: зміна властивості предмета структури \"Дробове\".") як адреса<Предмет>,
        місцезнаходження
      );
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_Код {
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_Модуль {
      предмет_модуля_змінити_властивість(
        М,
        предмет як адреса<ПредметМодуля>,
        назва,
        значення_властивості,
        місцезнаходження
      );
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_Параметр {
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_ПеребірДаних {
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_ПеребірДіапазонуДробових {
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_ПеребірДіапазонуЦілих {
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_ПеребірПослідовності {
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_ПеребірСловника {
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_ПеребірСписку {
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_ПеребірТексту {
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_Послідовність {
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_Словник {
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_Список {
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_Текст {
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_Ціле {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "ПОМИЛКА МАШИНИ: зміна властивості предмета структури \"Ціле\".") як адреса<Предмет>,
        місцезнаходження
      );
      вернути;
    }

    якщо предмет.тип == М.предмет_структури_Число {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "ПОМИЛКА МАШИНИ: зміна властивості предмета структури \"Число\".") як адреса<Предмет>,
        місцезнаходження
      );
      вернути;
    }

    складений_предмет_змінити_властивість(
      М,
      предмет як адреса<СкладенийПредмет>,
      назва,
      значення_властивості,
      місцезнаходження
    );
  }

  місцева дія здійснити_властивість(
    М: адреса<Машина>,
    значення: адреса<Предмет>,
    назва: адреса<ПредметТексту>,
    задіяні: Задіяні,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    змінна значення_властивості = отримати_властивість(
      М,
      значення,
      назва,
      місцезнаходження
    );

    якщо М.стан_падіння {
      звільнити_задіяні(М, задіяні);

      вернути пусто;
    }

    якщо значення_властивості == пусто {
      звільнити_задіяні(М, задіяні);

      змінна накопичувач_тексту = зробити_накопичувач_тексту(М, 50);
      покласти_в_накопичувач_тексту(М, накопичувач_тексту::адреса, "Неможливо здійснити недійсне (властивість \"");
      покласти_в_накопичувач_тексту(М, накопичувач_тексту::адреса, назва.значення);
      покласти_в_накопичувач_тексту(М, накопичувач_тексту::адреса, "\").");

      увімкнути_стан_падіння(
        М,
        створити_текст_без_копіювання(М, накопичувач_тексту.розмір, накопичувач_тексту.дані) як адреса<Предмет>,
        місцезнаходження
      );
      вернути пусто;
    }

    вернути здійснити(
      М,
      значення,
      значення_властивості,
      задіяні,
      місцезнаходження
    );
  }

  // позначка: прохід по типах предметів
  місцева дія отримати_елемент(
    М: адреса<Машина>,
    значення: адреса<Предмет>,
    ключ: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо значення != пусто і значення != М.значення_дійсне {
      змінна предмет = значення;

      якщо предмет.тип == М.предмет_структури_Дані {
        вернути предмет_даних_отримати_елемент(
          М,
          предмет як адреса<ПредметДаних>,
          ключ,
          місцезнаходження
        );
      }

      якщо предмет.тип == М.предмет_структури_Послідовність {
        вернути предмет_послідовності_отримати_елемент(
          М,
          предмет як адреса<ПредметПослідовності>,
          ключ,
          місцезнаходження
        );
      }

      якщо предмет.тип == М.предмет_структури_Словник {
        вернути предмет_словника_отримати_елемент(
          М,
          предмет як адреса<ПредметСловника>,
          ключ,
          місцезнаходження
        );
      }

      якщо предмет.тип == М.предмет_структури_Список {
        вернути предмет_списку_отримати_елемент(
          М,
          предмет як адреса<ПредметСписку>,
          ключ,
          місцезнаходження
        );
      }

      якщо предмет.тип == М.предмет_структури_Текст {
        вернути предмет_тексту_отримати_елемент(
          М,
          предмет як адреса<ПредметТексту>,
          ключ,
          місцезнаходження
        );
      }
    }

    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Неможливо отримати елемент з цього типу предмета.") як адреса<Предмет>,
      місцезнаходження
    );
    вернути пусто;
  }

  // позначка: прохід по типах предметів
  місцева дія змінити_елемент(
    М: адреса<Машина>,
    значення: адреса<Предмет>,
    ключ: адреса<Предмет>,
    значення_елемента: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> СтанПадіння {
    якщо значення != пусто і значення != М.значення_дійсне {
      змінна предмет = значення;

      якщо предмет.тип == М.предмет_структури_Словник {
        предмет_словника_змінити_елемент(
          М,
          предмет як адреса<ПредметСловника>,
          ключ,
          значення_елемента,
          місцезнаходження
        );
        вернути;
      }

      якщо предмет.тип == М.предмет_структури_Список {
        предмет_списку_змінити_елемент(
          М,
          предмет як адреса<ПредметСписку>,
          ключ,
          значення_елемента,
          місцезнаходження
        );
        вернути;
      }
    }

    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Неможливо змінити елемент цього типу предмета.") як адреса<Предмет>,
      місцезнаходження
    );
  }

  // позначка: прохід по типах предметів
  місцева дія отримати_текст(
    М: адреса<Машина>,
    значення: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> адреса<ПредметТексту> {
    якщо значення == пусто {
      вернути М.константи_назв._недійсне;
    }

    якщо значення == М.значення_дійсне {
      вернути М.константи_назв._дійсне;
    }

    // якщо значення.тип == М.предмет_структури_Ціле {
    //   змінна значення_кд: кд;

    //   якщо перетворити_ц64_в_кд(М, значення як адреса<ПредметЦілого>, значення_кд::адреса) {
    //     вернути створити_текст_без_копіювання(М, значення_кд.розмір, значення_кд.дані);
    //   }

    //   увімкнути_стан_падіння(
    //     М,
    //     створити_текст(М, "Не вдалось перетворити ціле число в текст.") як адреса<Предмет>,
    //     місцезнаходження
    //   );
    //   вернути пусто;
    // }

    // якщо значення.тип == М.предмет_структури_Дробове {
    //   змінна значення_кд: кд;

    //   якщо перетворити_д64_в_кд(М, значення.дані як д64, значення_кд::адреса) {
    //     вернути створити_текст_без_копіювання(М, значення_кд.розмір, значення_кд.дані);
    //   }

    //   увімкнути_стан_падіння(
    //     М,
    //     створити_текст(М, "Не вдалось перетворити дробове число в текст.") як адреса<Предмет>,
    //     місцезнаходження
    //   );
    //   вернути пусто;
    // }

    змінна предмет = значення;

    якщо предмет.тип == М.предмет_структури_Текст {
      вернути предмет як адреса<ПредметТексту>;
    }

    змінна результат = здійснити_властивість(
      М,
      значення,
      М.константи_назв._чародія_текст,
      Задіяні { 0, пусто, пусто },
      місцезнаходження
    );

    якщо М.стан_падіння {
      вернути пусто;
    }

    якщо результат == пусто або результат == М.значення_дійсне {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Не вдалось перетворити в текст.") як адреса<Предмет>,
        місцезнаходження
      );
      вернути пусто;
    }

    змінна предмет_результату = результат;

    якщо предмет_результату.тип != М.предмет_структури_Текст {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Не вдалось перетворити в текст.") як адреса<Предмет>,
        місцезнаходження
      );
      вернути пусто;
    }

    вернути предмет_результату як адреса<ПредметТексту>;
  }

  // позначка: прохід по типах предметів
  місцева дія отримати_діапазон(
    М: адреса<Машина>,
    від: адреса<Предмет>,
    до: адреса<Предмет>,
    включно: логічне,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо від.тип == М.предмет_структури_Ціле {
      якщо до == пусто {
        увімкнути_стан_падіння(
          М,
          створити_текст(М, "Неможливо створити діапазон (до == недійсне).") як адреса<Предмет>,
          місцезнаходження
        );
        вернути пусто;
      }

      якщо до.тип == М.предмет_структури_Ціле {
        вернути створити_діапазон_цілих(
          М,
          від як адреса<ПредметЦілого>,
          до як адреса<ПредметЦілого>,
          включно
        ) як адреса<Предмет>;
      }

      // якщо до.тип == М.предмет_структури_Дробове {
      //   вернути створити_діапазон_дробових(
      //     М,
      //     створити_дробове_з_д64(М, д64((від як адреса<ПредметЦілого>).значення)),
      //     до як адреса<ПредметДробового>,
      //     включно
      //   ) як адреса<Предмет>;
      // }
    }

    якщо від.тип == М.предмет_структури_Дробове {
      якщо до == пусто {
        увімкнути_стан_падіння(
          М,
          створити_текст(М, "Неможливо створити діапазон (до == недійсне).") як адреса<Предмет>,
          місцезнаходження
        );
        вернути пусто;
      }

      якщо до.тип == М.предмет_структури_Дробове {
        вернути створити_діапазон_дробових(
          М,
          від як адреса<ПредметДробового>,
          до як адреса<ПредметДробового>,
          включно
        ) як адреса<Предмет>;
      }

      // якщо до.тип == М.предмет_структури_Ціле {
      //   вернути створити_діапазон_дробових(
      //     М,
      //     від як адреса<ПредметДробового>,
      //     створити_дробове_з_д64(М, д64((до як адреса<ПредметЦілого>).значення)),
      //     включно
      //   ) як адреса<Предмет>;
      // }
    }

    змінна задіяні = Задіяні { 2, виділити_памʼять<адреса<Предмет>>(М, 2), пусто };
    задіяні.дані[0] = до;
    якщо включно {
      задіяні.дані[1] = М.значення_дійсне;
    } інакше {
      задіяні.дані[1] = пусто як адреса<Предмет>;
    }

    вернути здійснити_властивість(
      М,
      від,
      М.константи_назв._чародія_діапазон,
      задіяні,
      місцезнаходження
    );
  }

  // позначка: прохід по типах предметів
  місцева дія отримати_перебір(
    М: адреса<Машина>,
    значення: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо значення != пусто і значення != М.значення_дійсне {
      змінна предмет = значення;

      якщо предмет.тип == М.предмет_структури_Дані {
        вернути створити_перебір_даних(
          М,
          предмет як адреса<ПредметДаних>
        ) як адреса<Предмет>;
      }

      якщо предмет.тип == М.предмет_структури_ДіапазонДробових {
        вернути створити_перебір_діапазону_дробових(
          М,
          предмет як адреса<ПредметДіапазонуДробових>
        ) як адреса<Предмет>;
      }

      якщо предмет.тип == М.предмет_структури_ДіапазонЦілих {
        вернути створити_перебір_діапазону_цілих(
          М,
          предмет як адреса<ПредметДіапазонуЦілих>
        ) як адреса<Предмет>;
      }

      якщо предмет.тип == М.предмет_структури_Послідовність {
        вернути створити_перебір_послідовності(
          М,
          предмет як адреса<ПредметПослідовності>
        ) як адреса<Предмет>;
      }

      якщо предмет.тип == М.предмет_структури_Словник {
        вернути створити_перебір_словника(
          М,
          предмет як адреса<ПредметСловника>
        ) як адреса<Предмет>;
      }

      якщо предмет.тип == М.предмет_структури_Список {
        вернути створити_перебір_списку(
          М,
          предмет як адреса<ПредметСписку>
        ) як адреса<Предмет>;
      }

      якщо предмет.тип == М.предмет_структури_Текст {
        вернути створити_перебір_тексту(
          М,
          предмет як адреса<ПредметТексту>
        ) як адреса<Предмет>;
      }
    }

    вернути здійснити_властивість(
      М,
      значення,
      М.константи_назв._чародія_перебір,
      Задіяні { 0, пусто, пусто },
      місцезнаходження
    );
  }

  місцева дія виконати_додати(
    М: адреса<Машина>,
    ліво: адреса<Предмет>,
    право: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо ліво.тип == М.предмет_структури_Ціле {
      вернути предмет_цілого_додати(М, ліво як адреса<ПредметЦілого>, право, місцезнаходження);
    }

    якщо ліво.тип == М.предмет_структури_Дробове {
      вернути предмет_дробового_додати(М, ліво як адреса<ПредметДробового>, право, місцезнаходження);
    }

    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати додавання можна лише між числами.") як адреса<Предмет>,
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_відняти(
    М: адреса<Машина>,
    ліво: адреса<Предмет>,
    право: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо ліво.тип == М.предмет_структури_Ціле {
      вернути предмет_цілого_відняти(М, ліво як адреса<ПредметЦілого>, право, місцезнаходження);
    }

    якщо ліво.тип == М.предмет_структури_Дробове {
      вернути предмет_дробового_відняти(М, ліво як адреса<ПредметДробового>, право, місцезнаходження);
    }

    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати віднімання можна лише між числами.") як адреса<Предмет>,
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_помножити(
    М: адреса<Машина>,
    ліво: адреса<Предмет>,
    право: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо ліво.тип == М.предмет_структури_Ціле {
      вернути предмет_цілого_помножити(М, ліво як адреса<ПредметЦілого>, право, місцезнаходження);
    }

    якщо ліво.тип == М.предмет_структури_Дробове {
      вернути предмет_дробового_помножити(М, ліво як адреса<ПредметДробового>, право, місцезнаходження);
    }

    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати множення можна лише між числами.") як адреса<Предмет>,
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_поділити(
    М: адреса<Машина>,
    ліво: адреса<Предмет>,
    право: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо ліво.тип == М.предмет_структури_Ціле {
      вернути предмет_цілого_поділити(М, ліво як адреса<ПредметЦілого>, право, місцезнаходження);
    }

    якщо ліво.тип == М.предмет_структури_Дробове {
      вернути предмет_дробового_поділити(М, ліво як адреса<ПредметДробового>, право, місцезнаходження);
    }

    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати ділення можна лише між числами.") як адреса<Предмет>,
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_остача(
    М: адреса<Машина>,
    ліво: адреса<Предмет>,
    право: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо ліво.тип == М.предмет_структури_Ціле {
      вернути предмет_цілого_остача(М, ліво як адреса<ПредметЦілого>, право, місцезнаходження);
    }

    якщо ліво.тип == М.предмет_структури_Дробове {
      вернути предмет_дробового_остача(М, ліво як адреса<ПредметДробового>, право, місцезнаходження);
    }

    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати отримання остачі ділення можна лише між числами.") як адреса<Предмет>,
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_степінь(
    М: адреса<Машина>,
    ліво: адреса<Предмет>,
    право: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо ліво.тип == М.предмет_структури_Ціле {
      вернути предмет_цілого_степінь(М, ліво як адреса<ПредметЦілого>, право, місцезнаходження);
    }

    якщо ліво.тип == М.предмет_структури_Дробове {
      вернути предмет_дробового_степінь(М, ліво як адреса<ПредметДробового>, право, місцезнаходження);
    }

    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати піднесення до степеня можна лише між числами.") як адреса<Предмет>,
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_вліво(
    М: адреса<Машина>,
    ліво: адреса<Предмет>,
    право: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо ліво.тип == М.предмет_структури_Ціле {
      вернути предмет_цілого_вліво(М, ліво як адреса<ПредметЦілого>, право, місцезнаходження);
    }

    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати двійкове зсув вліво можна лише між цілими числами.") як адреса<Предмет>,
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_вправо(
    М: адреса<Машина>,
    ліво: адреса<Предмет>,
    право: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо ліво.тип == М.предмет_структури_Ціле {
      вернути предмет_цілого_вправо(М, ліво як адреса<ПредметЦілого>, право, місцезнаходження);
    }

    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати двійкове зсув вправо можна лише між цілими числами.") як адреса<Предмет>,
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_двійкове_і(
    М: адреса<Машина>,
    ліво: адреса<Предмет>,
    право: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо ліво.тип == М.предмет_структури_Ціле {
      вернути предмет_цілого_двійкове_і(М, ліво як адреса<ПредметЦілого>, право, місцезнаходження);
    }

    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати двійкове І можна лише між цілими числами.") як адреса<Предмет>,
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_двійкове_або(
    М: адреса<Машина>,
    ліво: адреса<Предмет>,
    право: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо ліво.тип == М.предмет_структури_Ціле {
      вернути предмет_цілого_двійкове_або(М, ліво як адреса<ПредметЦілого>, право, місцезнаходження);
    }

    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати двійкове АБО можна лише між цілими числами.") як адреса<Предмет>,
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_двійкове_вабо(
    М: адреса<Машина>,
    ліво: адреса<Предмет>,
    право: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо ліво.тип == М.предмет_структури_Ціле {
      вернути предмет_цілого_двійкове_вабо(М, ліво як адреса<ПредметЦілого>, право, місцезнаходження);
    }

    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати двійкове ВАБО можна лише між цілими числами.") як адреса<Предмет>,
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_менше(
    М: адреса<Машина>,
    ліво: адреса<Предмет>,
    право: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо ліво.тип == М.предмет_структури_Ціле {
      вернути предмет_цілого_менше(М, ліво як адреса<ПредметЦілого>, право, місцезнаходження);
    }

    якщо ліво.тип == М.предмет_структури_Дробове {
      вернути предмет_дробового_менше(М, ліво як адреса<ПредметДробового>, право, місцезнаходження);
    }

    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати перевірку чи менше можна лише між числами.") як адреса<Предмет>,
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_більше(
    М: адреса<Машина>,
    ліво: адреса<Предмет>,
    право: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо ліво.тип == М.предмет_структури_Ціле {
      вернути предмет_цілого_більше(М, ліво як адреса<ПредметЦілого>, право, місцезнаходження);
    }

    якщо ліво.тип == М.предмет_структури_Дробове {
      вернути предмет_дробового_більше(М, ліво як адреса<ПредметДробового>, право, місцезнаходження);
    }

    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати перевірку чи більше можна лише між числами.") як адреса<Предмет>,
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_двійкове_не(
    М: адреса<Машина>,
    значення: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо значення.тип == М.предмет_структури_Ціле {
      вернути предмет_цілого_двійкове_не(М, значення як адреса<ПредметЦілого>, місцезнаходження);
    }

    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати двійкове НЕ можна лише для цілого числа.") як адреса<Предмет>,
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_змінити_знак(
    М: адреса<Машина>,
    значення: адреса<Предмет>,
    місцезнаходження: Місцезнаходження
  ) -> ПредметАбоСтанПадіння {
    якщо значення.тип == М.предмет_структури_Ціле {
      вернути предмет_цілого_змінити_знак(М, значення як адреса<ПредметЦілого>, місцезнаходження);
    }

    якщо значення.тип == М.предмет_структури_Дробове {
      вернути предмет_дробового_змінити_знак(М, значення як адреса<ПредметДробового>, місцезнаходження);
    }

    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати зміну знаку можна лише для чисел.") як адреса<Предмет>,
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія перевірити_чи_значення_рівні(
    М: адреса<Машина>,
    а: адреса<Предмет>,
    б: адреса<Предмет>
  ) -> логічне {
    якщо а == пусто {
      якщо б == пусто {
        вернути так;
      }

      вернути ні;
    }

    якщо б == пусто {
      вернути ні;
    }

    якщо а == М.значення_дійсне {
      якщо б == М.значення_дійсне {
        вернути так;
      }

      вернути ні;
    }

    якщо а == б {
      вернути так;
    }

    якщо а.тип == б.тип {
      якщо а.тип == М.предмет_структури_Ціле {
        вернути ДЧ::порівняти_цілі((а як адреса<ПредметЦілого>).значення, (б як адреса<ПредметЦілого>).значення) == 0;
      }

      якщо а.тип == М.предмет_структури_Дробове {
        вернути (а як адреса<ПредметДробового>).значення == (б як адреса<ПредметДробового>).значення;
      }

      якщо а.тип == М.предмет_структури_Текст {
        вернути перевірити_чи_предмети_тексту_рівні(
          М,
          а як адреса<ПредметТексту>,
          б як адреса<ПредметТексту>
        );
      }

      вернути ні;
    }

    якщо а.тип == М.предмет_структури_Ціле {
      якщо б.тип == М.предмет_структури_Дробове {
        // вернути д64((а як адреса<ПредметЦілого>).значення) == (б як адреса<ПредметДробового>).значення;
      }
    }

    якщо а.тип == М.предмет_структури_Дробове {
      якщо б.тип == М.предмет_структури_Ціле {
        // вернути (а як адреса<ПредметДробового>).значення == д64((б як адреса<ПредметЦілого>).значення);
      }
    }

    вернути ні;
  }

  місцева дія перевірити_чи_значення_є(
    М: адреса<Машина>,
    значення: адреса<Предмет>,
    предмет_структури: адреса<ПредметСтруктури>
  ) -> логічне {
    якщо значення == пусто {
      вернути ні;
    }

    якщо значення == М.значення_дійсне {
      вернути ні;
    }

    якщо значення.тип == М.предмет_структури_Ціле {
      якщо предмет_структури == М.предмет_структури_Ціле {
        вернути так;
      }

      якщо предмет_структури == М.предмет_структури_Число {
        вернути так;
      }

      вернути ні;
    }

    якщо значення.тип == М.предмет_структури_Дробове {
      якщо предмет_структури == М.предмет_структури_Дробове {
        вернути так;
      }

      якщо предмет_структури == М.предмет_структури_Число {
        вернути так;
      }

      вернути ні;
    }

    вернути перевірити_чи_предмет_є(
      М,
      значення,
      предмет_структури
    );
  }

  місцева дія перевірити_чи_предмет_є(
    М: адреса<Машина>,
    а: адреса<Предмет>,
    б: адреса<ПредметСтруктури>
  ) -> логічне {
    змінна тип_а = а.тип;

  крок_перевірки_типу_а:
    якщо тип_а == б {
      вернути так;
    }

    якщо тип_а == пусто {
      вернути ні;
    }

    тип_а = тип_а.предок;

    стрибнути крок_перевірки_типу_а;
  }
}