взяти визначення мавка;
взяти визначення мавка/бібліотека;

зовнішня дія запустити_мавку(кількість_аргументів: ц32, аргументи: памʼять<ю8>): ц32 {
  ціль обчислювач = мавка::створити_обчислювач();

  ціль результат_визначення_глобальних = мавка::визначити_глобальні(обчислювач);
  якщо мавка::перевірити_чи_стан_падіння(обчислювач) {
    ціль інформація_падіння = мавка::отримати_інформацію_падіння(обчислювач);
    мавка::надрукувати_падіння(обчислювач, інформація_падіння, результат_визначення_глобальних);
    мавка::знищити_інформацію_падіння(обчислювач, інформація_падіння);
    мавка::знищити_обчислювач(обчислювач);
    вернути 1;
  }

  мавка::встановити_обробник_взяття_біб(обчислювач, мавка::бібліотека::обробник_взяття_біб);

  якщо кількість_аргументів == 1 {
    мавка::почати_діалог(обчислювач);
    мавка::знищити_обчислювач(обчислювач);
    вернути 0;
  }

  ціль результат = мавка::взяти_файл_шлях_ю8(обчислювач, аргументи[1]);
  якщо мавка::перевірити_чи_стан_падіння(обчислювач) {
    ціль інформація_падіння = мавка::отримати_інформацію_падіння(обчислювач);
    мавка::надрукувати_падіння(обчислювач, інформація_падіння, результат);
    мавка::знищити_інформацію_падіння(обчислювач, інформація_падіння);
    мавка::знищити_обчислювач(обчислювач);
    вернути 1;
  }

  мавка::знищити_обчислювач(обчислювач);

  вернути 0;
}