взяти означення пристрій_мавки;

зовнішня дія старт(кількість_аргументів: природне, аргументи: памʼять<ю8>) -> ц32 {
  змінна успіх: логічне;
  змінна абсолютний_шлях_до_файлу_модуля: адреса<мавка::ПредметДаних>;
  змінна шлях_до_теки_модулів: адреса<мавка::ПредметДаних>;
  змінна шлях_до_теки_паків: адреса<мавка::ПредметДаних>;
  змінна назва_модуля: адреса<мавка::ПредметТексту>;
  змінна М: адреса<мавка::Машина>;
  змінна кількість_назв_взяти: природне;
  змінна назви_взяти: памʼять<адреса<мавка::ПредметТексту>>;
  змінна інформація_падіння: мавка::ІнформаціяПадіння;
  
  М = мавка::пристрій::створити_машину();

  успіх = мавка::пристрій::розібрати_шлях_початкового_модуля(
    М,
    мавка::створити_дані(М, аргументи[1].розмір, аргументи[1].дані),
    абсолютний_шлях_до_файлу_модуля::адреса,
    шлях_до_теки_модулів::адреса,
    шлях_до_теки_паків::адреса,
    назва_модуля::адреса
  );

  якщо не успіх {
    мавка::вивести_кд(М, "Неможливо виконати файл \"");
    пристрій_мавки_вивести_ю8(аргументи[1].розмір, аргументи[1].дані);
    мавка::вивести_кд(М, "\"\р");
    вернути 1;
  }

  мавка::пристрій::записати_шлях_до_теки_модулів_та_паків(
    М,
    шлях_до_теки_модулів,
    шлях_до_теки_паків,
  );
  
  мавка::пристрій::визначити_глобальні_виводу(М);

  кількість_назв_взяти = 1;
  назви_взяти = мавка::виділити_памʼять<адреса<мавка::ПредметТексту>>(М, кількість_назв_взяти);
  назви_взяти[0] = назва_модуля;

  мавка::взяти_модуль(
    М,
    абсолютний_шлях_до_файлу_модуля,
    ні,
    кількість_назв_взяти,
    назви_взяти,
    мавка::Місцезнаходження { абсолютний_шлях_до_файлу_модуля, 0 }
  );

  якщо мавка::отримати_інформацію_падіння(М, інформація_падіння::адреса) {
    мавка::пристрій::надрукувати_інформацію_падіння(
      М,
      інформація_падіння
    );

    вернути 1;
  }

  вернути 0;
}