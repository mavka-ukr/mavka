взяти визначення мавка;
взяти визначення мавка/бібліотека;

структура Рушій;

синонім ОбробникПодіїРушія = (рушій: адреса<Рушій>, дані_обробника_події: невідома_адреса) -> ц32;

зовнішня дія мавка_біб_отримати_рушій(): адреса<Рушій>;
зовнішня дія мавка_біб_запустити_рушій(рушій: адреса<Рушій>, обробник_події: ОбробникПодіїРушія, дані_обробника_події: невідома_адреса);
зовнішня дія мавка_біб_знищити_рушій(рушій: адреса<Рушій>);

дія обробник_рушія(рушій: адреса<Рушій>, дані_обробника_події: невідома_адреса): ц32 {
  ціль обчислювач = дані_обробника_події як адреса<мавка::Обчислювач>;

  мавка::обробити_чергу_запланованих_виконань(обчислювач);

  вернути 0;
}

зовнішня дія запустити_мавку(кількість_аргументів: ц32, аргументи: памʼять<ю8>): ц32 {
  ціль рушій = мавка_біб_отримати_рушій();

  ціль обчислювач = мавка::створити_обчислювач();

  ціль результат_визначення_глобальних = мавка::визначити_глобальні(обчислювач);
  якщо мавка::перевірити_чи_стан_падіння(обчислювач) {
    ціль інформація_падіння = мавка::отримати_інформацію_падіння(обчислювач);
    мавка::надрукувати_падіння(обчислювач, інформація_падіння, результат_визначення_глобальних);
    мавка::знищити_інформацію_падіння(обчислювач, інформація_падіння);
    мавка::знищити_обчислювач(обчислювач);
    мавка_біб_знищити_рушій(рушій);
    вернути 1;
  }

  мавка::встановити_обробник_взяття_біб(обчислювач, мавка::бібліотека::обробник_взяття_біб);

  якщо кількість_аргументів == 1 {
    мавка::вивести_ю8(обчислювач, ю8"Діалог Мавки ");
    мавка::надрукувати_т8(обчислювач, мавка::отримати_версію_мавки_як_т8());
    мавка::почати_діалог(обчислювач);
    мавка::знищити_обчислювач(обчислювач);
    мавка_біб_знищити_рушій(рушій);
    вернути 0;
  }

  ціль результат_взяття = мавка::взяти_файл_шлях_ю8(обчислювач, аргументи[1]);
  якщо мавка::перевірити_чи_стан_падіння(обчислювач) {
    ціль інформація_падіння = мавка::отримати_інформацію_падіння(обчислювач);
    мавка::надрукувати_падіння(обчислювач, інформація_падіння, результат_взяття);
    мавка::знищити_інформацію_падіння(обчислювач, інформація_падіння);
    мавка::знищити_обчислювач(обчислювач);
    мавка_біб_знищити_рушій(рушій);
    вернути 1;
  }

  мавка::обробити_чергу_запланованих_виконань(обчислювач);

  мавка_біб_запустити_рушій(рушій, обробник_рушія, обчислювач);

  мавка::знищити_обчислювач(обчислювач);

  мавка_біб_знищити_рушій(рушій);

  вернути 0;
}