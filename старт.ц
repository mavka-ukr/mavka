взяти означення мавка;

структура Шлях {
  розмір: природне;
  дані: памʼять<п8>;
}

структура ВихідФайлу {
  розмір: природне;
  дані: памʼять<п8>;
}

зовнішня дія __вивести_формат(значення: природне) -> ніщо;
зовнішня дія __вивести_ю8(значення: адреса<ю8>) -> ніщо;
зовнішня дія __вивести_д64(значення: д64) -> ніщо;
зовнішня дія __надрукувати_ю8(значення: адреса<ю8>) -> ніщо;
зовнішня дія __надрукувати_д64(значення: д64) -> ніщо;
зовнішня дія __виділити_сиру_памʼять(розмір: природне) -> памʼять<п8>;
зовнішня дія __перевиділити_сиру_памʼять(значення: памʼять<п8>, новий_розмір: природне) -> памʼять<п8>;
зовнішня дія __звільнити_сиру_памʼять(значення: памʼять<п8>) -> ніщо;
зовнішня дія __вийти(код: ц32) -> ніщо;
зовнішня дія __прочитати_файл(шлях: адреса<Шлях>, вихід_файлу: адреса<ВихідФайлу>) -> логічне;
зовнішня дія __перетворити_п64_в_ю8(значення: п64, вихід_ю8: адреса<ю8>) -> логічне;
зовнішня дія __перетворити_ц64_в_ю8(значення: ц64, вихід_ю8: адреса<ю8>) -> логічне;
зовнішня дія __перетворити_д64_в_ю8(значення: д64, вихід_ю8: адреса<ю8>) -> логічне;
зовнішня дія __піднести_до_степеня_п64(значення: п64, степінь: п64) -> п64;
зовнішня дія __піднести_до_степеня_ц64(значення: ц64, степінь: ц64) -> ц64;
зовнішня дія __піднести_до_степеня_д64(значення: д64, степінь: д64) -> д64;

дія машина_виділити_сиру_памʼять(М: адреса<мавка::Машина>, розмір: природне) -> памʼять<п8> {
  вернути __виділити_сиру_памʼять(розмір);
}

дія машина_перевиділити_сиру_памʼять(М: адреса<мавка::Машина>, значення: памʼять<п8>, новий_розмір: природне) -> памʼять<п8> {
  вернути __перевиділити_сиру_памʼять(значення, новий_розмір);
}

дія машина_звільнити_сиру_памʼять(М: адреса<мавка::Машина>, значення: памʼять<п8>) {
  __звільнити_сиру_памʼять(значення);
}

дія машина_запанікувати(М: адреса<мавка::Машина>, повідомлення: кд) {
  мавка::надрукувати(М, повідомлення);

  __вийти(1);
}

дія машина_вивести(М: адреса<мавка::Машина>, значення: кд) {
  змінна значення_ю8: ю8;
  
  якщо мавка::перекодувати_кд_в_ю8(М, значення, значення_ю8::адреса, пусто) {
    __вивести_ю8(значення_ю8::адреса);

    мавка::звільнити_памʼять(М, значення_ю8.дані);
  } інакше {
    __надрукувати_ю8(ю8"Помилка виводу тексту"::адреса);
  }
}

дія машина_вивести_формат(М: адреса<мавка::Машина>, значення: природне) {
  __вивести_формат(значення);
}

дія машина_надрукувати(М: адреса<мавка::Машина>, значення: кд) {
  змінна значення_ю8: ю8;
  
  якщо мавка::перекодувати_кд_в_ю8(М, значення, значення_ю8::адреса, пусто) {
    __надрукувати_ю8(значення_ю8::адреса);

    мавка::звільнити_памʼять(М, значення_ю8.дані);
  } інакше {
    __надрукувати_ю8(ю8"Помилка друку тексту"::адреса);
  }
}

дія машина_перетворити_п64_в_кд(М: адреса<мавка::Машина>, значення: п64, вихід_текстового_значення: адреса<кд>) -> логічне {
  змінна значення_ю8: ю8;

  якщо __перетворити_п64_в_ю8(значення, значення_ю8::адреса) {
    змінна значення_кд: кд;

    якщо мавка::перекодувати_ю8_в_кд(М, значення_ю8, значення_кд::адреса, пусто) {
      вихід_текстового_значення.розмір = значення_кд.розмір;
      вихід_текстового_значення.дані = значення_кд.дані;

      мавка::звільнити_памʼять(М, значення_ю8.дані);

      вернути так;
    }

    мавка::звільнити_памʼять(М, значення_ю8.дані);  
  }

  вернути ні;
}

дія машина_перетворити_ц64_в_кд(М: адреса<мавка::Машина>, значення: ц64, вихід_текстового_значення: адреса<кд>) -> логічне {
  змінна значення_ю8: ю8;

  якщо __перетворити_ц64_в_ю8(значення, значення_ю8::адреса) {
    змінна значення_кд: кд;

    якщо мавка::перекодувати_ю8_в_кд(М, значення_ю8, значення_кд::адреса, пусто) {
      вихід_текстового_значення.розмір = значення_кд.розмір;
      вихід_текстового_значення.дані = значення_кд.дані;

      мавка::звільнити_памʼять(М, значення_ю8.дані);

      вернути так;
    }

    мавка::звільнити_памʼять(М, значення_ю8.дані);  
  }

  вернути ні;
}

дія машина_перетворити_д64_в_кд(М: адреса<мавка::Машина>, значення: д64, вихід_текстового_значення: адреса<кд>) -> логічне {
  змінна значення_ю8: ю8;

  якщо __перетворити_д64_в_ю8(значення, значення_ю8::адреса) {
    змінна значення_кд: кд;

    якщо мавка::перекодувати_ю8_в_кд(М, значення_ю8, значення_кд::адреса, пусто) {
      вихід_текстового_значення.розмір = значення_кд.розмір;
      вихід_текстового_значення.дані = значення_кд.дані;

      мавка::звільнити_памʼять(М, значення_ю8.дані);

      вернути так;
    }

    мавка::звільнити_памʼять(М, значення_ю8.дані);  
  }

  вернути ні;
}

дія машина_піднести_до_степеня_п64(М: адреса<мавка::Машина>, значення: п64, степінь: п64) -> п64 {
  вернути __піднести_до_степеня_п64(значення, степінь);
}

дія машина_піднести_до_степеня_ц64(М: адреса<мавка::Машина>, значення: ц64, степінь: ц64) -> ц64 {
  вернути __піднести_до_степеня_ц64(значення, степінь);
}

дія машина_піднести_до_степеня_д64(М: адреса<мавка::Машина>, значення: д64, степінь: д64) -> д64 {
  вернути __піднести_до_степеня_д64(значення, степінь);
}

дія машина_дія_взяття(
  М: адреса<мавка::Машина>,
  назва_паку: адреса<мавка::ПредметТексту>,
  звідки: адреса<мавка::ПредметТексту>,
  довжина_шляху: природне,
  шлях: памʼять<адреса<мавка::ПредметТексту>>
) -> мавка::ЗначенняАбоСтанПадіння {
  мавка::звільнити_памʼять(М, шлях);

  вернути пусто;
}

дія дія_виводу(М: адреса<мавка::Машина>, значення: кд) {
  мавка::вивести(М, значення);
}

зовнішня дія старт(кількість_аргументів: природне, аргументи: памʼять<ю8>) -> ц32 {
  змінна М = мавка::створити_машину(мавка::Налаштування {
    виділити_сиру_памʼять = машина_виділити_сиру_памʼять,
    перевиділити_сиру_памʼять = машина_перевиділити_сиру_памʼять,
    звільнити_сиру_памʼять = машина_звільнити_сиру_памʼять,
    запанікувати = машина_запанікувати,
    вивести = машина_вивести,
    вивести_формат = машина_вивести_формат,
    перетворити_п64_в_кд = машина_перетворити_п64_в_кд,
    перетворити_ц64_в_кд = машина_перетворити_ц64_в_кд,
    перетворити_д64_в_кд = машина_перетворити_д64_в_кд,
    піднести_до_степеня_п64 = машина_піднести_до_степеня_п64,
    піднести_до_степеня_ц64 = машина_піднести_до_степеня_ц64,
    піднести_до_степеня_д64 = машина_піднести_до_степеня_д64,
    дія_взяття = пусто
  });

  мавка::визначити_глобальні_виводу(М);

  змінна вихід_файлу: ВихідФайлу;
  змінна шлях_до_файлу = ю8"привіт.м";
  змінна шлях = шлях_до_файлу::адреса як адреса<Шлях>;
  якщо не __прочитати_файл(шлях, вихід_файлу::адреса) {
    мавка::надрукувати(М, "Не вдалося прочитати файл привіт.м");
    вернути 1;
  }

  змінна сирий_текст: кд;
  якщо не мавка::перекодувати_ю8_в_кд(М, ю8 { вихід_файлу.розмір, вихід_файлу.дані }, сирий_текст::адреса, пусто) {
    мавка::надрукувати(М, "Помилка перекодування файлу привіт.м в кд");
    вернути 1;
  }
  
  змінна слова: мавка::розбирач::Слова;
  змінна помилка_розбору_на_слова: мавка::розбирач::ПомилкаРозборуНаСлова;
  
  змінна успіх = мавка::розбирач::розібрати_на_слова(
    М, 
    сирий_текст, 
    слова::адреса, 
    помилка_розбору_на_слова::адреса
  );
  якщо не успіх {
    мавка::надрукувати(М, "Помилка розбору на слова");
    вернути 1;
  }

  змінна сполуки: мавка::розбирач::Сполуки;
  змінна помилка_розбору_на_сполуки: мавка::розбирач::ПомилкаРозборуНаСполуки;
  успіх = мавка::розбирач::розібрати_слова_на_сполуки(
    М,
    сирий_текст,
    слова,
    сполуки::адреса,
    помилка_розбору_на_сполуки::адреса
  );
  якщо не успіх {
    мавка::надрукувати(М, "Помилка розбору на сполуки");
    вернути 1;
  }

  змінна перекладений_код: адреса<мавка::ПредметКоду> = пусто;
  змінна помилка_перекладу: мавка::перекладач::ПомилкаПерекладу;

  змінна успіх_перекладу = мавка::перекласти_в_код(
    М,
    мавка::створити_дані(М, шлях_до_файлу.розмір, шлях_до_файлу.дані),
    сирий_текст,
    слова,
    сполуки,
    перекладений_код::адреса,
    помилка_перекладу::адреса
  );
  якщо не успіх_перекладу {
    мавка::надрукувати(М, "Помилка перекладу: ");
    мавка::надрукувати(М, помилка_перекладу.повідомлення);
    вернути 1;
  }
  
  змінна властивості_коду = перекладений_код як адреса<мавка::ПредметКоду>;
  змінна властивості_даних_коду = властивості_коду.вказівки як адреса<мавка::ПредметДаних>;
  // мавка::надрукувати_код(М, властивості_даних_коду.розмір, властивості_даних_коду.дані, дія_виводу);

  змінна д = мавка::створити_дію(
    М,
    М.глобальна_дійсність,
    пусто,
    пусто,
    пусто,
    перекладений_код як адреса<мавка::ПредметКоду>
  );

  змінна результат = мавка::здійснити(
    М,
    пусто,
    д як мавка::Значення,
    0,
    пусто,
    пусто,
    мавка::Місцезнаходження { пусто, 0 }
  );
  якщо М.стан_падіння {
    змінна інформація_падіння: мавка::ІнформаціяПадіння;
    якщо мавка::отримати_інформацію_падіння(М, інформація_падіння::адреса) {  
      якщо інформація_падіння.місцезнаходження.шлях != пусто {
        __вивести_ю8((ю8 { інформація_падіння.місцезнаходження.шлях.розмір, інформація_падіння.місцезнаходження.шлях.дані })::адреса);
  
        якщо інформація_падіння.місцезнаходження.рядок > 0 {
          мавка::вивести(М, ":");
          мавка::вивести_п64(М, п64(інформація_падіння.місцезнаходження.рядок));
        }
        
        мавка::вивести(М, " ");
      }

      мавка::вивести_формат(М, мавка::ФКолірТекстуЧервоний);
      мавка::вивести(М, "падіння");
      мавка::вивести_формат(М, мавка::ФОчистити);
      мавка::вивести(М, ": ");
      мавка::надрукувати_значення(М, інформація_падіння.значення, 2, 0);

      змінна п: природне = 0;
      
      якщо інформація_падіння.розмір_історії > 0 {
        мавка::надрукувати(М, "\рІсторія здійснення:");
      }

      поки п < інформація_падіння.розмір_історії {
        змінна елемент_історії = інформація_падіння.історія[п];
        
        якщо елемент_історії.назва != пусто {
          мавка::вивести(М, "  ");
          мавка::вивести_формат(М, мавка::ФКолірТекстуСиній);
          мавка::вивести(М, елемент_історії.назва.значення);
          мавка::вивести_формат(М, мавка::ФОчистити);
        
          якщо елемент_історії.місцезнаходження.шлях == пусто {
            мавка::вивести(М, "\р");
          } інакше {
            мавка::вивести(М, " в ");
            __вивести_ю8((ю8 { елемент_історії.місцезнаходження.шлях.розмір, елемент_історії.місцезнаходження.шлях.дані })::адреса);
  
            якщо елемент_історії.місцезнаходження.рядок > 0 {
              мавка::вивести(М, ":");
              мавка::вивести_п64(М, п64(елемент_історії.місцезнаходження.рядок));
            }
            
            мавка::вивести(М, "\р");
          }
        }
        
        п += 1;
      }
    }
    вернути 1;
  }

  вернути 0;
}