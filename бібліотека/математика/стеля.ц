взяти означення мавка/бібліотека;

простір мавка {
  простір бібліотека {
    дія рідна_дія__стеля(
      М: адреса<Машина>,
      здійснювач: Значення,
      предмет_дії: адреса<ПредметДії>,
      задіяні: Задіяні
    ) -> ЗначенняАбоСтанПадіння {
      змінна задіяне_значення: Значення;
      змінна знайдено_задіяне_значення = ні;

      знайдено_задіяне_значення = знайти_задіяне(М, задіяні, 0, М.константи_назв._значення, задіяне_значення::адреса);

      якщо не знайдено_задіяне_значення {
        увімкнути_стан_падіння(
          М,
          значення_предмет(створити_текст(М, "Задіяне \"значення\" має бути цілим або дробовим.")),
          Місцезнаходження { пусто, 0 }
        );
        вернути значення_недійсне();
      }

      якщо задіяне_значення.вид == ЗЦіле {
        вернути задіяне_значення;
      } інакше якщо задіяне_значення.вид == ЗДробове {
        вернути значення_ціле(ц64(бібліотека_мавки_стеля_д64(задіяне_значення.дані як д64)));
      } інакше {
        увімкнути_стан_падіння(
          М,
          значення_предмет(створити_текст(М, "Задіяне \"значення\" має бути цілим або дробовим.")),
          Місцезнаходження { пусто, 0 }
        );
        вернути значення_недійсне();
      }
    }

    місцева дія записати_дію__математика__стеля(
      М: адреса<Машина>,
      предмет_модуля: адреса<ПредметМодуля>
    ) {
      змінна предмет_дії = створити_рідну_дію(
        М,
        М.константи_назв._стеля,
        М.предмет_пустої_послідовності,
        пусто,
        рідна_дія__стеля,
        пусто
      );

      предмет_модуля_змінити_властивість(
        М,
        предмет_модуля,
        М.константи_назв._стеля,
        значення_предмет(предмет_дії),
        Місцезнаходження { пусто, 0 }
      );

      якщо М.стан_падіння {
        запанікувати(М, "Помилка при записі дії \"стеля\" для модуля бібліотеки \"математика\".");
      }
    }
  }
}