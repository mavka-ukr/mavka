взяти означення мавка/бібліотека;
взяти означення мавка/пристрій;

простір мавка {
  простір бібліотека {
    дія рідна_дія_читати(
      М: адреса<Машина>,
      здійснювач: Значення,
      предмет_дії: адреса<ПредметДії>,
      задіяні: Задіяні
    ) -> ЗначенняАбоСтанПадіння {
      змінна задіяне_перед: Значення;
      змінна знайдено_задіяне_перед= ні;

      знайдено_задіяне_перед = знайти_задіяне(М, задіяні, 0, М.константи_назв._перед, задіяне_перед::адреса);

      змінна перед = кд { 0, пусто };

      якщо знайдено_задіяне_перед {
        якщо задіяне_перед.вид == ЗПредмет {
          змінна предмет_тексту = задіяне_перед.дані як адреса<ПредметТексту>;

          якщо предмет_тексту.тип != М.предмет_структури_Текст {
            увімкнути_стан_падіння(
              М,
              значення_предмет(створити_текст(М, "Задіяне \"перед\" має бути текстом.")),
              Місцезнаходження { пусто, 0 }
            );

            вернути значення_недійсне();
          }

          перед = предмет_тексту.значення;
        } інакше {
          увімкнути_стан_падіння(
            М,
            значення_предмет(створити_текст(М, "Задіяне \"перед\" має бути текстом.")),
            Місцезнаходження { пусто, 0 }
          );

          вернути значення_недійсне();
        }
      }

      змінна значення: кд;
      змінна успіх = ні;
      змінна кінець_файлу = природне(ні);

      успіх = пристрій_мавки_читати_кд(
        перед.розмір,
        перед.дані,
        значення.розмір::адреса,
        значення.дані::адреса,
        природне(так),
        кінець_файлу::адреса
      );

      якщо не успіх {
        увімкнути_стан_падіння(
          М,
          значення_предмет(створити_текст(М, "Не вдалося прочитати зі стандартного вводу.")),
          Місцезнаходження { пусто, 0 }
        );

        вернути значення_недійсне();
      }

      якщо кінець_файлу {
        вернути значення_недійсне();
      }

      вернути значення_предмет(створити_текст_без_копіювання(
        М,
        значення.розмір,
        значення.дані
      ));
    }

    місцева дія створити_дію__читати(М: адреса<Машина>) -> адреса<ПредметДії> {
      // потім: створити параметри

      вернути створити_рідну_дію(
        М,
        створити_текст(М, "читати"),
        М.предмет_пустої_послідовності,
        пусто,
        рідна_дія_читати,
        пусто
      );
    }
  }
}