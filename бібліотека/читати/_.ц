взяти означення мавка/бібліотека;
взяти означення мавка/пристрій;

простір мавка {
  простір бібліотека {
    дія рідна_дія_читати(
      М: адреса<Машина>,
      діяльність: адреса<Діяльність>
    ) -> ЗначенняАбоСтанПадіння {
      змінна задіяне_перед = НЕДІЙСНЕ;
      змінна знайдено_задіяне_перед= ні;

      знайдено_задіяне_перед = знайти_задіяне(М, діяльність.задіяні, 0, М.константи_назв._перед, задіяне_перед::адреса);

      змінна перед = кд { 0, пусто };

      якщо знайдено_задіяне_перед {
        якщо задіяне_перед.вид == ЗПредмет {
          змінна предмет_задіяне_перед = задіяне_перед.дані як адреса<Предмет> як адреса<ПредметТексту>;

          якщо предмет_задіяне_перед.тип != М.предмет_структури_Текст {
            вернути ПАДІННЯ(М, створити_текст(М, "Неможливо здійснити дію \"читати\"."));
          }

          перед = предмет_задіяне_перед.значення;
        } інакше {
          вернути ПАДІННЯ(М, створити_текст(М, "Задіяне \"перед\" має бути текстом."));
        }
      }

      змінна значення: кд;
      змінна успіх = ні;
      змінна кінець_файлу = природне(ні);

      успіх = пристрій_мавки_читати_кд(
        перед.розмір,
        перед.дані,
        значення.розмір::адреса,
        значення.дані::адреса,
        природне(так),
        кінець_файлу::адреса
      );

      якщо не успіх {
        вернути ПАДІННЯ(М, створити_текст(М, "Не вдалося прочитати зі стандартного вводу."));
      }

      якщо кінець_файлу {
        вернути НЕДІЙСНЕ;
      }

      вернути ПРЕДМЕТ(створити_текст_без_копіювання(
        М,
        значення.розмір,
        значення.дані
      ));
    }

    місцева дія створити_дію__читати(М: адреса<Машина>) -> адреса<ПредметДії> {
      // потім: створити параметри

      вернути створити_рідну_дію(
        М,
        створити_текст(М, "читати"),
        М.предмет_пустої_послідовності,
        пусто,
        рідна_дія_читати,
        пусто
      );
    }
  }
}