взяти означення мавка/бібліотека;
взяти означення мавка/пристрій;

простір мавка {
  простір бібліотека {
    дія машина_взяти_біб(
      М: адреса<Машина>,
      довжина_шляху: природне,
      шлях: памʼять<адреса<ПредметТексту>>,
      місцезнаходження: Місцезнаходження
    ) -> ПредметАбоСтанПадіння {
      змінна кдбм = М.користувацькі_дані_бібліотеки.значення як адреса<КористувацькіДаніБібліотекиМавки>;

      якщо довжина_шляху == 1 {
        якщо шлях[0] == М.константи_назв._математика або перевірити_чи_предмети_тексту_рівні(М, шлях[0], М.константи_назв._математика) {
          якщо кдбм.взято__математика == пусто {
            змінна предмет_модуля_математика = створити_модуль(
              М,
              М.константи_назв._математика
            );

            кдбм.взято__математика = притримати_глобально(
              М,
              предмет_модуля_математика як адреса<Предмет>
            );

            ініціалізувати__математика(М, предмет_модуля_математика);

            якщо М.стан_падіння {
              відпустити_притримане_глобально(М, кдбм.взято__математика);
              кдбм.взято__математика = пусто;

              вернути пусто;
            }
          }

          вернути кдбм.взято__математика.значення;
        }

        якщо шлях[0] == М.константи_назв._читати або перевірити_чи_предмети_тексту_рівні(М, шлях[0], М.константи_назв._читати) {
          якщо кдбм.взято__читати == пусто {
            змінна предмет_дії_читати = створити_дію__читати(М);

            кдбм.взято__читати = притримати_глобально(
              М,
              предмет_дії_читати як адреса<Предмет>
            );
          }

          вернути кдбм.взято__читати.значення;
        }
      }

      звільнити_памʼять(М, шлях);

      вернути пусто;
    }

    дія дія_перед_знищенням_користувацьких_даних_бібліотеки_мавки(
      М: адреса<Машина>,
      користувацькі_дані: адреса<КористувацькіДані>
    ) {
      змінна кдбм = користувацькі_дані.значення як адреса<КористувацькіДаніБібліотекиМавки>;

      // потім: відпустити взяті і звільнити кдбм
    }

    місцева дія ініціалізувати(М: адреса<Машина>) {
      М.налаштування.взяти_біб = машина_взяти_біб;

      змінна кдбм = виділити<КористувацькіДаніБібліотекиМавки>(М);
      кдбм.взято__математика = пусто;
      кдбм.взято__читати = пусто;

      М.користувацькі_дані_бібліотеки = створити_користувацькі_дані(
        М,
        кдбм,
        дія_перед_знищенням_користувацьких_даних_бібліотеки_мавки
      );
    }
  }
}