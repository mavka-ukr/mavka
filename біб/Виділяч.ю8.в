секція біб {
  структура Виділяч;
}

зовнішня дія __біб__отримати_системний_виділяч(): адреса<біб::Виділяч>;

секція біб {
  структура Виділяч;

  структура Виділяч {
    користувацькі_дані: невідома_адреса;
    виділити_сиру_памʼять: (виділяч: адреса<Виділяч>, розмір: позитивне) -> памʼять<п8>;
    перевиділити_сиру_памʼять: (виділяч: адреса<Виділяч>, значення: памʼять<п8>, новий_розмір: позитивне) -> памʼять<п8>;
    звільнити_сиру_памʼять: (виділяч: адреса<Виділяч>, значення: памʼять<п8>) -> ніщо;
  }

  дія виділити<Т>(виділяч: адреса<Виділяч>): адреса<Т>;
  дія перевиділити<Т>(виділяч: адреса<Виділяч>, значення: адреса<Т>, кількість: позитивне): памʼять<Т>;
  дія звільнити(виділяч: адреса<Виділяч>, значення: невідома_адреса);
  дія виділити_памʼять<Т>(виділяч: адреса<Виділяч>, кількість: позитивне): памʼять<Т>;
  дія перевиділити_памʼять<Т>(виділяч: адреса<Виділяч>, значення: памʼять<Т>, кількість: позитивне): памʼять<Т>;
  дія звільнити_памʼять(виділяч: адреса<Виділяч>, значення: невідома_памʼять);

  дія виділити<Т>(виділяч: адреса<Виділяч>): адреса<Т> {
    вернути виділяч.виділити_сиру_памʼять(виділяч, Т.розмір) як адреса<Т>;
  }

  дія перевиділити<Т>(виділяч: адреса<Виділяч>, значення: адреса<Т>, кількість: позитивне): памʼять<Т> {
    вернути виділяч.перевиділити_сиру_памʼять(виділяч, значення як невідома_адреса, кількість * Т.розмір) як памʼять<Т>;
  }

  дія звільнити(виділяч: адреса<Виділяч>, значення: невідома_адреса) {
    виділяч.звільнити_сиру_памʼять(виділяч, значення як памʼять<п8>);
  }

  дія виділити_памʼять<Т>(виділяч: адреса<Виділяч>, кількість: позитивне): памʼять<Т> {
    вернути виділяч.виділити_сиру_памʼять(виділяч, кількість * Т.розмір) як памʼять<Т>;
  }

  дія перевиділити_памʼять<Т>(виділяч: адреса<Виділяч>, значення: памʼять<Т>, кількість: позитивне): памʼять<Т> {
    вернути виділяч.перевиділити_сиру_памʼять(виділяч, значення як памʼять<п8>, кількість * Т.розмір) як памʼять<Т>;
  }

  дія звільнити_памʼять(виділяч: адреса<Виділяч>, значення: невідома_памʼять) {
    виділяч.звільнити_сиру_памʼять(виділяч, значення як памʼять<п8>);
  }

  дія отримати_системний_виділяч(): адреса<Виділяч> {
    вернути __біб__отримати_системний_виділяч();
  }
}