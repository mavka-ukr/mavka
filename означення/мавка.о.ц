простір мавка {
  структура Місцезнаходження;
  структура Притримане;
  структура Притримувач;
  структура КористувацькіДані;
  структура Дійсне;
  структура Послідовність;
  структура Дійсність;
  структура Предмет;
  структура Машина;

  стала ВДодати = 1;
  стала ВВідняти = 2;
  стала ВПомножити = 3;
  стала ВПоділити = 4;

  структура Місцезнаходження {
    значення: адреса<Дійсне>;
    рядок: природне;
  }

  структура Притримане {
    попереднє: адреса<Притримане>;
    наступне: адреса<Притримане>;
    значення: адреса<Дійсне>;
  }

  структура Притримувач {
    перше: адреса<Притримане>;
    останнє: адреса<Притримане>;
  }

  структура КористувацькіДані {
    значення: невідома_адреса;
    притримувач: адреса<Притримувач>;
    дія_перед_знищенням: (М: адреса<Машина>, користувацькі_дані: адреса<КористувацькіДані>) -> ніщо;
  }

  структура Дійсне {
    попереднє: адреса<Дійсне>;
    позначено: логічне;
    тип: адреса<Дійсне>;
    користувацькі_дані: адреса<КористувацькіДані>;
  }

  структура Послідовність {
    попереднє: адреса<Дійсне>;
    позначено: логічне;
    тип: адреса<Дійсне>;
    користувацькі_дані: адреса<КористувацькіДані>;
    розмір: адреса<Дійсне>;
    дані: памʼять<адреса<Дійсне>>;
  }

  структура Діяч {
    назва: адреса<Дійсне>;
    значення: адреса<Дійсне>;
  }

  структура Дійсність {
    попереднє: адреса<Дійсне>;
    позначено: логічне;
    тип: адреса<Дійсне>;
    зовні: адреса<Дійсність>;
    здійснювач: адреса<Дійсне>;
    кількість_діячів: природне;
    діячі: памʼять<Діяч>;
  }

  структура Властивості {
    розмір: природне;
    ключі: памʼять<адреса<Дійсне>>;
    значення: памʼять<адреса<Дійсне>>;
    вмісткість: природне;
  }

  структура ВластивостіСтруктури {
    назва: адреса<Дійсне>;
    предок: адреса<Дійсне>;
    задіювані: адреса<Послідовність>;
  }

  структура ВластивостіКоду {
    константи: адреса<Послідовність>;
    вказівки: адреса<Дійсне>; // Дані
  }

  синонім РіднаДія = (М: адреса<Машина>,
                      здійснювач: адреса<Дійсне>,
                      значення: адреса<Дійсне>,
                      кількість_задіяних: природне, 
                      задіяні: памʼять<адреса<Дійсне>>,
                      кількість_іменовано_задіяних: природне, 
                      іменовано_задіяні: памʼять<адреса<Дійсне>>) -> адреса<Дійсне>;

  структура ВластивостіДії {
    рідна: логічне;
    назва: адреса<Дійсне>;
    задіювані: адреса<Послідовність>;
    код: адреса<Дійсне> | РіднаДія;
    дійсність_створення: адреса<Дійсність>;
  }

  структура ВластивостіЧисла {
    значення: д64;
  }

  структура ВластивостіТексту {
    значення: кд;
  }

  структура ВластивостіСписку {
    розмір: природне;
    елементи: памʼять<адреса<Дійсне>>;
    вмісткість: природне;
  }

  структура ВластивостіСловника {
    розмір: природне;
    ключі: памʼять<адреса<Дійсне>>;
    значення: памʼять<адреса<Дійсне>>;
    вмісткість: природне;
  }

  структура ВластивостіДаних {
    розмір: природне;
    дані: памʼять<п8>;
  }

  структура ВластивостіЗміннихДаних {
    розмір: природне;
    дані: памʼять<п8>;
    вмісткість: природне;
  }

  структура ВластивостіЗадіюваного {
    назва: адреса<Дійсне>;
  }

  структура Предмет {
    попереднє: адреса<Дійсне>;
    позначено: логічне;
    тип: адреса<Дійсне>;
    користувацькі_дані: адреса<КористувацькіДані>;
    властивості: Властивості
               | ВластивостіСтруктури
               | ВластивостіКоду
               | ВластивостіДії
               | ВластивостіЧисла
               | ВластивостіТексту
               | ВластивостіСписку
               | ВластивостіСловника
               | ВластивостіДаних
               | ВластивостіЗміннихДаних
               | ВластивостіЗадіюваного;
  }

  структура Налаштування {
    виділити_сиру_памʼять: (М: адреса<Машина>, розмір: природне) -> памʼять<п8>;
    перевиділити_сиру_памʼять: (М: адреса<Машина>, значення: памʼять<п8>, новий_розмір: природне) -> памʼять<п8>;
    звільнити_сиру_памʼять: (М: адреса<Машина>, значення: памʼять<п8>) -> ніщо;
    запанікувати: (М: адреса<Машина>, повідомлення: кд) -> ніщо;
  }

  структура НакопичувачЗначень {
    розмір: природне;
    дані: памʼять<адреса<Дійсне>>;
    вмісткість: природне;
  }

  структура Здійснюване {
    здійснювач: адреса<Дійсне>;
    дійсність: адреса<Дійсне>;
    значення: адреса<Дійсне>;
  }

  структура ІсторіяЗдійснення {
    розмір: природне;
    дані: памʼять<Здійснюване>;
    вмісткість: природне;
  }
  
  структура Машина {
    налаштування: Налаштування;
    перше_дійсне: адреса<Дійсне>;
    останнє_дійсне: адреса<Дійсне>;
    значення_дійсне: адреса<Дійсне>;
    значення_логічне: адреса<Дійсне>;
    значення_так: адреса<Дійсне>;
    значення_ні: адреса<Дійсне>;
    значення_послідовність: адреса<Дійсне>;
    значення_середовище: адреса<Дійсне>;
    значення_предмет: адреса<Дійсне>;
    значення_структура_Структура: адреса<Дійсне>;
    значення_структура_Задіюване: адреса<Дійсне>;
    значення_структура_Число: адреса<Дійсне>;
    значення_структура_Текст: адреса<Дійсне>;
    значення_структура_Код: адреса<Дійсне>;
    значення_структура_Дія: адреса<Дійсне>;
    значення_структура_Список: адреса<Дійсне>;
    значення_структура_Словник: адреса<Дійсне>;
    значення_структура_Дані: адреса<Дійсне>;
    значення_структура_ЗмінніДані: адреса<Дійсне>;
    накопичувач_значень: НакопичувачЗначень;
    історія_здійснення: ІсторіяЗдійснення;
    стан_падіння: логічне;
    значення_падіння: адреса<Дійсне>;
    місцезнаходження_падіння: Місцезнаходження;
  }

  дія виділити<Т>(М: адреса<Машина>) -> адреса<Т> {
    вернути М.налаштування.виділити_сиру_памʼять(М, Т.розмір) як адреса<Т>;
  }

  дія виділити_памʼять<Т>(М: адреса<Машина>, кількість: природне) -> памʼять<Т> {
    вернути М.налаштування.виділити_сиру_памʼять(М, кількість * Т.розмір) як памʼять<Т>;
  }

  дія перевиділити_памʼять<Т>(М: адреса<Машина>, значення: невідома_памʼять, нова_кількість: природне) -> памʼять<Т> {
    вернути М.налаштування.перевиділити_сиру_памʼять(М, значення як памʼять<п8>, нова_кількість * Т.розмір) як памʼять<Т>;
  }

  дія звільнити(М: адреса<Машина>, значення: невідома_адреса) -> ніщо {
    М.налаштування.звільнити_сиру_памʼять(М, значення як памʼять<п8>);
  }

  дія звільнити_памʼять(М: адреса<Машина>, значення: невідома_памʼять) -> ніщо {
    М.налаштування.звільнити_сиру_памʼять(М, значення як памʼять<п8>);
  }

  дія запанікувати(М: адреса<Машина>, повідомлення: кд) {
    М.налаштування.запанікувати(М, повідомлення);
  }

  дія виділити_дійсне(М: адреса<Машина>) -> адреса<Дійсне> {
    змінна дійсне = виділити<Дійсне>(М);
    якщо М.перше_дійсне == пусто {
      М.перше_дійсне = дійсне;
    }
    дійсне.попереднє = М.останнє_дійсне;
    М.останнє_дійсне = дійсне;
    дійсне.позначено = ні;
    дійсне.тип = М.значення_дійсне;
    дійсне.користувацькі_дані = пусто;
    вернути дійсне;
  }

  дія виділити_дійсність(М: адреса<Машина>) -> адреса<Дійсність> {
    змінна дійсність = виділити<Дійсність>(М);
    якщо М.перше_дійсне == пусто {
      М.перше_дійсне = дійсність як адреса<Дійсне>;
    }
    дійсність.попереднє = М.останнє_дійсне;
    М.останнє_дійсне = дійсність як адреса<Дійсне>;
    дійсність.позначено = ні;
    дійсність.тип = М.значення_дійсне;
    дійсність.зовні = пусто;
    дійсність.здійснювач = пусто;
    вернути дійсність;
  }

  дія виділити_предмет(М: адреса<Машина>, тип: адреса<Дійсне>) -> адреса<Предмет> {
    змінна предмет = виділити<Предмет>(М);
    якщо М.перше_дійсне == пусто {
      М.перше_дійсне = предмет як адреса<Дійсне>;
    }
    предмет.попереднє = М.останнє_дійсне;
    М.останнє_дійсне = предмет як адреса<Дійсне>;
    предмет.позначено = ні;
    предмет.тип = тип;
    предмет.користувацькі_дані = пусто;
    вернути предмет;
  }

  дія створити_дійсність(М: адреса<Машина>, зовні: адреса<Дійсність>, здійснювач: адреса<Дійсне>) -> адреса<Дійсність> {
    змінна дійсність = виділити_дійсність(М);
    дійсність.зовні = зовні;
    дійсність.здійснювач = здійснювач;
    вернути дійсність;
  }

  дія створити_число(М: адреса<Машина>, значення: д64) -> адреса<Дійсне> {
    змінна предмет = виділити_предмет(М, М.значення_структура_Число як адреса<Дійсне>);
    предмет.властивості = ВластивостіЧисла { значення };
    вернути предмет як адреса<Дійсне>;
  }

  дія створити_текст(М: адреса<Машина>, значення: кд) -> адреса<Дійсне> {
    змінна предмет = виділити_предмет(М, М.значення_структура_Текст як адреса<Дійсне>);

    змінна копія_тексту = виділити_памʼять<п8>(М, значення.розмір);
    
    змінна п: природне = 0;
    поки п < значення.розмір {
      копія_тексту[п] = значення.дані[п];

      п += 1;
    }
    
    предмет.властивості = ВластивостіТексту { кд { значення.розмір, копія_тексту } };
    вернути предмет як адреса<Дійсне>;
  }

  дія створити_рідну_дію(М: адреса<Машина>, назва: адреса<Дійсне>, рідна_дія: РіднаДія) -> адреса<Дійсне> {
    змінна предмет = виділити_предмет(М, М.значення_структура_Дія як адреса<Дійсне>);
    предмет.властивості = ВластивостіДії {
      рідна = так,
      назва = назва,
      задіювані = пусто,
      код = рідна_дія,
      дійсність_створення = пусто
    };
    вернути предмет як адреса<Дійсне>;
  }

  дія покласти_в_накопичувач_значень(М: адреса<Машина>, значення: адреса<Дійсне>) {
    якщо М.накопичувач_значень.розмір == М.накопичувач_значень.вмісткість {
      запанікувати(М, "Накопичувач значень заповнено!");
      вернути;
    }

    М.накопичувач_значень.дані[М.накопичувач_значень.розмір] = значення;
    М.накопичувач_значень.розмір += 1;
  }

  дія забрати_з_накопичувача_значень(М: адреса<Машина>) -> адреса<Дійсне> {
    якщо М.накопичувач_значень.розмір == 0 {
      запанікувати(М, "Накопичувач значень порожній!");
    }

    М.накопичувач_значень.розмір -= 1;
    вернути М.накопичувач_значень.дані[М.накопичувач_значень.розмір];
  }

  дія перевірити_рівність_дійсних(М: адреса<Машина>, а: адреса<Дійсне>, б: адреса<Дійсне>) -> логічне {
    якщо а == б {
      вернути так;
    }
    
    якщо а.тип == б.тип {
      якщо а.тип == М.значення_структура_Число {
        змінна предмет_числа_а = а як адреса<Предмет>;
        змінна властивості_числа_а = предмет_числа_а.властивості як ВластивостіЧисла;

        змінна предмет_числа_б = б як адреса<Предмет>;
        змінна властивості_числа_б = предмет_числа_б.властивості як ВластивостіЧисла;

        якщо властивості_числа_а.значення == властивості_числа_б.значення {
          вернути так;
        }

        вернути ні;
      }

      якщо а.тип == М.значення_структура_Текст {
        змінна предмет_тексту_а = а як адреса<Предмет>;
        змінна властивості_тексту_а = предмет_тексту_а.властивості як ВластивостіТексту;

        змінна предмет_тексту_б = б як адреса<Предмет>;
        змінна властивості_тексту_б = предмет_тексту_б.властивості як ВластивостіТексту;

        якщо властивості_тексту_а.значення.розмір != властивості_тексту_б.значення.розмір {
          вернути ні;
        }

        змінна п: природне = 0;
        поки п < властивості_тексту_а.значення.розмір {
          якщо властивості_тексту_а.значення.дані[п] != властивості_тексту_б.значення.дані[п] {
            вернути ні;
          }

          п += 1;
        }

        вернути так;
      }
    }
    
    вернути ні;
  }

  дія визначити_діяча(М: адреса<Машина>, дійсність: адреса<Дійсність>, назва: адреса<Дійсне>, значення: адреса<Дійсне>) {
    змінна знайдений_діяч: адреса<Діяч> = пусто;

    змінна п: природне = 0;
    поки п < дійсність.кількість_діячів {
      змінна діяч = дійсність.діячі[п];

      якщо перевірити_рівність_дійсних(М, діяч.назва, назва) {
        дійсність.діячі[п].значення = значення;
        
        вернути;
      }

      п += 1;
    }

    дійсність.діячі = перевиділити_памʼять<Діяч>(М, дійсність.діячі, дійсність.кількість_діячів + 1);
    дійсність.діячі[дійсність.кількість_діячів] = Діяч { назва, значення };
    дійсність.кількість_діячів += 1;
  }

  дія налаштувати_машину(М: адреса<Машина>, налаштування: Налаштування) {
    М.налаштування = налаштування;
    М.значення_дійсне = виділити_дійсне(М);
    М.значення_дійсне.тип = М.значення_дійсне;
    М.значення_логічне = виділити_дійсне(М);
    М.значення_так = виділити_дійсне(М);
    М.значення_ні = виділити_дійсне(М);
    М.значення_послідовність = виділити_дійсне(М);
    М.значення_предмет = виділити_дійсне(М);
    
    змінна значення_структура_Структура = виділити_предмет(М, пусто);
    значення_структура_Структура.тип = М.значення_структура_Структура;
    М.значення_структура_Структура = значення_структура_Структура як адреса<Дійсне>;

    змінна значення_структура_Задіюване = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Задіюване = значення_структура_Задіюване як адреса<Дійсне>;

    змінна значення_структура_Число = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Число = значення_структура_Число як адреса<Дійсне>;

    змінна значення_структура_Текст = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Текст = значення_структура_Текст як адреса<Дійсне>;

    змінна значення_структура_Код = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Код = значення_структура_Код як адреса<Дійсне>;

    змінна значення_структура_Дія = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Дія = значення_структура_Дія як адреса<Дійсне>;

    змінна значення_структура_Список = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Список = значення_структура_Список як адреса<Дійсне>;

    змінна значення_структура_Словник = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Словник = значення_структура_Словник як адреса<Дійсне>;

    змінна значення_структура_Дані = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Дані = значення_структура_Дані як адреса<Дійсне>;

    змінна значення_структура_ЗмінніДані = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_ЗмінніДані = значення_структура_ЗмінніДані як адреса<Дійсне>;

    значення_структура_Структура.властивості = ВластивостіСтруктури {
      назва = створити_текст(М, "Структура"),
      предок = М.значення_предмет,
      задіювані = пусто,
    };

    значення_структура_Задіюване.властивості = ВластивостіСтруктури {
      назва = створити_текст(М, "Задіюване"),
      предок = М.значення_предмет,
      задіювані = пусто,
    };

    значення_структура_Текст.властивості = ВластивостіСтруктури {
      назва = створити_текст(М, "Текст"),
      предок = М.значення_предмет,
      задіювані = пусто,
    };

    значення_структура_Число.властивості = ВластивостіСтруктури {
      назва = створити_текст(М, "Число"),
      предок = М.значення_предмет,
      задіювані = пусто,
    };

    значення_структура_Код.властивості = ВластивостіСтруктури {
      назва = створити_текст(М, "Код"),
      предок = М.значення_предмет,
      задіювані = пусто,
    };

    значення_структура_Дія.властивості = ВластивостіСтруктури {
      назва = створити_текст(М, "Дія"),
      предок = М.значення_предмет,
      задіювані = пусто,
    };

    значення_структура_Список.властивості = ВластивостіСтруктури {
      назва = створити_текст(М, "Список"),
      предок = М.значення_предмет,
      задіювані = пусто,
    };

    значення_структура_Словник.властивості = ВластивостіСтруктури {
      назва = створити_текст(М, "Словник"),
      предок = М.значення_предмет,
      задіювані = пусто,
    };

    значення_структура_Дані.властивості = ВластивостіСтруктури {
      назва = створити_текст(М, "Дані"),
      предок = М.значення_предмет,
      задіювані = пусто,
    };

    значення_структура_ЗмінніДані.властивості = ВластивостіСтруктури {
      назва = створити_текст(М, "ЗмінніДані"),
      предок = М.значення_предмет,
      задіювані = пусто,
    };

    стала вмісткість_накопичувача_значень = 1024;
    М.накопичувач_значень = НакопичувачЗначень {
      розмір = 0,
      дані = виділити_памʼять<адреса<Дійсне>>(М, вмісткість_накопичувача_значень),
      вмісткість = вмісткість_накопичувача_значень
    };

    стала вмісткість_історії_здійснення = 1024;
    М.історія_здійснення = ІсторіяЗдійснення {
      розмір = 0,
      дані = виділити_памʼять<Здійснюване>(М, вмісткість_історії_здійснення),
      вмісткість = вмісткість_історії_здійснення
    };
  }

  місцева дія здійснити(М: адреса<Машина>,
                        здійснювач: адреса<Дійсне>,
                        значення: адреса<Дійсне>,
                        кількість_задіяних: природне, 
                        задіяні: памʼять<адреса<Дійсне>>,
                        кількість_іменовано_задіяних: природне, 
                        іменовано_задіяні: памʼять<адреса<Дійсне>>) -> адреса<Дійсне>;
  
  місцева дія виконати_додавання(М: адреса<Машина>, 
                                 ліво: адреса<Дійсне>, 
                                 право: адреса<Дійсне>) -> адреса<Дійсне>;
  місцева дія виконати_відімання(М: адреса<Машина>, 
                                 ліво: адреса<Дійсне>, 
                                 право: адреса<Дійсне>) -> адреса<Дійсне>;
  місцева дія виконати_множення(М: адреса<Машина>, 
                                ліво: адреса<Дійсне>, 
                                право: адреса<Дійсне>) -> адреса<Дійсне>;
  місцева дія виконати_ділення(М: адреса<Машина>, 
                               ліво: адреса<Дійсне>, 
                               право: адреса<Дійсне>) -> адреса<Дійсне>;

  місцева дія виконати_взяти(М: адреса<Машина>, 
                             звідки: адреса<Дійсне>, 
                             довжина_шляху: природне, 
                             шлях: памʼять<адреса<Дійсне>>) -> адреса<Дійсне>;
}