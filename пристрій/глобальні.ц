взяти означення пристрій_мавки;

простір мавка {
  простір пристрій {
    дія рідна_дія_друк(
      М: адреса<Машина>,
      здійснювач: Значення,
      предмет_дії: адреса<ПредметДії>,
      кількість_задіяних: природне,
      задіяні: памʼять<Значення>,
      іменовано_задіяні: адреса<ІменованіЗадіяні>
    ) -> адреса<Дійсне> {
      змінна розмір_відступів: природне = 0;

      якщо кількість_задіяних == 1 {
        розмір_відступів = 2;
      }

      змінна п: природне = 0;
      поки п < кількість_задіяних {
        вивести_значення(М, задіяні[п], розмір_відступів, 0);

        якщо кількість_задіяних - п > 1 {
          вивести_кд(М, " ");
        }

        п += 1;
      }

      вивести_кд(М, "\р");

      вернути пусто;
    }

    місцева дія визначити_глобальні_виводу(М: адреса<Машина>) {
      змінна дія_друк = створити_рідну_дію(
        М,
        створити_текст(М, "друк"),
        пусто,
        пусто,
        рідна_дія_друк,
        пусто
      );

      записати_в_дійсність(
        М,
        М.глобальна_дійсність,
        створити_текст(М, "друк"),
        дія_друк як Значення
      );
    }
  }
}