взяти означення пристрій_мавки;

простір мавка {
  простір пристрій {
    дія дія_виводу(М: адреса<Машина>, значення: кд) {
      вивести_кд(М, значення);
    }

    місцева дія взяти_файл(
      М: адреса<Машина>,
      шлях_до_файлу: адреса<ПредметДаних>,
      назва_модуля: адреса<ПредметТексту>,
      місцезнаходження: Місцезнаходження
    ) -> ЗначенняАбоСтанПадіння {
      змінна успіх: логічне = ні;
      змінна значення: Значення = пусто;
      змінна розмір_файлу: природне = 0;
      змінна дані_файлу: памʼять<п8> = пусто;
      змінна сирий_текст: кд = кд { 0, пусто };
      змінна слова: розбирач::Слова;
      змінна помилка_розбору_на_слова: розбирач::ПомилкаРозборуНаСлова;
      змінна сполуки: розбирач::Сполуки;
      змінна помилка_розбору_на_сполуки: розбирач::ПомилкаРозборуНаСполуки;
      змінна перекладений_код: адреса<ПредметКоду> = пусто;
      змінна помилка_перекладу: перекладач::ПомилкаПерекладу;

      якщо знайти_взяте_значення(М, шлях_до_файлу, значення::адреса) {
        вернути значення;
      }

      успіх = пристрій_мавки_прочитати_файл(
        шлях_до_файлу.розмір,
        шлях_до_файлу.дані,
        розмір_файлу::адреса,
        дані_файлу::адреса
      );

      якщо не успіх {
        увімкнути_стан_падіння(
          М,
          створити_текст(М, "Не вдалося прочитати файл.") як Значення,
          місцезнаходження
        );
        вернути пусто;
      }

      успіх = перекодувати_ю8_в_кд(
        М,
        ю8 { розмір_файлу, дані_файлу },
        сирий_текст::адреса,
        пусто
      );

      якщо не успіх {
        звільнити_памʼять(М, дані_файлу);
        увімкнути_стан_падіння(
          М,
          створити_текст(М, "Не вдалося перекодувати файл в код мавки.") як Значення,
          Місцезнаходження { шлях_до_файлу, 0 }
        );
        вернути пусто;
      }

      успіх = розбирач::розібрати_на_слова(
        М,
        сирий_текст,
        слова::адреса,
        помилка_розбору_на_слова::адреса
      );

      якщо не успіх {
        змінна рядок = розбирач::отримати_рядок_помилки_розбору_на_слова(
          М,
          помилка_розбору_на_слова
        );
        увімкнути_стан_падіння(
          М,
          створити_текст(М, "Не вдалось розібрати.") як Значення,
          Місцезнаходження { шлях_до_файлу, рядок }
        );
        вернути пусто;
      }

      успіх = розбирач::розібрати_слова_на_сполуки(
        М,
        сирий_текст,
        слова,
        сполуки::адреса,
        помилка_розбору_на_сполуки::адреса
      );

      якщо не успіх {
        змінна рядок = розбирач::отримати_рядок_помилки_розбору_на_сполуки(
          М,
          помилка_розбору_на_сполуки
        );
        увімкнути_стан_падіння(
          М,
          створити_текст(М, "Не вдалось розібрати.") як Значення,
          Місцезнаходження { шлях_до_файлу, рядок }
        );
        вернути пусто;
      }

      успіх = перекласти_в_код(
        М,
        створити_дані(М, шлях_до_файлу.розмір, шлях_до_файлу.дані),
        сирий_текст,
        слова,
        сполуки,
        перекладений_код::адреса,
        помилка_перекладу::адреса
      );

      // надрукувати_код(М, перекладений_код.вказівки.розмір, перекладений_код.вказівки.дані, дія_виводу);

      якщо не успіх {
        увімкнути_стан_падіння(
          М,
          створити_текст(М, "Не вдалось перекласти.") як Значення,
          місцезнаходження
        );
        вернути пусто;
      }

      змінна предмет_модуля = створити_модуль(
        М,
        назва_модуля
      );

      записати_взяте_значення(
        М,
        шлях_до_файлу,
        предмет_модуля як Значення
      );

      змінна предмет_дії_модуля = створити_дію(
        М,
        М.глобальна_дійсність,
        назва_модуля,
        пусто,
        пусто,
        перекладений_код як адреса<ПредметКоду>,
        шлях_до_файлу
      );

      змінна результат = предмет_дії_здійснити(
        М,
        предмет_модуля як Значення,
        предмет_дії_модуля,
        Задіяні { 0, пусто, пусто },
        місцезнаходження
      );

      якщо М.стан_падіння {
        видалити_взяте_значення(М, шлях_до_файлу);
      }

      вернути предмет_модуля як Значення;
    }
  }
}