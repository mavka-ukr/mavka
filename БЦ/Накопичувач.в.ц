взяти визначення ./Виділяч;

секція біб {
  структура Накопичувач<Т> {
    виділяч: адреса<Виділяч>;
    розмір: позитивне;
    дані: памʼять<Т>;
    вмісткість: позитивне;
  }

  дія виділити_накопичувач<Т>(виділяч: адреса<Виділяч>, початкова_вмісткість: позитивне): адреса<Накопичувач<Т>> {
    змінна накопичувач = виділити<Накопичувач<Т>>(виділяч);

    накопичувач.виділяч = виділяч;
    накопичувач.розмір = 0;
    якщо початкова_вмісткість > 0 {
      накопичувач.дані = виділити_памʼять<Т>(виділяч, початкова_вмісткість);
    } інакше {
      накопичувач.дані = пусто;
    }
    накопичувач.вмісткість = початкова_вмісткість;

    вернути накопичувач;
  }

  дія зробити_накопичувач<Т>(виділяч: адреса<Виділяч>, початкова_вмісткість: позитивне): Накопичувач<Т> {
    змінна накопичувач: Накопичувач<Т>;

    накопичувач.виділяч = виділяч;
    накопичувач.розмір = 0;
    якщо початкова_вмісткість > 0 {
      накопичувач.дані = виділити_памʼять<Т>(виділяч, початкова_вмісткість);
    } інакше {
      накопичувач.дані = пусто;
    }
    накопичувач.вмісткість = початкова_вмісткість;

    вернути накопичувач;
  }

  дія покласти_в_накопичувач<Т>(накопичувач: адреса<Накопичувач<Т>>, значення: Т) {
    якщо накопичувач.розмір == накопичувач.вмісткість {
      накопичувач.вмісткість += 1;
      накопичувач.дані = перевиділити_памʼять<Т>(накопичувач.виділяч, накопичувач.дані, накопичувач.вмісткість);
    }

    накопичувач.дані[накопичувач.розмір] = значення;
    накопичувач.розмір += 1;
  }

  дія змінити_в_накопичувачі<Т>(накопичувач: адреса<Накопичувач<Т>>, позиція: позитивне, значення: Т) {
    якщо позиція >= накопичувач.вмісткість {
      накопичувач.вмісткість = позиція + 1;
      накопичувач.дані = перевиділити_памʼять<Т>(накопичувач.виділяч, накопичувач.дані, накопичувач.вмісткість);
    }

    накопичувач.дані[позиція] = значення;
    накопичувач.розмір = позиція + 1;
  }

  дія змінити_в_накопичувачі_без_розширення<Т>(накопичувач: адреса<Накопичувач<Т>>, позиція: позитивне, значення: Т) {
    накопичувач.дані[позиція] = значення;
  }

  дія звільнити_значення_накопичувача<Т>(накопичувач: Накопичувач<Т>) {
    біб::звільнити_памʼять(накопичувач.виділяч, накопичувач.дані);
  }

  дія звільнити_накопичувач<Т>(накопичувач: адреса<Накопичувач<Т>>) {
    звільнити_значення_накопичувача<Т>(накопичувач::вміст);
    біб::звільнити(накопичувач.виділяч, накопичувач);
  }
}