взяти означення ./Памʼять;

простір біб {
  структура Накопичувач<Т> {
    розмір: природне;
    дані: памʼять<Т>;
    вмісткість: природне;
  }

  дія виділити_накопичувач<Т>(початкова_вмісткість: природне) -> адреса<Накопичувач<Т>> {
    змінна накопичувач = виділити<Накопичувач<Т>>();

    накопичувач.розмір = 0;
    якщо початкова_вмісткість > 0 {
      накопичувач.дані = виділити_памʼять<Т>(початкова_вмісткість);
    } інакше {
      накопичувач.дані = пусто;
    }
    накопичувач.вмісткість = початкова_вмісткість;

    вернути накопичувач;
  }

  дія зробити_накопичувач<Т>(початкова_вмісткість: природне) -> Накопичувач<Т> {
    змінна накопичувач: Накопичувач<Т>;

    накопичувач.розмір = 0;
    якщо початкова_вмісткість > 0 {
      накопичувач.дані = виділити_памʼять<Т>(початкова_вмісткість);
    } інакше {
      накопичувач.дані = пусто;
    }
    накопичувач.вмісткість = початкова_вмісткість;

    вернути накопичувач;
  }

  дія покласти_в_накопичувач<Т>(накопичувач: адреса<Накопичувач<Т>>, значення: Т) {
    якщо накопичувач.розмір == накопичувач.вмісткість {
      накопичувач.вмісткість += 1;
      накопичувач.дані = перевиділити_памʼять<Т>(накопичувач.дані, накопичувач.вмісткість);
    }

    накопичувач.дані[накопичувач.розмір] = значення;
    накопичувач.розмір += 1;
  }

  дія змінити_в_накопичувачі<Т>(накопичувач: адреса<Накопичувач<Т>>, позиція: природне, значення: Т) {
    якщо позиція >= накопичувач.вмісткість {
      накопичувач.вмісткість = позиція + 1;
      накопичувач.дані = перевиділити_памʼять<Т>(накопичувач.дані, накопичувач.вмісткість);
    }

    накопичувач.дані[позиція] = значення;
    накопичувач.розмір = позиція + 1;
  }

  дія змінити_в_накопичувачі_без_розширення<Т>(накопичувач: адреса<Накопичувач<Т>>, позиція: природне, значення: Т) {
    накопичувач.дані[позиція] = значення;
  }

  дія звільнити_значення_накопичувача<Т>(накопичувач: Накопичувач<Т>) {
    біб::звільнити_памʼять(накопичувач.дані);
  }

  дія звільнити_накопичувач<Т>(накопичувач: адреса<Накопичувач<Т>>) {
    звільнити_значення_накопичувача<Т>(накопичувач::вміст);
    біб::звільнити(накопичувач);
  }
}