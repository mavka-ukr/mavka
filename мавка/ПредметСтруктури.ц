взяти означення мавка;

простір мавка {
  місцева дія створити_структуру(М: адреса<Машина>, назва: Значення, предок: Значення, параметри: адреса<Послідовність>) -> Значення {
    змінна предмет = виділити_предмет(М, М.значення_структура_Структура як Значення);
    
    предмет.властивості = ВластивостіСтруктури {
      заборонити_наслідування = ні,
      назва = назва,
      предок = предок,
      параметри = параметри,
      методи = Методи { 0, пусто }
    };

    вернути предмет як Значення;
  }

  // позначка: прохід по типах предметів
  місцева дія предмет_структури_здійснити(
    М: адреса<Машина>,
    здійснювач: Значення,
    значення: Значення,
    кількість_задіяних: природне,
    задіяні: памʼять<Значення>,
    іменовано_задіяні: адреса<ІменованіЗадіяні>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    змінна результат: Значення = пусто;
    
    якщо значення == М.значення_дійсне {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_послідовність {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_дійсність {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_предмет {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_структура_Модуль {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_структура_Параметр {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_структура_Число {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_структура_Ціле {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_структура_Дробове {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_структура_Текст {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_структура_Код {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_структура_Дія {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_структура_Список {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_структура_Словник {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_структура_Дані {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_структура_ЗмінніДані {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_структура_ДіапазонЦілих {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_структура_ДіапазонДробових {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_структура_ПеребірДіапазонуЦілих {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_структура_ПеребірДіапазонуДробових {
      стрибнути крок_вихід;
    }

    змінна предмет_структури = значення як адреса<Предмет>;
    змінна властивості_структури = предмет_структури.властивості як ВластивостіСтруктури;

    результат = створити_предмет(М, значення);
    змінна п: природне = 0;

    якщо властивості_структури.параметри != пусто {
      поки п < властивості_структури.параметри.розмір {
        змінна параметр = властивості_структури.параметри.дані[п];
        змінна предмет_параметра = параметр як адреса<Предмет>;
        змінна властивості_параметра = предмет_параметра.властивості як ВластивостіПараметра;
  
        якщо кількість_задіяних > п {
          змінити_властивість(
            М,
            результат,
            властивості_параметра.назва,
            задіяні[п],
            місцезнаходження
          );
        }
  
        п += 1;
      }
  
      якщо іменовано_задіяні != пусто {
        п = 0;

        поки п < іменовано_задіяні.розмір {
          змінна іменовано_задіяне = іменовано_задіяні.елементи[п];
  
          змінити_властивість(
            М,
            результат,
            іменовано_задіяне.назва,
            іменовано_задіяне.значення,
            місцезнаходження
          );
  
          п += 1;
        }
      }
    }

  крок_вихід:
    звільнити_памʼять(М, задіяні);
    якщо іменовано_задіяні != пусто {
      знищити_іменовано_задіяні(М, іменовано_задіяні);
    }

    вернути результат;
  }

  місцева дія записати_метод_структури(М: адреса<Машина>, значення_структури: Значення, назва: Значення, значення_дії: Значення) {
    змінна предмет_структури = значення_структури як адреса<Предмет>;
    змінна властивості_структури = (предмет_структури.властивості як ВластивостіСтруктури)::адреса;

    змінна п: природне = 0;
    поки п < властивості_структури.методи.розмір {
      змінна метод = властивості_структури.методи.дані[п]::адреса;

      якщо перевірити_чи_значення_рівні(М, метод.назва, назва) {
        метод.значення = значення_дії;

        вернути;
      }

      п += 1;
    }

    властивості_структури.методи.дані = перевиділити_памʼять<Метод>(М, властивості_структури.методи.дані, властивості_структури.методи.розмір + 1);
    властивості_структури.методи.дані[властивості_структури.методи.розмір] = Метод { назва, значення_дії };
    властивості_структури.методи.розмір += 1;
  }

  місцева дія знайти_метод_в_структурі(М: адреса<Машина>, значення_структури: Значення, назва: Значення) -> Значення {
    змінна предмет_структури = значення_структури як адреса<Предмет>;
    змінна властивості_структури = (предмет_структури.властивості як ВластивостіСтруктури)::адреса;

    змінна п: природне = 0;
    поки п < властивості_структури.методи.розмір {
      змінна метод = властивості_структури.методи.дані[п];

      якщо перевірити_чи_значення_рівні(М, метод.назва, назва) {
        вернути метод.значення;
      }

      п += 1;
    }

    вернути пусто;
  }
}