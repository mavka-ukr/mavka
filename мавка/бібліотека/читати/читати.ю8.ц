взяти визначення мавка;
взяти визначення мавка/бібліотека;

зовнішня дія мавка_система_прочитати_ю8_зі_стандартного_вводу(префікс: ю8, вихід: адреса<ю8>, додати_в_історію: логічне): логічне;

секція мавка {
  секція бібліотека {
    дія рідна_дія_читати(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<МаМа::ПредметДії>, предмет_я: адреса<Предмет>, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>, іменовані_аргументи: адреса<ІменованіАргументи>, місцезнаходження: МаМа::Місцезнаходження): предмет_або_стан_падіння {
      змінна знайдено_аргумент_префікс = ні;
      змінна аргумент_префікс: адреса<Предмет> = пусто;
      змінна М = обчислювач;
      якщо іменовані_аргументи != пусто {
        знайдено_аргумент_префікс = МаМа::знайти_іменований_аргумент(обчислювач, іменовані_аргументи, МаМа::назва(М, "префікс"), аргумент_префікс::адреса);
      }
      якщо аргументи != пусто {
        якщо знайдено_аргумент_префікс == ні {
          якщо кількість_аргументів > 0 {
            знайдено_аргумент_префікс = так;
            аргумент_префікс = аргументи[0];
          }
        }
      }
      змінна префікс = т8 { 0, пусто };
      якщо знайдено_аргумент_префікс == так {
        якщо аргумент_префікс != пусто {
          якщо МаМа::перевірити_чи_предмет_є_текстом(обчислювач, аргумент_префікс) == ні {
            вернути падіння(обчислювач, "Префікс має бути текстом або пусто");
          }
          якщо отримати_копію_значення_тексту(обчислювач, аргумент_префікс як адреса<МаМа::ПредметТексту>, префікс::адреса) == ні {
            вернути падіння(обчислювач, "Не вдалось отримати значення префікса");
          }
        }
      }
      змінна префікс_ю8 = ю8 { 0, пусто };
      якщо МаМа::перекодувати_т8_в_ю8(обчислювач, префікс, префікс_ю8::адреса, ні) == ні {
        вернути падіння(обчислювач, "Не вдалось перекодувати префікс в ю8");
      }
      змінна прочитане_значення_ю8 = ю8 { 0, пусто };
      якщо мавка_система_прочитати_ю8_зі_стандартного_вводу(префікс_ю8, прочитане_значення_ю8::адреса, ні) == ні {
        звільнити_памʼять(обчислювач, префікс.дані);
        звільнити_памʼять(обчислювач, префікс_ю8.дані);
        вернути пусто;
      }
      звільнити_памʼять(обчислювач, префікс.дані);
      звільнити_памʼять(обчислювач, префікс_ю8.дані);
      змінна прочитане_значення = т8 { 0, пусто };
      якщо МаМа::перекодувати_ю8_в_т8(обчислювач, прочитане_значення_ю8, прочитане_значення::адреса, ні) == ні {
        вернути падіння(обчислювач, "Не вдалось перекодувати значення в т8");
      }
      змінна предмет_тексту = МаМа::створити_предмет_тексту_без_копіювання(обчислювач, прочитане_значення) як адреса<Предмет>;
      звільнити_памʼять(обчислювач, прочитане_значення_ю8.дані);
      вернути предмет_тексту;
    }

    дія створити_дію_читати(обчислювач: адреса<Обчислювач>): предмет_або_стан_падіння {
    змінна М = обчислювач;
      вернути МаМа::створити_предмет_рідної_дії(обчислювач, МаМа::назва(М, т8"читати"), 0, пусто, пусто, рідна_дія_читати, пусто, пусто, пусто) як адреса<Предмет>;
    }

    зовнішня дія взяти_модуль_біб_читати(обчислювач: адреса<Обчислювач>): предмет_або_стан_падіння {
      змінна М = обчислювач;
      змінна результат = МаМа::створити_предмет_тексту(обчислювач, "мавка:бібліотека/читати/читати.ц") як адреса<Предмет>;
      якщо перевірити_чи_стан_падіння(обчислювач) {
        вернути результат;
      }
      змінна предмет_шляху = результат;
      змінна взятий_предмет: адреса<МаМа::Предмет> = пусто;
      якщо МаМа::отримати_взятий_предмет(М, предмет_шляху як адреса<МаМа::Предмет>, взятий_предмет::адреса) {
        вернути взятий_предмет як адреса<Предмет>;
      }
      результат = створити_дію_читати(обчислювач);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        вернути результат;
      }
      МаМа::покласти_взятий_предмет(М, предмет_шляху як адреса<МаМа::Предмет>, результат як адреса<МаМа::Предмет>);
      вернути результат;
    }
  }
}