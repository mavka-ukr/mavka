взяти визначення мавка;
взяти визначення мавка/бібліотека;

зовнішня дія мавка_система_прочитати_ю8_зі_стандартного_вводу(префікс: адреса<ю8>, вихід: адреса<ю8>, додати_в_історію: логічне): логічне;

секція мавка {
  секція бібліотека {
    дія рідна_дія_читати(М: адреса<Машина>, предмет_дії: адреса<ПредметДії>, предмет_я: адреса<Предмет>, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>, іменовані_аргументи: адреса<ІменованіАргументи>, місцезнаходження: Місцезнаходження): предмет_або_стан_падіння {
      змінна знайдено_аргумент_префікс = ні;
      змінна аргумент_префікс: адреса<Предмет> = пусто;

      якщо іменовані_аргументи != пусто {
        знайдено_аргумент_префікс = знайти_іменований_аргумент(М, іменовані_аргументи, створити_назву(М, "префікс"), аргумент_префікс::адреса);
      }
      якщо аргументи != пусто {
        якщо знайдено_аргумент_префікс == ні {
          якщо кількість_аргументів > 0 {
            знайдено_аргумент_префікс = так;
            аргумент_префікс = аргументи[0];
          }
        }
      }
      змінна префікс = т8 { 0, пусто };
      якщо знайдено_аргумент_префікс == так {
        якщо аргумент_префікс != пусто {
          якщо перевірити_чи_предмет_є_текстом(М, аргумент_префікс) == ні {
            вернути падіння(М, "Префікс має бути текстом або пусто");
          }
          якщо отримати_копію_значення_тексту(М, аргумент_префікс як адреса<ПредметТексту>, префікс::адреса) == ні {
            вернути падіння(М, "Не вдалось отримати значення префікса");
          }
        }
      }
      змінна префікс_ю8 = ю8 { 0, пусто };
      якщо перекодувати_т8_в_ю8(М, префікс, префікс_ю8::адреса, ні) == ні {
        вернути падіння(М, "Не вдалось перекодувати префікс в ю8");
      }
      змінна прочитане_значення_ю8 = ю8 { 0, пусто };
      якщо мавка_система_прочитати_ю8_зі_стандартного_вводу(префікс_ю8::адреса, прочитане_значення_ю8::адреса, ні) == ні {
        звільнити_памʼять(М, префікс.дані);
        звільнити_памʼять(М, префікс_ю8.дані);
        вернути пусто;
      }
      звільнити_памʼять(М, префікс.дані);
      звільнити_памʼять(М, префікс_ю8.дані);
      змінна прочитане_значення = т8 { 0, пусто };
      якщо перекодувати_ю8_в_т8(М, прочитане_значення_ю8, прочитане_значення::адреса, ні) == ні {
        вернути падіння(М, "Не вдалось перекодувати значення в т8");
      }
      змінна предмет_тексту = створити_предмет_тексту_без_копіювання(М, прочитане_значення) як адреса<Предмет>;
      звільнити_памʼять(М, прочитане_значення_ю8.дані);
      вернути предмет_тексту;
    }

    дія створити_дію_читати(М: адреса<Машина>): предмет_або_стан_падіння {

      вернути створити_предмет_рідної_дії(М, створити_назву(М, "читати"), 0, пусто, пусто, рідна_дія_читати, пусто, пусто, пусто) як адреса<Предмет>;
    }

    місцева дія взяти_модуль_біб_читати(М: адреса<Машина>): предмет_або_стан_падіння {

      змінна результат = створити_предмет_тексту(М, "мавка:бібліотека/читати/читати.ц") як адреса<Предмет>;
      якщо перевірити_чи_стан_падіння(М) {
        вернути результат;
      }
      змінна предмет_шляху = результат;
      змінна взятий_предмет: адреса<Предмет> = пусто;
      якщо отримати_взятий_предмет(М, предмет_шляху як адреса<Предмет>, взятий_предмет::адреса) {
        вернути взятий_предмет як адреса<Предмет>;
      }
      результат = створити_дію_читати(М);
      якщо перевірити_чи_стан_падіння(М) {
        вернути результат;
      }
      покласти_взятий_предмет(М, предмет_шляху як адреса<Предмет>, результат як адреса<Предмет>);
      вернути результат;
    }
  }
}