взяти визначення МаМа;
взяти визначення мавка/бібліотека;

секція мавка {
  секція бібліотека {
    дія нативна_дія_бібліотеки_мавки_читати_юнікод(предмет_нативної_дії: адреса<Предмет>, обчислювач: адреса<Обчислювач>, предмет_я: адреса<Предмет>, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>, іменовані_аргументи: адреса<ІменованіАргументи>, дані: адреса): Результат {
      змінна ціль знайдено_аргумент_префікс = ні;
      змінна ціль аргумент_префікс: адреса<Предмет> = пусто;
      якщо іменовані_аргументи != пусто {
        знайдено_аргумент_префікс = знайти_іменований_аргумент(обчислювач, іменовані_аргументи, обчислювач.М.БН.С.префікс як адреса<Назва>, аргумент_префікс::адреса);
      }
      якщо аргументи != пусто {
        якщо знайдено_аргумент_префікс == ні {
          якщо кількість_аргументів > 0 {
            знайдено_аргумент_префікс = так;
            аргумент_префікс = аргументи[0];
          }
        }
      }
      змінна ціль префікс = ю8 { 0, пусто };
      якщо знайдено_аргумент_префікс {
        якщо перевірити_чи_предмет_є_юнікодом(аргумент_префікс, обчислювач) {
          якщо отримати_копію_даних_юнікоду_як_ю8(аргумент_префікс, обчислювач, префікс::адреса) == ні {
            вернути результат_падіння(обчислювач, пусто, пусто);
          }
        } інакше {
          вернути результат_падіння(обчислювач, створити_текст_з_ю8(обчислювач, ю8"Префікс має бути юнікодом"), пусто);
        }
      }
      ціль значення = ю8 { 0, пусто };
      якщо система::прочитати_ю8_зі_стандартного_вводу(обчислювач, префікс, значення::адреса) {
        ціль предмет_юнікоду = створити_юнікод_з_ю8(обчислювач, значення);
        звільнити_памʼять(обчислювач, префікс.дані);
        звільнити_памʼять(обчислювач, значення.дані);
        вернути результат_успіх(обчислювач, предмет_юнікоду);
      } інакше {
        звільнити_памʼять(обчислювач, префікс.дані);
        вернути результат_успіх(обчислювач, пусто);
      }
    }

    місцева дія заповнити_модуль_читати_юнікод(обчислювач: адреса<Обчислювач>, предмет_модуля: адреса<Предмет>) {
      предмет_змінити_властивість(предмет_модуля, обчислювач, назва_з_ю8(обчислювач, ю8"чародія_виконати"), створити_нативну_дію(обчислювач, назва_з_ю8(обчислювач, ю8"читати_юнікод"), нативна_дія_бібліотеки_мавки_читати_юнікод, пусто, пусто));
    }
  }
}