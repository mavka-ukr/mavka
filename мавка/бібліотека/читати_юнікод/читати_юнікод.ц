взяти визначення мавка;
взяти визначення мавка/бібліотека;

зовнішня дія мавка_система_прочитати_ю8_зі_стандартного_вводу(префікс: ю8, вихід: адреса<ю8>, додати_в_історію: логічне): логічне;

секція мавка {
  секція бібліотека {
    дія рідна_дія_читати_юнікод(М: адреса<Машина>, предмет_дії: адреса<ПредметДії>, предмет_я: адреса<Предмет>, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>, іменовані_аргументи: адреса<ІменованіАргументи>, місцезнаходження: Місцезнаходження): предмет_або_стан_падіння {
      змінна знайдено_аргумент_префікс = ні;
      змінна аргумент_префікс: адреса<Предмет> = пусто;

      якщо іменовані_аргументи != пусто {
        знайдено_аргумент_префікс = знайти_іменований_аргумент(М, іменовані_аргументи, назва(М, "префікс"), аргумент_префікс::адреса);
      }
      якщо аргументи != пусто {
        якщо знайдено_аргумент_префікс == ні {
          якщо кількість_аргументів > 0 {
            знайдено_аргумент_префікс = так;
            аргумент_префікс = аргументи[0];
          }
        }
      }
      змінна префікс = ю8 { 0, пусто };
      якщо знайдено_аргумент_префікс == так {
        якщо аргумент_префікс != пусто {
          якщо перевірити_чи_предмет_є_юнікодом(М, аргумент_префікс) == ні {
            вернути падіння(М, "Префікс має бути юнікодом або пусто");
          }
          якщо отримати_копію_значення_юнікоду_як_ю8(М, аргумент_префікс як адреса<ПредметЮнікоду>, префікс::адреса) == ні {
            вернути падіння(М, "Не вдалось отримати ю8 префікса");
          }
        }
      }
      змінна прочитане_значення = ю8 { 0, пусто };
      якщо мавка_система_прочитати_ю8_зі_стандартного_вводу(префікс, прочитане_значення::адреса, ні) == ні {
        звільнити_памʼять(М, префікс.дані);
        вернути пусто;
      }
      звільнити_памʼять(М, префікс.дані);
      змінна предмет_юнікоду = створити_предмет_юнікоду_з_ю8(М, прочитане_значення) як адреса<Предмет>;
      звільнити_памʼять(М, прочитане_значення.дані);
      вернути предмет_юнікоду;
    }

    дія створити_дію_читати_юнікод(М: адреса<Машина>): предмет_або_стан_падіння {

      вернути створити_предмет_рідної_дії(М, назва(М, т8"читати_юнікод"), 0, пусто, пусто, рідна_дія_читати_юнікод, пусто, пусто, пусто) як адреса<Предмет>;
    }

    місцева дія взяти_модуль_біб_читати_юнікод(М: адреса<Машина>): предмет_або_стан_падіння {

      змінна результат = створити_предмет_тексту(М, "мавка:бібліотека/читати_юнікод/читати_юнікод.ц") як адреса<Предмет>;
      якщо перевірити_чи_стан_падіння(М) {
        вернути результат;
      }
      змінна предмет_шляху = результат;
      змінна взятий_предмет: адреса<Предмет> = пусто;
      якщо отримати_взятий_предмет(М, предмет_шляху як адреса<Предмет>, взятий_предмет::адреса) {
        вернути взятий_предмет як адреса<Предмет>;
      }
      результат = створити_дію_читати_юнікод(М);
      якщо перевірити_чи_стан_падіння(М) {
        вернути результат;
      }
      покласти_взятий_предмет(М, предмет_шляху як адреса<Предмет>, результат як адреса<Предмет>);
      вернути результат;
    }
  }
}