взяти визначення МаМа;
взяти визначення мавка/бібліотека;

зовнішня дія мавка_система_прочитати_ю8_зі_стандартного_вводу(префікс: ю8, вихід: адреса<ю8>, додати_в_історію: логічне): логічне;

секція мавка {
  секція бібліотека {
    дія рідна_дія_читати_юнікод(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<Предмет>, предмет_я: адреса<Предмет>, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>, іменовані_аргументи: адреса<ІменованіАргументи>): предмет_або_стан_падіння {
      змінна знайдено_аргумент_префікс = ні;
      змінна аргумент_префікс: адреса<Предмет> = пусто;
      якщо іменовані_аргументи != пусто {
        знайдено_аргумент_префікс = знайти_іменований_аргумент(обчислювач, іменовані_аргументи, назва(обчислювач, "префікс"), аргумент_префікс::адреса);
      }
      якщо аргументи != пусто {
        якщо знайдено_аргумент_префікс == ні {
          якщо кількість_аргументів > 0 {
            знайдено_аргумент_префікс = так;
            аргумент_префікс = аргументи[0];
          }
        }
      }
      змінна префікс = ю8 { 0, пусто };
      якщо знайдено_аргумент_префікс == так {
        якщо аргумент_префікс != пусто {
          якщо перевірити_чи_предмет_є_юнікодом(обчислювач, аргумент_префікс) == ні {
            вернути падіння(обчислювач, "Префікс має бути юнікодом або пусто");
          }
          якщо отримати_копію_значення_юнікоду_як_ю8(обчислювач, аргумент_префікс, префікс::адреса) == ні {
            вернути падіння(обчислювач, "Не вдалось отримати ю8 префікса");
          }
        }
      }
      змінна прочитане_значення = ю8 { 0, пусто };
      якщо мавка_система_прочитати_ю8_зі_стандартного_вводу(префікс, прочитане_значення::адреса, ні) == ні {
        звільнити_памʼять(обчислювач, префікс.дані);
        вернути пусто;
      }
      звільнити_памʼять(обчислювач, префікс.дані);
      змінна предмет_юнікоду = створити_юнікод_з_ю8(обчислювач, прочитане_значення);
      звільнити_памʼять(обчислювач, прочитане_значення.дані);
      вернути предмет_юнікоду;
    }

    дія створити_дію_читати_юнікод(обчислювач: адреса<Обчислювач>): предмет_або_стан_падіння {
      вернути створити_рідну_дію(обчислювач, назва(обчислювач, т8"читати_юнікод"), 0, пусто, пусто, рідна_дія_читати_юнікод);
    }

    зовнішня дія взяти_модуль_біб_читати_юнікод(обчислювач: адреса<Обчислювач>): предмет_або_стан_падіння {
      змінна М = обчислювач;
      змінна результат = створити_текст(обчислювач, "мавка:бібліотека/читати_юнікод/читати_юнікод.ц");
      якщо перевірити_чи_стан_падіння(обчислювач) {
        вернути результат;
      }
      змінна предмет_шляху = результат;
      змінна взятий_предмет: адреса<МаМа::Предмет> = пусто;
      якщо МаМа::отримати_взятий_предмет(М, предмет_шляху як адреса<МаМа::Предмет>, взятий_предмет::адреса) {
        вернути взятий_предмет як адреса<Предмет>;
      }
      результат = створити_дію_читати_юнікод(обчислювач);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        вернути результат;
      }
      МаМа::покласти_взятий_предмет(М, предмет_шляху як адреса<МаМа::Предмет>, результат як адреса<МаМа::Предмет>);
      вернути результат;
    }
  }
}