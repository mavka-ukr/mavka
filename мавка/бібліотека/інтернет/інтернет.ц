взяти визначення МаМа;
взяти визначення мавка/бібліотека;
взяти визначення біб/Реєстр;
взяти визначення біб/Друк;
взяти визначення біб/турбо;

// Звʼязок
секція мавка {
  секція бібліотека {
    структура ДаніВідкликуІнтернетЗвʼязку {
      обчислювач: адреса<Обчислювач>;
      глобально_притриманий_предмет_звʼязку: адреса<ГлобальноПритриманийПредмет>;
      глобально_притриманий_предмет_обробника_підключення: адреса<ГлобальноПритриманийПредмет>;
    }

    структура ДаніВідкликуНаРезультатЗаписуВІнтернетЗвʼязок {
      обчислювач: адреса<Обчислювач>;
      глобально_притриманий_предмет_звʼязку: адреса<ГлобальноПритриманийПредмет>;
      глобально_притриманий_предмет_даних: адреса<ГлобальноПритриманийПредмет>;
      розмір: позитивне;
      дані: памʼять<п8>;
      дані_треба_звільнити: позитивне;
    }

    дія обробник_запланованого_виконання_відклику_на_підключення_інтернет_звʼязку(обчислювач: адреса<Обчислювач>, дані: невідома_адреса, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>): предмет_або_стан_падіння {
      ціль предмет_обробника_підключення = аргументи[0];
      вернути предмет_виконати(обчислювач, предмет_обробника_підключення, пусто, 2, аргументи[1]::адреса як памʼять<адреса<Предмет>>, пусто, Місцезнаходження { пусто, 0 });
    }

    дія обробник_запланованого_виконання_відклику_на_дані_інтернет_звʼязку(обчислювач: адреса<Обчислювач>, дані: невідома_адреса, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>): предмет_або_стан_падіння {
      ціль предмет_звʼязку = аргументи[0];
      вернути предмет_виконати_властивість(обчислювач, предмет_звʼязку, назва(обчислювач, "обробник_даних"), 2, аргументи, пусто, Місцезнаходження { пусто, 0 });
    }

    дія обробник_запланованого_виконання_відклику_на_закінчення_інтернет_звʼязку(обчислювач: адреса<Обчислювач>, дані: невідома_адреса, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>): предмет_або_стан_падіння {
      ціль предмет_звʼязку = аргументи[0];
      вернути предмет_виконати_властивість(обчислювач, предмет_звʼязку, назва(обчислювач, "обробник_закінчення"), 1, аргументи, пусто, Місцезнаходження { пусто, 0 });
    }

    дія обробник_запланованого_виконання_відклику_на_відключення_інтернет_звʼязку(обчислювач: адреса<Обчислювач>, дані: невідома_адреса, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>): предмет_або_стан_падіння {
      ціль предмет_звʼязку = аргументи[0];
      вернути предмет_виконати_властивість(обчислювач, предмет_звʼязку, назва(обчислювач, "обробник_відключення"), 2, аргументи, пусто, Місцезнаходження { пусто, 0 });
    }

    дія обробник_запису_інтернет_звʼязку(турбіна: адреса<турбо::Турбіна>, інтернет_звʼязок: адреса<турбо::ІнтернетЗвʼязок>, аргумент_інтернет_звʼязку: невідома_адреса, аргумент_запису: невідома_адреса, розмір: позитивне, дані: памʼять<п8>) {
      ціль дані_запису = аргумент_запису як адреса<ДаніВідкликуНаРезультатЗаписуВІнтернетЗвʼязок>;
      ціль обчислювач = дані_запису.обчислювач;
      ціль предмет_звʼязку = отримати_глобально_притриманий_предмет(обчислювач, дані_запису.глобально_притриманий_предмет_звʼязку);
      ціль предмет_даних = отримати_глобально_притриманий_предмет(обчислювач, дані_запису.глобально_притриманий_предмет_даних);

      відпустити_глобально(обчислювач, дані_запису.глобально_притриманий_предмет_звʼязку);
      відпустити_глобально(обчислювач, дані_запису.глобально_притриманий_предмет_даних);
      якщо дані_запису.дані_треба_звільнити == позитивне(так) {
        звільнити_памʼять(обчислювач, дані_запису.дані);
      }

      звільнити(обчислювач, дані_запису);
    }

    дія обробник_підключення_інтернет_звʼязку(турбіна: адреса<турбо::Турбіна>, інтернет_звʼязок: адреса<турбо::ІнтернетЗвʼязок>, аргумент: невідома_адреса, успішно: логічне, код_помилки: позитивне) {
      ціль дані_звʼязку = аргумент як адреса<ДаніВідкликуІнтернетЗвʼязку>;
      ціль обчислювач = дані_звʼязку.обчислювач;
      ціль предмет_звʼязку = отримати_глобально_притриманий_предмет(обчислювач, дані_звʼязку.глобально_притриманий_предмет_звʼязку);
      ціль предмет_обробника_підключення = отримати_глобально_притриманий_предмет(обчислювач, дані_звʼязку.глобально_притриманий_предмет_обробника_підключення);

      ціль кількість_аргументів: позитивне = 3;
      змінна аргументи = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів);
      аргументи[0] = предмет_обробника_підключення;
      аргументи[1] = предмет_звʼязку;

      якщо успішно {
        аргументи[2] = пусто як адреса<Предмет>;

        предмет_змінити_користувацькі_дані(обчислювач, предмет_звʼязку, інтернет_звʼязок);
      } інакше {
        аргументи[2] = створити_число(обчислювач, д64(код_помилки));
        якщо перевірити_чи_стан_падіння(обчислювач) {
          ПОМЕРТИ(обчислювач, "НОП: не вдалось створити число");
        }

        відпустити_глобально(обчислювач, дані_звʼязку.глобально_притриманий_предмет_звʼязку);
        відпустити_глобально(обчислювач, дані_звʼязку.глобально_притриманий_предмет_обробника_підключення);
        звільнити(обчислювач, дані_звʼязку);
      }

      запланувати_виконання(обчислювач, обробник_запланованого_виконання_відклику_на_підключення_інтернет_звʼязку, пусто, пусто, кількість_аргументів, аргументи);
    }

    дія обробник_даних_інтернет_звʼязку(турбіна: адреса<турбо::Турбіна>, інтернет_звʼязок: адреса<турбо::ІнтернетЗвʼязок>, аргумент: невідома_адреса, розмір: позитивне, дані: памʼять<п8>) {
      ціль дані_звʼязку = аргумент як адреса<ДаніВідкликуІнтернетЗвʼязку>;
      ціль обчислювач = дані_звʼязку.обчислювач;
      ціль предмет_звʼязку = отримати_глобально_притриманий_предмет(обчислювач, дані_звʼязку.глобально_притриманий_предмет_звʼязку);

      ціль предмет_буфера = створити_буфер_без_копіювання(обчислювач, розмір, дані);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        ПОМЕРТИ(обчислювач, "НОП: не вдалось створити буфер без копіювання");
      }

      ціль кількість_аргументів: позитивне = 2;
      змінна аргументи = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів);
      аргументи[0] = предмет_звʼязку;
      аргументи[1] = предмет_буфера;

      запланувати_виконання(обчислювач, обробник_запланованого_виконання_відклику_на_дані_інтернет_звʼязку, пусто, пусто, кількість_аргументів, аргументи);
    }

    дія обробник_закінчення_інтернет_звʼязку(турбіна: адреса<турбо::Турбіна>, інтернет_звʼязок: адреса<турбо::ІнтернетЗвʼязок>, аргумент: невідома_адреса) {
      ціль дані_звʼязку = аргумент як адреса<ДаніВідкликуІнтернетЗвʼязку>;
      ціль обчислювач = дані_звʼязку.обчислювач;
      ціль предмет_звʼязку = отримати_глобально_притриманий_предмет(обчислювач, дані_звʼязку.глобально_притриманий_предмет_звʼязку);

      ціль кількість_аргументів: позитивне = 1;
      змінна аргументи = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів);
      аргументи[0] = предмет_звʼязку;

      запланувати_виконання(обчислювач, обробник_запланованого_виконання_відклику_на_закінчення_інтернет_звʼязку, пусто, пусто, кількість_аргументів, аргументи);
    }

    дія обробник_відключення_інтернет_звʼязку(турбіна: адреса<турбо::Турбіна>, інтернет_звʼязок: адреса<турбо::ІнтернетЗвʼязок>, аргумент: невідома_адреса, з_помилкою: логічне, код_помилки: позитивне) {
      ціль дані_звʼязку = аргумент як адреса<ДаніВідкликуІнтернетЗвʼязку>;
      ціль обчислювач = дані_звʼязку.обчислювач;
      ціль предмет_звʼязку = отримати_глобально_притриманий_предмет(обчислювач, дані_звʼязку.глобально_притриманий_предмет_звʼязку);

      предмет_змінити_користувацькі_дані(обчислювач, предмет_звʼязку, пусто);

      ціль кількість_аргументів: позитивне = 2;
      змінна аргументи = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів);
      аргументи[0] = предмет_звʼязку;
      якщо з_помилкою {
        аргументи[1] = створити_число(обчислювач, д64(код_помилки));
      } інакше {
        аргументи[1] = пусто як адреса<Предмет>;
      }
      запланувати_виконання(обчислювач, обробник_запланованого_виконання_відклику_на_відключення_інтернет_звʼязку, пусто, пусто, кількість_аргументів, аргументи);

      відпустити_глобально(обчислювач, дані_звʼязку.глобально_притриманий_предмет_звʼязку);
      якщо дані_звʼязку.глобально_притриманий_предмет_обробника_підключення != пусто {
        відпустити_глобально(обчислювач, дані_звʼязку.глобально_притриманий_предмет_обробника_підключення);
      }
      звільнити(обчислювач, дані_звʼязку);
    }

    дія створити_предмет_структури_Звʼязок(обчислювач: адреса<Обчислювач>): предмет_або_стан_падіння {
      ціль притримувач = покласти_притримувач(обчислювач);

      ціль кількість_параметрів: позитивне = 5;
      змінна параметри = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_параметрів);

      ціль параметр_адреса = створити_параметр(обчислювач, назва(обчислювач, "адреса"), пусто, пусто);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути параметр_адреса;
      }
      параметри[0] = параметр_адреса;
      притримати(обчислювач, притримувач, параметр_адреса);

      ціль параметр_порт = створити_параметр(обчислювач, назва(обчислювач, "порт"), пусто, пусто);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути параметр_порт;
      }
      параметри[1] = параметр_порт;
      притримати(обчислювач, притримувач, параметр_порт);

      ціль параметр_обробник_даних = створити_параметр(обчислювач, назва(обчислювач, "обробник_даних"), пусто, пусто);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути параметр_обробник_даних;
      }
      параметри[2] = параметр_обробник_даних;
      притримати(обчислювач, притримувач, параметр_обробник_даних);

      ціль параметр_обробник_закінчення = створити_параметр(обчислювач, назва(обчислювач, "обробник_закінчення"), пусто, пусто);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути параметр_обробник_закінчення;
      }
      параметри[3] = параметр_обробник_закінчення;
      притримати(обчислювач, притримувач, параметр_обробник_закінчення);

      ціль параметр_обробник_відключення = створити_параметр(обчислювач, назва(обчислювач, "обробник_відключення"), пусто, пусто);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути параметр_обробник_відключення;
      }
      параметри[4] = параметр_обробник_відключення;
      притримати(обчислювач, притримувач, параметр_обробник_відключення);

      ціль предмет_структури_Звʼязок = створити_структуру(обчислювач, назва(обчислювач, "Звʼязок"), отримати_предмет_структури_предмет(обчислювач), кількість_параметрів, параметри);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути предмет_структури_Звʼязок;
      }

      забрати_притримувач(обчислювач);
      вернути предмет_структури_Звʼязок;
    }

    дія рідна_дія_модуля_інтернет_звʼязатись(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<Предмет>, предмет_я: адреса<Предмет>, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>, іменовані_аргументи: адреса<ІменованіАргументи>, місцезнаходження: Місцезнаходження): предмет_або_стан_падіння {
      змінна знайдено_аргумент_адреса = ні;
      змінна аргумент_адреса: адреса<Предмет> = пусто;
      змінна знайдено_аргумент_порт = ні;
      змінна аргумент_порт: адреса<Предмет> = пусто;
      змінна знайдено_аргумент_обробник_підключення = ні;
      змінна аргумент_обробник_підключення: адреса<Предмет> = пусто;
      змінна знайдено_аргумент_формат_даних = ні;
      змінна аргумент_формат_даних: адреса<Предмет> = пусто;
      якщо іменовані_аргументи != пусто {
        знайдено_аргумент_адреса = знайти_іменований_аргумент(обчислювач, іменовані_аргументи, назва(обчислювач, "адреса"), аргумент_адреса::адреса);
        знайдено_аргумент_порт = знайти_іменований_аргумент(обчислювач, іменовані_аргументи, назва(обчислювач, "порт"), аргумент_порт::адреса);
        знайдено_аргумент_обробник_підключення = знайти_іменований_аргумент(обчислювач, іменовані_аргументи, назва(обчислювач, "обробник_підключення"), аргумент_обробник_підключення::адреса);
        знайдено_аргумент_формат_даних = знайти_іменований_аргумент(обчислювач, іменовані_аргументи, назва(обчислювач, "формат_даних"), аргумент_формат_даних::адреса);
      }
      якщо аргументи != пусто {
        якщо знайдено_аргумент_адреса == ні {
          якщо кількість_аргументів > 0 {
            знайдено_аргумент_адреса = так;
            аргумент_адреса = аргументи[0];
          }
        }
        якщо знайдено_аргумент_порт == ні {
          якщо кількість_аргументів > 1 {
            знайдено_аргумент_порт = так;
            аргумент_порт = аргументи[1];
          }
        }
        якщо знайдено_аргумент_обробник_підключення == ні {
          якщо кількість_аргументів > 2 {
            знайдено_аргумент_обробник_підключення = так;
            аргумент_обробник_підключення = аргументи[2];
          }
        }
        якщо знайдено_аргумент_формат_даних == ні {
          якщо кількість_аргументів > 3 {
            знайдено_аргумент_формат_даних = так;
            аргумент_формат_даних = аргументи[3];
          }
        }
      }

      змінна адреса_ю8 = ю8 { 0, пусто };

      якщо перевірити_чи_предмет_є_текстом(обчислювач, аргумент_адреса) {
        змінна адреса_т8 = отримати_значення_тексту(обчислювач, аргумент_адреса);

        якщо перекодувати_т8_в_ю8(обчислювач, адреса_т8, адреса_ю8::адреса) == ні {
          вернути падіння(обчислювач, "Не вдалось отримати ю8 з тексту адреси");
        }
      } інакше якщо перевірити_чи_предмет_є_юнікодом(обчислювач, аргумент_адреса) {
        змінна адреса_ю32 = отримати_значення_юнікоду(обчислювач, аргумент_адреса);

        якщо перекодувати_ю32_в_ю8(обчислювач, адреса_ю32, адреса_ю8::адреса) == ні {
          вернути падіння(обчислювач, "Не вдалось отримати ю8 з юнікоду адреси");
        }
      } інакше {
        вернути падіння(обчислювач, "Адреса має бути текстом або юнікодом");
      }

      якщо перевірити_чи_предмет_є_числом(обчислювач, аргумент_порт) == ні {
        звільнити_памʼять(обчислювач, адреса_ю8.дані);
        вернути падіння(обчислювач, "Порт має бути числом");
      }

      ціль порт = ц32(отримати_значення_числа(обчислювач, аргумент_порт));

      ціль предмет_структури_Звʼязок = отримати_глобально_притриманий_предмет(обчислювач, отримати_дані_дії(обчислювач, предмет_дії) як адреса<ГлобальноПритриманийПредмет>);

      ціль кількість_аргументів_виконання_Звʼязок: позитивне = 2;
      ціль аргументи_виконання_Звʼязок = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів_виконання_Звʼязок);
      аргументи_виконання_Звʼязок[0] = аргумент_адреса;
      аргументи_виконання_Звʼязок[1] = аргумент_порт;
      ціль предмет_звʼязку = предмет_виконати(обчислювач, предмет_структури_Звʼязок, пусто, кількість_аргументів_виконання_Звʼязок, аргументи_виконання_Звʼязок, пусто, Місцезнаходження { пусто, 0 });
      звільнити_памʼять(обчислювач, аргументи_виконання_Звʼязок);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        вернути предмет_звʼязку;
      }

      ціль турбіна = турбо::отримати_глобальну_турбіну();

      змінна дані_звʼязку = виділити<ДаніВідкликуІнтернетЗвʼязку>(обчислювач);
      дані_звʼязку.обчислювач = обчислювач;
      дані_звʼязку.глобально_притриманий_предмет_звʼязку = притримати_глобально(обчислювач, предмет_звʼязку);
      дані_звʼязку.глобально_притриманий_предмет_обробника_підключення = притримати_глобально(обчислювач, аргумент_обробник_підключення);

      змінна іа: позитивне = 0;
      ціль результат_іа = турбо::отримати_інтернет_адресу_з_ю8(турбіна, адреса_ю8, іа::адреса);
      звільнити_памʼять(обчислювач, адреса_ю8.дані);
      якщо результат_іа == ні {
        вернути падіння(обчислювач, "Не вдалось отримати іа");
      }

      турбо::підключити_інтернет_звʼязок(
        турбіна,
        іа,
        позитивне(порт),
        обробник_підключення_інтернет_звʼязку,
        обробник_даних_інтернет_звʼязку,
        обробник_закінчення_інтернет_звʼязку,
        обробник_відключення_інтернет_звʼязку,
        дані_звʼязку
      );

      вернути пусто;
    }

    дія зробити_копію_даних_дії_модуля_інтернет_звʼязатись(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<Предмет>): невідома_адреса {
      ціль дані_дії = отримати_дані_дії(обчислювач, предмет_дії) як адреса<ГлобальноПритриманийПредмет>;
      вернути притримати_глобально(обчислювач, отримати_глобально_притриманий_предмет(обчислювач, дані_дії));
    }

    дія відклик_перед_звільненням_дії_модуля_інтернет_звʼязатись(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<Предмет>) {
      ціль дані_дії = отримати_дані_дії(обчислювач, предмет_дії) як адреса<ГлобальноПритриманийПредмет>;
      відпустити_глобально(обчислювач, дані_дії);
    }

    дія створити_дію_модуля_інтернет_звʼязатись(обчислювач: адреса<Обчислювач>, предмет_структури_Звʼязок: адреса<Предмет>): предмет_або_стан_падіння {
      вернути створити_рідну_дію_з_даними(
        обчислювач,
        назва(обчислювач, т8"звʼязатись"),
        0,
        пусто,
        пусто,
        рідна_дія_модуля_інтернет_звʼязатись,
        притримати_глобально(обчислювач, предмет_структури_Звʼязок),
        зробити_копію_даних_дії_модуля_інтернет_звʼязатись,
        відклик_перед_звільненням_дії_модуля_інтернет_звʼязатись
      );
    }

    дія рідна_дія_модуля_інтернет_надіслати(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<Предмет>, предмет_я: адреса<Предмет>, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>, іменовані_аргументи: адреса<ІменованіАргументи>, місцезнаходження: Місцезнаходження): предмет_або_стан_падіння {
      змінна знайдено_аргумент_повідомлення = ні;
      змінна аргумент_повідомлення: адреса<Предмет> = пусто;
      змінна знайдено_аргумент_отримувач = ні;
      змінна аргумент_отримувач: адреса<Предмет> = пусто;
      якщо іменовані_аргументи != пусто {
        знайдено_аргумент_повідомлення = знайти_іменований_аргумент(обчислювач, іменовані_аргументи, назва(обчислювач, "повідомлення"), аргумент_повідомлення::адреса);
        знайдено_аргумент_отримувач = знайти_іменований_аргумент(обчислювач, іменовані_аргументи, назва(обчислювач, "отримувач"), аргумент_отримувач::адреса);
      }
      якщо аргументи != пусто {
        якщо знайдено_аргумент_повідомлення == ні {
          якщо кількість_аргументів > 0 {
            знайдено_аргумент_повідомлення = так;
            аргумент_повідомлення = аргументи[0];
          }
        }
        якщо знайдено_аргумент_отримувач == ні {
          якщо кількість_аргументів > 1 {
            знайдено_аргумент_отримувач = так;
            аргумент_отримувач = аргументи[1];
          }
        }
      }

      змінна байти_треба_звільнити = ні;
      змінна байти = Байти { 0, пусто };
      якщо перевірити_чи_предмет_є_байтами(обчислювач, аргумент_повідомлення) {
        байти = отримати_значення_байтів(обчислювач, аргумент_повідомлення);
      } інакше якщо перевірити_чи_предмет_є_буфером(обчислювач, аргумент_повідомлення) {
        байти = отримати_значення_буфера(обчислювач, аргумент_повідомлення);
      } інакше якщо перевірити_чи_предмет_є_текстом(обчислювач, аргумент_повідомлення) {
        ціль значення_тексту = отримати_значення_тексту(обчислювач, аргумент_повідомлення);
        байти = Байти { значення_тексту.розмір, значення_тексту.дані };
      } інакше якщо перевірити_чи_предмет_є_юнікодом(обчислювач, аргумент_повідомлення) {
        ціль значення_юнікоду = отримати_значення_юнікоду(обчислювач, аргумент_повідомлення);
        змінна значення_ю8 = ю8 { 0, пусто };
        якщо перекодувати_ю32_в_ю8(обчислювач, значення_юнікоду, значення_ю8::адреса) == ні {
          вернути падіння(обчислювач, "Не вдалось отримати ю8 з юнікоду повідомлення");
        }
        байти_треба_звільнити = так;
        байти = Байти { значення_ю8.розмір, значення_ю8.дані };
      } інакше {
        вернути падіння(обчислювач, "Повідомлення має бути байтами, буфером, текстом або юнікодом");
      }

      ціль предмет_структури_Звʼязок = отримати_глобально_притриманий_предмет(обчислювач, отримати_дані_дії(обчислювач, предмет_дії) як адреса<ГлобальноПритриманийПредмет>);

      якщо перевірити_чи_предмет_є(обчислювач, аргумент_отримувач, предмет_структури_Звʼязок) == ні {
        вернути падіння(обчислювач, "Отримувач має бути звʼязком");
      }

      ціль інтернет_звʼязок = предмет_отримати_користувацькі_дані(обчислювач, аргумент_отримувач) як адреса<турбо::ІнтернетЗвʼязок>;
      якщо інтернет_звʼязок == пусто {
        вернути пусто;
      }

      ціль турбіна = турбо::отримати_глобальну_турбіну();

      змінна дані_запису = виділити<ДаніВідкликуНаРезультатЗаписуВІнтернетЗвʼязок>(обчислювач);
      дані_запису.обчислювач = обчислювач;
      дані_запису.глобально_притриманий_предмет_звʼязку = притримати_глобально(обчислювач, аргумент_отримувач);
      дані_запису.глобально_притриманий_предмет_даних = притримати_глобально(обчислювач, аргумент_повідомлення);
      дані_запису.розмір = байти.розмір;
      дані_запису.дані = байти.дані;
      дані_запису.дані_треба_звільнити = позитивне(байти_треба_звільнити);

      турбо::записати_в_інтернет_звʼязок(турбіна, інтернет_звʼязок, байти.розмір, байти.дані, обробник_запису_інтернет_звʼязку, дані_запису);

      вернути пусто;
    }

    дія зробити_копію_даних_дії_модуля_інтернет_надіслати(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<Предмет>): невідома_адреса {
      ціль дані_дії = отримати_дані_дії(обчислювач, предмет_дії) як адреса<ГлобальноПритриманийПредмет>;
      вернути притримати_глобально(обчислювач, отримати_глобально_притриманий_предмет(обчислювач, дані_дії));
    }

    дія відклик_перед_звільненням_дії_модуля_інтернет_надіслати(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<Предмет>) {
      ціль дані_дії = отримати_дані_дії(обчислювач, предмет_дії) як адреса<ГлобальноПритриманийПредмет>;
      відпустити_глобально(обчислювач, дані_дії);
    }

    дія створити_дію_модуля_інтернет_надіслати(обчислювач: адреса<Обчислювач>, предмет_структури_Звʼязок: адреса<Предмет>): предмет_або_стан_падіння {
      вернути створити_рідну_дію_з_даними(
        обчислювач,
        назва(обчислювач, т8"надіслати"),
        0,
        пусто,
        пусто,
        рідна_дія_модуля_інтернет_надіслати,
        притримати_глобально(обчислювач, предмет_структури_Звʼязок),
        зробити_копію_даних_дії_модуля_інтернет_надіслати,
        відклик_перед_звільненням_дії_модуля_інтернет_надіслати
      );
    }

    дія рідна_дія_модуля_інтернет_закінчити(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<Предмет>, предмет_я: адреса<Предмет>, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>, іменовані_аргументи: адреса<ІменованіАргументи>, місцезнаходження: Місцезнаходження): предмет_або_стан_падіння {
      змінна знайдено_аргумент_звʼязок = ні;
      змінна аргумент_звʼязок: адреса<Предмет> = пусто;
      якщо іменовані_аргументи != пусто {
        знайдено_аргумент_звʼязок = знайти_іменований_аргумент(обчислювач, іменовані_аргументи, назва(обчислювач, "звʼязок"), аргумент_звʼязок::адреса);
      }
      якщо аргументи != пусто {
        якщо знайдено_аргумент_звʼязок == ні {
          якщо кількість_аргументів > 0 {
            знайдено_аргумент_звʼязок = так;
            аргумент_звʼязок = аргументи[0];
          }
        }
      }

      ціль предмет_структури_Звʼязок = отримати_глобально_притриманий_предмет(обчислювач, отримати_дані_дії(обчислювач, предмет_дії) як адреса<ГлобальноПритриманийПредмет>);

      якщо перевірити_чи_предмет_є(обчислювач, аргумент_звʼязок, предмет_структури_Звʼязок) == ні {
        вернути падіння(обчислювач, "Перший аргумент має бути звʼязком");
      }

      ціль інтернет_звʼязок = предмет_отримати_користувацькі_дані(обчислювач, аргумент_звʼязок) як адреса<турбо::ІнтернетЗвʼязок>;
      якщо інтернет_звʼязок == пусто {
        вернути пусто;
      }

      ціль турбіна = турбо::отримати_глобальну_турбіну();

      турбо::закінчити_інтернет_звʼязок(турбіна, інтернет_звʼязок);

      вернути пусто;
    }

    дія зробити_копію_даних_дії_модуля_інтернет_закінчити(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<Предмет>): невідома_адреса {
      ціль дані_дії = отримати_дані_дії(обчислювач, предмет_дії) як адреса<ГлобальноПритриманийПредмет>;
      вернути притримати_глобально(обчислювач, отримати_глобально_притриманий_предмет(обчислювач, дані_дії));
    }

    дія відклик_перед_звільненням_дії_модуля_інтернет_закінчити(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<Предмет>) {
      ціль дані_дії = отримати_дані_дії(обчислювач, предмет_дії) як адреса<ГлобальноПритриманийПредмет>;
      відпустити_глобально(обчислювач, дані_дії);
    }

    дія створити_дію_модуля_інтернет_закінчити(обчислювач: адреса<Обчислювач>, предмет_структури_Звʼязок: адреса<Предмет>): предмет_або_стан_падіння {
      вернути створити_рідну_дію_з_даними(
        обчислювач,
        назва(обчислювач, т8"закінчити"),
        0,
        пусто,
        пусто,
        рідна_дія_модуля_інтернет_закінчити,
        притримати_глобально(обчислювач, предмет_структури_Звʼязок),
        зробити_копію_даних_дії_модуля_інтернет_закінчити,
        відклик_перед_звільненням_дії_модуля_інтернет_закінчити
      );
    }
  }
}

// Обслуговувач
секція мавка {
  секція бібліотека {
    структура ДаніВідкликуІнтернетОбслуговувача {
      обчислювач: адреса<Обчислювач>;
      глобально_притриманий_предмет_обслуговувача: адреса<ГлобальноПритриманийПредмет>;
      глобально_притриманий_предмет_обробника_запуску: адреса<ГлобальноПритриманийПредмет>;
      глобально_притриманий_предмет_структури_Звʼязок: адреса<ГлобальноПритриманийПредмет>;
    }

    структура ДаніДіїОбслуговувати {
      обчислювач: адреса<Обчислювач>;
      глобально_притриманий_предмет_структури_Обслуговувач: адреса<ГлобальноПритриманийПредмет>;
      глобально_притриманий_предмет_структури_Звʼязок: адреса<ГлобальноПритриманийПредмет>;
    }

    дія обробник_запланованого_виконання_відклику_на_запуск_інтернет_обслуговувача(обчислювач: адреса<Обчислювач>, дані: невідома_адреса, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>): предмет_або_стан_падіння {
      ціль предмет_обробника_запуску = аргументи[0];
      вернути предмет_виконати(обчислювач, предмет_обробника_запуску, пусто, 3, аргументи[1]::адреса як памʼять<адреса<Предмет>>, пусто, Місцезнаходження { пусто, 0 });
    }

    дія обробник_запланованого_виконання_відклику_на_підключення_звʼязку_інтернет_обслуговувача(обчислювач: адреса<Обчислювач>, дані: невідома_адреса, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>): предмет_або_стан_падіння {
      ціль предмет_обслуговувача = аргументи[0];
      вернути предмет_виконати_властивість(обчислювач, предмет_обслуговувача, назва(обчислювач, "обробник_підключення_звʼязку"), 3, аргументи, пусто, Місцезнаходження { пусто, 0 });
    }

    дія обробник_запланованого_виконання_відклику_на_зупинку_інтернет_обслуговувача(обчислювач: адреса<Обчислювач>, дані: невідома_адреса, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>): предмет_або_стан_падіння {
      ціль предмет_обслуговувача = аргументи[0];
      вернути предмет_виконати_властивість(обчислювач, предмет_обслуговувача, назва(обчислювач, "обробник_зупинки"), 2, аргументи, пусто, Місцезнаходження { пусто, 0 });
    }

    дія обробник_запуску_інтернет_обслуговувача(турбіна: адреса<турбо::Турбіна>, інтернет_обслуговувач: адреса<турбо::ІнтернетОбслуговувач>, аргумент: невідома_адреса, успішно: логічне, код_помилки: ц32, іа: позитивне, порт: позитивне) {
      ціль дані_обслуговувача = аргумент як адреса<ДаніВідкликуІнтернетОбслуговувача>;
      ціль обчислювач = дані_обслуговувача.обчислювач;
      ціль предмет_обслуговувача = отримати_глобально_притриманий_предмет(обчислювач, дані_обслуговувача.глобально_притриманий_предмет_обслуговувача);
      ціль предмет_обробника_запуску = отримати_глобально_притриманий_предмет(обчислювач, дані_обслуговувача.глобально_притриманий_предмет_обробника_запуску);

      ціль кількість_аргументів: позитивне = 3;
      змінна аргументи = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів);
      аргументи[0] = предмет_обробника_запуску;
      аргументи[1] = предмет_обслуговувача;

      якщо успішно {
        аргументи[2] = пусто як адреса<Предмет>;

        предмет_змінити_користувацькі_дані(обчислювач, предмет_обслуговувача, інтернет_обслуговувач);
      } інакше {
        аргументи[2] = створити_число(обчислювач, д64(код_помилки));
        якщо перевірити_чи_стан_падіння(обчислювач) {
          ПОМЕРТИ(обчислювач, "НОП: не вдалось створити число");
        }

        відпустити_глобально(обчислювач, дані_обслуговувача.глобально_притриманий_предмет_обслуговувача);
        відпустити_глобально(обчислювач, дані_обслуговувача.глобально_притриманий_предмет_обробника_запуску);
        відпустити_глобально(обчислювач, дані_обслуговувача.глобально_притриманий_предмет_структури_Звʼязок);
        звільнити(обчислювач, дані_обслуговувача);
      }

      запланувати_виконання(обчислювач, обробник_запланованого_виконання_відклику_на_запуск_інтернет_обслуговувача, пусто, пусто, кількість_аргументів, аргументи);
    }

    дія обробник_підключення_звʼязку_інтернет_обслуговувача(турбіна: адреса<турбо::Турбіна>, інтернет_обслуговувач: адреса<турбо::ІнтернетОбслуговувач>, інтернет_звʼязок: адреса<турбо::ІнтернетЗвʼязок>, аргумент: невідома_адреса) {
      ціль дані_обслуговувача = аргумент як адреса<ДаніВідкликуІнтернетОбслуговувача>;
      ціль обчислювач = дані_обслуговувача.обчислювач;
      ціль предмет_обслуговувача = отримати_глобально_притриманий_предмет(обчислювач, дані_обслуговувача.глобально_притриманий_предмет_обслуговувача);
      ціль предмет_структури_Звʼязок = отримати_глобально_притриманий_предмет(обчислювач, дані_обслуговувача.глобально_притриманий_предмет_структури_Звʼязок);

      ціль кількість_аргументів: позитивне = 3;
      змінна аргументи = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів);
      аргументи[0] = предмет_обслуговувача;

      ціль предмет_адреси = пусто як адреса<Предмет>;
      ціль предмет_порта = пусто як адреса<Предмет>;

      ціль кількість_аргументів_виконання_Звʼязок: позитивне = 2;
      ціль аргументи_виконання_Звʼязок = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів_виконання_Звʼязок);
      аргументи_виконання_Звʼязок[0] = предмет_адреси;
      аргументи_виконання_Звʼязок[1] = предмет_порта;
      ціль предмет_звʼязку = предмет_виконати(обчислювач, предмет_структури_Звʼязок, пусто, кількість_аргументів_виконання_Звʼязок, аргументи_виконання_Звʼязок, пусто, Місцезнаходження { пусто, 0 });
      звільнити_памʼять(обчислювач, аргументи_виконання_Звʼязок);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        ПОМЕРТИ(обчислювач, "НОП: не вдалось створити предмет структури Звʼязок");
      } інакше {
        предмет_змінити_користувацькі_дані(обчислювач, предмет_звʼязку, інтернет_звʼязок);

        змінна дані_звʼязку = виділити<ДаніВідкликуІнтернетЗвʼязку>(обчислювач);
        дані_звʼязку.обчислювач = обчислювач;
        дані_звʼязку.глобально_притриманий_предмет_звʼязку = притримати_глобально(обчислювач, предмет_звʼязку);
        дані_звʼязку.глобально_притриманий_предмет_обробника_підключення = пусто;

        турбо::встановити_обробники_інтернет_звʼязку(
          турбіна,
          інтернет_звʼязок,
          обробник_даних_інтернет_звʼязку,
          обробник_закінчення_інтернет_звʼязку,
          обробник_відключення_інтернет_звʼязку,
          дані_звʼязку
        );

        аргументи[1] = предмет_звʼязку;
        аргументи[2] = пусто як адреса<Предмет>;

        запланувати_виконання(обчислювач, обробник_запланованого_виконання_відклику_на_підключення_звʼязку_інтернет_обслуговувача, пусто, пусто, кількість_аргументів, аргументи);
      }
    }

    дія обробник_зупинки_інтернет_обслуговувача(турбіна: адреса<турбо::Турбіна>, інтернет_обслуговувач: адреса<турбо::ІнтернетОбслуговувач>, аргумент: невідома_адреса) {
      ціль дані_обслуговувача = аргумент як адреса<ДаніВідкликуІнтернетОбслуговувача>;
      ціль обчислювач = дані_обслуговувача.обчислювач;
      ціль предмет_обслуговувача = отримати_глобально_притриманий_предмет(обчислювач, дані_обслуговувача.глобально_притриманий_предмет_обслуговувача);

      предмет_змінити_користувацькі_дані(обчислювач, предмет_обслуговувача, пусто);

      ціль кількість_аргументів: позитивне = 1;
      змінна аргументи = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів);
      аргументи[0] = предмет_обслуговувача;

      запланувати_виконання(обчислювач, обробник_запланованого_виконання_відклику_на_зупинку_інтернет_обслуговувача, пусто, пусто, кількість_аргументів, аргументи);
    }

    дія створити_предмет_структури_Обслуговувач(обчислювач: адреса<Обчислювач>): предмет_або_стан_падіння {
      ціль притримувач = покласти_притримувач(обчислювач);

      ціль кількість_параметрів: позитивне = 4;
      змінна параметри = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_параметрів);

      ціль параметр_адреса = створити_параметр(обчислювач, назва(обчислювач, "адреса"), пусто, пусто);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути параметр_адреса;
      }
      параметри[0] = параметр_адреса;
      притримати(обчислювач, притримувач, параметр_адреса);

      ціль параметр_порт = створити_параметр(обчислювач, назва(обчислювач, "порт"), пусто, пусто);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути параметр_порт;
      }
      параметри[1] = параметр_порт;
      притримати(обчислювач, притримувач, параметр_порт);

      ціль параметр_обробник_підключення_звʼязку = створити_параметр(обчислювач, назва(обчислювач, "обробник_підключення_звʼязку"), пусто, пусто);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути параметр_обробник_підключення_звʼязку;
      }
      параметри[2] = параметр_обробник_підключення_звʼязку;
      притримати(обчислювач, притримувач, параметр_обробник_підключення_звʼязку);

      ціль параметр_обробник_зупинки = створити_параметр(обчислювач, назва(обчислювач, "обробник_зупинки"), пусто, пусто);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути параметр_обробник_зупинки;
      }
      параметри[3] = параметр_обробник_зупинки;
      притримати(обчислювач, притримувач, параметр_обробник_зупинки);

      ціль предмет_структури_Обслуговувач = створити_структуру(обчислювач, назва(обчислювач, "Обслуговувач"), отримати_предмет_структури_предмет(обчислювач), кількість_параметрів, параметри);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути предмет_структури_Обслуговувач;
      }

      забрати_притримувач(обчислювач);
      вернути предмет_структури_Обслуговувач;
    }

    дія рідна_дія_модуля_інтернет_обслуговувати(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<Предмет>, предмет_я: адреса<Предмет>, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>, іменовані_аргументи: адреса<ІменованіАргументи>, місцезнаходження: Місцезнаходження): предмет_або_стан_падіння {
      змінна знайдено_аргумент_адреса = ні;
      змінна аргумент_адреса: адреса<Предмет> = пусто;
      змінна знайдено_аргумент_порт = ні;
      змінна аргумент_порт: адреса<Предмет> = пусто;
      змінна знайдено_аргумент_обробник_запуску = ні;
      змінна аргумент_обробник_запуску: адреса<Предмет> = пусто;
      змінна знайдено_аргумент_формат_даних = ні;
      змінна аргумент_формат_даних: адреса<Предмет> = пусто;
      якщо іменовані_аргументи != пусто {
        знайдено_аргумент_адреса = знайти_іменований_аргумент(обчислювач, іменовані_аргументи, назва(обчислювач, "адреса"), аргумент_адреса::адреса);
        знайдено_аргумент_порт = знайти_іменований_аргумент(обчислювач, іменовані_аргументи, назва(обчислювач, "порт"), аргумент_порт::адреса);
        знайдено_аргумент_обробник_запуску = знайти_іменований_аргумент(обчислювач, іменовані_аргументи, назва(обчислювач, "обробник_запуску"), аргумент_обробник_запуску::адреса);
        знайдено_аргумент_формат_даних = знайти_іменований_аргумент(обчислювач, іменовані_аргументи, назва(обчислювач, "формат_даних"), аргумент_формат_даних::адреса);
      }
      якщо аргументи != пусто {
        якщо знайдено_аргумент_адреса == ні {
          якщо кількість_аргументів > 0 {
            знайдено_аргумент_адреса = так;
            аргумент_адреса = аргументи[0];
          }
        }
        якщо знайдено_аргумент_порт == ні {
          якщо кількість_аргументів > 1 {
            знайдено_аргумент_порт = так;
            аргумент_порт = аргументи[1];
          }
        }
        якщо знайдено_аргумент_обробник_запуску == ні {
          якщо кількість_аргументів > 2 {
            знайдено_аргумент_обробник_запуску = так;
            аргумент_обробник_запуску = аргументи[2];
          }
        }
        якщо знайдено_аргумент_формат_даних == ні {
          якщо кількість_аргументів > 3 {
            знайдено_аргумент_формат_даних = так;
            аргумент_формат_даних = аргументи[3];
          }
        }
      }

      змінна адреса_ю8 = ю8 { 0, пусто };

      якщо перевірити_чи_предмет_є_текстом(обчислювач, аргумент_адреса) {
        змінна адреса_т8 = отримати_значення_тексту(обчислювач, аргумент_адреса);

        якщо перекодувати_т8_в_ю8(обчислювач, адреса_т8, адреса_ю8::адреса) == ні {
          вернути падіння(обчислювач, "Не вдалось отримати ю8 з тексту адреси");
        }
      } інакше якщо перевірити_чи_предмет_є_юнікодом(обчислювач, аргумент_адреса) {
        змінна адреса_ю32 = отримати_значення_юнікоду(обчислювач, аргумент_адреса);

        якщо перекодувати_ю32_в_ю8(обчислювач, адреса_ю32, адреса_ю8::адреса) == ні {
          вернути падіння(обчислювач, "Не вдалось отримати ю8 з юнікоду адреси");
        }
      } інакше {
        вернути падіння(обчислювач, "Адреса має бути текстом або юнікодом");
      }

      якщо перевірити_чи_предмет_є_числом(обчислювач, аргумент_порт) == ні {
        звільнити_памʼять(обчислювач, адреса_ю8.дані);
        вернути падіння(обчислювач, "Порт має бути числом");
      }

      ціль порт = ц32(отримати_значення_числа(обчислювач, аргумент_порт));

      ціль дані_дії_обслуговувати = отримати_дані_дії(обчислювач, предмет_дії) як адреса<ДаніДіїОбслуговувати>;

      ціль предмет_структури_Обслуговувач = отримати_глобально_притриманий_предмет(обчислювач, дані_дії_обслуговувати.глобально_притриманий_предмет_структури_Обслуговувач як адреса<ГлобальноПритриманийПредмет>);
      ціль предмет_структури_Звʼязок = отримати_глобально_притриманий_предмет(обчислювач, дані_дії_обслуговувати.глобально_притриманий_предмет_структури_Звʼязок як адреса<ГлобальноПритриманийПредмет>);

      ціль кількість_аргументів_виконання_Обслуговувач: позитивне = 2;
      ціль аргументи_виконання_Обслуговувач = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів_виконання_Обслуговувач);
      аргументи_виконання_Обслуговувач[0] = аргумент_адреса;
      аргументи_виконання_Обслуговувач[1] = аргумент_порт;
      ціль предмет_обслуговувача = предмет_виконати(обчислювач, предмет_структури_Обслуговувач, пусто, кількість_аргументів_виконання_Обслуговувач, аргументи_виконання_Обслуговувач, пусто, Місцезнаходження { пусто, 0 });
      звільнити_памʼять(обчислювач, аргументи_виконання_Обслуговувач);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        вернути предмет_обслуговувача;
      }

      ціль турбіна = турбо::отримати_глобальну_турбіну();

      змінна дані_обслуговувача = виділити<ДаніВідкликуІнтернетОбслуговувача>(обчислювач);
      дані_обслуговувача.обчислювач = обчислювач;
      дані_обслуговувача.глобально_притриманий_предмет_обслуговувача = притримати_глобально(обчислювач, предмет_обслуговувача);
      дані_обслуговувача.глобально_притриманий_предмет_обробника_запуску = притримати_глобально(обчислювач, аргумент_обробник_запуску);
      дані_обслуговувача.глобально_притриманий_предмет_структури_Звʼязок = притримати_глобально(обчислювач, предмет_структури_Звʼязок);

      змінна іа: позитивне = 0;
      ціль результат_іа = турбо::отримати_інтернет_адресу_з_ю8(турбіна, адреса_ю8, іа::адреса);
      звільнити_памʼять(обчислювач, адреса_ю8.дані);
      якщо результат_іа == ні {
        вернути падіння(обчислювач, "Не вдалось отримати іа");
      }

      турбо::запустити_інтернет_обслуговувач(
        турбіна,
        іа,
        позитивне(порт),
        обробник_запуску_інтернет_обслуговувача,
        обробник_підключення_звʼязку_інтернет_обслуговувача,
        обробник_зупинки_інтернет_обслуговувача,
        дані_обслуговувача
      );

      вернути пусто;
    }

    дія зробити_копію_даних_дії_модуля_інтернет_обслуговувати(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<Предмет>): невідома_адреса {
      // потім: втілити
      вернути пусто;
    }

    дія відклик_перед_звільненням_дії_модуля_інтернет_обслуговувати(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<Предмет>) {
      ціль дані_дії_обслуговувати = отримати_дані_дії(обчислювач, предмет_дії) як адреса<ДаніДіїОбслуговувати>;
      відпустити_глобально(обчислювач, дані_дії_обслуговувати.глобально_притриманий_предмет_структури_Обслуговувач);
      відпустити_глобально(обчислювач, дані_дії_обслуговувати.глобально_притриманий_предмет_структури_Звʼязок);
      звільнити(обчислювач, дані_дії_обслуговувати);
    }

    дія створити_дію_модуля_інтернет_обслуговувати(обчислювач: адреса<Обчислювач>, предмет_структури_Обслуговувач: адреса<Предмет>, предмет_структури_Звʼязок: адреса<Предмет>): предмет_або_стан_падіння {
      змінна дані_дії_обслуговувати = виділити<ДаніДіїОбслуговувати>(обчислювач);
      дані_дії_обслуговувати.глобально_притриманий_предмет_структури_Обслуговувач = притримати_глобально(обчислювач, предмет_структури_Обслуговувач);
      дані_дії_обслуговувати.глобально_притриманий_предмет_структури_Звʼязок = притримати_глобально(обчислювач, предмет_структури_Звʼязок);

      вернути створити_рідну_дію_з_даними(
        обчислювач,
        назва(обчислювач, т8"обслуговувати"),
        0,
        пусто,
        пусто,
        рідна_дія_модуля_інтернет_обслуговувати,
        дані_дії_обслуговувати,
        зробити_копію_даних_дії_модуля_інтернет_обслуговувати,
        відклик_перед_звільненням_дії_модуля_інтернет_обслуговувати
      );
    }
  }
}

секція мавка {
  секція бібліотека {
    дія створити_модуль_інтернет(обчислювач: адреса<Обчислювач>): предмет_або_стан_падіння {
      ціль притримувач = покласти_притримувач(обчислювач);

      ціль предмет_модуля = створити_модуль(обчислювач, назва(обчислювач, "інтернет"), 0, пусто);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути предмет_модуля;
      }
      притримати(обчислювач, притримувач, предмет_модуля);

      ціль предмет_структури_Звʼязок = створити_предмет_структури_Звʼязок(обчислювач);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути предмет_структури_Звʼязок;
      }
      притримати(обчислювач, притримувач, предмет_структури_Звʼязок);
      змінна результат_зміни_властивості = предмет_змінити_властивість(обчислювач, предмет_модуля, назва(обчислювач, "Звʼязок"), предмет_структури_Звʼязок, Місцезнаходження { пусто, 0 });
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути результат_зміни_властивості;
      }

      ціль предмет_структури_Обслуговувач = створити_предмет_структури_Обслуговувач(обчислювач);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути предмет_структури_Обслуговувач;
      }
      притримати(обчислювач, притримувач, предмет_структури_Обслуговувач);
      результат_зміни_властивості = предмет_змінити_властивість(обчислювач, предмет_модуля, назва(обчислювач, "Обслуговувач"), предмет_структури_Обслуговувач, Місцезнаходження { пусто, 0 });
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути результат_зміни_властивості;
      }

      ціль предмет_дії_обслуговувати = створити_дію_модуля_інтернет_обслуговувати(обчислювач, предмет_структури_Обслуговувач, предмет_структури_Звʼязок);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути предмет_дії_обслуговувати;
      }
      притримати(обчислювач, притримувач, предмет_дії_обслуговувати);
      результат_зміни_властивості = предмет_змінити_властивість(обчислювач, предмет_модуля, назва(обчислювач, "обслуговувати"), предмет_дії_обслуговувати, Місцезнаходження { пусто, 0 });
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути результат_зміни_властивості;
      }

      ціль предмет_дії_звʼязатись = створити_дію_модуля_інтернет_звʼязатись(обчислювач, предмет_структури_Звʼязок);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути предмет_дії_звʼязатись;
      }
      притримати(обчислювач, притримувач, предмет_дії_звʼязатись);
      результат_зміни_властивості = предмет_змінити_властивість(обчислювач, предмет_модуля, назва(обчислювач, "звʼязатись"), предмет_дії_звʼязатись, Місцезнаходження { пусто, 0 });
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути результат_зміни_властивості;
      }

      ціль предмет_дії_надіслати = створити_дію_модуля_інтернет_надіслати(обчислювач, предмет_структури_Звʼязок);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути предмет_дії_надіслати;
      }
      притримати(обчислювач, притримувач, предмет_дії_надіслати);
      результат_зміни_властивості = предмет_змінити_властивість(обчислювач, предмет_модуля, назва(обчислювач, "надіслати"), предмет_дії_надіслати, Місцезнаходження { пусто, 0 });
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути результат_зміни_властивості;
      }

      ціль предмет_дії_закінчити = створити_дію_модуля_інтернет_закінчити(обчислювач, предмет_структури_Звʼязок);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути предмет_дії_закінчити;
      }
      притримати(обчислювач, притримувач, предмет_дії_закінчити);
      результат_зміни_властивості = предмет_змінити_властивість(обчислювач, предмет_модуля, назва(обчислювач, "закінчити"), предмет_дії_закінчити, Місцезнаходження { пусто, 0 });
      якщо перевірити_чи_стан_падіння(обчислювач) {
        забрати_притримувач(обчислювач);
        вернути результат_зміни_властивості;
      }

      забрати_притримувач(обчислювач);
      вернути предмет_модуля;
    }

    зовнішня дія взяти_модуль_біб_інтернет(обчислювач: адреса<Обчислювач>): предмет_або_стан_падіння {
      ціль М = обчислювач;
      змінна результат = створити_текст(обчислювач, "мавка:бібліотека/інтернет/інтернет.ц");
      якщо перевірити_чи_стан_падіння(обчислювач) {
        вернути результат;
      }
      ціль предмет_шляху = результат;
      змінна взятий_предмет: адреса<МаМа::Предмет> = пусто;
      якщо МаМа::отримати_взятий_предмет(М, предмет_шляху як адреса<МаМа::Предмет>, взятий_предмет::адреса) {
        вернути взятий_предмет як адреса<Предмет>;
      }
      результат = створити_модуль_інтернет(обчислювач);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        вернути результат;
      }
      МаМа::покласти_взятий_предмет(М, предмет_шляху як адреса<МаМа::Предмет>, результат як адреса<МаМа::Предмет>);
      вернути результат;
    }
  }
}