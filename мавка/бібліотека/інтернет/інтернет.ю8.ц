взяти визначення мавка;
взяти визначення мавка/бібліотека;
взяти визначення біб/Реєстр;
взяти визначення біб/Друк;
взяти визначення біб/турбо;

// Звʼязок
секція мавка {
  секція бібліотека {
    структура ДаніВідкликуІнтернетЗвʼязку {
      обчислювач: адреса<Обчислювач>;
      глобально_притриманий_предмет_звʼязку: адреса<ГлобальноПритриманийПредмет>;
      глобально_притриманий_предмет_обробника_підключення: адреса<ГлобальноПритриманийПредмет>;
    }

    структура ДаніВідкликуНаРезультатЗаписуВІнтернетЗвʼязок {
      обчислювач: адреса<Обчислювач>;
      глобально_притриманий_предмет_звʼязку: адреса<ГлобальноПритриманийПредмет>;
      глобально_притриманий_предмет_даних: адреса<ГлобальноПритриманийПредмет>;
      розмір: позитивне;
      дані: памʼять<п8>;
      дані_треба_звільнити: позитивне;
    }

    дія обробник_запланованого_виконання_відклику_на_підключення_інтернет_звʼязку(обчислювач: адреса<Обчислювач>, дані: невідома_адреса, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>): предмет_або_стан_падіння {
      змінна предмет_обробника_підключення = аргументи[0];
      змінна М = обчислювач;
      вернути МаМа::предмет_виконати(М, предмет_обробника_підключення, пусто, 2, аргументи[1]::адреса як памʼять<адреса<Предмет>>, пусто, МаМа::Місцезнаходження { пусто як адреса<МаМа::ПредметТексту>, 0 });
    }

    дія обробник_запланованого_виконання_відклику_на_дані_інтернет_звʼязку(обчислювач: адреса<Обчислювач>, дані: невідома_адреса, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>): предмет_або_стан_падіння {
      змінна предмет_звʼязку = аргументи[0];
      змінна М = обчислювач;
      вернути МаМа::предмет_виконати_властивість(М, предмет_звʼязку, МаМа::назва(М, "обробник_даних"), 2, аргументи, пусто, МаМа::Місцезнаходження { пусто як адреса<МаМа::ПредметТексту>, 0 });
    }

    дія обробник_запланованого_виконання_відклику_на_закінчення_інтернет_звʼязку(обчислювач: адреса<Обчислювач>, дані: невідома_адреса, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>): предмет_або_стан_падіння {
      змінна предмет_звʼязку = аргументи[0];
      змінна М = обчислювач;
      вернути МаМа::предмет_виконати_властивість(М, предмет_звʼязку, МаМа::назва(М, "обробник_закінчення"), 1, аргументи, пусто, МаМа::Місцезнаходження { пусто як адреса<МаМа::ПредметТексту>, 0 });
    }

    дія обробник_запланованого_виконання_відклику_на_відключення_інтернет_звʼязку(обчислювач: адреса<Обчислювач>, дані: невідома_адреса, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>): предмет_або_стан_падіння {
      змінна предмет_звʼязку = аргументи[0];
      змінна М = обчислювач;
      вернути МаМа::предмет_виконати_властивість(М, предмет_звʼязку, МаМа::назва(М, "обробник_відключення"), 2, аргументи, пусто, МаМа::Місцезнаходження { пусто як адреса<МаМа::ПредметТексту>, 0 });
    }

    дія обробник_запису_інтернет_звʼязку(турбіна: адреса<турбо::Турбіна>, інтернет_звʼязок: адреса<турбо::ІнтернетЗвʼязок>, аргумент_інтернет_звʼязку: невідома_адреса, аргумент_запису: невідома_адреса, розмір: позитивне, дані: памʼять<п8>) {
      змінна дані_запису = аргумент_запису як адреса<ДаніВідкликуНаРезультатЗаписуВІнтернетЗвʼязок>;
      змінна обчислювач = дані_запису.обчислювач;
      змінна предмет_звʼязку = мавка__отримати_глобально_притриманий_предмет(обчислювач, дані_запису.глобально_притриманий_предмет_звʼязку);
      змінна предмет_даних = мавка__отримати_глобально_притриманий_предмет(обчислювач, дані_запису.глобально_притриманий_предмет_даних);

      мавка__відпустити_глобально(обчислювач, дані_запису.глобально_притриманий_предмет_звʼязку);
      мавка__відпустити_глобально(обчислювач, дані_запису.глобально_притриманий_предмет_даних);
      якщо дані_запису.дані_треба_звільнити == позитивне(так) {
        звільнити_памʼять(обчислювач, дані_запису.дані);
      }

      звільнити(обчислювач, дані_запису);
    }

    дія обробник_підключення_інтернет_звʼязку(турбіна: адреса<турбо::Турбіна>, інтернет_звʼязок: адреса<турбо::ІнтернетЗвʼязок>, аргумент: невідома_адреса, успішно: логічне, код_помилки: позитивне) {
      змінна дані_звʼязку = аргумент як адреса<ДаніВідкликуІнтернетЗвʼязку>;
      змінна обчислювач = дані_звʼязку.обчислювач;
      змінна предмет_звʼязку = мавка__отримати_глобально_притриманий_предмет(обчислювач, дані_звʼязку.глобально_притриманий_предмет_звʼязку);
      змінна предмет_обробника_підключення = мавка__отримати_глобально_притриманий_предмет(обчислювач, дані_звʼязку.глобально_притриманий_предмет_обробника_підключення);

      змінна кількість_аргументів: позитивне = 3;
      змінна аргументи = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів);
      аргументи[0] = предмет_обробника_підключення;
      аргументи[1] = предмет_звʼязку;

      якщо успішно {
        аргументи[2] = пусто як адреса<Предмет>;

        мавка__предмет_змінити_користувацькі_дані(обчислювач, предмет_звʼязку, інтернет_звʼязок);
      } інакше {
        аргументи[2] = МаМа::створити_предмет_числа(обчислювач, д64(код_помилки)) як адреса<Предмет>;
        якщо перевірити_чи_стан_падіння(обчислювач) {
          мавка__ПОМЕРТИ(обчислювач, "НОП: не вдалось створити число");
        }

        мавка__відпустити_глобально(обчислювач, дані_звʼязку.глобально_притриманий_предмет_звʼязку);
        мавка__відпустити_глобально(обчислювач, дані_звʼязку.глобально_притриманий_предмет_обробника_підключення);
        звільнити(обчислювач, дані_звʼязку);
      }

      мавка__запланувати_виконання(обчислювач, обробник_запланованого_виконання_відклику_на_підключення_інтернет_звʼязку, пусто, пусто, кількість_аргументів, аргументи);
    }

    дія обробник_даних_інтернет_звʼязку(турбіна: адреса<турбо::Турбіна>, інтернет_звʼязок: адреса<турбо::ІнтернетЗвʼязок>, аргумент: невідома_адреса, розмір: позитивне, дані: памʼять<п8>) {
      змінна дані_звʼязку = аргумент як адреса<ДаніВідкликуІнтернетЗвʼязку>;
      змінна обчислювач = дані_звʼязку.обчислювач;
      змінна предмет_звʼязку = мавка__отримати_глобально_притриманий_предмет(обчислювач, дані_звʼязку.глобально_притриманий_предмет_звʼязку);

      змінна предмет_буфера = МаМа::створити_предмет_буфера_без_копіювання(обчислювач, розмір, дані) як адреса<Предмет>;
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__ПОМЕРТИ(обчислювач, "НОП: не вдалось створити буфер без копіювання");
      }

      змінна кількість_аргументів: позитивне = 2;
      змінна аргументи = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів);
      аргументи[0] = предмет_звʼязку;
      аргументи[1] = предмет_буфера;

      мавка__запланувати_виконання(обчислювач, обробник_запланованого_виконання_відклику_на_дані_інтернет_звʼязку, пусто, пусто, кількість_аргументів, аргументи);
    }

    дія обробник_закінчення_інтернет_звʼязку(турбіна: адреса<турбо::Турбіна>, інтернет_звʼязок: адреса<турбо::ІнтернетЗвʼязок>, аргумент: невідома_адреса) {
      змінна дані_звʼязку = аргумент як адреса<ДаніВідкликуІнтернетЗвʼязку>;
      змінна обчислювач = дані_звʼязку.обчислювач;
      змінна предмет_звʼязку = мавка__отримати_глобально_притриманий_предмет(обчислювач, дані_звʼязку.глобально_притриманий_предмет_звʼязку);

      змінна кількість_аргументів: позитивне = 1;
      змінна аргументи = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів);
      аргументи[0] = предмет_звʼязку;

      мавка__запланувати_виконання(обчислювач, обробник_запланованого_виконання_відклику_на_закінчення_інтернет_звʼязку, пусто, пусто, кількість_аргументів, аргументи);
    }

    дія обробник_відключення_інтернет_звʼязку(турбіна: адреса<турбо::Турбіна>, інтернет_звʼязок: адреса<турбо::ІнтернетЗвʼязок>, аргумент: невідома_адреса, з_помилкою: логічне, код_помилки: позитивне) {
      змінна дані_звʼязку = аргумент як адреса<ДаніВідкликуІнтернетЗвʼязку>;
      змінна обчислювач = дані_звʼязку.обчислювач;
      змінна предмет_звʼязку = мавка__отримати_глобально_притриманий_предмет(обчислювач, дані_звʼязку.глобально_притриманий_предмет_звʼязку);

      мавка__предмет_змінити_користувацькі_дані(обчислювач, предмет_звʼязку, пусто);

      змінна кількість_аргументів: позитивне = 2;
      змінна аргументи = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів);
      аргументи[0] = предмет_звʼязку;
      якщо з_помилкою {
        аргументи[1] = МаМа::створити_предмет_числа(обчислювач, д64(код_помилки)) як адреса<Предмет>;
      } інакше {
        аргументи[1] = пусто як адреса<Предмет>;
      }
      мавка__запланувати_виконання(обчислювач, обробник_запланованого_виконання_відклику_на_відключення_інтернет_звʼязку, пусто, пусто, кількість_аргументів, аргументи);

      мавка__відпустити_глобально(обчислювач, дані_звʼязку.глобально_притриманий_предмет_звʼязку);
      якщо дані_звʼязку.глобально_притриманий_предмет_обробника_підключення != пусто {
        мавка__відпустити_глобально(обчислювач, дані_звʼязку.глобально_притриманий_предмет_обробника_підключення);
      }
      звільнити(обчислювач, дані_звʼязку);
    }

    дія створити_предмет_структури_Звʼязок(обчислювач: адреса<Обчислювач>): предмет_або_стан_падіння {
      змінна притримувач = мавка__покласти_притримувач(обчислювач);

      змінна кількість_параметрів: позитивне = 6;
      змінна параметри = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_параметрів);

      змінна М = обчислювач;

      змінна параметр_адреса = МаМа::створити_предмет_параметра(обчислювач, МаМа::назва(М, "адреса"), пусто, пусто) як адреса<Предмет>;
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути параметр_адреса;
      }
      параметри[0] = параметр_адреса;
      мавка__притримати(обчислювач, притримувач, параметр_адреса);

      змінна параметр_порт = МаМа::створити_предмет_параметра(обчислювач, МаМа::назва(М, "порт"), пусто, пусто) як адреса<Предмет>;
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути параметр_порт;
      }
      параметри[1] = параметр_порт;
      мавка__притримати(обчислювач, притримувач, параметр_порт);

      змінна параметр_обробник_даних = МаМа::створити_предмет_параметра(обчислювач, МаМа::назва(М, "обробник_даних"), пусто, пусто) як адреса<Предмет>;
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути параметр_обробник_даних;
      }
      параметри[2] = параметр_обробник_даних;
      мавка__притримати(обчислювач, притримувач, параметр_обробник_даних);

      змінна параметр_обробник_закінчення = МаМа::створити_предмет_параметра(обчислювач, МаМа::назва(М, "обробник_закінчення"), пусто, пусто) як адреса<Предмет>;
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути параметр_обробник_закінчення;
      }
      параметри[3] = параметр_обробник_закінчення;
      мавка__притримати(обчислювач, притримувач, параметр_обробник_закінчення);

      змінна параметр_обробник_відключення = МаМа::створити_предмет_параметра(обчислювач, МаМа::назва(М, "обробник_відключення"), пусто, пусто) як адреса<Предмет>;
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути параметр_обробник_відключення;
      }
      параметри[4] = параметр_обробник_відключення;
      мавка__притримати(обчислювач, притримувач, параметр_обробник_відключення);

      змінна параметр_користувацькі_дані = МаМа::створити_предмет_параметра(обчислювач, МаМа::назва(М, "користувацькі_дані"), пусто, пусто) як адреса<Предмет>;
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути параметр_користувацькі_дані;
      }
      параметри[5] = параметр_користувацькі_дані;
      мавка__притримати(обчислювач, притримувач, параметр_користувацькі_дані);

      змінна предмет_структури_Звʼязок = МаМа::створити_предмет_структури(
        обчислювач,
        МаМа::назва(М, "Звʼязок"),
        обчислювач.предмет_структури_предмет,
        кількість_параметрів, параметри як памʼять<адреса<МаМа::ПредметПараметра>>
      ) як адреса<Предмет>;
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути предмет_структури_Звʼязок;
      }

      мавка__забрати_притримувач(обчислювач);
      вернути предмет_структури_Звʼязок;
    }

    дія рідна_дія_модуля_інтернет_звʼязатись(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<МаМа::ПредметДії>, предмет_я: адреса<Предмет>, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>, іменовані_аргументи: адреса<ІменованіАргументи>, місцезнаходження: МаМа::Місцезнаходження): предмет_або_стан_падіння {
      змінна знайдено_аргумент_адреса = ні;
      змінна аргумент_адреса: адреса<Предмет> = пусто;
      змінна знайдено_аргумент_порт = ні;
      змінна аргумент_порт: адреса<Предмет> = пусто;
      змінна знайдено_аргумент_обробник_підключення = ні;
      змінна аргумент_обробник_підключення: адреса<Предмет> = пусто;
      змінна знайдено_аргумент_формат_даних = ні;
      змінна аргумент_формат_даних: адреса<Предмет> = пусто;
        змінна М = обчислювач;
      якщо іменовані_аргументи != пусто {
        знайдено_аргумент_адреса = МаМа::знайти_іменований_аргумент(обчислювач, іменовані_аргументи, МаМа::назва(М, "адреса"), аргумент_адреса::адреса);
        знайдено_аргумент_порт = МаМа::знайти_іменований_аргумент(обчислювач, іменовані_аргументи, МаМа::назва(М, "порт"), аргумент_порт::адреса);
        знайдено_аргумент_обробник_підключення = МаМа::знайти_іменований_аргумент(обчислювач, іменовані_аргументи, МаМа::назва(М, "обробник_підключення"), аргумент_обробник_підключення::адреса);
        знайдено_аргумент_формат_даних = МаМа::знайти_іменований_аргумент(обчислювач, іменовані_аргументи, МаМа::назва(М, "формат_даних"), аргумент_формат_даних::адреса);
      }
      якщо аргументи != пусто {
        якщо знайдено_аргумент_адреса == ні {
          якщо кількість_аргументів > 0 {
            знайдено_аргумент_адреса = так;
            аргумент_адреса = аргументи[0];
          }
        }
        якщо знайдено_аргумент_порт == ні {
          якщо кількість_аргументів > 1 {
            знайдено_аргумент_порт = так;
            аргумент_порт = аргументи[1];
          }
        }
        якщо знайдено_аргумент_обробник_підключення == ні {
          якщо кількість_аргументів > 2 {
            знайдено_аргумент_обробник_підключення = так;
            аргумент_обробник_підключення = аргументи[2];
          }
        }
        якщо знайдено_аргумент_формат_даних == ні {
          якщо кількість_аргументів > 3 {
            знайдено_аргумент_формат_даних = так;
            аргумент_формат_даних = аргументи[3];
          }
        }
      }

      якщо аргумент_адреса == пусто {
        вернути падіння(обчислювач, "Адреса має бути текстом або юнікодом");
      }
      якщо аргумент_порт == пусто {
        вернути падіння(обчислювач, "Порт має бути числом");
      }

      змінна адреса_ю8 = ю8 { 0, пусто };

      якщо МаМа::перевірити_чи_предмет_є_текстом(обчислювач, аргумент_адреса) {
        змінна адреса_т8 = отримати_значення_тексту(обчислювач, аргумент_адреса як адреса<МаМа::ПредметТексту>);

        якщо МаМа::перекодувати_т8_в_ю8(обчислювач, адреса_т8, адреса_ю8::адреса, ні) == ні {
          вернути падіння(обчислювач, "Не вдалось отримати ю8 з тексту адреси");
        }
      } інакше якщо МаМа::перевірити_чи_предмет_є_юнікодом(обчислювач, аргумент_адреса) {
        змінна адреса_ю32 = отримати_значення_юнікоду(обчислювач, аргумент_адреса як адреса<МаМа::ПредметЮнікоду>);

        якщо МаМа::перекодувати_ю32_в_ю8(обчислювач, адреса_ю32, адреса_ю8::адреса, ні) == ні {
          вернути падіння(обчислювач, "Не вдалось отримати ю8 з юнікоду адреси");
        }
      } інакше {
        вернути падіння(обчислювач, "Адреса має бути текстом або юнікодом");
      }

      якщо МаМа::перевірити_чи_предмет_є_числом(обчислювач, аргумент_порт) == ні {
        звільнити_памʼять(обчислювач, адреса_ю8.дані);
        вернути падіння(обчислювач, "Порт має бути числом");
      }

      змінна порт = ц32(отримати_значення_числа(обчислювач, аргумент_порт як адреса<МаМа::ПредметЧисла>));

      змінна предмет_структури_Звʼязок = мавка__отримати_глобально_притриманий_предмет(обчислювач, предмет_дії.дані як адреса<ГлобальноПритриманийПредмет>);

      змінна кількість_аргументів_виконання_Звʼязок: позитивне = 2;
      змінна аргументи_виконання_Звʼязок = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів_виконання_Звʼязок);
      аргументи_виконання_Звʼязок[0] = аргумент_адреса;
      аргументи_виконання_Звʼязок[1] = аргумент_порт;
      змінна предмет_звʼязку = МаМа::предмет_виконати(М, предмет_структури_Звʼязок, пусто, кількість_аргументів_виконання_Звʼязок, аргументи_виконання_Звʼязок, пусто, МаМа::Місцезнаходження { пусто як адреса<МаМа::ПредметТексту>, 0 });
      звільнити_памʼять(обчислювач, аргументи_виконання_Звʼязок);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        вернути предмет_звʼязку;
      }

      змінна турбіна = турбо::отримати_глобальну_турбіну();

      змінна дані_звʼязку = виділити<ДаніВідкликуІнтернетЗвʼязку>(обчислювач);
      дані_звʼязку.обчислювач = обчислювач;
      дані_звʼязку.глобально_притриманий_предмет_звʼязку = мавка__притримати_глобально(обчислювач, предмет_звʼязку);
      дані_звʼязку.глобально_притриманий_предмет_обробника_підключення = мавка__притримати_глобально(обчислювач, аргумент_обробник_підключення);

      змінна іа: позитивне = 0;
      змінна результат_іа = турбо::отримати_інтернет_адресу_з_ю8(турбіна, адреса_ю8, іа::адреса);
      звільнити_памʼять(обчислювач, адреса_ю8.дані);
      якщо результат_іа == ні {
        вернути падіння(обчислювач, "Не вдалось отримати іа");
      }

      турбо::підключити_інтернет_звʼязок(
        турбіна,
        іа,
        позитивне(порт),
        обробник_підключення_інтернет_звʼязку,
        обробник_даних_інтернет_звʼязку,
        обробник_закінчення_інтернет_звʼязку,
        обробник_відключення_інтернет_звʼязку,
        дані_звʼязку
      );

      вернути пусто;
    }

    дія зробити_копію_даних_дії_модуля_інтернет_звʼязатись(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<МаМа::ПредметДії>): невідома_адреса {
      змінна дані_дії = предмет_дії.дані як адреса<ГлобальноПритриманийПредмет>;
      вернути мавка__притримати_глобально(обчислювач, мавка__отримати_глобально_притриманий_предмет(обчислювач, дані_дії));
    }

    дія відклик_перед_звільненням_дії_модуля_інтернет_звʼязатись(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<МаМа::ПредметДії>) {
      змінна дані_дії = предмет_дії.дані як адреса<ГлобальноПритриманийПредмет>;
      мавка__відпустити_глобально(обчислювач, дані_дії);
    }

    дія створити_дію_модуля_інтернет_звʼязатись(обчислювач: адреса<Обчислювач>, предмет_структури_Звʼязок: адреса<Предмет>): предмет_або_стан_падіння {
      змінна М = обчислювач;
      вернути МаМа::створити_предмет_рідної_дії(
        обчислювач,
        МаМа::назва(М, т8"звʼязатись"),
        0,
        пусто,
        пусто,
        рідна_дія_модуля_інтернет_звʼязатись,
        мавка__притримати_глобально(обчислювач, предмет_структури_Звʼязок),
        зробити_копію_даних_дії_модуля_інтернет_звʼязатись,
        відклик_перед_звільненням_дії_модуля_інтернет_звʼязатись
      ) як адреса<Предмет>;
    }

    дія рідна_дія_модуля_інтернет_надіслати(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<МаМа::ПредметДії>, предмет_я: адреса<Предмет>, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>, іменовані_аргументи: адреса<ІменованіАргументи>, місцезнаходження: МаМа::Місцезнаходження): предмет_або_стан_падіння {
      змінна знайдено_аргумент_повідомлення = ні;
      змінна аргумент_повідомлення: адреса<Предмет> = пусто;
      змінна знайдено_аргумент_отримувач = ні;
      змінна аргумент_отримувач: адреса<Предмет> = пусто;
      змінна М = обчислювач;
      якщо іменовані_аргументи != пусто {
        знайдено_аргумент_повідомлення = МаМа::знайти_іменований_аргумент(обчислювач, іменовані_аргументи, МаМа::назва(М, "повідомлення"), аргумент_повідомлення::адреса);
        знайдено_аргумент_отримувач = МаМа::знайти_іменований_аргумент(обчислювач, іменовані_аргументи, МаМа::назва(М, "отримувач"), аргумент_отримувач::адреса);
      }
      якщо аргументи != пусто {
        якщо знайдено_аргумент_повідомлення == ні {
          якщо кількість_аргументів > 0 {
            знайдено_аргумент_повідомлення = так;
            аргумент_повідомлення = аргументи[0];
          }
        }
        якщо знайдено_аргумент_отримувач == ні {
          якщо кількість_аргументів > 1 {
            знайдено_аргумент_отримувач = так;
            аргумент_отримувач = аргументи[1];
          }
        }
      }

      якщо аргумент_повідомлення == пусто {
        вернути падіння(обчислювач, "Повідомлення не може бути пусто");
      }
      якщо аргумент_отримувач == пусто {
        вернути падіння(обчислювач, "Отримувач не може бути пусто");
      }

      змінна байти_треба_звільнити = ні;
      змінна байти = Байти { 0, пусто };
      якщо МаМа::перевірити_чи_предмет_є_байтами(обчислювач, аргумент_повідомлення) {
        байти = отримати_значення_байтів(обчислювач, аргумент_повідомлення як адреса<МаМа::ПредметБайтів>);
      } інакше якщо МаМа::перевірити_чи_предмет_є_буфером(обчислювач, аргумент_повідомлення) {
        байти = отримати_значення_буфера(обчислювач, аргумент_повідомлення як адреса<МаМа::ПредметБуфера>);
      } інакше якщо МаМа::перевірити_чи_предмет_є_текстом(обчислювач, аргумент_повідомлення) {
        змінна значення_тексту = отримати_значення_тексту(обчислювач, аргумент_повідомлення як адреса<МаМа::ПредметТексту>);
        байти = Байти { значення_тексту.розмір, значення_тексту.дані };
      } інакше якщо МаМа::перевірити_чи_предмет_є_юнікодом(обчислювач, аргумент_повідомлення) {
        змінна значення_юнікоду = отримати_значення_юнікоду(обчислювач, аргумент_повідомлення як адреса<МаМа::ПредметЮнікоду>);
        змінна значення_ю8 = ю8 { 0, пусто };
        якщо МаМа::перекодувати_ю32_в_ю8(обчислювач, значення_юнікоду, значення_ю8::адреса, ні) == ні {
          вернути падіння(обчислювач, "Не вдалось отримати ю8 з юнікоду повідомлення");
        }
        байти_треба_звільнити = так;
        байти = Байти { значення_ю8.розмір, значення_ю8.дані };
      } інакше {
        вернути падіння(обчислювач, "Повідомлення має бути байтами, буфером, текстом або юнікодом");
      }

      змінна предмет_структури_Звʼязок = мавка__отримати_глобально_притриманий_предмет(обчислювач, предмет_дії.дані як адреса<ГлобальноПритриманийПредмет>);

      якщо МаМа::перевірити_чи_предмет_є(М, аргумент_отримувач, предмет_структури_Звʼязок як адреса<МаМа::ПредметСтруктури>) == ні {
        вернути падіння(обчислювач, "Отримувач має бути звʼязком");
      }

      змінна інтернет_звʼязок = мавка__предмет_отримати_користувацькі_дані(обчислювач, аргумент_отримувач) як адреса<турбо::ІнтернетЗвʼязок>;
      якщо інтернет_звʼязок == пусто {
        вернути падіння(обчислювач, "Звʼязок відключено");
      }

      змінна турбіна = турбо::отримати_глобальну_турбіну();

      змінна дані_запису = виділити<ДаніВідкликуНаРезультатЗаписуВІнтернетЗвʼязок>(обчислювач);
      дані_запису.обчислювач = обчислювач;
      дані_запису.глобально_притриманий_предмет_звʼязку = мавка__притримати_глобально(обчислювач, аргумент_отримувач);
      дані_запису.глобально_притриманий_предмет_даних = мавка__притримати_глобально(обчислювач, аргумент_повідомлення);
      дані_запису.розмір = байти.розмір;
      дані_запису.дані = байти.дані;
      дані_запису.дані_треба_звільнити = позитивне(байти_треба_звільнити);

      турбо::записати_в_інтернет_звʼязок(турбіна, інтернет_звʼязок, байти.розмір, байти.дані, обробник_запису_інтернет_звʼязку, дані_запису);

      вернути пусто;
    }

    дія зробити_копію_даних_дії_модуля_інтернет_надіслати(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<МаМа::ПредметДії>): невідома_адреса {
      змінна дані_дії = предмет_дії.дані як адреса<ГлобальноПритриманийПредмет>;
      вернути мавка__притримати_глобально(обчислювач, мавка__отримати_глобально_притриманий_предмет(обчислювач, дані_дії));
    }

    дія відклик_перед_звільненням_дії_модуля_інтернет_надіслати(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<МаМа::ПредметДії>) {
      змінна дані_дії = предмет_дії.дані як адреса<ГлобальноПритриманийПредмет>;
      мавка__відпустити_глобально(обчислювач, дані_дії);
    }

    дія створити_дію_модуля_інтернет_надіслати(обчислювач: адреса<Обчислювач>, предмет_структури_Звʼязок: адреса<Предмет>): предмет_або_стан_падіння {
      змінна М = обчислювач;
      вернути МаМа::створити_предмет_рідної_дії(
        обчислювач,
        МаМа::назва(М, т8"надіслати"),
        0,
        пусто,
        пусто,
        рідна_дія_модуля_інтернет_надіслати,
        мавка__притримати_глобально(обчислювач, предмет_структури_Звʼязок),
        зробити_копію_даних_дії_модуля_інтернет_надіслати,
        відклик_перед_звільненням_дії_модуля_інтернет_надіслати
      ) як адреса<Предмет>;
    }

    дія рідна_дія_модуля_інтернет_закінчити(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<МаМа::ПредметДії>, предмет_я: адреса<Предмет>, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>, іменовані_аргументи: адреса<ІменованіАргументи>, місцезнаходження: МаМа::Місцезнаходження): предмет_або_стан_падіння {
      змінна знайдено_аргумент_звʼязок = ні;
      змінна аргумент_звʼязок: адреса<Предмет> = пусто;
      змінна М = обчислювач;
      якщо іменовані_аргументи != пусто {
        знайдено_аргумент_звʼязок = МаМа::знайти_іменований_аргумент(обчислювач, іменовані_аргументи, МаМа::назва(М, "звʼязок"), аргумент_звʼязок::адреса);
      }
      якщо аргументи != пусто {
        якщо знайдено_аргумент_звʼязок == ні {
          якщо кількість_аргументів > 0 {
            знайдено_аргумент_звʼязок = так;
            аргумент_звʼязок = аргументи[0];
          }
        }
      }

      якщо аргумент_звʼязок == пусто {
        вернути падіння(обчислювач, "Перший аргумент має бути звʼязком");
      }

      змінна предмет_структури_Звʼязок = мавка__отримати_глобально_притриманий_предмет(обчислювач, предмет_дії.дані як адреса<ГлобальноПритриманийПредмет>);

      якщо МаМа::перевірити_чи_предмет_є(М, аргумент_звʼязок, предмет_структури_Звʼязок як адреса<МаМа::ПредметСтруктури>) == ні {
        вернути падіння(обчислювач, "Перший аргумент має бути звʼязком");
      }

      змінна інтернет_звʼязок = мавка__предмет_отримати_користувацькі_дані(обчислювач, аргумент_звʼязок) як адреса<турбо::ІнтернетЗвʼязок>;
      якщо інтернет_звʼязок == пусто {
        вернути пусто;
      }

      змінна турбіна = турбо::отримати_глобальну_турбіну();

      турбо::закінчити_інтернет_звʼязок(турбіна, інтернет_звʼязок);

      вернути пусто;
    }

    дія зробити_копію_даних_дії_модуля_інтернет_закінчити(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<МаМа::ПредметДії>): невідома_адреса {
      змінна дані_дії = предмет_дії.дані як адреса<ГлобальноПритриманийПредмет>;
      вернути мавка__притримати_глобально(обчислювач, мавка__отримати_глобально_притриманий_предмет(обчислювач, дані_дії));
    }

    дія відклик_перед_звільненням_дії_модуля_інтернет_закінчити(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<МаМа::ПредметДії>) {
      змінна дані_дії = предмет_дії.дані як адреса<ГлобальноПритриманийПредмет>;
      мавка__відпустити_глобально(обчислювач, дані_дії);
    }

    дія створити_дію_модуля_інтернет_закінчити(обчислювач: адреса<Обчислювач>, предмет_структури_Звʼязок: адреса<Предмет>): предмет_або_стан_падіння {
      змінна М = обчислювач;
      вернути МаМа::створити_предмет_рідної_дії(
        обчислювач,
        МаМа::назва(М, т8"закінчити"),
        0,
        пусто,
        пусто,
        рідна_дія_модуля_інтернет_закінчити,
        мавка__притримати_глобально(обчислювач, предмет_структури_Звʼязок),
        зробити_копію_даних_дії_модуля_інтернет_закінчити,
        відклик_перед_звільненням_дії_модуля_інтернет_закінчити
      ) як адреса<Предмет>;
    }
  }
}

// Обслуговувач
секція мавка {
  секція бібліотека {
    структура ДаніВідкликуІнтернетОбслуговувача {
      обчислювач: адреса<Обчислювач>;
      глобально_притриманий_предмет_обслуговувача: адреса<ГлобальноПритриманийПредмет>;
      глобально_притриманий_предмет_обробника_запуску: адреса<ГлобальноПритриманийПредмет>;
      глобально_притриманий_предмет_структури_Звʼязок: адреса<ГлобальноПритриманийПредмет>;
    }

    структура ДаніДіїОбслуговувати {
      обчислювач: адреса<Обчислювач>;
      глобально_притриманий_предмет_структури_Обслуговувач: адреса<ГлобальноПритриманийПредмет>;
      глобально_притриманий_предмет_структури_Звʼязок: адреса<ГлобальноПритриманийПредмет>;
    }

    дія обробник_запланованого_виконання_відклику_на_запуск_інтернет_обслуговувача(обчислювач: адреса<Обчислювач>, дані: невідома_адреса, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>): предмет_або_стан_падіння {
      змінна предмет_обробника_запуску = аргументи[0];
      змінна М = обчислювач;
      вернути МаМа::предмет_виконати(М, предмет_обробника_запуску, пусто, 3, аргументи[1]::адреса як памʼять<адреса<Предмет>>, пусто, МаМа::Місцезнаходження { пусто як адреса<МаМа::ПредметТексту>, 0 });
    }

    дія обробник_запланованого_виконання_відклику_на_підключення_звʼязку_інтернет_обслуговувача(обчислювач: адреса<Обчислювач>, дані: невідома_адреса, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>): предмет_або_стан_падіння {
      змінна предмет_обслуговувача = аргументи[0];
      змінна М = обчислювач;
      вернути МаМа::предмет_виконати_властивість(М, предмет_обслуговувача, МаМа::назва(М, "обробник_підключення_звʼязку"), 3, аргументи, пусто, МаМа::Місцезнаходження { пусто як адреса<МаМа::ПредметТексту>, 0 });
    }

    дія обробник_запланованого_виконання_відклику_на_зупинку_інтернет_обслуговувача(обчислювач: адреса<Обчислювач>, дані: невідома_адреса, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>): предмет_або_стан_падіння {
      змінна предмет_обслуговувача = аргументи[0];
      змінна М = обчислювач;
      вернути МаМа::предмет_виконати_властивість(М, предмет_обслуговувача, МаМа::назва(М, "обробник_зупинки"), 2, аргументи, пусто, МаМа::Місцезнаходження { пусто як адреса<МаМа::ПредметТексту>, 0 });
    }

    дія обробник_запуску_інтернет_обслуговувача(турбіна: адреса<турбо::Турбіна>, інтернет_обслуговувач: адреса<турбо::ІнтернетОбслуговувач>, аргумент: невідома_адреса, успішно: логічне, код_помилки: ц32, іа: позитивне, порт: позитивне) {
      змінна дані_обслуговувача = аргумент як адреса<ДаніВідкликуІнтернетОбслуговувача>;
      змінна обчислювач = дані_обслуговувача.обчислювач;
      змінна предмет_обслуговувача = мавка__отримати_глобально_притриманий_предмет(обчислювач, дані_обслуговувача.глобально_притриманий_предмет_обслуговувача);
      змінна предмет_обробника_запуску = мавка__отримати_глобально_притриманий_предмет(обчислювач, дані_обслуговувача.глобально_притриманий_предмет_обробника_запуску);

      змінна кількість_аргументів: позитивне = 3;
      змінна аргументи = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів);
      аргументи[0] = предмет_обробника_запуску;
      аргументи[1] = предмет_обслуговувача;

      якщо успішно {
        аргументи[2] = пусто як адреса<Предмет>;

        мавка__предмет_змінити_користувацькі_дані(обчислювач, предмет_обслуговувача, інтернет_обслуговувач);
      } інакше {
        аргументи[2] = МаМа::створити_предмет_числа(обчислювач, д64(код_помилки)) як адреса<Предмет>;
        якщо перевірити_чи_стан_падіння(обчислювач) {
          мавка__ПОМЕРТИ(обчислювач, "НОП: не вдалось створити число");
        }

        мавка__відпустити_глобально(обчислювач, дані_обслуговувача.глобально_притриманий_предмет_обслуговувача);
        мавка__відпустити_глобально(обчислювач, дані_обслуговувача.глобально_притриманий_предмет_обробника_запуску);
        мавка__відпустити_глобально(обчислювач, дані_обслуговувача.глобально_притриманий_предмет_структури_Звʼязок);
        звільнити(обчислювач, дані_обслуговувача);
      }

      мавка__запланувати_виконання(обчислювач, обробник_запланованого_виконання_відклику_на_запуск_інтернет_обслуговувача, пусто, пусто, кількість_аргументів, аргументи);
    }

    дія обробник_підключення_звʼязку_інтернет_обслуговувача(турбіна: адреса<турбо::Турбіна>, інтернет_обслуговувач: адреса<турбо::ІнтернетОбслуговувач>, інтернет_звʼязок: адреса<турбо::ІнтернетЗвʼязок>, аргумент: невідома_адреса) {
      змінна дані_обслуговувача = аргумент як адреса<ДаніВідкликуІнтернетОбслуговувача>;
      змінна обчислювач = дані_обслуговувача.обчислювач;
      змінна предмет_обслуговувача = мавка__отримати_глобально_притриманий_предмет(обчислювач, дані_обслуговувача.глобально_притриманий_предмет_обслуговувача);
      змінна предмет_структури_Звʼязок = мавка__отримати_глобально_притриманий_предмет(обчислювач, дані_обслуговувача.глобально_притриманий_предмет_структури_Звʼязок);

      змінна кількість_аргументів: позитивне = 3;
      змінна аргументи = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів);
      аргументи[0] = предмет_обслуговувача;

      змінна предмет_адреси = пусто як адреса<Предмет>;
      змінна предмет_порта = пусто як адреса<Предмет>;

      змінна кількість_аргументів_виконання_Звʼязок: позитивне = 2;
      змінна аргументи_виконання_Звʼязок = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів_виконання_Звʼязок);
      аргументи_виконання_Звʼязок[0] = предмет_адреси;
      аргументи_виконання_Звʼязок[1] = предмет_порта;
      змінна М = обчислювач;
      змінна предмет_звʼязку = МаМа::предмет_виконати(М, предмет_структури_Звʼязок, пусто, кількість_аргументів_виконання_Звʼязок, аргументи_виконання_Звʼязок, пусто, МаМа::Місцезнаходження { пусто як адреса<МаМа::ПредметТексту>, 0 });
      звільнити_памʼять(обчислювач, аргументи_виконання_Звʼязок);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__ПОМЕРТИ(обчислювач, "НОП: не вдалось створити предмет структури Звʼязок");
      } інакше {
        мавка__предмет_змінити_користувацькі_дані(обчислювач, предмет_звʼязку, інтернет_звʼязок);

        змінна дані_звʼязку = виділити<ДаніВідкликуІнтернетЗвʼязку>(обчислювач);
        дані_звʼязку.обчислювач = обчислювач;
        дані_звʼязку.глобально_притриманий_предмет_звʼязку = мавка__притримати_глобально(обчислювач, предмет_звʼязку);
        дані_звʼязку.глобально_притриманий_предмет_обробника_підключення = пусто;

        турбо::встановити_обробники_інтернет_звʼязку(
          турбіна,
          інтернет_звʼязок,
          обробник_даних_інтернет_звʼязку,
          обробник_закінчення_інтернет_звʼязку,
          обробник_відключення_інтернет_звʼязку,
          дані_звʼязку
        );

        аргументи[1] = предмет_звʼязку;
        аргументи[2] = пусто як адреса<Предмет>;

        мавка__запланувати_виконання(обчислювач, обробник_запланованого_виконання_відклику_на_підключення_звʼязку_інтернет_обслуговувача, пусто, пусто, кількість_аргументів, аргументи);
      }
    }

    дія обробник_зупинки_інтернет_обслуговувача(турбіна: адреса<турбо::Турбіна>, інтернет_обслуговувач: адреса<турбо::ІнтернетОбслуговувач>, аргумент: невідома_адреса) {
      змінна дані_обслуговувача = аргумент як адреса<ДаніВідкликуІнтернетОбслуговувача>;
      змінна обчислювач = дані_обслуговувача.обчислювач;
      змінна предмет_обслуговувача = мавка__отримати_глобально_притриманий_предмет(обчислювач, дані_обслуговувача.глобально_притриманий_предмет_обслуговувача);

      мавка__предмет_змінити_користувацькі_дані(обчислювач, предмет_обслуговувача, пусто);

      змінна кількість_аргументів: позитивне = 1;
      змінна аргументи = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів);
      аргументи[0] = предмет_обслуговувача;

      мавка__запланувати_виконання(обчислювач, обробник_запланованого_виконання_відклику_на_зупинку_інтернет_обслуговувача, пусто, пусто, кількість_аргументів, аргументи);
    }

    дія створити_предмет_структури_Обслуговувач(обчислювач: адреса<Обчислювач>): предмет_або_стан_падіння {
      змінна притримувач = мавка__покласти_притримувач(обчислювач);

      змінна кількість_параметрів: позитивне = 4;
      змінна параметри = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_параметрів);

      змінна М = обчислювач;

      змінна параметр_адреса = МаМа::створити_предмет_параметра(обчислювач, МаМа::назва(М, "адреса"), пусто, пусто) як адреса<Предмет>;
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути параметр_адреса;
      }
      параметри[0] = параметр_адреса;
      мавка__притримати(обчислювач, притримувач, параметр_адреса);

      змінна параметр_порт = МаМа::створити_предмет_параметра(обчислювач, МаМа::назва(М, "порт"), пусто, пусто) як адреса<Предмет>;
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути параметр_порт;
      }
      параметри[1] = параметр_порт;
      мавка__притримати(обчислювач, притримувач, параметр_порт);

      змінна параметр_обробник_підключення_звʼязку = МаМа::створити_предмет_параметра(обчислювач, МаМа::назва(М, "обробник_підключення_звʼязку"), пусто, пусто) як адреса<Предмет>;
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути параметр_обробник_підключення_звʼязку;
      }
      параметри[2] = параметр_обробник_підключення_звʼязку;
      мавка__притримати(обчислювач, притримувач, параметр_обробник_підключення_звʼязку);

      змінна параметр_обробник_зупинки = МаМа::створити_предмет_параметра(обчислювач, МаМа::назва(М, "обробник_зупинки"), пусто, пусто) як адреса<Предмет>;
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути параметр_обробник_зупинки;
      }
      параметри[3] = параметр_обробник_зупинки;
      мавка__притримати(обчислювач, притримувач, параметр_обробник_зупинки);

      змінна предмет_структури_Обслуговувач = МаМа::створити_предмет_структури(
        обчислювач,
        МаМа::назва(М, "Обслуговувач"),
        обчислювач.предмет_структури_предмет,
        кількість_параметрів,
        параметри як памʼять<адреса<МаМа::ПредметПараметра>>
      ) як адреса<Предмет>;
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути предмет_структури_Обслуговувач;
      }

      мавка__забрати_притримувач(обчислювач);
      вернути предмет_структури_Обслуговувач;
    }

    дія рідна_дія_модуля_інтернет_обслуговувати(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<МаМа::ПредметДії>, предмет_я: адреса<Предмет>, кількість_аргументів: позитивне, аргументи: памʼять<адреса<Предмет>>, іменовані_аргументи: адреса<ІменованіАргументи>, місцезнаходження: МаМа::Місцезнаходження): предмет_або_стан_падіння {
      змінна знайдено_аргумент_адреса = ні;
      змінна аргумент_адреса: адреса<Предмет> = пусто;
      змінна знайдено_аргумент_порт = ні;
      змінна аргумент_порт: адреса<Предмет> = пусто;
      змінна знайдено_аргумент_обробник_запуску = ні;
      змінна аргумент_обробник_запуску: адреса<Предмет> = пусто;
      змінна знайдено_аргумент_формат_даних = ні;
      змінна аргумент_формат_даних: адреса<Предмет> = пусто;
      змінна М = обчислювач;
      якщо іменовані_аргументи != пусто {
        знайдено_аргумент_адреса = МаМа::знайти_іменований_аргумент(обчислювач, іменовані_аргументи, МаМа::назва(М, "адреса"), аргумент_адреса::адреса);
        знайдено_аргумент_порт = МаМа::знайти_іменований_аргумент(обчислювач, іменовані_аргументи, МаМа::назва(М, "порт"), аргумент_порт::адреса);
        знайдено_аргумент_обробник_запуску = МаМа::знайти_іменований_аргумент(обчислювач, іменовані_аргументи, МаМа::назва(М, "обробник_запуску"), аргумент_обробник_запуску::адреса);
        знайдено_аргумент_формат_даних = МаМа::знайти_іменований_аргумент(обчислювач, іменовані_аргументи, МаМа::назва(М, "формат_даних"), аргумент_формат_даних::адреса);
      }
      якщо аргументи != пусто {
        якщо знайдено_аргумент_адреса == ні {
          якщо кількість_аргументів > 0 {
            знайдено_аргумент_адреса = так;
            аргумент_адреса = аргументи[0];
          }
        }
        якщо знайдено_аргумент_порт == ні {
          якщо кількість_аргументів > 1 {
            знайдено_аргумент_порт = так;
            аргумент_порт = аргументи[1];
          }
        }
        якщо знайдено_аргумент_обробник_запуску == ні {
          якщо кількість_аргументів > 2 {
            знайдено_аргумент_обробник_запуску = так;
            аргумент_обробник_запуску = аргументи[2];
          }
        }
        якщо знайдено_аргумент_формат_даних == ні {
          якщо кількість_аргументів > 3 {
            знайдено_аргумент_формат_даних = так;
            аргумент_формат_даних = аргументи[3];
          }
        }
      }

      якщо аргумент_адреса == пусто {
        вернути падіння(обчислювач, "Адреса має бути текстом або юнікодом");
      }
      якщо аргумент_порт == пусто {
        вернути падіння(обчислювач, "Порт має бути числом");
      }

      змінна адреса_ю8 = ю8 { 0, пусто };

      якщо МаМа::перевірити_чи_предмет_є_текстом(обчислювач, аргумент_адреса) {
        змінна адреса_т8 = отримати_значення_тексту(обчислювач, аргумент_адреса як адреса<МаМа::ПредметТексту>);

        якщо МаМа::перекодувати_т8_в_ю8(обчислювач, адреса_т8, адреса_ю8::адреса, ні) == ні {
          вернути падіння(обчислювач, "Не вдалось отримати ю8 з тексту адреси");
        }
      } інакше якщо МаМа::перевірити_чи_предмет_є_юнікодом(обчислювач, аргумент_адреса) {
        змінна адреса_ю32 = отримати_значення_юнікоду(обчислювач, аргумент_адреса як адреса<МаМа::ПредметЮнікоду>);

        якщо МаМа::перекодувати_ю32_в_ю8(обчислювач, адреса_ю32, адреса_ю8::адреса, ні) == ні {
          вернути падіння(обчислювач, "Не вдалось отримати ю8 з юнікоду адреси");
        }
      } інакше {
        вернути падіння(обчислювач, "Адреса має бути текстом або юнікодом");
      }

      якщо МаМа::перевірити_чи_предмет_є_числом(обчислювач, аргумент_порт) == ні {
        звільнити_памʼять(обчислювач, адреса_ю8.дані);
        вернути падіння(обчислювач, "Порт має бути числом");
      }

      змінна порт = ц32(отримати_значення_числа(обчислювач, аргумент_порт як адреса<МаМа::ПредметЧисла>));

      змінна дані_дії_обслуговувати = предмет_дії.дані як адреса<ДаніДіїОбслуговувати>;

      змінна предмет_структури_Обслуговувач = мавка__отримати_глобально_притриманий_предмет(обчислювач, дані_дії_обслуговувати.глобально_притриманий_предмет_структури_Обслуговувач як адреса<ГлобальноПритриманийПредмет>);
      змінна предмет_структури_Звʼязок = мавка__отримати_глобально_притриманий_предмет(обчислювач, дані_дії_обслуговувати.глобально_притриманий_предмет_структури_Звʼязок як адреса<ГлобальноПритриманийПредмет>);

      змінна кількість_аргументів_виконання_Обслуговувач: позитивне = 2;
      змінна аргументи_виконання_Обслуговувач = виділити_памʼять<адреса<Предмет>>(обчислювач, кількість_аргументів_виконання_Обслуговувач);
      аргументи_виконання_Обслуговувач[0] = аргумент_адреса;
      аргументи_виконання_Обслуговувач[1] = аргумент_порт;
      змінна предмет_обслуговувача = МаМа::предмет_виконати(М, предмет_структури_Обслуговувач, пусто, кількість_аргументів_виконання_Обслуговувач, аргументи_виконання_Обслуговувач, пусто, МаМа::Місцезнаходження { пусто як адреса<МаМа::ПредметТексту>, 0 });
      звільнити_памʼять(обчислювач, аргументи_виконання_Обслуговувач);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        вернути предмет_обслуговувача;
      }

      змінна турбіна = турбо::отримати_глобальну_турбіну();

      змінна дані_обслуговувача = виділити<ДаніВідкликуІнтернетОбслуговувача>(обчислювач);
      дані_обслуговувача.обчислювач = обчислювач;
      дані_обслуговувача.глобально_притриманий_предмет_обслуговувача = мавка__притримати_глобально(обчислювач, предмет_обслуговувача);
      дані_обслуговувача.глобально_притриманий_предмет_обробника_запуску = мавка__притримати_глобально(обчислювач, аргумент_обробник_запуску);
      дані_обслуговувача.глобально_притриманий_предмет_структури_Звʼязок = мавка__притримати_глобально(обчислювач, предмет_структури_Звʼязок);

      змінна іа: позитивне = 0;
      змінна результат_іа = турбо::отримати_інтернет_адресу_з_ю8(турбіна, адреса_ю8, іа::адреса);
      звільнити_памʼять(обчислювач, адреса_ю8.дані);
      якщо результат_іа == ні {
        вернути падіння(обчислювач, "Не вдалось отримати іа");
      }

      турбо::запустити_інтернет_обслуговувач(
        турбіна,
        іа,
        позитивне(порт),
        обробник_запуску_інтернет_обслуговувача,
        обробник_підключення_звʼязку_інтернет_обслуговувача,
        обробник_зупинки_інтернет_обслуговувача,
        дані_обслуговувача
      );

      вернути пусто;
    }

    дія зробити_копію_даних_дії_модуля_інтернет_обслуговувати(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<МаМа::ПредметДії>): невідома_адреса {
      // потім: втілити
      вернути пусто;
    }

    дія відклик_перед_звільненням_дії_модуля_інтернет_обслуговувати(обчислювач: адреса<Обчислювач>, предмет_дії: адреса<МаМа::ПредметДії>) {
      змінна дані_дії_обслуговувати = предмет_дії.дані як адреса<ДаніДіїОбслуговувати>;
      мавка__відпустити_глобально(обчислювач, дані_дії_обслуговувати.глобально_притриманий_предмет_структури_Обслуговувач);
      мавка__відпустити_глобально(обчислювач, дані_дії_обслуговувати.глобально_притриманий_предмет_структури_Звʼязок);
      звільнити(обчислювач, дані_дії_обслуговувати);
    }

    дія створити_дію_модуля_інтернет_обслуговувати(обчислювач: адреса<Обчислювач>, предмет_структури_Обслуговувач: адреса<Предмет>, предмет_структури_Звʼязок: адреса<Предмет>): предмет_або_стан_падіння {
      змінна дані_дії_обслуговувати = виділити<ДаніДіїОбслуговувати>(обчислювач);
      дані_дії_обслуговувати.глобально_притриманий_предмет_структури_Обслуговувач = мавка__притримати_глобально(обчислювач, предмет_структури_Обслуговувач);
      дані_дії_обслуговувати.глобально_притриманий_предмет_структури_Звʼязок = мавка__притримати_глобально(обчислювач, предмет_структури_Звʼязок);

      змінна М = обчислювач;

      вернути МаМа::створити_предмет_рідної_дії(
        обчислювач,
        МаМа::назва(М, т8"обслуговувати"),
        0,
        пусто,
        пусто,
        рідна_дія_модуля_інтернет_обслуговувати,
        дані_дії_обслуговувати,
        зробити_копію_даних_дії_модуля_інтернет_обслуговувати,
        відклик_перед_звільненням_дії_модуля_інтернет_обслуговувати
      ) як адреса<Предмет>;
    }
  }
}

секція мавка {
  секція бібліотека {
    дія створити_модуль_інтернет(обчислювач: адреса<Обчислювач>): предмет_або_стан_падіння {
      змінна притримувач = мавка__покласти_притримувач(обчислювач);

      змінна М = обчислювач;

      змінна предмет_модуля = МаМа::створити_предмет_модуля(обчислювач, МаМа::назва(М, "інтернет")) як адреса<Предмет>;
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути предмет_модуля;
      }
      мавка__притримати(обчислювач, притримувач, предмет_модуля);

      змінна предмет_структури_Звʼязок = створити_предмет_структури_Звʼязок(обчислювач);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути предмет_структури_Звʼязок;
      }
      мавка__притримати(обчислювач, притримувач, предмет_структури_Звʼязок);
      змінна результат_зміни_властивості = МаМа::предмет_змінити_властивість(М, предмет_модуля, МаМа::назва(М, "Звʼязок"), предмет_структури_Звʼязок, МаМа::Місцезнаходження { пусто як адреса<МаМа::ПредметТексту>, 0 });
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути результат_зміни_властивості;
      }

      змінна предмет_структури_Обслуговувач = створити_предмет_структури_Обслуговувач(обчислювач);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути предмет_структури_Обслуговувач;
      }
      мавка__притримати(обчислювач, притримувач, предмет_структури_Обслуговувач);
      результат_зміни_властивості = МаМа::предмет_змінити_властивість(М, предмет_модуля, МаМа::назва(М, "Обслуговувач"), предмет_структури_Обслуговувач, МаМа::Місцезнаходження { пусто як адреса<МаМа::ПредметТексту>, 0 });
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути результат_зміни_властивості;
      }

      змінна предмет_дії_обслуговувати = створити_дію_модуля_інтернет_обслуговувати(обчислювач, предмет_структури_Обслуговувач, предмет_структури_Звʼязок);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути предмет_дії_обслуговувати;
      }
      мавка__притримати(обчислювач, притримувач, предмет_дії_обслуговувати);
      результат_зміни_властивості = МаМа::предмет_змінити_властивість(М, предмет_модуля, МаМа::назва(М, "обслуговувати"), предмет_дії_обслуговувати, МаМа::Місцезнаходження { пусто як адреса<МаМа::ПредметТексту>, 0 });
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути результат_зміни_властивості;
      }

      змінна предмет_дії_звʼязатись = створити_дію_модуля_інтернет_звʼязатись(обчислювач, предмет_структури_Звʼязок);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути предмет_дії_звʼязатись;
      }
      мавка__притримати(обчислювач, притримувач, предмет_дії_звʼязатись);
      результат_зміни_властивості = МаМа::предмет_змінити_властивість(М, предмет_модуля, МаМа::назва(М, "звʼязатись"), предмет_дії_звʼязатись, МаМа::Місцезнаходження { пусто як адреса<МаМа::ПредметТексту>, 0 });
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути результат_зміни_властивості;
      }

      змінна предмет_дії_надіслати = створити_дію_модуля_інтернет_надіслати(обчислювач, предмет_структури_Звʼязок);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути предмет_дії_надіслати;
      }
      мавка__притримати(обчислювач, притримувач, предмет_дії_надіслати);
      результат_зміни_властивості = МаМа::предмет_змінити_властивість(М, предмет_модуля, МаМа::назва(М, "надіслати"), предмет_дії_надіслати, МаМа::Місцезнаходження { пусто як адреса<МаМа::ПредметТексту>, 0 });
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути результат_зміни_властивості;
      }

      змінна предмет_дії_закінчити = створити_дію_модуля_інтернет_закінчити(обчислювач, предмет_структури_Звʼязок);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути предмет_дії_закінчити;
      }
      мавка__притримати(обчислювач, притримувач, предмет_дії_закінчити);
      результат_зміни_властивості = МаМа::предмет_змінити_властивість(М, предмет_модуля, МаМа::назва(М, "закінчити"), предмет_дії_закінчити, МаМа::Місцезнаходження { пусто як адреса<МаМа::ПредметТексту>, 0 });
      якщо перевірити_чи_стан_падіння(обчислювач) {
        мавка__забрати_притримувач(обчислювач);
        вернути результат_зміни_властивості;
      }

      мавка__забрати_притримувач(обчислювач);
      вернути предмет_модуля;
    }

    зовнішня дія взяти_модуль_біб_інтернет(обчислювач: адреса<Обчислювач>): предмет_або_стан_падіння {
      змінна М = обчислювач;
      змінна результат = МаМа::створити_предмет_тексту(обчислювач, "мавка:бібліотека/інтернет/інтернет.ц") як адреса<Предмет>;
      якщо перевірити_чи_стан_падіння(обчислювач) {
        вернути результат як адреса<Предмет>;
      }
      змінна предмет_шляху = результат;
      змінна взятий_предмет: адреса<МаМа::Предмет> = пусто;
      якщо МаМа::отримати_взятий_предмет(М, предмет_шляху як адреса<МаМа::Предмет>, взятий_предмет::адреса) {
        вернути взятий_предмет як адреса<Предмет>;
      }
      результат = створити_модуль_інтернет(обчислювач);
      якщо перевірити_чи_стан_падіння(обчислювач) {
        вернути результат;
      }
      МаМа::покласти_взятий_предмет(М, предмет_шляху як адреса<МаМа::Предмет>, результат як адреса<МаМа::Предмет>);
      вернути результат;
    }
  }
}