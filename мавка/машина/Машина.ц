/* Машина Мавки */

взяти означення мавка;

простір мавка {

  місцева дія покласти_в_накопичувач_значень(М: адреса<Машина>, значення: Значення) {
    якщо М.накопичувач_значень.розмір == М.накопичувач_значень.вмісткість {
      запанікувати(М, "Накопичувач значень заповнено!");
      вернути;
    }

    М.накопичувач_значень.дані[М.накопичувач_значень.розмір] = значення;
    М.накопичувач_значень.розмір += 1;
  }

  місцева дія забрати_з_накопичувача_значень(М: адреса<Машина>) -> Значення {
    якщо М.накопичувач_значень.розмір == 0 {
      запанікувати(М, "Накопичувач значень порожній 1!");
    }

    М.накопичувач_значень.розмір -= 1;
    вернути М.накопичувач_значень.дані[М.накопичувач_значень.розмір];
  }

  місцева дія взяти_з_накопичувача_значень(М: адреса<Машина>, позиція: природне) -> Значення {
    якщо М.накопичувач_значень.розмір == 0 {
      запанікувати(М, "Накопичувач значень порожній 2!");
    }

    вернути М.накопичувач_значень.дані[М.накопичувач_значень.розмір - 1 - позиція];
  }

  місцева дія видалити_з_накопичувача_значень(М: адреса<Машина>, кількість: природне) {
    якщо М.накопичувач_значень.розмір == 0 {
      запанікувати(М, "Накопичувач значень порожній 3!");
    }
    
    М.накопичувач_значень.розмір -= кількість;
  }

  місцева дія перевірити_чи_значення_рівні(М: адреса<Машина>, а: Значення, б: Значення) -> логічне {
    якщо а == б {
      вернути так;
    }

    якщо а == пусто або б == пусто {
      вернути ні;
    }
    
    якщо а.тип == б.тип {
      якщо а.тип == М.значення_структура_Ціле {
        змінна предмет_цілого_а = а як адреса<Предмет>;
        змінна властивості_цілого_а = предмет_цілого_а.властивості як ВластивостіЦілого;

        змінна предмет_цілого_б = б як адреса<Предмет>;
        змінна властивості_цілого_б = предмет_цілого_б.властивості як ВластивостіЦілого;

        якщо властивості_цілого_а.значення == властивості_цілого_б.значення {
          вернути так;
        }

        вернути ні;
      }
      
      якщо а.тип == М.значення_структура_Дробове {
        змінна предмет_дробового_а = а як адреса<Предмет>;
        змінна властивості_дробового_а = предмет_дробового_а.властивості як ВластивостіДробового;

        змінна предмет_дробового_б = б як адреса<Предмет>;
        змінна властивості_дробового_б = предмет_дробового_б.властивості як ВластивостіДробового;

        якщо властивості_дробового_а.значення == властивості_дробового_б.значення {
          вернути так;
        }

        вернути ні;
      }

      якщо а.тип == М.значення_структура_Текст {
        змінна предмет_тексту_а = а як адреса<Предмет>;
        змінна властивості_тексту_а = предмет_тексту_а.властивості як ВластивостіТексту;

        змінна предмет_тексту_б = б як адреса<Предмет>;
        змінна властивості_тексту_б = предмет_тексту_б.властивості як ВластивостіТексту;

        якщо властивості_тексту_а.значення.розмір != властивості_тексту_б.значення.розмір {
          вернути ні;
        }

        змінна п: природне = 0;
        поки п < властивості_тексту_а.значення.розмір {
          якщо властивості_тексту_а.значення.дані[п] != властивості_тексту_б.значення.дані[п] {
            вернути ні;
          }

          п += 1;
        }

        вернути так;
      }
    }

    якщо а.тип == М.значення_структура_Ціле і б.тип == М.значення_структура_Дробове {
      змінна предмет_цілого_а = а як адреса<Предмет>;
      змінна властивості_цілого_а = предмет_цілого_а.властивості як ВластивостіЦілого;

      змінна предмет_дробового_б = б як адреса<Предмет>;
      змінна властивості_дробового_б = предмет_дробового_б.властивості як ВластивостіДробового;

      якщо д64(властивості_цілого_а.значення) == властивості_дробового_б.значення {
        вернути так;
      }

      вернути ні;
    }

    якщо а.тип == М.значення_структура_Дробове і б.тип == М.значення_структура_Ціле {
      змінна предмет_дробового_а = а як адреса<Предмет>;
      змінна властивості_дробового_а = предмет_дробового_а.властивості як ВластивостіДробового;

      змінна предмет_цілого_б = б як адреса<Предмет>;
      змінна властивості_цілого_б = предмет_цілого_б.властивості як ВластивостіЦілого;

      якщо властивості_дробового_а.значення == д64(властивості_цілого_б.значення) {
        вернути так;
      }

      вернути ні;
    }
    
    вернути ні;
  }

  місцева дія перевірити_чи_значення_є(М: адреса<Машина>, а: Значення, б: Значення) -> логічне {
    якщо а == пусто {
      якщо б == пусто {
        вернути так;
      }

      вернути ні;
    }

    якщо б == пусто {
      вернути ні;
    }

    якщо б == М.значення_дійсне {
      вернути так;
    }

    змінна тип_а = а.тип;

    поки тип_а != М.значення_дійсне {
      якщо тип_а == б {
        вернути так;
      }

      якщо тип_а == М.значення_предмет {
        вернути ні;
      }

      змінна предмет_типу_а = тип_а як адреса<Предмет>;
      змінна властивості_типу_а = предмет_типу_а.властивості як ВластивостіСтруктури;

      тип_а = властивості_типу_а.предок;
    }
    
    вернути ні;
  }

  місцева дія налаштувати_машину(М: адреса<Машина>, налаштування: Налаштування) {
    М.налаштування = налаштування;
    М.значення_дійсне = виділити_дійсне(М);
    М.значення_дійсне.тип = М.значення_дійсне;
    М.значення_дійсність = виділити_дійсне(М);
    М.значення_послідовність = виділити_дійсне(М);
    М.значення_предмет = виділити_дійсне(М);
    
    змінна значення_структура_Структура = виділити_предмет(М, пусто);
    значення_структура_Структура.тип = М.значення_структура_Структура;
    М.значення_структура_Структура = значення_структура_Структура як Значення;

    змінна значення_структура_Модуль = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Модуль = значення_структура_Модуль як Значення;

    змінна значення_структура_Параметр = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Параметр = значення_структура_Параметр як Значення;

    змінна значення_структура_Число = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Число = значення_структура_Число як Значення;

    змінна значення_структура_Ціле = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Ціле = значення_структура_Ціле як Значення;

    змінна значення_структура_Дробове = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Дробове = значення_структура_Дробове як Значення;

    змінна значення_структура_Текст = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Текст = значення_структура_Текст як Значення;

    змінна значення_структура_Код = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Код = значення_структура_Код як Значення;

    змінна значення_структура_Дія = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Дія = значення_структура_Дія як Значення;

    змінна значення_структура_Список = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Список = значення_структура_Список як Значення;

    змінна значення_структура_Словник = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Словник = значення_структура_Словник як Значення;

    змінна значення_структура_Дані = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_Дані = значення_структура_Дані як Значення;

    змінна значення_структура_ЗмінніДані = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_ЗмінніДані = значення_структура_ЗмінніДані як Значення;

    змінна значення_структура_ДіапазонЦілих = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_ДіапазонЦілих = значення_структура_ДіапазонЦілих як Значення;

    змінна значення_структура_ДіапазонДробових = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_ДіапазонДробових = значення_структура_ДіапазонДробових як Значення;

    змінна значення_структура_ПеребірДіапазонуЦілих = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_ПеребірДіапазонуЦілих = значення_структура_ПеребірДіапазонуЦілих як Значення;

    змінна значення_структура_ПеребірДіапазонуДробових = виділити_предмет(М, М.значення_структура_Структура);
    М.значення_структура_ПеребірДіапазонуДробових = значення_структура_ПеребірДіапазонуДробових як Значення;

    значення_структура_Структура.властивості = ВластивостіСтруктури {
      заборонити_наслідування = так,
      назва = створити_текст(М, "Структура"),
      предок = М.значення_предмет,
      параметри = пусто,
      методи = Методи { 0, пусто }
    };

    значення_структура_Модуль.властивості = ВластивостіСтруктури {
      заборонити_наслідування = так,
      назва = створити_текст(М, "Модуль"),
      предок = М.значення_предмет,
      параметри = пусто,
      методи = Методи { 0, пусто }
    };

    значення_структура_Параметр.властивості = ВластивостіСтруктури {
      заборонити_наслідування = так,
      назва = створити_текст(М, "Параметр"),
      предок = М.значення_предмет,
      параметри = пусто,
      методи = Методи { 0, пусто }
    };

    значення_структура_Текст.властивості = ВластивостіСтруктури {
      заборонити_наслідування = так,
      назва = створити_текст(М, "Текст"),
      предок = М.значення_предмет,
      параметри = пусто,
      методи = Методи { 0, пусто }
    };

    значення_структура_Число.властивості = ВластивостіСтруктури {
      заборонити_наслідування = так,
      назва = створити_текст(М, "Число"),
      предок = М.значення_предмет,
      параметри = пусто,
      методи = Методи { 0, пусто }
    };

    значення_структура_Ціле.властивості = ВластивостіСтруктури {
      заборонити_наслідування = так,
      назва = створити_текст(М, "Ціле"),
      предок = М.значення_структура_Число,
      параметри = пусто,
      методи = Методи { 0, пусто }
    };

    значення_структура_Дробове.властивості = ВластивостіСтруктури {
      заборонити_наслідування = так,
      назва = створити_текст(М, "Дробове"),
      предок = М.значення_структура_Число,
      параметри = пусто,
      методи = Методи { 0, пусто }
    };

    значення_структура_Код.властивості = ВластивостіСтруктури {
      заборонити_наслідування = так,
      назва = створити_текст(М, "Код"),
      предок = М.значення_предмет,
      параметри = пусто,
      методи = Методи { 0, пусто }
    };

    значення_структура_Дія.властивості = ВластивостіСтруктури {
      заборонити_наслідування = так,
      назва = створити_текст(М, "Дія"),
      предок = М.значення_предмет,
      параметри = пусто,
      методи = Методи { 0, пусто }
    };

    значення_структура_Список.властивості = ВластивостіСтруктури {
      заборонити_наслідування = так,
      назва = створити_текст(М, "Список"),
      предок = М.значення_предмет,
      параметри = пусто,
      методи = Методи { 0, пусто }
    };

    значення_структура_Словник.властивості = ВластивостіСтруктури {
      заборонити_наслідування = так,
      назва = створити_текст(М, "Словник"),
      предок = М.значення_предмет,
      параметри = пусто,
      методи = Методи { 0, пусто }
    };

    значення_структура_Дані.властивості = ВластивостіСтруктури {
      заборонити_наслідування = так,
      назва = створити_текст(М, "Дані"),
      предок = М.значення_предмет,
      параметри = пусто,
      методи = Методи { 0, пусто }
    };

    значення_структура_ЗмінніДані.властивості = ВластивостіСтруктури {
      заборонити_наслідування = так,
      назва = створити_текст(М, "ЗмінніДані"),
      предок = М.значення_предмет,
      параметри = пусто,
      методи = Методи { 0, пусто }
    };

    значення_структура_ДіапазонЦілих.властивості = ВластивостіСтруктури {
      заборонити_наслідування = так,
      назва = створити_текст(М, "ДіапазонЦілих"),
      предок = М.значення_предмет,
      параметри = пусто,
      методи = Методи { 0, пусто }
    };

    значення_структура_ДіапазонДробових.властивості = ВластивостіСтруктури {
      заборонити_наслідування = так,
      назва = створити_текст(М, "ДіапазонДробових"),
      предок = М.значення_предмет,
      параметри = пусто,
      методи = Методи { 0, пусто }
    };

    значення_структура_ПеребірДіапазонуЦілих.властивості = ВластивостіСтруктури {
      заборонити_наслідування = так,
      назва = створити_текст(М, "ПеребірДіапазонуЦілих"),
      предок = М.значення_предмет,
      параметри = пусто,
      методи = Методи { 0, пусто }
    };

    значення_структура_ПеребірДіапазонуДробових.властивості = ВластивостіСтруктури {
      заборонити_наслідування = так,
      назва = створити_текст(М, "ПеребірДіапазонуДробових"),
      предок = М.значення_предмет,
      параметри = пусто,
      методи = Методи { 0, пусто }
    };

    М.глобальна_дійсність = створити_дійсність(М, пусто, пусто);

    стала вмісткість_накопичувача_значень = 1024;
    М.накопичувач_значень = НакопичувачЗначень {
      розмір = 0,
      дані = виділити_памʼять<Значення>(М, вмісткість_накопичувача_значень),
      вмісткість = вмісткість_накопичувача_значень
    };

    стала вмісткість_історії_здійснення = 1024;
    М.історія_здійснення = ІсторіяЗдійснення {
      розмір = 0,
      дані = виділити_памʼять<Здійснюване>(М, вмісткість_історії_здійснення),
      вмісткість = вмісткість_історії_здійснення
    };

    М.стан_падіння = ні;
    М.значення_падіння = пусто;
    М.місцезнаходження_падіння = Місцезнаходження { пусто, 0 };

    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "дійсне"), М.значення_дійсне);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "послідовність"), М.значення_послідовність);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "дійсність"), М.значення_дійсність);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "предмет"), М.значення_предмет);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "Структура"), М.значення_структура_Структура);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "Модуль"), М.значення_структура_Модуль);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "Параметр"), М.значення_структура_Параметр);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "Число"), М.значення_структура_Число);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "Ціле"), М.значення_структура_Ціле);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "Дробове"), М.значення_структура_Дробове);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "Текст"), М.значення_структура_Текст);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "Код"), М.значення_структура_Код);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "Дія"), М.значення_структура_Дія);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "Список"), М.значення_структура_Список);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "Словник"), М.значення_структура_Словник);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "Дані"), М.значення_структура_Дані);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "ЗмінніДані"), М.значення_структура_ЗмінніДані);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "ДіапазонЦілих"), М.значення_структура_ДіапазонЦілих);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "ДіапазонДробових"), М.значення_структура_ДіапазонДробових);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "ПеребірДіапазонуЦілих"), М.значення_структура_ПеребірДіапазонуЦілих);
    визначити_в_дійсності(М, М.глобальна_дійсність, створити_текст(М, "ПеребірДіапазонуДробових"), М.значення_структура_ПеребірДіапазонуДробових);
  }

  місцева дія створити_машину(налаштування: Налаштування) -> адреса<Машина> {
    змінна М = налаштування.виділити_сиру_памʼять(пусто, Машина.розмір) як адреса<Машина>;
  
    налаштувати_машину(М, налаштування);
  
    вернути М;
  }

  місцева дія покласти_спробу(М: адреса<Машина>, спроби: адреса<Спроби>, спроба: Спроба) {
    спроби.дані = перевиділити_памʼять<Спроба>(М, спроби.дані, спроби.розмір + 1);
    спроби.дані[спроби.розмір] = спроба;
    спроби.розмір += 1;
  }

  місцева дія видалити_останню_спробу(М: адреса<Машина>, спроби: адреса<Спроби>) {
    якщо спроби.розмір == 0 {
      запанікувати(М, "Немає спроб для видалення.");
      вернути;
    }

    спроби.розмір -= 1;
  }

  місцева дія зробити_накопичувач_тексту(М: адреса<Машина>, вмісткість: природне) -> НакопичувачТексту {
    змінна дані: памʼять<п8> = пусто;

    якщо вмісткість > 0 {
      дані = виділити_памʼять<п8>(М, вмісткість);
    }
    
    вернути НакопичувачТексту {
      розмір = 0,
      дані = дані,
      вмісткість = вмісткість
    };
  }

  місцева дія покласти_в_накопичувач_тексту(М: адреса<Машина>, накопичувач: адреса<НакопичувачТексту>, значення: кд) {
    змінна новий_розмір = накопичувач.розмір + значення.розмір;

    накопичувач.дані = перевиділити_памʼять<п8>(М, накопичувач.дані, новий_розмір);

    змінна п: природне = 0;
    поки п < значення.розмір {
      накопичувач.дані[накопичувач.розмір + п] = значення.дані[п];
      п += 1;
    }

    накопичувач.розмір = новий_розмір;
  }

  місцева дія увімкнути_стан_падіння(М: адреса<Машина>, значення_падіння: Значення, місцезнаходження_падіння: Місцезнаходження) -> ніщо {
    М.стан_падіння = так;
    М.значення_падіння = значення_падіння;
    М.місцезнаходження_падіння = місцезнаходження_падіння;
  }

  дія вивести_пропуски(М: адреса<Машина>, кількість: природне) {
    змінна п: природне = 0;
    
    поки п < кількість {
      вивести(М, " ");
    
      п += 1;
    }
  }

  // позначка: прохід по типах предметів
  місцева дія вивести_значення(М: адреса<Машина>, значення: Значення, розмір_відступу: природне, глибина: природне) {
    якщо значення == пусто {
      вивести_формат(М, ФКолірТекстуЖовтий);
      
      вивести(М, "недійсне");
      
      вивести_формат(М, ФОчистити);

      вернути;
    }
    
    якщо значення == М.значення_дійсне {
      вивести_формат(М, ФКолірТекстуЖовтий);
      
      вивести(М, "дійсне");

      вивести_формат(М, ФОчистити);

      вернути;
    }
    
    якщо значення == М.значення_послідовність {
      вивести(М, "послідовність");
      вернути;
    }
    
    якщо значення == М.значення_дійсність {
      вивести(М, "дійсність");
      вернути;
    }
    
    якщо значення == М.значення_предмет {
      вивести(М, "предмет");
      вернути;
    }

    якщо значення.тип == М.значення_дійсне {
      вивести(М, "дійсне()");
      вернути;
    }
    
    якщо значення.тип == М.значення_послідовність {
      вивести(М, "послідовність()");
      вернути;
    }
    
    якщо значення.тип == М.значення_дійсність {
      вивести(М, "дійсність()");
      вернути;
    }
    
    якщо значення.тип == М.значення_предмет {
      вивести(М, "предмет()");
      вернути;
    }
    
    якщо значення.тип == М.значення_структура_Структура {
      змінна предмет = значення як адреса<Предмет>;
      змінна властивості = предмет.властивості як ВластивостіСтруктури;

      вивести_формат(М, ФКолірТекстуСиній);

      якщо властивості.назва == пусто {
        вивести(М, "<структура>");
      } інакше {
        вивести(М, "<структура ");
        
        вивести_значення(М, властивості.назва, 0, 0);

        вивести(М, ">");
      }

      вивести_формат(М, ФОчистити);

      вернути;
    }
    
    якщо значення.тип == М.значення_структура_Модуль {
      змінна предмет = значення як адреса<Предмет>;
      змінна властивості_модуля = предмет.властивості як ВластивостіМодуля;

      вивести_формат(М, ФКолірТекстуСиній);

      якщо властивості_модуля.назва == пусто {
        вивести(М, "<модуль");
      } інакше {
        вивести(М, "<модуль ");

        вивести_значення(М, властивості_модуля.назва, 0, 0);
      }

      якщо властивості_модуля.властивості.розмір > 0 {
        вивести(М, "[");
        змінна п: природне = 0;

        поки п < властивості_модуля.властивості.розмір {
          вивести_значення(М, властивості_модуля.властивості.дані[п].назва, 0, 0);
  
          якщо п + 1 < властивості_модуля.властивості.розмір {
            вивести(М, ", ");
          }
  
          п += 1;
        }
        вивести(М, "]");
      }
      
      вивести(М, ">");

      вивести_формат(М, ФОчистити);

      вернути;
    }
    
    якщо значення.тип == М.значення_структура_Параметр {
      вивести(М, "Параметр()");
      вернути;
    }
    
    якщо значення.тип == М.значення_структура_Число {
      запанікувати(М, "Неможливо вивести значення типу Число без конкретизації Ціле або Дробове.");
    }
    
    якщо значення.тип == М.значення_структура_Ціле {
      змінна предмет = значення як адреса<Предмет>;
      змінна властивості = предмет.властивості як ВластивостіЦілого;

      вивести_формат(М, ФКолірТекстуЖовтий);

      вивести_ц64(М, властивості.значення);

      вивести_формат(М, ФОчистити);

      вернути;
    }
    
    якщо значення.тип == М.значення_структура_Дробове {
      змінна предмет = значення як адреса<Предмет>;
      змінна властивості = предмет.властивості як ВластивостіДробового;

      вивести_формат(М, ФКолірТекстуЖовтий);

      вивести_д64(М, властивості.значення);

      вивести_формат(М, ФОчистити);

      вернути;
    }
    
    якщо значення.тип == М.значення_структура_Текст {
      змінна предмет = значення як адреса<Предмет>;
      змінна властивості = предмет.властивості як ВластивостіТексту;


      якщо глибина != 0 {
        вивести_формат(М, ФКолірТекстуЗелений);
        вивести(М, "\"");
      }

      якщо глибина == 0 {
        вивести(М, властивості.значення);
      } інакше {
        змінна п: природне = 0;
        поки п < властивості.значення.розмір {
          змінна символ = властивості.значення.дані[п];

          якщо символ == КД::символи::Міжряд {
            вивести(М, "\\р");
          } інакше якщо символ == КД::символи::Дволапка {
            вивести(М, "\\\"");
          } інакше якщо символ == КД::символи::Обернена_похила {
            вивести(М, "\\\\");
          } інакше {
            вивести(М, кд { 1, символ::адреса як памʼять<п8> });
          }

          п += 1;
        }
      }

      якщо глибина != 0 {
        вивести(М, "\"");
        вивести_формат(М, ФОчистити);
      }

      вернути;
    }
    
    якщо значення.тип == М.значення_структура_Код {
      вивести(М, "Код()");
      вернути;
    }
    
    якщо значення.тип == М.значення_структура_Дія {
      змінна предмет = значення як адреса<Предмет>;
      змінна властивості = предмет.властивості як ВластивостіДії;

      вивести_формат(М, ФКолірТекстуСиній);

      якщо властивості.назва == пусто {
        вивести(М, "<дія>");
      } інакше {
        вивести(М, "<дія ");
        
        вивести_значення(М, властивості.назва, 0, 0);

        вивести(М, ">");
      }

      вивести_формат(М, ФОчистити);

      вернути;
    }
    
    якщо значення.тип == М.значення_структура_Список {
      змінна предмет_списку = значення як адреса<Предмет>;
      змінна властивості_списку = предмет_списку.властивості як ВластивостіСписку;
      
      вивести(М, "[");

      якщо властивості_списку.розмір > 0 {
        якщо розмір_відступу > 0 {
          вивести(М, "\р");
        }

        змінна п: природне = 0;
        поки п < властивості_списку.розмір {
          змінна елемент = властивості_списку.елементи[п];
          вивести_пропуски(М, розмір_відступу * (глибина + 1));
          вивести_значення(М, елемент, розмір_відступу, глибина + 1);
          якщо п + 1 < властивості_списку.розмір {
            вивести(М, ",");
            якщо розмір_відступу > 0 {
              вивести(М, "\р");
            } інакше {
              вивести(М, " ");
            }
          }

          п += 1;
        }

        якщо розмір_відступу > 0 {
          вивести(М, "\р");
        }
        вивести_пропуски(М, розмір_відступу * глибина);
      }

      вивести(М, "]");
      
      вернути;
    }
    
    якщо значення.тип == М.значення_структура_Словник {
      змінна предмет_словника = значення як адреса<Предмет>;
      змінна властивості_словника = предмет_словника.властивості як ВластивостіСловника;

      вивести(М, "{");
      якщо властивості_словника.розмір > 0 {
        якщо розмір_відступу > 0 {
          вивести(М, "\р");
        }

        змінна п: природне = 0;
        поки п < властивості_словника.розмір {
          змінна пара = властивості_словника.елементи[п];
          вивести_пропуски(М, розмір_відступу * (глибина + 1));
          вивести_значення(М, пара.ключ, розмір_відступу, глибина + 1);
          вивести(М, " => ");
          вивести_значення(М, пара.значення, розмір_відступу, глибина + 1);
          якщо п + 1 < властивості_словника.розмір {
            вивести(М, ",");
            якщо розмір_відступу > 0 {
              вивести(М, "\р");
            } інакше {
              вивести(М, " ");
            }
          }

          п += 1;
        }

        якщо розмір_відступу > 0 {
          вивести(М, "\р");
        }
        вивести_пропуски(М, розмір_відступу * глибина);
      }
      
      вивести(М, "}");

      вернути;
    }
    
    якщо значення.тип == М.значення_структура_Дані {
      вивести(М, "Дані()");
      вернути;
    }
    
    якщо значення.тип == М.значення_структура_ЗмінніДані {
      вивести(М, "ЗмінніДані()");
      вернути;
    }
    
    якщо значення.тип == М.значення_структура_ДіапазонЦілих {
      вивести(М, "ДіапазонЦілих()");
      вернути;
    }
    
    якщо значення.тип == М.значення_структура_ДіапазонДробових {
      вивести(М, "ДіапазонДробових()");
      вернути;
    }
    
    якщо значення.тип == М.значення_структура_ПеребірДіапазонуЦілих {
      вивести(М, "ПеребірДіапазонуЦілих()");
      вернути;
    }
    
    якщо значення.тип == М.значення_структура_ПеребірДіапазонуДробових {
      вивести(М, "ПеребірДіапазонуДробових()");
      вернути;
    }

    змінна предмет_структури = значення.тип як адреса<Предмет>;
    змінна властивості_структури = предмет_структури.властивості як ВластивостіСтруктури;

    вивести_формат(М, ФКолірТекстуСиній);

    якщо властивості_структури.назва == пусто {
      вивести(М, "<без_назви>");
    } інакше {
      вивести_значення(М, властивості_структури.назва, 0, 0);
    }

    вивести_формат(М, ФОчистити);

    вивести(М, "(");

    якщо властивості_структури.параметри != пусто {
      якщо розмір_відступу > 0 {
        вивести(М, "\р");
      }

      змінна п: природне = 0;
      поки п < властивості_структури.параметри.розмір {
        змінна параметр = властивості_структури.параметри.дані[п];
        змінна предмет_параметра = параметр як адреса<Предмет>;
        змінна властивості_параметра = предмет_параметра.властивості як ВластивостіПараметра;
  
        змінна значення_властивості = отримати_властивість(М, значення, властивості_параметра.назва, Місцезнаходження { пусто, 0});
        вивести_пропуски(М, розмір_відступу * (глибина + 1));
        вивести_значення(М, властивості_параметра.назва, 0, 0);
        вивести(М, "=");
        вивести_значення(М, значення_властивості, розмір_відступу, глибина + 1);
        якщо п + 1 < властивості_структури.параметри.розмір {
          вивести(М, ",");
          якщо розмір_відступу > 0 {
            вивести(М, "\р");
          } інакше {
            вивести(М, " ");
          }
        }
  
        п += 1;
      }

      якщо розмір_відступу > 0 {
        вивести(М, "\р");
      }
      вивести_пропуски(М, розмір_відступу * глибина);
    }

    вивести(М, ")");
  }

  місцева дія надрукувати_значення(М: адреса<Машина>, значення: Значення, розмір_відступу: природне, глибина: природне) {
    вивести_значення(М, значення, розмір_відступу, глибина);
    вивести(М, "\р");
  }

  дія рідна_дія_друк(М: адреса<Машина>,
                     здійснювач: Значення,
                     значення: Значення,
                     кількість_задіяних: природне, 
                     задіяні: памʼять<Значення>,
                     іменовано_задіяні: адреса<ІменованіЗадіяні>) -> адреса<Дійсне> {
    змінна п: природне = 0;
    поки п < кількість_задіяних {
      надрукувати_значення(М, задіяні[п], 2, 0);
    
      п += 1;
    }
    
    вернути пусто;                  
  }

  місцева дія визначити_глобальні_виводу(М: адреса<Машина>) {
    змінна дія_друк = створити_рідну_дію(
      М,
      створити_текст(М, "друк"),
      пусто,
      пусто,
      рідна_дія_друк
    );
  
    визначити_в_дійсності(
      М,
      М.глобальна_дійсність,
      створити_текст(М, "друк"),
      дія_друк
    );
  }

  дія КД_виділити_сиру_памʼять(система: адреса<КД::Система>, розмір: природне) -> памʼять<п8> {
    змінна М = система.дані як адреса<Машина>;

    вернути виділити_памʼять<п8>(М, розмір);
  }
  
  дія КД_перевиділити_сиру_памʼять(система: адреса<КД::Система>, значення: памʼять<п8>, новий_розмір: природне) -> памʼять<п8> {
    змінна М = система.дані як адреса<Машина>;

    вернути перевиділити_памʼять<п8>(М, значення, новий_розмір);
  }
  
  дія КД_звільнити_сиру_памʼять(система: адреса<КД::Система>, значення: невідома_памʼять) {
    змінна М = система.дані як адреса<Машина>;

    звільнити_памʼять(М, значення як памʼять<п8>);
  }

  місцева дія перекодувати_ю8_в_кд(М: адреса<Машина>, вхід: ю8, вихід: адреса<кд>, вихід_позиції_помилки: адреса<природне>) -> логічне {
    змінна система_КД = КД::Система {
      дані = М,
      виділити_сиру_памʼять = КД_виділити_сиру_памʼять,
      звільнити_сиру_памʼять = КД_звільнити_сиру_памʼять,
      перевиділити_сиру_памʼять = КД_перевиділити_сиру_памʼять
    };

    вернути КД::перекодувати_з_ю8(система_КД::адреса, вхід, ні, ні, вихід, вихід_позиції_помилки);
  }

  місцева дія перекодувати_кд_в_ю8(М: адреса<Машина>, вхід: кд, вихід: адреса<ю8>, вихід_позиції_помилки: адреса<природне>) -> логічне {
    змінна система_КД = КД::Система {
      дані = М,
      виділити_сиру_памʼять = КД_виділити_сиру_памʼять,
      звільнити_сиру_памʼять = КД_звільнити_сиру_памʼять,
      перевиділити_сиру_памʼять = КД_перевиділити_сиру_памʼять
    };

    вернути КД::перекодувати_в_ю8(система_КД::адреса, вхід, ні, вихід, вихід_позиції_помилки);
  }

  місцева дія здійснити(
    М: адреса<Машина>,
    здійснювач: Значення,
    значення: Значення,
    кількість_задіяних: природне,
    задіяні: памʼять<Значення>,
    іменовано_задіяні: адреса<ІменованіЗадіяні>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    якщо значення.тип == М.значення_структура_Дія {
      вернути предмет_дії_здійснити(
        М,
        здійснювач,
        значення,
        кількість_задіяних,
        задіяні,
        іменовано_задіяні,
        місцезнаходження
      );
    }
    
    якщо значення.тип == М.значення_структура_Структура {
      вернути предмет_структури_здійснити(
        М,
        здійснювач,
        значення,
        кількість_задіяних,
        задіяні,
        іменовано_задіяні,
        місцезнаходження
      );
    }          
    
    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати дію можна лише над діями."),
      місцезнаходження
    );
    вернути пусто;               
  }

  місцева дія виконати_додавання(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо ліво == пусто {
      стрибнути крок_помилка;
    }
    
    якщо ліво.тип == М.значення_структура_Ціле {
      вернути предмет_цілого_додати(М, ліво, право, місцезнаходження);
    }

    якщо ліво.тип == М.значення_структура_Дробове {
      вернути предмет_дробового_додати(М, ліво, право, місцезнаходження);
    }
  
  крок_помилка:
    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати додавання можна лише між числами."),
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_відімання(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо ліво == пусто {
      стрибнути крок_помилка;
    }
    
    якщо ліво.тип == М.значення_структура_Ціле {
      вернути предмет_цілого_відняти(М, ліво, право, місцезнаходження);
    }

    якщо ліво.тип == М.значення_структура_Дробове {
      вернути предмет_дробового_відняти(М, ліво, право, місцезнаходження);
    }
  
  крок_помилка:
    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати віднімання можна лише між числами."),
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_множення(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо ліво == пусто {
      стрибнути крок_помилка;
    }
    
    якщо ліво.тип == М.значення_структура_Ціле {
      вернути предмет_цілого_помножити(М, ліво, право, місцезнаходження);
    }

    якщо ліво.тип == М.значення_структура_Дробове {
      вернути предмет_дробового_помножити(М, ліво, право, місцезнаходження);
    }
  
  крок_помилка:
    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати множення можна лише між числами."),
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_ділення(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо ліво == пусто {
      стрибнути крок_помилка;
    }
    
    якщо ліво.тип == М.значення_структура_Ціле {
      вернути предмет_цілого_поділити(М, ліво, право, місцезнаходження);
    }

    якщо ліво.тип == М.значення_структура_Дробове {
      вернути предмет_дробового_поділити(М, ліво, право, місцезнаходження);
    }
  
  крок_помилка:
    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати ділення можна лише між числами."),
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_остача(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо ліво == пусто {
      стрибнути крок_помилка;
    }
    
    якщо ліво.тип == М.значення_структура_Ціле {
      вернути предмет_цілого_остача(М, ліво, право, місцезнаходження);
    }

    якщо ліво.тип == М.значення_структура_Дробове {
      вернути предмет_дробового_остача(М, ліво, право, місцезнаходження);
    }
  
  крок_помилка:
    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати отримання остачі ділення можна лише між числами."),
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_неповна_частка(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо ліво == пусто {
      стрибнути крок_помилка;
    }
    
    якщо ліво.тип == М.значення_структура_Ціле {
      вернути предмет_цілого_неповна_частка(М, ліво, право, місцезнаходження);
    }

    якщо ліво.тип == М.значення_структура_Дробове {
      вернути предмет_дробового_неповна_частка(М, ліво, право, місцезнаходження);
    }
  
  крок_помилка:
    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати отримання неповної частки ділення можна лише між числами."),
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_степінь(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо ліво == пусто {
      стрибнути крок_помилка;
    }
    
    якщо ліво.тип == М.значення_структура_Ціле {
      вернути предмет_цілого_степінь(М, ліво, право, місцезнаходження);
    }

    якщо ліво.тип == М.значення_структура_Дробове {
      вернути предмет_дробового_степінь(М, ліво, право, місцезнаходження);
    }
  
  крок_помилка:
    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати піднесення до степеня можна лише між числами."),
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_вліво(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо ліво == пусто {
      стрибнути крок_помилка;
    }
    
    якщо ліво.тип == М.значення_структура_Ціле {
      вернути предмет_цілого_зсунути_вліво(М, ліво, право, місцезнаходження);
    }
  
  крок_помилка:
    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати двійкове зсув вліво можна лише між цілими числами."),
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_вправо(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо ліво == пусто {
      стрибнути крок_помилка;
    }
    
    якщо ліво.тип == М.значення_структура_Ціле {
      вернути предмет_цілого_зсунути_вправо(М, ліво, право, місцезнаходження);
    }
  
  крок_помилка:
    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати двійкове зсув вправо можна лише між цілими числами."),
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_вправо_2(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо ліво == пусто {
      стрибнути крок_помилка;
    }
    
    якщо ліво.тип == М.значення_структура_Ціле {
      вернути предмет_цілого_зсунути_вправо_2(М, ліво, право, місцезнаходження);
    }
  
  крок_помилка:
    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати двійкове зсув вправо 2 можна лише між цілими числами."),
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_двійкове_і(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо ліво == пусто {
      стрибнути крок_помилка;
    }
    
    якщо ліво.тип == М.значення_структура_Ціле {
      вернути предмет_цілого_двійкове_і(М, ліво, право, місцезнаходження);
    }
  
  крок_помилка:
    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати двійкове І можна лише між цілими числами."),
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_двійкове_або(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо ліво == пусто {
      стрибнути крок_помилка;
    }
    
    якщо ліво.тип == М.значення_структура_Ціле {
      вернути предмет_цілого_двійкове_або(М, ліво, право, місцезнаходження);
    }
  
  крок_помилка:
    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати двійкове АБО можна лише між цілими числами."),
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_двійкове_вабо(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо ліво == пусто {
      стрибнути крок_помилка;
    }
    
    якщо ліво.тип == М.значення_структура_Ціле {
      вернути предмет_цілого_двійкове_вабо(М, ліво, право, місцезнаходження);
    }
  
  крок_помилка:
    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати двійкове ВАБО можна лише між цілими числами."),
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_менше(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо ліво == пусто {
      стрибнути крок_помилка;
    }
    
    якщо ліво.тип == М.значення_структура_Ціле {
      вернути предмет_цілого_менше(М, ліво, право, місцезнаходження);
    }

    якщо ліво.тип == М.значення_структура_Дробове {
      вернути предмет_дробового_більше(М, ліво, право, місцезнаходження);
    }
  
  крок_помилка:
    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати перевірку чи менше можна лише між числами."),
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_не_менше(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    змінна результат = виконати_менше(М, ліво, право, місцезнаходження);

    якщо М.стан_падіння {
      вернути пусто;
    }

    якщо результат == М.значення_дійсне {
      вернути пусто;
    }

    вернути М.значення_дійсне;
  }

  місцева дія виконати_більше(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо ліво == пусто {
      стрибнути крок_помилка;
    }
    
    якщо ліво.тип == М.значення_структура_Ціле {
      вернути предмет_цілого_більше(М, ліво, право, місцезнаходження);
    }

    якщо ліво.тип == М.значення_структура_Дробове {
      вернути предмет_дробового_більше(М, ліво, право, місцезнаходження);
    }
  
  крок_помилка:
    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати перевірку чи більше можна лише між числами."),
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_не_більше(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    змінна результат = виконати_більше(М, ліво, право, місцезнаходження);

    якщо М.стан_падіння {
      вернути пусто;
    }

    якщо результат == М.значення_дійсне {
      вернути пусто;
    }

    вернути М.значення_дійсне;
  }

  місцева дія виконати_рівно(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо перевірити_чи_значення_рівні(М, ліво, право) {
      вернути М.значення_дійсне;
    } інакше {
      вернути пусто;
    }
  }

  місцева дія виконати_не_рівно(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо перевірити_чи_значення_рівні(М, ліво, право) {
      вернути пусто;
    } інакше {
      вернути М.значення_дійсне;
    }
  }

  місцева дія виконати_містить(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    вернути пусто;
  }

  місцева дія виконати_не_містить(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    вернути пусто;
  }

  місцева дія виконати_є(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо перевірити_чи_значення_є(М, ліво, право) {
      вернути М.значення_дійсне;
    } інакше {
      вернути пусто;
    }
  }

  місцева дія виконати_не_є(М: адреса<Машина>, ліво: Значення, право: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо перевірити_чи_значення_є(М, ліво, право) {
      вернути пусто;
    } інакше {
      вернути М.значення_дійсне;
    }
  }

  місцева дія виконати_двійкове_не(М: адреса<Машина>, значення: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо значення == пусто {
      стрибнути крок_помилка;
    }
    
    якщо значення.тип == М.значення_структура_Ціле {
      вернути предмет_цілого_двійкове_не(М, значення, місцезнаходження);
    }
  
  крок_помилка:
    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати двійкове НЕ можна лише для цілого числа."),
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_змінити_знак(М: адреса<Машина>, значення: Значення, місцезнаходження: Місцезнаходження) -> Значення {
    якщо значення == пусто {
      стрибнути крок_помилка;
    }
    
    якщо значення.тип == М.значення_структура_Ціле {
      вернути предмет_цілого_змінити_знак(М, значення, місцезнаходження);
    }
    
    якщо значення.тип == М.значення_структура_Дробове {
      вернути предмет_дробового_змінити_знак(М, значення, місцезнаходження);
    }
  
  крок_помилка:
    увімкнути_стан_падіння(
      М,
      створити_текст(М, "Виконати зміну знаку можна лише для чисел."),
      місцезнаходження
    );
    вернути пусто;
  }

  місцева дія виконати_взяти(М: адреса<Машина>, модуль: Значення, звідки: Значення, довжина_шляху: природне, шлях: памʼять<Значення>, місцезнаходження: Місцезнаходження) -> Значення {
    вернути пусто;
  }

  місцева дія надрукувати_код(М: адреса<Машина>, розмір: природне, дані: памʼять<п8>, дія_виводу: (М: адреса<Машина>, значення: кд) -> ніщо) {
    змінна п: природне = 0;
    поки п < розмір {
      змінна вказівка = дані[п];
      якщо вказівка == ВПокластиНедійсне { дія_виводу(М, "ПокластиНедійсне\р"); }
      інакше якщо вказівка == ВПокластиДійсне { дія_виводу(М, "ПокластиДійсне\р"); }
      інакше якщо вказівка == ВПокластиПредмет { дія_виводу(М, "ПокластиПредмет\р"); }
      інакше якщо вказівка == ВПокластиПослідовність { дія_виводу(М, "ПокластиПослідовність\р"); }
      інакше якщо вказівка == ВПокластиЯ { дія_виводу(М, "ПокластиЯ\р"); }
      інакше якщо вказівка == ВПокластиКонстанту { дія_виводу(М, "ПокластиКонстанту\р"); п += 4; }
      інакше якщо вказівка == ВСтворитиПослідовність { дія_виводу(М, "СтворитиПослідовність\р"); }
      інакше якщо вказівка == ВСтворитиДію { дія_виводу(М, "СтворитиДію\р"); п += 4; }
      інакше якщо вказівка == ВСтворитиСтруктуру { дія_виводу(М, "СтворитиСтруктуру\р"); п += 4; }
      інакше якщо вказівка == ВСтворитиМодуль { дія_виводу(М, "СтворитиМодуль\р"); }
      інакше якщо вказівка == ВСтворитиСписок { дія_виводу(М, "СтворитиСписок\р"); п += 4; }
      інакше якщо вказівка == ВСтворитиСловник { дія_виводу(М, "СтворитиСловник\р"); п += 4; }
      інакше якщо вказівка == ВСтворитиДіапазон { дія_виводу(М, "СтворитиДіапазон\р"); }
      інакше якщо вказівка == ВЗаписатиМетод { дія_виводу(М, "ЗаписатиМетод\р"); }
      інакше якщо вказівка == ВСтворитиПараметр { дія_виводу(М, "СтворитиПараметр\р"); }
      інакше якщо вказівка == ВВизначити { дія_виводу(М, "Визначити\р"); }
      інакше якщо вказівка == ВЗвернутись { дія_виводу(М, "Звернутись\р"); }
      інакше якщо вказівка == ВЗдійснити { дія_виводу(М, "Здійснити\р"); п += 4; п += 4; }
      інакше якщо вказівка == ВОтриматиВластивість { дія_виводу(М, "ОтриматиВластивість\р"); }
      інакше якщо вказівка == ВЗмінитиВластивість { дія_виводу(М, "ЗмінитиВластивість\р"); }
      інакше якщо вказівка == ВЗдійснитиВластивість { дія_виводу(М, "ЗдійснитиВластивість\р"); п += 4; п += 4; }
      інакше якщо вказівка == ВОтриматиЕлемент { дія_виводу(М, "ОтриматиЕлемент\р"); }
      інакше якщо вказівка == ВЗмінитиЕлемент { дія_виводу(М, "ЗмінитиЕлемент\р"); }
      інакше якщо вказівка == ВОтриматиПеребір { дія_виводу(М, "ОтриматиПеребір\р"); }
      інакше якщо вказівка == ВДодати { дія_виводу(М, "Додати\р"); }
      інакше якщо вказівка == ВВідняти { дія_виводу(М, "Відняти\р"); }
      інакше якщо вказівка == ВПомножити { дія_виводу(М, "Помножити\р"); }
      інакше якщо вказівка == ВПоділити { дія_виводу(М, "Поділити\р"); }
      інакше якщо вказівка == ВОтриматиОстачуДілення { дія_виводу(М, "ОтриматиОстачуДілення\р"); }
      інакше якщо вказівка == ВОтриматиНеповнуЧасткуДілення { дія_виводу(М, "ОтриматиНеповнуЧасткуДілення\р"); }
      інакше якщо вказівка == ВПіднестиДоСтепеня { дія_виводу(М, "ПіднестиДоСтепеня\р"); }
      інакше якщо вказівка == ВЗсунутиВліво { дія_виводу(М, "ЗсунутиВліво\р"); }
      інакше якщо вказівка == ВЗсунутиВправо { дія_виводу(М, "ЗсунутиВправо\р"); }
      інакше якщо вказівка == ВЗсунутиВправо2 { дія_виводу(М, "ЗсунутиВправо2\р"); }
      інакше якщо вказівка == ВДвійковеІ { дія_виводу(М, "ДвійковеІ\р"); }
      інакше якщо вказівка == ВДвійковеАБО { дія_виводу(М, "ДвійковеАБО\р"); }
      інакше якщо вказівка == ВДвійковеВиключнеАБО { дія_виводу(М, "ДвійковеВиключнеАБО\р"); }
      інакше якщо вказівка == ВМенше { дія_виводу(М, "Менше\р"); }
      інакше якщо вказівка == ВБільше { дія_виводу(М, "Більше\р"); }
      інакше якщо вказівка == ВНеБільше { дія_виводу(М, "НеБільше\р"); }
      інакше якщо вказівка == ВНеМенше { дія_виводу(М, "НеМенше\р"); }
      інакше якщо вказівка == ВРівно { дія_виводу(М, "Рівно\р"); }
      інакше якщо вказівка == ВНеРівно { дія_виводу(М, "НеРівно\р"); }
      інакше якщо вказівка == ВМістить { дія_виводу(М, "Містить\р"); }
      інакше якщо вказівка == ВНеМістить { дія_виводу(М, "НеМістить\р"); }
      інакше якщо вказівка == ВЄ { дія_виводу(М, "Є\р"); }
      інакше якщо вказівка == ВНеЄ { дія_виводу(М, "НеЄ\р"); }
      інакше якщо вказівка == ВДвійковеНЕ { дія_виводу(М, "ДвійковеНЕ\р"); }
      інакше якщо вказівка == ВВідʼємне { дія_виводу(М, "Відʼємне\р"); }
      інакше якщо вказівка == ВЗаперечити { дія_виводу(М, "Заперечити\р"); }
      інакше якщо вказівка == ВПочатиСпробу { дія_виводу(М, "ПочатиСпробу\р"); п += 4; }
      інакше якщо вказівка == ВВпасти { дія_виводу(М, "Впасти\р"); }
      інакше якщо вказівка == ВЗакінчитиСпробу { дія_виводу(М, "ЗакінчитиСпробу\р"); п += 4; }
      інакше якщо вказівка == ВВзяти { дія_виводу(М, "Взяти\р"); п += 4; }
      інакше якщо вказівка == ВДати { дія_виводу(М, "Дати\р"); }
      інакше якщо вказівка == ВПеребратиДалі { дія_виводу(М, "ПеребратиДалі\р"); }
      інакше якщо вказівка == ВЗбитиТекст { дія_виводу(М, "ЗбитиТекст\р"); п += 4; }
      інакше якщо вказівка == ВСтрибнути { дія_виводу(М, "Стрибнути\р"); п += 4; }
      інакше якщо вказівка == ВСтрибнутиЯкщоНі { дія_виводу(М, "СтрибнутиЯкщоНі\р"); п += 4; }
      інакше якщо вказівка == ВЗабрати { дія_виводу(М, "Забрати\р"); }
      інакше якщо вказівка == ВВернути { дія_виводу(М, "Вернути\р"); }
      інакше якщо вказівка == ВСтрибнутиЯкщоТак { дія_виводу(М, "СтрибнутиЯкщоТак\р"); п += 4; }
      інакше якщо вказівка == ВДублювати { дія_виводу(М, "Дублювати\р"); }
      інакше { дія_виводу(М, "НЕВІДОМА ВКАЗІВКА\р"); }
      п += 1;
    }
  }
}