взяти означення мавка;

простір мавка {
  дія рідна_дія_метода__далі(
    М: адреса<Машина>,
    здійснювач: Значення,
    значення: Значення,
    кількість_задіяних: природне, 
    задіяні: памʼять<Значення>,
    іменовано_задіяні: адреса<ІменованіЗадіяні>
  ) -> ЗначенняАбоСтанПадіння {
    якщо здійснювач.тип != М.предмет_структури_ПеребірДіапазонуДробових {
      увімкнути_стан_падіння(
        М,
        створити_текст(М, "Перевірте свій код, щось неправильно.") як Значення,
        Місцезнаходження { пусто, 0 }
      );
      вернути пусто;
    }

    змінна предмет_перебору_діапазону_дробових = здійснювач як адреса<ПредметПереборуДіапазонуДробових>;

    якщо предмет_перебору_діапазону_дробових.поточне_значення == пусто {
      предмет_перебору_діапазону_дробових.поточне_значення = предмет_перебору_діапазону_дробових.предмет_діапазону.від;
    } інакше {
      предмет_перебору_діапазону_дробових.поточне_значення = створити_дробове(М, предмет_перебору_діапазону_дробових.поточне_значення.значення + 1.0);
    }

    якщо предмет_перебору_діапазону_дробових.предмет_діапазону.включно {
      якщо предмет_перебору_діапазону_дробових.поточне_значення.значення <= предмет_перебору_діапазону_дробових.предмет_діапазону.до.значення {
        вернути М.значення_дійсне;
      }
    } інакше {
      якщо предмет_перебору_діапазону_дробових.поточне_значення.значення < предмет_перебору_діапазону_дробових.предмет_діапазону.до.значення {
        вернути М.значення_дійсне;
      }
    }

    вернути пусто;
  }

  дія записати_метод__далі(М: адреса<Машина>) {
    змінна предмет_дії = створити_рідну_дію(
      М,
      М.константи_назв._далі,
      пусто,
      пусто,
      рідна_дія_метода__далі
    );

    записати_метод_структури(
      М,
      М.предмет_структури_ПеребірДіапазонуДробових,
      М.константи_назв._далі,
      предмет_дії
    );
  }
}

простір мавка {
  місцева дія заповнити_структуру_ПеребірДіапазонуДробових(М: адреса<Машина>) {
    записати_метод__далі(М);
  }

  місцева дія створити_перебір_діапазону_дробових(М: адреса<Машина>, предмет_діапазону: адреса<ПредметДіапазонуДробових>) -> адреса<ПредметПереборуДіапазонуДробових> {
    змінна предмет = виділити_предмет<ПредметПереборуДіапазонуДробових>(М, М.предмет_структури_ПеребірДіапазонуДробових);

    предмет.предмет_діапазону = предмет_діапазону;
    предмет.поточне_значення = пусто;

    вернути предмет;
  }

  місцева дія предмет_перебору_діапазону_дробових_отримати_властивість(
    М: адреса<Машина>,
    предмет_перебору_діапазону_дробових: адреса<ПредметПереборуДіапазонуДробових>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    якщо назва == М.константи_назв._значення або перевірити_чи_предмети_тексту_рівні(М, назва, М.константи_назв._значення) {
      вернути предмет_перебору_діапазону_дробових.поточне_значення як Значення;
    }

    вернути знайти_метод_в_структурі(М, предмет_перебору_діапазону_дробових.тип, назва) як Значення;
  }
}