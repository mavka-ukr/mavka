взяти означення мавка;

простір мавка {
  місцева дія створити_структуру(
    М: адреса<Машина>,
    назва: адреса<ПредметТексту>,
    предок: адреса<ПредметСтруктури>,
    параметри: адреса<Послідовність>
  ) -> адреса<ПредметСтруктури> {
    змінна предмет = виділити_предмет<ПредметСтруктури>(М, М.предмет_структури_Структура);
    
    предмет.заборонити_наслідування = ні;
    предмет.назва = назва;
    предмет.предок = предок;
    предмет.параметри = параметри;
    предмет.методи = Методи { 0, пусто };
    предмет.властивості = Властивості { 0, пусто };

    вернути предмет;
  }

  // позначка: прохід по типах предметів
  місцева дія предмет_структури_здійснити(
    М: адреса<Машина>,
    значення: адреса<ПредметСтруктури>,
    кількість_задіяних: природне,
    задіяні: памʼять<Значення>,
    іменовано_задіяні: адреса<ІменованіЗадіяні>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    змінна результат: Значення = пусто;
    
    якщо значення == М.значення_дійсне {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_послідовність {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_дійсність {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.значення_предмет {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.предмет_структури_Модуль {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.предмет_структури_Параметр {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.предмет_структури_Число {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.предмет_структури_Ціле {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.предмет_структури_Дробове {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.предмет_структури_Текст {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.предмет_структури_Код {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.предмет_структури_Дія {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.предмет_структури_Список {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.предмет_структури_Словник {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.предмет_структури_Дані {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.предмет_структури_ЗмінніДані {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.предмет_структури_ДіапазонЦілих {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.предмет_структури_ДіапазонДробових {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.предмет_структури_ПеребірДіапазонуЦілих {
      стрибнути крок_вихід;
    }
    
    якщо значення == М.предмет_структури_ПеребірДіапазонуДробових {
      стрибнути крок_вихід;
    }

    змінна властивості_структури = значення як адреса<ПредметСтруктури>;

    результат = створити_предмет(М, значення як адреса<ПредметСтруктури>) як Значення;
    змінна п: природне = 0;

    якщо властивості_структури.параметри != пусто {
      поки п < властивості_структури.параметри.розмір {
        змінна параметр = властивості_структури.параметри.дані[п];
        змінна властивості_параметра = параметр як адреса<ПредметПараметра>;
  
        якщо кількість_задіяних > п {
          змінити_властивість(
            М,
            результат,
            властивості_параметра.назва,
            задіяні[п],
            місцезнаходження
          );
        }
  
        п += 1;
      }
  
      якщо іменовано_задіяні != пусто {
        п = 0;

        поки п < іменовано_задіяні.розмір {
          змінна іменовано_задіяне = іменовано_задіяні.елементи[п];
  
          змінити_властивість(
            М,
            результат,
            іменовано_задіяне.назва,
            іменовано_задіяне.значення,
            місцезнаходження
          );
  
          п += 1;
        }
      }
    }

  крок_вихід:
    звільнити_памʼять(М, задіяні);
    якщо іменовано_задіяні != пусто {
      знищити_іменовано_задіяні(М, іменовано_задіяні);
    }

    вернути результат;
  }

  місцева дія записати_метод_структури(
    М: адреса<Машина>,
    значення_структури: адреса<ПредметСтруктури>,
    назва: адреса<ПредметТексту>,
    значення_дії: адреса<ПредметДії>
  ) {
    змінна властивості_структури = значення_структури як адреса<ПредметСтруктури>;

    змінна п: природне = 0;
    поки п < властивості_структури.методи.розмір {
      змінна метод = властивості_структури.методи.дані[п]::адреса;

      якщо перевірити_чи_значення_рівні(М, метод.назва як Значення, назва як Значення) {
        метод.значення = значення_дії;

        вернути;
      }

      п += 1;
    }

    властивості_структури.методи.дані = перевиділити_памʼять<Метод>(М, властивості_структури.методи.дані, властивості_структури.методи.розмір + 1);
    властивості_структури.методи.дані[властивості_структури.методи.розмір] = Метод { назва, значення_дії };
    властивості_структури.методи.розмір += 1;
  }

  місцева дія знайти_метод_в_структурі(
    М: адреса<Машина>,
    значення_структури: адреса<ПредметСтруктури>,
    назва: адреса<ПредметТексту>
  ) -> адреса<ПредметДії> {
    змінна властивості_структури = значення_структури як адреса<ПредметСтруктури>;

    змінна п: природне = 0;
    поки п < властивості_структури.методи.розмір {
      змінна метод = властивості_структури.методи.дані[п];

      якщо перевірити_чи_значення_рівні(М, метод.назва як Значення, назва як Значення) {
        вернути метод.значення;
      }

      п += 1;
    }

    вернути пусто;
  }
  
  місцева дія предмет_структури_змінити_властивість(
    М: адреса<Машина>,
    предмет_структури: адреса<ПредметСтруктури>,
    назва: адреса<ПредметТексту>,
    значення: Значення,
    місцезнаходження: Місцезнаходження
  ) {      
    змінна п: природне = 0;
    поки п < предмет_структури.властивості.розмір {
      змінна властивість = предмет_структури.властивості.дані[п]::адреса;

      якщо перевірити_чи_значення_рівні(М, властивість.назва як Значення, назва як Значення) {
        властивість.значення = значення;

        вернути;
      }

      п += 1;
    }

    предмет_структури.властивості.дані = перевиділити_памʼять<Властивість>(М, предмет_структури.властивості.дані, предмет_структури.властивості.розмір + 1);
    предмет_структури.властивості.дані[предмет_структури.властивості.розмір] = Властивість { назва, значення };
    предмет_структури.властивості.розмір += 1;
  }
  
  місцева дія предмет_структури_отримати_властивість(
    М: адреса<Машина>,
    предмет_структури: адреса<ПредметСтруктури>,
    назва: адреса<ПредметТексту>,
    місцезнаходження: Місцезнаходження
  ) -> ЗначенняАбоСтанПадіння {
    змінна п: природне = 0;

    поки п < предмет_структури.властивості.розмір {
      змінна властивість = предмет_структури.властивості.дані[п]::адреса;

      якщо властивість.назва == назва або перевірити_чи_предмети_тексту_рівні(М, властивість.назва, назва) {
        вернути властивість.значення;
      }
    }

    вернути пусто;
  }
}